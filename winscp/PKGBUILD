# Maintainer: David Macek <david.macek.0@gmail.com>

_realname=winscp
_choconame=winscp
pkgbase="app-${_realname}"
pkgname=("app-i686-${_realname}")
epoch=1
pkgver=5.21.8
pkgrel=1
pkgdesc="An open source two-pane SFTP/FTP/WebDAV/SCP client"
url="https://winscp.net/"
arch=('any')
license=('custom')
makedepends=("app-innounp")
provides=("${pkgbase}")
options=(!strip)
source=("WinSCP-Setup.exe::https://download.sourceforge.net/winscp/WinSCP-${pkgver}-Setup.exe")
sha512sums=('3a78933331d08ac3abbcde620dfd2c94514d091e523ff1ece8d10565914b104882b2ce9d53b75bc2df4a5b9455bbda9f9ec915f68f5639bf93f24ff9722395a0')

_lc() {
    local d f n

    for f in "${@}"
    do
        d="$(dirname "${f}")"
        f="$(basename "${f}")"
        n="$(echo "${f}" | tr 'A-Z' 'a-z')"
        if test "${f}" != "${n}"
        then
            mv "${d}/${f}" "${d}/${n}"
        fi
    done
}

prepare() {
    /apps/innounp/innounp -x -q -m -a -y "WinSCP-Setup.exe" -d"WinSCP-Setup.dir"
}

package() {
    local a="${pkgdir}/apps/${_realname}"
    mkdir -p "${a}"
    cp -R "WinSCP-Setup.dir/{app}"/* "${a}"
    rm -R "${a}/winscp.map" "${a}/putty"
    _lc "${a}"/*
}
