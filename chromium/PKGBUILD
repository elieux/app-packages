# Maintainer: David Macek <david.macek.0@gmail.com>

_realname=chromium
_choconame=chromium
_realver=870763
pkgbase="app-${_realname}"
pkgname=("app-x86_64-${_realname}")
epoch=1
pkgver=91.0.4472.164
pkgrel=1
pkgdesc="The open-source project behind Google Chrome, an attempt at creating a safer, faster, and more stable browser"
url="https://www.chromium.org/Home"
arch=('any')
license=('BSD')
replaces=("app-i686-${_realname}")
provides=("${pkgbase}")
options=(!strip)
source=("chrome.sync.7z::https://github.com/Hibbiki/chromium-win64/releases/download/v${pkgver}-r${_realver}/chrome.sync.7z")
sha512sums=('d204e55400a476aab8216ab758ee684175fff3351c0ea8107547212a3b3b066ddcd6c38116ae864b5c69a67dfb80a60fc0c6fb8d1f9f4c1345c0cfac6e3d445d')

_lc() {
    local d f n

    for f in "${@}"
    do
        d="$(dirname "${f}")"
        f="$(basename "${f}")"
        n="$(echo "${f}" | tr 'A-Z' 'a-z')"
        if test "${f}" != "${n}"
        then
            mv "${d}/${f}" "${d}/${n}"
        fi
    done
}

package() {
    local a="${pkgdir}/apps/${_realname}"
    mkdir -p "${a}"
    cp -R "Chrome-bin"/* "${a}"
    mv "${a}/${pkgver}"/* "${a}"
    rm -R "${a}/${pkgver}"
    _lc "${a}"/*
}
