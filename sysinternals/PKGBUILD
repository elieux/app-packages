# Maintainer: David Macek <david.macek.0@gmail.com>

_realname=sysinternals
pkgbase="app-${_realname}"
pkgname=("app-i686-${_realname}"
         "app-x86_64-${_realname}")
pkgver=2018.02.12
pkgrel=1
pkgdesc="Advanced system utilities for Windows"
url="https://docs.microsoft.com/en-us/sysinternals/"
arch=('any')
license=('custom')
makedepends=("unzip")
provides=("${pkgbase}")
options=(!strip)
source=("http://download.sysinternals.com/files/SysinternalsSuite.zip"
        "http://download.sysinternals.com/files/RootkitRevealer.zip"
        "http://download.sysinternals.com/files/BlueScreen.zip")
sha512sums=('124f36df6c4cdacc24baf5ab7832c3c15c58357dd9dbeb945df9424fc286ead9e143f50beaf6b25978083505f99a889d3c9179a818334c3ec7e528bf871e2294'
            '2e5f89552fc1912b5fa37eb443ef2aa86e2a60f2f7f24f420df56e1ec5f59bd64e2b71e38365f7beaace419299762b9960b7617de9aff6301020277d3d7c1e61'
            '2e39c4b36e9928851fc86d9fc541956c3079a1ded09cca5ee677fbe1b3c9272b87651b65e53279cede3c4ed37028fcbae0823c4ee25180d5bc132cef8d155507')

_lc() {
    local d f n

    for f in "${@}"
    do
        d="$(dirname "${f}")"
        f="$(basename "${f}")"
        n="$(echo "${f}" | tr 'A-Z' 'a-z')"
        if test "${f}" != "${n}"
        then
            mv "${d}/${f}" "${d}/${n}"
        fi
    done
}

pkgver() {
    #find . -mindepth 1 -maxdepth 1 -name '*.exe' -printf '%TY.%Tm.%Td\n' | sort | tail -1
    find . -mindepth 1 -maxdepth 1 -name '*.zip' -execdir bash -c 'unzip -l "{}" | tail -n +4 | head -n -2' \; | sed -r 's/[ ]+/\t/g' | cut -f 3 | tr '-' '.' | sort | tail -1
}

prepare() {
    _lc *
}

_package() {
    a="${pkgdir}/apps${_apparch}/${_realname}"
    mkdir -p "${a}"
    d="${pkgdir}/docs${_apparch}/${_realname}"
    mkdir -p "${d}"
    for f in "${@}"
    do
        n="${f##*/}"
        n="${n/64/}"
        case "${n}" in
        *.exe|*.sys|*.scr)
            cp "${f}" "${a}/${n}"
            ;;
        *.chm|*.txt|*.hlp)
            cp "${f}" "${d}/${n}"
            ;;
        esac
    done
    if test -z "$(find "${d}" -type f)"
    then
        rm -d "${d}"
    fi
}

package_app-i686-sysinternals() {
    _apparch=32
    _package $(find . -mindepth 1 -maxdepth 1 -not -name '*64*')
}

package_app-x86_64-sysinternals() {
    _apparch=64
    _package $(find . -mindepth 1 -maxdepth 1 -name '*64*') *.txt
}
