# Maintainer: David Macek <david.macek.0@gmail.com>

_realname=teamviewer
pkgbase="app-${_realname}"
pkgname=("app-i686-${_realname}")
epoch=1
pkgver=14.7.1965
pkgrel=1
pkgdesc="All-in-one software for remote support and online meetings"
url="https://www.teamviewer.com/"
arch=('any')
license=('custom')
makedepends=("unzip")
provides=("${pkgbase}")
options=(!strip)
source=("TeamViewerPortable.zip::https://download.teamviewer.com/download/TeamViewerPortable.zip")
sha512sums=('2ab812b34db71683151c7d6110a9877113d34082853bb05d707e88fe41bfbb12795b668745f8008fed67a17ae020c43eee1f78a7fd6f7bb353abb600faeff0d1')

_lc() {
    local d f n

    for f in "${@}"
    do
        d="$(dirname "${f}")"
        f="$(basename "${f}")"
        n="$(echo "${f}" | tr 'A-Z' 'a-z')"
        if test "${f}" != "${n}"
        then
            mv "${d}/${f}" "${d}/${n}"
        fi
    done
}

prepare() {
    _lc *
}

pkgver() {
    "${SYSTEMROOT}/System32/WindowsPowerShell/v1.0/powershell" -Command '(Get-Item "teamviewer.exe").VersionInfo.FileVersion' | sed -r 's/([0-9]+[.][0-9]+[.][0-9]+)[.]0$/\1/'
}

package() {
    a="${pkgdir}/apps/${_realname}"
    mkdir -p "${a}"
    cp * "${a}"
    rm "${a}/teamviewerportable.zip"
}
