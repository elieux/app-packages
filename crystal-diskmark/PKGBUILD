# Maintainer: David Macek <david.macek.0@gmail.com>

_realname=crystaldiskmark
_dirname=crystal
pkgbase="app-${_realname}"
pkgname=("app-x86_64-${_realname}")
epoch=1
pkgver=6.0.0
pkgrel=1
pkgdesc="A disk benchmarking software (Crystal Dew World)"
url="http://crystalmark.info/software/CrystalDiskMark/index-e.html"
arch=('any')
license=('MIT')
replaces=("app-i686-${_realname}")
makedepends=("unzip")
provides=("${pkgbase}")
options=(!strip)
source=("CrystalDiskMark.zip::https://osdn.net/dl/crystaldiskmark/CrystalDiskMark${pkgver//./_}.zip")
sha512sums=('41dfe6f639d2b97527e83f7069c97c6bac61764e728fb4d994f45fca5de9c4a987b40d2b3d69a9a3d0069260f84fc667d7f4416acf39b5872dc5dfe9ee8c7e73')

_lc() {
    local d f n

    for f in "${@}"
    do
        d="$(dirname "${f}")"
        f="$(basename "${f}")"
        n="$(echo "${f}" | tr 'A-Z' 'a-z')"
        if test "${f}" != "${n}"
        then
            mv "${d}/${f}" "${d}/${n}"
        fi
    done
}

prepare() {
    _lc *
}

package() {
    a="${pkgdir}/apps/${_dirname}"
    mkdir -p "${a}"
    cp "diskmark64.exe" "${a}/crystaldiskmark.exe"
    cp -r "cdmresource" "${a}"
    rm "${a}/cdmresource/diskspd"/*
    cp "cdmresource/diskspd/diskspd64.exe" "${a}/cdmresource/diskspd"
}
