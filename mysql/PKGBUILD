# Maintainer: David Macek <david.macek.0@gmail.com>

_realname=mysql
pkgbase="app-${_realname}"
pkgname=("app-i686-${_realname}"
         "app-x86_64-${_realname}")
pkgver=5.7.17
pkgrel=1
pkgdesc="Fast SQL database server, community edition"
url="https://www.mysql.com/products/community/"
arch=('any')
license=('GPL')
makedepends=("unzip")
provides=("${pkgbase}")
options=(!strip)
source=("mysql-win32.zip::https://dev.mysql.com/get/Downloads/MySQL-${pkgver%.*}/mysql-${pkgver}-win32.zip"
        "mysql-winx64.zip::https://dev.mysql.com/get/Downloads/MySQL-${pkgver%.*}/mysql-${pkgver}-winx64.zip")
sha512sums=('68aa27b9dc9956a2e2c827a663de11427290469bfbfdd222986fd4caadd7627a8ef35a88349209b84806a8e508913078934e330544573b4ea9c5a5e0f2d9e499'
            'b89cbcb53c1a7d9005a169dbadc09e642d483c30e005c837440fbc73dc6f208acf9285513e3361d6c320c5e5046af248f6ea49425c53c024c274fb62b8465425')

_package() {
    a="${pkgdir}/apps${_apparch}/${_realname}"
    mkdir -p "${a}"
    cp -r "mysql-${pkgver}-win${1}"/* "${a}"
    find "${a}" -name 'debug' -exec rm -r '{}' \; -prune
    find "${a}" -name '*.pdb' -delete
    find "${a}" -name '*.lib' -delete
    rm "${a}/bin"/msvc{p,r}120.dll

    d="${pkgdir}/docs${_apparch}/${_realname}"
    mkdir -p "${d}"
    mv "${a}"/{docs,COPYING,README} "${a}"/*.ini "${d}"
}

package_app-i686-mysql() {
    _apparch=32
    _package "32"
}

package_app-x86_64-mysql() {
    _apparch=64
    _package "x64"
}
