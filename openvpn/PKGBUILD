# Maintainer: David Macek <david.macek.0@gmail.com>

_realname=openvpn
_choconame=openvpn
pkgbase="app-${_realname}"
pkgname=("app-x86_64-${_realname}")
epoch=1
pkgver=2.6.5.001
pkgrel=1
pkgdesc="A full-featured SSL VPN solution"
url="https://openvpn.net/community/"
arch=('any')
license=('custom')
makedepends=("app-lessmsi")
provides=("${pkgbase}")
options=(!strip)
source=("OpenVPN-amd64.msi::https://swupdate.openvpn.org/community/releases/OpenVPN-${pkgver%.*}-I${pkgver##*.}-amd64.msi")
sha512sums=('2ba0c5333e15cf6ee451f82606ee3f682b9b057e478dd8c960513349f8835443de6404135e13d816324511509eba02f30691ef3e4ec4f30fcbaac5713fe6ab5a')

_rmmsdll() {
    local ms

    ms=("msvbvm[0-9]+"
        "ucrtbase"
        "mfcm?[0-9]+[a-z]+"
        "msvc[rp][0-9]+(_([0-9]|codecvt_ids|atomic_wait))?"
        "vc([ao]mp|corlib|runtime)[0-9]+(_[0-9])?"
        "concrt[0-9]+"
        "pgort[0-9]+"
        "api-ms-win-[0-9a-z-]+")
    find "${1}" -iname '*.dll' | grep -Eif <(for m in "${ms[@]}"; do echo "/${m}[.]dll$"; done) | xargs -r rm
}

prepare() {
    rm -fR "OpenVPN-amd64"
    /apps/lessmsi/lessmsi x "OpenVPN-amd64.msi" > /dev/null
}

package() {
    local a="${pkgdir}/apps/${_realname}"
    mkdir -p "${a}"
    cp -R "OpenVPN-amd64/SourceDir/OpenVPN"/* "${a}"
    rm -R "${a}"/{easy-rsa,include,config,config-auto,log}
    _rmmsdll "${a}"
}
