# Maintainer: David Macek <david.macek.0@gmail.com>

_realname=musescore
_choconame=musescore
pkgbase="app-${_realname}"
pkgname=("app-x86_64-${_realname}")
epoch=1
pkgver=3.6.2.548021803
pkgrel=1
pkgdesc="Create, play and print beautiful sheet music"
url="https://musescore.org/"
arch=('any')
license=('GPL')
replaces=("app-i686-${_realname}")
makedepends=("app-lessmsi")
provides=("${pkgbase}")
options=(!strip)
source=("MuseScore-x86_64.msi::https://github.com/musescore/MuseScore/releases/download/v$(echo "${pkgver%.*}" | sed -r -e 's/[.]0$//')/MuseScore-${pkgver}-x86_64.msi")
sha512sums=('b6fc966c93b9af4991f51b65a3a93e773ab3efa10f3b14a844e705d4ae64e8c10f396aa5b15396fce3ee72beaccf4f5cfd35138ed72d9bb211ea7700cccba7db')

_rmmsdll() {
    local ms

    ms=("msvbvm[0-9]+"
        "ucrtbase"
        "mfcm?[0-9]+[a-z]+"
        "msvc[rp][0-9]+(_([0-9]|codecvt_ids|atomic_wait))?"
        "vc([ao]mp|corlib|runtime)[0-9]+(_[0-9])?"
        "concrt[0-9]+"
        "pgort[0-9]+"
        "api-ms-win-[0-9a-z-]+")
    find "${1}" -iname '*.dll' | grep -Eif <(for m in "${ms[@]}"; do echo "/${m}[.]dll$"; done) | xargs -r rm
}

prepare() {
    rm -fR "MuseScore-x86_64"
    /apps/lessmsi/lessmsi x "MuseScore-x86_64.msi" > /dev/null
}

package() {
    local a="${pkgdir}/apps/${_realname}"
    mkdir -p "${a}"
    cp -R "MuseScore-x86_64/SourceDir/MuseScore 3"/* "${a}"
    _rmmsdll "${a}"
}
