# Maintainer: David Macek <david.macek.0@gmail.com>

_realname=filezilla-server
_apparch=32
pkgbase="app-${_realname}"
pkgname=("app-i686-${_realname}")
pkgver=0.9.56
pkgrel=1
pkgdesc="A popular FTP server"
url="http://filezilla-project.org/"
arch=('any')
license=('GPL')
makedepends=("p7zip")
provides=("${pkgbase}")
options=(!strip)
source=("FileZilla_Server.exe::http://downloads.sourceforge.net/filezilla/FileZilla%20Server/${pkgver}/FileZilla_Server-${pkgver//./_}.exe")
sha512sums=('482e770c75bb8654dc9d913ad092227b28544b0a7291c0e5bc92eebd6c13faa1e4c107fa8474fb41e58062336b0cf7762128e419762cd07b47e9ec7e00335f53')

_lc() {
    for f in "${@}"
    do
        n="$(echo "${f}" | tr 'A-Z' 'a-z')"
        if test "${f}" != "${n}"
        then
            mv "${f}" "${n}"
        fi
    done
}

prepare() {
    7z x -y -bd "FileZilla_Server.exe"
    _lc *
}

package() {
    a="${pkgdir}/apps${_apparch}/${_realname}"
    mkdir -p "${a}"
    cp -r "filezilla server.exe" "filezilla server interface.exe" *.dll "${a}"

    d="${pkgdir}/docs${_apparch}/${_realname}"
    mkdir -p "${d}"
    cp -r "license.txt" *.htm "${d}"
}
