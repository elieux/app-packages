# Maintainer: David Macek <david.macek.0@gmail.com>

_status=("broken")
_realname=viber
_apparch=32
pkgbase="app-${_realname}"
pkgname=("app-i686-${_realname}")
pkgver=5.2.0.2529
pkgrel=1
pkgdesc="Free messages and calls in any country"
url="http://www.viber.com/en/products/windows"
arch=('any')
license=('custom')
makedepends=("p7zip")
provides=("${pkgbase}")
options=(!strip)
source=("ViberSetup.exe::http://download.viber.com/desktop/windows/ViberSetup.exe")
sha512sums=('353f3dc1c29cfe5a2df5d040275affc84067d5a240444d7388b8c5579f05d8b69d3d33282df9fdb48fc944f2beb7ab33f7394b989f82f5b11ffdfb66acac873f')

_lc() {
    local d f n

    for f in "${@}"
    do
        d="$(dirname "${f}")"
        f="$(basename "${f}")"
        n="$(echo "${f}" | tr 'A-Z' 'a-z')"
        if test "${f}" != "${n}"
        then
            mv "${d}/${f}" "${d}/${n}"
        fi
    done
}

pkgver() {
    b="$(cygpath -wa "ViberSetup.exe" | sed 's/\\/\\\\/g')"
    wmic datafile where name=\""${b}"\" get version | grep 'Version' -A 1 | tail -1 | tr -d ' '
}

prepare() {
    7z x -y -bd "ViberSetup.exe"
    7z x -y -bd "pack.exe" -o"pack"
    _lc * "pack"/*
}

package() {
    a="${pkgdir}/apps${_apparch}/${_realname}"
    mkdir -p "${a}"
    cp -r "pack"/* "${a}"
    rm "${a}"/msvc{p,r}120.dll
}
