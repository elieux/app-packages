# Maintainer: David Macek <david.macek.0@gmail.com>

_realname=git
_realver=1.9.5-preview20150319
_apparch=32
pkgbase="app-${_realname}"
pkgname=("app-i686-${_realname}")
pkgver="$(echo -n "${_realver}" | sed -r 's/^([0-9.]+)-preview([0-9]{4})([0-9]{2})([0-9]{2})+$/\1.p\2.\3.\4/')"
pkgrel=1
pkgdesc="The fast distributed version control system"
url="http://git-scm.com/"
arch=('any')
license=('custom')
makedepends=("p7zip")
provides=("${pkgbase}")
options=(!strip)
source=("PortableGit.7z::https://github.com/msysgit/msysgit/releases/download/Git-${_realver}/PortableGit-${_realver}.7z")
md5sums=('cd6ed21ed2108d9f8269cb62287500f6')

prepare() {
    7z x -y -bd "PortableGit.7z"
    sed 's/autocrlf = true/autocrlf = false/' -i "etc/gitconfig"
}

package() {
    a="${pkgdir}/apps${_apparch}/${_realname}"
    mkdir -p "${a}"
    cp -r bin cmd etc git-cheetah lib libexec share ssl git*.bat "${a}"
    cp -r "Git Bash.vbs" "${a}/git-bash.vbs"

    d="${pkgdir}/docs${_apparch}/${_realname}"
    mkdir -p "${d}"
    cp -r doc "README.portable" *.rtf "${d}"
}
