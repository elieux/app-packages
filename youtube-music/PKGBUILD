# Maintainer: David Macek <david.macek.0@gmail.com>

_realname=youtube-music
_choconame=th-ch-youtube-music
pkgbase="app-${_realname}"
pkgname=("app-x86_64-${_realname}")
epoch=1
pkgver=1.12.1
pkgrel=1
pkgdesc="Unofficial YouTube Music Desktop App with built-in ad blocker and downloader"
url="https://th-ch.github.io/youtube-music/"
arch=('any')
license=('MIT')
makedepends=("p7zip")
provides=("${pkgbase}")
options=(!strip)
source=("YouTube-Music-Setup.exe::https://github.com/th-ch/youtube-music/releases/download/v${pkgver}/YouTube-Music-Setup-${pkgver}.exe")
sha512sums=('0ce6d36a46c591edfd76738222db44ddf545a014bd2e93f7da5d44842c1a56b0bd932b5560397603b85d877104a74e561017cdfc25b16d337e3d0ee6eefda2f9')

_lc() {
    local d f n

    for f in "${@}"
    do
        d="$(dirname "${f}")"
        f="$(basename "${f}")"
        n="$(echo "${f}" | tr 'A-Z' 'a-z')"
        if test "${f}" != "${n}"
        then
            mv "${d}/${f}" "${d}/${n}"
        fi
    done
}

prepare() {
    7z x -bd "YouTube-Music-Setup.exe" -o"YouTube-Music-Setup" > /dev/null
    7z x -bd "YouTube-Music-Setup"/'$PLUGINSDIR'/"app-64.7z" -o"app-64" > /dev/null
}

package() {
    local a="${pkgdir}/apps/${_realname}"
    mkdir -p "${a}"
    cp -R "app-64"/* "${a}"
    mv "${a}/YouTube Music.exe" "${a}/YouTube-Music.exe"
    _lc "${a}"/*
}
