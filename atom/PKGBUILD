# Maintainer: David Macek <david.macek.0@gmail.com>

_realname=atom
pkgbase="app-${_realname}"
pkgname=("app-i686-${_realname}"
         "app-x86_64-${_realname}")
pkgver=1.21.1
pkgrel=1
pkgdesc="A hackable text editor for the 21st Century"
arch=('any')
url="https://atom.io/"
license=('custom')
makedepends=("unzip")
provides=("${pkgbase}")
options=(!strip)
source=("atom-x32-windows.zip::https://github.com/atom/atom/releases/download/v${pkgver}/atom-windows.zip"
        "atom-x64-windows.zip::https://github.com/atom/atom/releases/download/v${pkgver}/atom-x64-windows.zip")
sha512sums=('da34d6cd2dc6d76d559e61ac6068e818ec19c88d7869a1bd6a13c1d01185ed06c97787ea4463acc410a173aa38cd83eec196d765debeba568ed5137ebb8e7023'
            'f8e5288eba48da67a002f3cb68640c98ff7e3a45a0f5dcd944302b650ad99d165a275d0db2363023658bf82780574a25e589a5d5ea920d065ad16be6b653fc49')

_lc() {
    local d f n

    for f in "${@}"
    do
        d="$(dirname "${f}")"
        f="$(basename "${f}")"
        n="$(echo "${f}" | tr 'A-Z' 'a-z')"
        if test "${f}" != "${n}"
        then
            mv "${d}/${f}" "${d}/${n}"
        fi
    done
}

_prepare() {
    _lc "Atom x${_apparch}"/*
}

prepare() {
    mv "Atom" "Atom x32"

    for _apparch in 32 64
    do
        _prepare
    done
}

_package() {
    a="${pkgdir}/apps${_apparch}/${_realname}"
    mkdir -p "${a}"
    cp -r "Atom x${_apparch}"/* "${a}"

    d="${pkgdir}/docs${_apparch}/${_realname}"
    mkdir -p "${d}"
    mv "${a}"/{version,license} "${a}"/*.html "${a}/resources/LICENSE.md" "${d}"
}

package_app-i686-atom() {
    _apparch=32
    _package
}

package_app-x86_64-atom() {
    _apparch=64
    _package
}
