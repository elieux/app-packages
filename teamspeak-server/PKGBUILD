# Maintainer: David Macek <david.macek.0@gmail.com>

_realname=teamspeak-server
pkgbase="app-${_realname}"
pkgname=("app-i686-${_realname}"
         "app-x86_64-${_realname}")
pkgver=3.0.13.6
pkgrel=1
pkgdesc="A proprietary VoIP conference software"
url="http://www.teamspeak.com/"
arch=('any')
license=('custom')
makedepends=("unzip")
provides=("${pkgbase}")
options=(!strip)
source=("teamspeak3-server_win32.zip::http://dl.4players.de/ts/releases/${pkgver}/teamspeak3-server_win32-${pkgver}.zip"
        "teamspeak3-server_win64.zip::http://dl.4players.de/ts/releases/${pkgver}/teamspeak3-server_win64-${pkgver}.zip")
sha512sums=('bb6a99f375a73dad35708d8b8ad459c5d7686261b3059c34f24650894ec8ff2df49c880856ede55912e1a4197a5ec24f55ef1638e6bb67ec7b6bd17655906f10'
            '1c8565c7978a39a85180fffa9d741cf397a297852f7e5cb3a5c932e6afc18ce123f43f819f41eb17f8027d189295a88361c23f871ad0753a611dafea01a3339f')

_package() {
    a="${pkgdir}/apps${_apparch}/${_realname}"
    mkdir -p "${a}"
    cp -r "teamspeak3-server_win${_apparch}/redist/libmariadb.dll" "teamspeak3-server_win${_apparch}/tsdns/tsdnsserver.exe" "teamspeak3-server_win${_apparch}"/*.{exe,dll} "${a}"

    d="${pkgdir}/docs${_apparch}/${_realname}"
    mkdir -p "${d}" "${d}/tsdns"
    cp -r "teamspeak3-server_win${_apparch}"/*.txt "teamspeak3-server_win${_apparch}"/{doc,sql,serverquerydocs} "${d}"
    cp "teamspeak3-server_win${_apparch}/tsdns"/*.{txt,sample} "${d}/tsdns"
}

package_app-i686-teamspeak-server() {
    _apparch=32
    _package
}

package_app-x86_64-teamspeak-server() {
    _apparch=64
    _package
}
