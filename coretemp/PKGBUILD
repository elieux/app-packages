# Maintainer: David Macek <david.macek.0@gmail.com>

_realname=coretemp
pkgbase="app-${_realname}"
pkgname=("app-i686-${_realname}"
         "app-x86_64-${_realname}")
pkgver=1.7.0
pkgrel=1
pkgdesc="A compact program to monitor processor temperature"
arch=('any')
url="http://www.alcpu.com/CoreTemp/"
license=('custom')
makedepends=("unzip")
provides=("${pkgbase}")
options=(!strip)
source=("CoreTemp32.zip::http://www.alcpu.com/CoreTemp/CoreTemp32.zip"
        "CoreTemp64.zip::http://www.alcpu.com/CoreTemp/CoreTemp64.zip"
        "CoreTempTranslator.zip::http://www.alcpu.com/CoreTemp/CoreTempTranslator.zip")
sha512sums=('b642bc811081ea4dfede03c2f11d012887996636772d1f3cb9d6ff53916bfb8797a639f8432899ea2f874dc0fcaeaadaf780453c0d915d6e9c14a7eea19d7695'
            '6506c6c2f9fa06f0011b8260cad69827fb368da24c08afd3e5c51a766f80a7074d5577595c7a1dba72ad26b7ba86bafdf621573a025662be64404c277321db92'
            'b158538ad7b184b9dc8853d2c48a0d0d89780e792b73ab29099374d87aa871d90fcd02035d1c8764f378442c485edafff1c50e73a74c1b34f5b0c8a55b89f291')
noextract=("CoreTemp32.zip"
           "CoreTemp64.zip")

_eqver() {
    local r v
    for v in "${@}"
    do
        if [ -n "${r}" -a "${r}" != "${v}" ]
        then
            echo "Version mismatch between ${r} and ${v}."
        fi
        r="${v}"
    done
    echo "${r}"
}

_lc() {
    local d f n

    for f in "${@}"
    do
        d="$(dirname "${f}")"
        f="$(basename "${f}")"
        n="$(echo "${f}" | tr 'A-Z' 'a-z')"
        if test "${f}" != "${n}"
        then
            mv "${d}/${f}" "${d}/${n}"
        fi
    done
}

pkgver() {
    _vers=( $(
        for _apparch in 32 64
        do
            b="$(cygpath -wa "coretemp${_apparch}/core temp.exe" | sed 's/\\/\\\\/g')"
            wmic datafile where name=\""${b}"\" get version | grep 'Version' -A 1 | tail -1 | tr -d ' ' | sed -r 's/([0-9]+[.][0-9]+[.][0-9]+)[.]0$/\1/'
        done
    ) )
    _eqver "${_vers[@]}"
}

_prepare() {
    unzip -o "CoreTemp${_apparch}.zip" -d "CoreTemp${_apparch}"
    _lc "CoreTemp${_apparch}"/*
}

prepare() {
    for _apparch in 32 64
    do
        _prepare
    done
    _lc "languages"/* *
}

_package() {
    a="${pkgdir}/apps${_apparch}/${_realname}"
    mkdir -p "${a}"
    cp -r *.{dll,exe,lng} "languages" "coretemp${_apparch}"/*.exe "${a}"
    mv "${a}/core temp.exe" "${a}/coretemp.exe"

    d="${pkgdir}/docs${_apparch}/${_realname}"
    mkdir -p "${d}" "${d}/translator"
    cp "coretemp${_apparch}"/*.{url,txt} "${d}"
    cp *.{txt,png} "${d}/translator"
}

package_app-i686-coretemp() {
    _apparch=32
    _package
}

package_app-x86_64-coretemp() {
    _apparch=64
    _package
}
