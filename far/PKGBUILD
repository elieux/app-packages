# Maintainer: David Macek <david.macek.0@gmail.com>

_realname=far
_realver=30b5151-20180227
pkgbase="app-${_realname}"
pkgname=("app-x86_64-${_realname}")
epoch=1
pkgver="$(echo -n "${_realver}" | sed -r 's/^([0-9])([0-9])b([0-9]+)-[0-9]+$/\1.\2.\3/')"
pkgrel=1
pkgdesc="Extensible text mode file manager"
url="http://farmanager.com/index.php?l=en"
arch=('any')
license=('GPL2' 'BSD' 'custom')
replaces=("app-i686-${_realname}")
makedepends=("p7zip")
provides=("${pkgbase}")
options=(!strip)
source=("Far.x64.7z::http://farmanager.com/files/Far${_realver%%-*}.x64.${_realver##*-}.7z")
sha512sums=('39c008345c1dea04d74e1d17503187dd06bbb434dc5a16db3380310692741aa8c41c7fb7c2c154b3f32c991aece191f76b3fd162b03369e24a77ada03d664df8')
noextract=("${source[@]%%::*}")

_lc() {
    local d f n

    for f in "${@}"
    do
        d="$(dirname "${f}")"
        f="$(basename "${f}")"
        n="$(echo "${f}" | tr 'A-Z' 'a-z')"
        if test "${f}" != "${n}"
        then
            mv "${d}/${f}" "${d}/${n}"
        fi
    done
}

prepare() {
    7z x -y -bd "Far.x64.7z" -o"far"
    _lc "far"/*
}

package() {
    a="${pkgdir}/apps/${_realname}"
    mkdir -p "${a}"
    cp -r "far"/*.{exe,lng,hlf,dll,cmd} "far"/{addons,plugins,pluginsdk} "${a}"
    cp -r "far"/*.{ini,diz,map} "far"/changelog{,_eng} "far"/{documentation,encyclopedia} "${a}"
}
