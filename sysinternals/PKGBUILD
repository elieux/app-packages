# Maintainer: David Macek <david.macek.0@gmail.com>

_realname=sysinternals
_apparch=32
pkgname=("app-i686-${_realname}")
pkgver=2015.01.29
pkgrel=1
pkgdesc="Advanced system utilities for Windows"
url="http://technet.microsoft.com/en-us/sysinternals/bb545021.aspx"
arch=('any')
license=('custom')
makedepends=("unzip")
options=(!strip) # digital signatures
source=("http://download.sysinternals.com/files/SysinternalsSuite.zip"
        "http://download.sysinternals.com/files/BlueScreen.zip")
md5sums=('e6c6d5fa4f837d0fa97b76b53294dac7'
         'e985acdd52b7bccdd6feb85ebd74c722')

prepare() {
    cd "${srcdir}"

    for f in *
    do
        n=$(echo "${f}" | tr 'A-Z' 'a-z')
        if test "${f}" != "${n}"
        then
            mv "${f}" "${n}"
        fi
    done
}

package() {
    mkdir -p "${pkgdir}/apps${_apparch}/${_realname}"
    mkdir -p "${pkgdir}/docs${_apparch}/${_realname}"
    cp "${srcdir}"/*.{exe,sys,scr} "${pkgdir}/apps${_apparch}/${_realname}/"
    cp "${srcdir}"/*.{chm,txt,hlp} "${pkgdir}/docs${_apparch}/${_realname}/"
}
