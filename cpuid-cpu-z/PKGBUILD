# Maintainer: David Macek <david.macek.0@gmail.com>

_realname=cpu-z
_dirname=cpuid
pkgbase="app-${_realname}"
pkgname=("app-i686-${_realname}"
         "app-x86_64-${_realname}")
pkgver=1.75
pkgrel=1
pkgdesc="Gathers information on some of the main devices of your system (CPUID)"
url="http://www.cpuid.com/softwares/cpu-z.html"
arch=('any')
license=('custom')
makedepends=("unzip")
provides=("${pkgbase}")
options=(!strip)
source=("cpu-z-en.zip::http://download.cpuid.com/${_realname}/${_realname}_${pkgver}-en.zip")
sha512sums=('b782ae973854c531eef553c93f8f3fa73f3fc66eda919966c9990ad0f7fbfa9df8ed5409daf0e62edf04833794558b85ce8d56351f67643701b9d1cfe64ba064')

_package() {
    a="${pkgdir}/apps${_apparch}/${_dirname}"
    mkdir -p "${a}"
    cp "cpuz_x${_apparch}.exe" "${a}/cpuz.exe"
    cp "cpuz.ini" "${a}"

    d="${pkgdir}/docs${_apparch}/${_dirname}"
    mkdir -p "${d}"
    cp "cpuz_readme.txt" "${d}/cpuz.txt"
}

package_app-i686-cpu-z() {
    _apparch=32
    _package
}

package_app-x86_64-cpu-z() {
    _apparch=64
    _package
}
