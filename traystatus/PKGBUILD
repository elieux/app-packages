# Maintainer: David Macek <david.macek.0@gmail.com>

_realname=traystatus
pkgbase="app-${_realname}"
pkgname=("app-i686-${_realname}")
epoch=1
pkgver=4.3
pkgrel=1
pkgdesc="Show status of Caps Lock, Num Lock, Scroll Lock, and more in system tray"
url="https://www.traystatus.com/"
arch=('any')
license=('custom')
provides=("${pkgbase}")
options=(!strip)
source=("TrayStatus.zip::https://binaryfortressdownloads.com/Download/BFSFiles/114/TrayStatus-${pkgver}.zip")
sha512sums=('9ac7881938f38709084b3c81376216d9b806673c6a69514b424a16b4b6e2c7b6edb3c9c8f2150c7c5ae469b2cd676563f4eeda232cdb07684c2e7acb4ae81b51')

_lc() {
    local d f n

    for f in "${@}"
    do
        d="$(dirname "${f}")"
        f="$(basename "${f}")"
        n="$(echo "${f}" | tr 'A-Z' 'a-z')"
        if test "${f}" != "${n}"
        then
            mv "${d}/${f}" "${d}/${n}"
        fi
    done
}

package() {
    local a="${pkgdir}/apps/${_realname}"
    mkdir -p "${a}"
    cp -R "TrayStatus"/* "${a}"
    rm "${a}"/System.*.dll
    _lc "${a}"/*
}
