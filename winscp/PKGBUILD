# Maintainer: David Macek <david.macek.0@gmail.com>

_realname=winscp
_apparch=32
pkgbase="app-${_realname}"
pkgname=("app-i686-${_realname}")
pkgver=5.11.2
pkgrel=1
pkgdesc="An open source two-pane SFTP/FTP/WebDAV/SCP client"
url="http://winscp.net/"
arch=('any')
license=('custom')
makedepends=("app-i686-innounp"
             "unzip")
provides=("${pkgbase}")
options=(!strip)
source=("winscpsetup.exe::http://download.sourceforge.net/winscp/WinSCP-${pkgver}-Setup.exe"
        "winscp.zip::http://download.sourceforge.net/winscp/WinSCP-${pkgver}-Portable.zip"
        "winscpautomation.zip::http://download.sourceforge.net/winscp/WinSCP-${pkgver}-Automation.zip")
sha512sums=('483b2c8ba8f1291bef6421a8572f243f9396bbbbb738cfb52e7ba159b5366f2c442e66893c52b402ee2931110a6c9527706fce43c70502361673186197b0902f'
            '8b356fc71ed2f7dbf3ed3c69d4d737f1d7e1f979a728ec60d7dba67ac0ac78b59004f9b102a7adca651038fa31965f6eab4239978dc59a8cdb712f750f9cac52'
            'fdf3f5bcf82313d4edc26190a3c7c5e0060e4e71cd3f9cf501d7519bf9798781b4ede580c3266f4918326d177b86292269935ecf2b8b54fd943ac995eaf6ccb2')

_lc() {
    local d f n

    for f in "${@}"
    do
        d="$(dirname "${f}")"
        f="$(basename "${f}")"
        n="$(echo "${f}" | tr 'A-Z' 'a-z')"
        if test "${f}" != "${n}"
        then
            mv "${d}/${f}" "${d}/${n}"
        fi
    done
}

prepare() {
    /apps32/innounp/innounp -x -q -m -a -y "winscpsetup.exe"
    _lc '{app}'/* *
}

package() {
    a="${pkgdir}/apps${_apparch}/${_realname}"
    mkdir -p "${a}"
    cp -r "winscpnet.dll" '{app}'/dragext*.dll '{app}'/winscp.* '{app}'/"extensions" '{app}'/"translations"  "${a}"
    rm "${a}/winscp.map"

    d="${pkgdir}/docs${_apparch}/${_realname}"
    mkdir -p "${d}"
    cp *.txt "${d}"
}
