# Maintainer: David Macek <david.macek.0@gmail.com>

_realname=opera
_choconame=Opera
pkgbase="app-${_realname}"
pkgname=("app-x86_64-${_realname}")
epoch=1
pkgver=126.0.5750.18
pkgrel=1
pkgdesc="A fast and free Blink-based web browser"
url="https://www.opera.com/computer"
arch=('any')
license=('custom')
replaces=("app-i686-${_realname}")
makedepends=("p7zip")
provides=("${pkgbase}")
options=(!strip)
source=("Opera_Setup_x64.exe::https://ftp.opera.com/pub/opera/desktop/${pkgver}/win/Opera_${pkgver}_Setup_x64.exe")
sha512sums=('ed242d4707ec6d0e87dccbed1dffc000e4929125d0164d6d51f47343d830c9049a99d32096b91a18f56beb129c679d54b95a43be169c3c0fa7eca487da265503')
noextract=("Opera_Setup_x64.exe")

_lc() {
    local d f n

    for f in "${@}"
    do
        d="$(dirname "${f}")"
        f="$(basename "${f}")"
        n="$(echo "${f}" | tr 'A-Z' 'a-z')"
        if test "${f}" != "${n}"
        then
            mv "${d}/${f}" "${d}/${n}"
        fi
    done
}

_rmmsdll() {
    local ms

    ms=("msvbvm[0-9]+"
        "ucrtbase"
        "mfcm?[0-9]+[a-z]+"
        "msvc[rp][0-9]+(_([0-9]|codecvt_ids|atomic_wait))?"
        "vc([ao]mp|corlib|runtime)[0-9]+(_([0-9]|threads))?"
        "concrt[0-9]+"
        "pgort[0-9]+"
        "api-ms-win-[0-9a-z-]+")
    find "${1}" -iname '*.dll' | grep -Eif <(for m in "${ms[@]}"; do echo "/${m}[.]dll$"; done) | xargs -r rm
}

prepare() {
    7z x -y -bd "Opera_Setup_x64.exe" -o"Opera_Setup_x64.dir" > /dev/null
}

package() {
    local a="${pkgdir}/apps/${_realname}"
    mkdir -p "${a}"
    cp -R "Opera_Setup_x64.dir"/* "${a}"
    rm -R "${a}"/{assets,root_files_list,files_list,resources.pri} "${a}"/{installer.exe,installer_helper_64.exe,opera_autoupdate.exe,opera_autoupdate.licenses,opera_autoupdate.version}
    _rmmsdll "${a}"
    _lc "${a}"/*
}
