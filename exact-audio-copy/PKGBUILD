# Maintainer: David Macek <david.macek.0@gmail.com>

_realname=exact-audio-copy
_apparch=32
pkgbase="app-${_realname}"
pkgname=("app-i686-${_realname}")
pkgver=1.1
pkgrel=2
pkgdesc="A very accurate CD audio grabber"
url="http://www.exactaudiocopy.de/en/"
arch=('any')
license=('custom')
makedepends=("p7zip")
provides=("${pkgbase}")
options=(!strip)
source=("eac.exe::http://www.exactaudiocopy.de/eac-1.1.exe")
sha512sums=('401c4eb70428dca9b65e0fdcee43afff441296be9e37a9f80fa6b95b0678339b9d98358298add4583009b471b9c8ed3bfaa4c948de4e0fb34b1d0382a0f37781')

_lc() {
    for f in "${@}"
    do
        n="$(echo "${f}" | tr 'A-Z' 'a-z')"
        if test "${f}" != "${n}"
        then
            mv "${f}" "${n}"
        fi
    done
}

prepare() {
    7z x -y -bd "eac.exe" -o"eac"
    _lc "eac"/*
}

package() {
    a="${pkgdir}/apps${_apparch}/${_realname}"
    mkdir -p "${a}"
    cp -r "eac"/* "${a}"
    rm -r "${a}"/*.nsi "${a}"/'$pluginsdir' "${a}/microsoft.vc80.crt" "${a}/uninst.exe"

    d="${pkgdir}/docs${_apparch}/${_realname}"
    mkdir -p "${d}"
    mv "${a}/cdrdao/GPL license.txt" "${d}/cdrdao-license.txt"
    mv "${a}"/'[LICENSE].rtf' "${d}/license.rtf"
    mv "${a}"/*.rtf "${a}/documentation"/* "${d}"
    rm -r "${a}/documentation"
}
