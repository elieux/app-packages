# Maintainer: David Macek <david.macek.0@gmail.com>

_realname=google-drive
_apparch=32
pkgbase="app-${_realname}"
pkgname=("app-i686-${_realname}")
pkgver=2.34.5075.1619
pkgrel=1
pkgdesc="The Google Drive synchronization tool"
url="http://www.google.com/drive/"
arch=('any')
license=('custom')
provides=("${pkgbase}")
options=(!strip)
source=("gsync.msi::http://dl.google.com/drive/${pkgver}/gsync.msi")
sha512sums=('957bab787d9f0299e8366bfa0a2bf5c3bf38de736a17f748cf51cefc8e06d8308eb3ca1dc4829877657bc9c168f6f1b8ac83ba434fbf8c023a66218a75c02713')

_lc() {
    local d f n

    for f in "${@}"
    do
        d="$(dirname "${f}")"
        f="$(basename "${f}")"
        n="$(echo "${f}" | tr 'A-Z' 'a-z')"
        if test "${f}" != "${n}"
        then
            mv "${d}/${f}" "${d}/${n}"
        fi
    done
}

build() {
    msiexec -a "gsync.msi" "TARGETDIR=$(cygpath -wa "msi")" -qn
    _lc "msi/Google/Drive"/*
}

package() {
    s="msi/Google/Drive"
    a="${pkgdir}/apps${_apparch}/${_realname}"
    mkdir -p "${a}"
    cp -r "${s}"/*.{dll,exe} "${s}/languages" "${a}"
}
