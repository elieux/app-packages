# Maintainer: David Macek <david.macek.0@gmail.com>

_realname=nexus-mod-manager
_choconame=nmm
pkgbase="app-${_realname}"
pkgname=("app-i686-${_realname}")
epoch=1
pkgver=0.80.13
pkgrel=1
pkgdesc="Game modifications manager originally from Nexus Mods"
url="https://www.nexusmods.com/site/mods/4"
arch=('any')
license=('GPL2')
makedepends=("app-innounp")
provides=("${pkgbase}")
options=(!strip)
source=("NMM.exe::https://github.com/Nexus-Mods/Nexus-Mod-Manager/releases/download/${pkgver}/NMM-${pkgver}.exe")
sha512sums=('16e9388ecee7f726b72527ab05a8931b52942bd59159c2b56325e02cca3d4cd62bdefc69b50773ccbbf137cb1a3b3d541641c7babb1df84cd83ab122366dec04')

_lc() {
    local d f n

    for f in "${@}"
    do
        d="$(dirname "${f}")"
        f="$(basename "${f}")"
        n="$(echo "${f}" | tr 'A-Z' 'a-z')"
        if test "${f}" != "${n}"
        then
            mv "${d}/${f}" "${d}/${n}"
        fi
    done
}

prepare() {
    /apps/innounp/innounp -x -q -m -a -y "NMM.exe" -d"NMM.dir"
}

package() {
    local a="${pkgdir}/apps/${_realname}"
    mkdir -p "${a}"
    cp -R "NMM.dir/{app}"/* "${a}"
    _lc "${a}"/*
}
