# Maintainer: David Macek <david.macek.0@gmail.com>

_realname=wireshark
pkgbase="app-${_realname}"
pkgname=("app-x86_64-${_realname}")
epoch=1
pkgver=2.4.5
pkgrel=1
pkgdesc="A free network protocol analyzer"
url="https://www.wireshark.org/"
arch=('any')
license=('GPL2')
replaces=("app-i686-${_realname}")
makedepends=("p7zip")
provides=("${pkgbase}")
options=(!strip)
source=("Wireshark-win64.exe::https://1.eu.dl.wireshark.org/win64/Wireshark-win64-${pkgver}.exe")
sha512sums=('adacac1ed40f5762378f963b06e5e43b792e0859610a1a5cba3b39b7bc1f28e6938299be76f397dee68bda1130bc6b099ed8ffc133062e1e72daba3c16081e66')

_lc() {
    local d f n

    for f in "${@}"
    do
        d="$(dirname "${f}")"
        f="$(basename "${f}")"
        n="$(echo "${f}" | tr 'A-Z' 'a-z')"
        if test "${f}" != "${n}"
        then
            mv "${d}/${f}" "${d}/${n}"
        fi
    done
}

prepare() {
    7z x -y -bd "Wireshark-win64.exe" -o"Wireshark-win"
    _lc "Wireshark-win"/*
}

package() {
    a="${pkgdir}/apps/${_realname}"
    mkdir -p "${a}"
    cp -r "Wireshark-win"/* "${a}"
    rm -r "${a}"/winpcap_*.exe "${a}"/usbpcap*.exe "${a}/vcredist_x64.exe" "${a}/uninstall.exe" "${a}"/'$pluginsdir'
}
