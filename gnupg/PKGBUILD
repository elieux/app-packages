# Maintainer: David Macek <david.macek.0@gmail.com>

_realname=gnupg
_realver=20251230
_choconame=gnupg
pkgbase="app-${_realname}"
pkgname=("app-i686-${_realname}")
epoch=1
pkgver=2.5.16
pkgrel=1
pkgdesc="Complete and free implementation of the OpenPGP standard"
url="https://www.gnupg.org/"
arch=('any')
license=('custom')
makedepends=("p7zip")
provides=("${pkgbase}")
options=(!strip)
source=("gnupg-w32.exe::https://www.gnupg.org/ftp/gcrypt/binary/gnupg-w32-${pkgver}_${_realver}.exe")
sha512sums=('aff7945c0f5366c62b0bac34812729014e0221879060204679874df3e1717247992ec035e415d680a3184ce160042e0bc606c13a9559fd404920f0e40c278dc4')
noextract=("gnupg-w32.exe")

prepare() {
    7z x -y -bd "gnupg-w32.exe" -o"gnupg-w32.dir" > /dev/null
}

package() {
    local a="${pkgdir}/apps/${_realname}"
    mkdir -p "${a}"
    cp -R "gnupg-w32.dir"/* "${a}"
    rm -R "${a}"/'$PLUGINSDIR'
    find "${a}" -iname '*.tmp' -delete
}
