# Maintainer: David Macek <david.macek.0@gmail.com>

_realname=f-lux
_apparch=32
pkgbase="app-${_realname}"
pkgname=("app-i686-${_realname}")
pkgver=4.66.0.0
pkgrel=1
pkgdesc="Changes the display color temperature according to the time of the day"
url="http://justgetflux.com/"
arch=('any')
license=('custom')
makedepends=("p7zip")
provides=("${pkgbase}")
options=(!strip)
source=("flux-setup.exe::https://justgetflux.com/flux-setup.exe")
sha512sums=('b49e1e56740ec6e9070a11b50cba09ef6b2172a4eef6d568e8ca9ae891ccd523c51cb4343a7ba80b1cb334bd08b3c344db0ba48ae28f32b4ec4a6efed2324e38')

pkgver() {
    7z x -y -bd "flux-setup.exe" "flux.exe" > /dev/null
    b="$(cygpath -wa "flux.exe" | sed 's/\\/\\\\/g')"
    wmic datafile where name=\""${b}"\" get version | grep 'Version' -A 1 | tail -1 | tr -d ' '
}

prepare() {
    7z x -y -bd "flux-setup.exe"
}

package() {
    a="${pkgdir}/apps${_apparch}/${_realname}"
    mkdir -p "${a}"
    cp -r "flux.exe" "runtime" "${a}"
}
