# Maintainer: David Macek <david.macek.0@gmail.com>

_realname=unbound
_apparch=32
pkgbase="app-${_realname}"
pkgname=("app-i686-${_realname}")
pkgver=1.5.8
pkgrel=1
pkgdesc="Validating, recursive, and caching DNS resolver"
arch=('any')
url="http://unbound.net/"
license=('custom:BSD')
makedepends=("unzip")
provides=("${pkgbase}")
options=(!strip)
source=("unbound.zip::https://www.unbound.net/downloads/unbound-${pkgver}.zip")
sha512sums=('f778b0e63894fce9bfe0250e210535513a6d546e06a2957f2b7a5bd8ac7f8a4626a3bd2e751a118d01bab30b219199a359cca49b39d8b9cdd0d29582060d9d4f')

_lc() {
    for f in "${@}"
    do
        n="$(echo "${f}" | tr 'A-Z' 'a-z')"
        if test "${f}" != "${n}"
        then
            mv "${f}" "${n}"
        fi
    done
}

prepare() {
    _lc *
}

package() {
    a="${pkgdir}/apps${_apparch}/${_realname}"
    mkdir -p "${a}"
    cp *.{exe,cmd} "${a}"

    d="${pkgdir}/docs${_apparch}/${_realname}"
    mkdir -p "${d}"
    cp *.{txt,conf,url} "license" "${d}"
}
