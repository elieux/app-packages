# Maintainer: David Macek <david.macek.0@gmail.com>

_chocover() {
    # 21.1.9 -> 2021.01.09
    # 21.1.19 -> 2021.01.19
    # 21.11.9 -> 2021.11.09
    # 21.11.11 -> 2021.11.11
    echo "${1}" | sed -r -e 's/^([0-9]{2})[.]([0-9]{1,2})[.]([0-9]{1,2})$/20\1.\2.\3/' -e 's/[.]([0-9])[.]/.0\1./' -e 's/[.]([0-9])$/.0\1/'
}

_realname=imdisk-toolkit
_choconame=ImDisk-Toolkit
pkgbase="app-${_realname}"
pkgname=("app-x86_64-${_realname}")
epoch=1
pkgver=2025.02.06
pkgrel=1
pkgdesc="Ramdisk for Windows and mounting of image files"
arch=('any')
url="https://sourceforge.net/projects/imdisk-toolkit/"
license=('unknown')
makedepends=("p7zip")
provides=("${pkgbase}")
options=(!strip)
source=("ImDiskTk-x64.zip::https://downloads.sourceforge.net/imdisk-toolkit/${pkgver//./}/ImDiskTk-x64.zip")
sha512sums=('033b8d5326cfa4369153f4ee1e5fe8e8baa89f772e1cba3cbd01d9cae42b842892912a7ad3aa2d85799dbae8161f957affb82665e3ebbe56de7a72608ae330f8')

_lc() {
    local d f n

    for f in "${@}"
    do
        d="$(dirname "${f}")"
        f="$(basename "${f}")"
        n="$(echo "${f}" | tr 'A-Z' 'a-z')"
        if test "${f}" != "${n}"
        then
            mv "${d}/${f}" "${d}/${n}"
        fi
    done
}

prepare() {
    7z x -y -bd "ImDiskTk${pkgver//./}/files.cab" -o"files" > /dev/null
}

package() {
    depends=("app-ltrdata")

    local a="${pkgdir}/apps/${_realname}"
    mkdir -p "${a}"
    cp -R "files"/* "${a}"
    rm -R "${a}/driver"
    _lc "${a}"/*
}
