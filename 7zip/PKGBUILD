# Maintainer: David Macek <david.macek.0@gmail.com>

_chocover() {
    # 22.1.0 -> 22.01
    echo "${1}" | sed -r 's/^([0-9]+)[.]([0-9])[.]0$/\1.0\2/'
}

_realname=7zip
_choconame=7zip
pkgbase="app-${_realname}"
pkgname=("app-x86_64-${_realname}")
epoch=1
pkgver=26.00
pkgrel=1
pkgdesc="A free file archiver for extremely high compression"
url="https://www.7-zip.org/"
arch=('any')
license=('LGPL2' 'custom')
replaces=("app-i686-${_realname}")
makedepends=("p7zip")
provides=("${pkgbase}")
options=(!strip)
source=("7z-x64.exe::https://7-zip.org/a/7z${pkgver//./}-x64.exe")
sha512sums=('d50e76ca3299fa131156d758ba54079ee347fea30708d0dbcba40d72f307f4d16446ea13041549cb78aabd032fb5dd048d3bbf192821ad9ce89eea5d96581001')
noextract=("7z-x64.exe")

_lc() {
    local d f n

    for f in "${@}"
    do
        d="$(dirname "${f}")"
        f="$(basename "${f}")"
        n="$(echo "${f}" | tr 'A-Z' 'a-z')"
        if test "${f}" != "${n}"
        then
            mv "${d}/${f}" "${d}/${n}"
        fi
    done
}

prepare() {
    7z x -y -bd "7z-x64.exe" -o"7z-x64.dir" > /dev/null
}

package() {
    local a="${pkgdir}/apps/${_realname}"
    mkdir -p "${a}"
    cp -R "7z-x64.dir"/* "${a}"
    rm "${a}/Uninstall.exe"
    _lc "${a}"/*
}
