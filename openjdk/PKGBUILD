# Maintainer: David Macek <david.macek.0@gmail.com>

_realname=openjdk
_realver=c9ecb94cd31b495da20a27d4581645e8/9
_choconame=openjdk
pkgbase="app-${_realname}"
pkgname=("app-x86_64-${_realname}")
epoch=1
pkgver=22.0.2
pkgrel=1
pkgdesc="Open Java Development Kit"
url="https://openjdk.org/"
arch=('any')
license=('custom')
replaces=("app-i686-jdk" "app-x86_64-jdk")
provides=("${pkgbase}")
options=(!strip)
source=("openjdk_windows-x64_bin.zip::https://download.java.net/java/GA/jdk${pkgver}/${_realver}/GPL/openjdk-${pkgver}_windows-x64_bin.zip")
sha512sums=('8a8e8b2755e5543c40ce90e6b5ad616901666bf2ce2de9ff36276914743337892c6fd646fa6f622b8140b8686704b84b5cf291682ffbae1632391264e6d8587d')

_rmmsdll() {
    local ms

    ms=("msvbvm[0-9]+"
        "ucrtbase"
        "mfcm?[0-9]+[a-z]+"
        "msvc[rp][0-9]+(_([0-9]|codecvt_ids|atomic_wait))?"
        "vc([ao]mp|corlib|runtime)[0-9]+(_([0-9]|threads))?"
        "concrt[0-9]+"
        "pgort[0-9]+"
        "api-ms-win-[0-9a-z-]+")
    find "${1}" -iname '*.dll' | grep -Eif <(for m in "${ms[@]}"; do echo "/${m}[.]dll$"; done) | xargs -r rm
}

package() {
    local a="${pkgdir}/apps/${_realname}"
    mkdir -p "${a}"
    cp -R "jdk-${pkgver}"/* "${a}"
    _rmmsdll "${a}"
}
