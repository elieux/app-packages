# Maintainer: David Macek <david.macek.0@gmail.com>

# adding quotes around the cookie value makes the download fail
DLAGENTS=('http::/usr/bin/curl -qb oraclelicense=accept-securebackup-cookie -fLC - -o %o %u')

_apparch=64
_realname=jdk
_realver=9.0.1+11
pkgbase="app-${_realname}"
pkgname=("app-x86_64-${_realname}")
pkgver=${_realver%+*}
pkgrel=1
pkgdesc="Oracle Java Development Kit"
url="http://www.oracle.com/technetwork/java/javase/downloads/index.html"
arch=('any')
license=('custom')
makedepends=("p7zip" "unzip" "curl")
provides=("${pkgbase}")
options=(!strip)
source=("jdk-windows64.exe::http://download.oracle.com/otn-pub/java/jdk/${_realver}/jdk-${_realver%+*}_windows-x64_bin.exe")
sha512sums=('4b44b0ad2312309e5bac65feb31f02ef974664b05b203afaa907816b6cfb6d03f5e58c30b09ab2e6e3887df85bd5946fe5299d573c5fb20cd957d944ea694de7')

_lc() {
    local d f n

    for f in "${@}"
    do
        d="$(dirname "${f}")"
        f="$(basename "${f}")"
        n="$(echo "${f}" | tr 'A-Z' 'a-z')"
        if test "${f}" != "${n}"
        then
            mv "${d}/${f}" "${d}/${n}"
        fi
    done
}

_rmmsdll() {
    local ms

    ms=("msvbvm[0-9]+"
        "ucrtbase"
        "mfcm?[0-9]+[a-z]+"
        "msvc[rp][0-9]+"
        "vc([ao]mp|corlib|runtime)[0-9]+"
        "concrt[0-9]+"
        "pgort[0-9]+"
        "api-ms-win-[0-9a-z-]+")
    find "${1}" -iname '*.dll' | grep -Eif <(for m in "${ms[@]}"; do echo "/${m}[.]dll$"; done) | xargs -r rm
}

prepare() {
    7z x -y -bd "jdk-windows${_apparch}.exe"
    unzip -o "tools.zip" -d "tools${_apparch}"
    attrib -r //s
    _lc "tools${_apparch}"/*
    for f in $(find "tools${_apparch}" -iname '*.pack' -type f)
    do
        "tools${_apparch}/bin/unpack200" "${f}" "${f%.pack}.jar"
        rm "${f}"
    done
}

package() {
    s="tools${_apparch}"

    a="${pkgdir}/apps${_apparch}/${_realname}"
    mkdir -p "${a}"
    cp -r "${s}"/* "${a}"
    _rmmsdll "${a}"

    d="${pkgdir}/docs${_apparch}/${_realname}"
    mkdir -p "${d}"
    mv "${a}"/{release,readme.html} "${a}/legal" "${d}"
}
