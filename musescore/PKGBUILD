# Maintainer: David Macek <david.macek.0@gmail.com>

_realname=musescore
_choconame=musescore
pkgbase="app-${_realname}"
pkgname=("app-x86_64-${_realname}")
epoch=1
pkgver=4.6.0.252730940
pkgrel=1
pkgdesc="Create, play and print beautiful sheet music"
url="https://musescore.org/"
arch=('any')
license=('GPL')
replaces=("app-i686-${_realname}")
makedepends=("app-lessmsi")
provides=("${pkgbase}")
options=(!strip)
source=("MuseScore-Studio-x86_64.msi::https://github.com/musescore/MuseScore/releases/download/v${pkgver%.*}/MuseScore-Studio-${pkgver}-x86_64.msi")
sha512sums=('1d2abc36a8af494a56a3c53e79f2a79c1476dd633a0f8a62936312dbaa28ed11eff910539ac2472a0274406fda77994f64bc3c45ffd594432601cc6463273316')

_rmmsdll() {
    local ms

    ms=("msvbvm[0-9]+"
        "ucrtbase"
        "mfcm?[0-9]+[a-z]+"
        "msvc[rp][0-9]+(_([0-9]|codecvt_ids|atomic_wait))?"
        "vc([ao]mp|corlib|runtime)[0-9]+(_([0-9]|threads))?"
        "concrt[0-9]+"
        "pgort[0-9]+"
        "api-ms-win-[0-9a-z-]+")
    find "${1}" -iname '*.dll' | grep -Eif <(for m in "${ms[@]}"; do echo "/${m}[.]dll$"; done) | xargs -r rm
}

prepare() {
    rm -fR "MuseScore-Studio-x86_64"
    /apps/lessmsi/lessmsi x "MuseScore-Studio-x86_64.msi" > /dev/null
}

package() {
    local a="${pkgdir}/apps/${_realname}"
    mkdir -p "${a}"
    cp -R "MuseScore-Studio-x86_64/SourceDir/MuseScore ${pkgver%%.*}"/* "${a}"
    _rmmsdll "${a}"
}
