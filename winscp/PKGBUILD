# Maintainer: David Macek <david.macek.0@gmail.com>

_realname=winscp
_apparch=32
pkgbase="app-${_realname}"
pkgname=("app-i686-${_realname}")
pkgver=5.7.7
pkgrel=1
pkgdesc="An open source two-pane SFTP/FTP/WebDAV/SCP client"
url="http://winscp.net/"
arch=('any')
license=('custom')
makedepends=("app-i686-innounp"
             "unzip")
provides=("${pkgbase}")
options=(!strip)
source=("winscpsetup.exe::http://download.sourceforge.net/winscp/WinSCP/${pkgver}/winscp${pkgver//./}setup.exe"
        "winscp.zip::http://download.sourceforge.net/winscp/WinSCP/${pkgver}/winscp${pkgver//./}.zip"
        "winscpautomation.zip::http://download.sourceforge.net/winscp/WinSCP/${pkgver}/winscp${pkgver//./}automation.zip")
sha512sums=('e09495fa4f0335ddfc96ea059dd657da9f9599174d74a2e7f08ab62b0d44a1daeb33da369b9885f81696e9ca930b834d957d5668332b84dd86094a32c69d7e8f'
            '5be6f29d0afaa2655940b840687c0b40fa89d886bb8c3668a3c2162cbb79894ec1eb7c21341aef7cbae309a35eebac0de97764324ec24a2699272474eef0cfb3'
            'fe6eb0e658940a4b1483b167300f29d82e32ee4e49d0775954ce8afc5812296579b27d9715a6454123e29ba9758a4db923cf27ae562266ad54e60c1ea552ecd1')

_lc() {
    for f in "${@}"
    do
        n="$(echo "${f}" | tr 'A-Z' 'a-z')"
        if test "${f}" != "${n}"
        then
            mv "${f}" "${n}"
        fi
    done
}

prepare() {
    /apps32/innounp/innounp -x -q -m -a -y "winscpsetup.exe"
    _lc '{app}'/* *
}

package() {
    a="${pkgdir}/apps${_apparch}/${_realname}"
    mkdir -p "${a}"
    cp "winscpnet.dll" '{app}'/dragext*.dll '{app}'/winscp.* "${a}"
    rm "${a}/winscp.map"

    d="${pkgdir}/docs${_apparch}/${_realname}"
    mkdir -p "${d}"
    cp *.txt "${d}"
}
