# Maintainer: David Macek <david.macek.0@gmail.com>

_realname=calibre
pkgbase="app-${_realname}"
pkgname=("app-i686-${_realname}"
         "app-x86_64-${_realname}")
pkgver=2.53
pkgrel=1
pkgdesc="Ebook management application"
url="http://calibre-ebook.com/"
arch=('any')
license=('GPL3')
provides=("${pkgbase}")
options=(!strip)
source=("calibre32.msi::http://download.calibre-ebook.com/${pkgver}.0/${_realname}-${pkgver}.0.msi"
        "calibre64.msi::http://download.calibre-ebook.com/${pkgver}.0/${_realname}-64bit-${pkgver}.0.msi")
sha512sums=('3ca155cc5c1c59374b8d6c660715130beb637e22015602f72dc957c7853b3a894f38ebd3802eae314d643a3c8f33188b4912173ea2e5ffe7031a1e58886990c0'
            '31e600e2ac0cb8c871fb8ca4172888622efee51c5a9ee11eaa47194d8aab732fb5d3de0ae6ed16856b52481116d2819d38b0602203420ea827b918020a9508bb')

_lc() {
    for f in "${@}"
    do
        n="$(echo "${f}" | tr 'A-Z' 'a-z')"
        if test "${f}" != "${n}"
        then
            mv "${f}" "${n}"
        fi
    done
}

_build() {
    msiexec -a "calibre${_apparch}.msi" "TARGETDIR=$(cygpath -wa "install${_apparch}")" -qn
    attrib -r //s
    _lc "install${_apparch}/PFiles"/*
    _lc "install${_apparch}/PFiles"/calibre*/*
}

build() {
    for _apparch in 32 64
    do
        _build
    done
}

_package() {
    a="${pkgdir}/apps${_apparch}/${_realname}"
    mkdir -p "${a}"
    cp -r "install${_apparch}/PFiles"/calibre*/* "${a}"
    rm -rf "${a}/microsoft.vc90.crt" # write-protected

    d="${pkgdir}/docs${_apparch}/${_realname}"
    mkdir -p "${d}"
    mv "${a}/license" "${d}"
}

package_app-i686-calibre() {
    _apparch=32
    _package
}

package_app-x86_64-calibre() {
    _apparch=64
    _package
}
