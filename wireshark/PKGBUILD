# Maintainer: David Macek <david.macek.0@gmail.com>

_realname=wireshark
_choconame=wireshark
pkgbase="app-${_realname}"
pkgname=("app-x86_64-${_realname}")
epoch=1
pkgver=3.4.6
pkgrel=1
pkgdesc="A free network protocol analyzer"
url="https://www.wireshark.org/"
arch=('any')
license=('GPL2')
replaces=("app-i686-${_realname}")
makedepends=("p7zip")
provides=("${pkgbase}")
options=(!strip)
source=("Wireshark-win64.exe::https://1.eu.dl.wireshark.org/win64/Wireshark-win64-${pkgver}.exe")
sha512sums=('8302aaf17542c06f91013d73edafb3a5a61125164390d7b58a5ae2e82d24fddef8b99fb64b1b84a7af979b2e5d5b16db52eaf02e77648e877709d7c207c24803')

_lc() {
    local d f n

    for f in "${@}"
    do
        d="$(dirname "${f}")"
        f="$(basename "${f}")"
        n="$(echo "${f}" | tr 'A-Z' 'a-z')"
        if test "${f}" != "${n}"
        then
            mv "${d}/${f}" "${d}/${n}"
        fi
    done
}

prepare() {
    7z x -y -bd "Wireshark-win64.exe" -o"Wireshark-win64.dir" > /dev/null
}

package() {
    local a="${pkgdir}/apps/${_realname}"
    mkdir -p "${a}"
    cp -R "Wireshark-win64.dir"/* "${a}"
    rm -R "${a}"/npcap-*.exe "${a}"/USBPcapSetup-*.exe "${a}/vcredist_x64.exe" "${a}/uninstall.exe" "${a}"/'$PLUGINSDIR'
    _lc "${a}"/*
}
