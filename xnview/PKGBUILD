# Maintainer: David Macek <david.macek.0@gmail.com>

_realname=xnview
pkgbase="app-${_realname}"
pkgname=("app-i686-${_realname}")
epoch=1
pkgver=2.44
pkgrel=1
pkgdesc="An efficient multimedia viewer, browser and converter"
url="https://www.xnview.com/en/xnview/"
arch=('any')
license=('custom')
depends=("app-nconvert")
makedepends=("unzip")
provides=("${pkgbase}")
options=(!strip)
source=("XnView-win-full.zip::http://download3.xnview.com/XnView-win-full.zip")
sha512sums=('89297d4a174eabfd01d10ccbe94b098c4890cf3c37bd1fc4e037463b4dcad1c1583a7e63cc6fd274c77f04d7f2c9c8f6c85085466cfb76104da0b1842ab85768')

_lc() {
    local d f n

    for f in "${@}"
    do
        d="$(dirname "${f}")"
        f="$(basename "${f}")"
        n="$(echo "${f}" | tr 'A-Z' 'a-z')"
        if test "${f}" != "${n}"
        then
            mv "${d}/${f}" "${d}/${n}"
        fi
    done
}

pkgver() {
    "${SYSTEMROOT}/System32/WindowsPowerShell/v1.0/powershell" -command '(Get-Item "XnView/xnview.exe").VersionInfo.ProductVersion'
}

prepare() {
    _lc "XnView"/*
}

package() {
    a="${pkgdir}/apps/${_realname}"
    mkdir -p "${a}"
    cp -R "XnView"/* "${a}"
    rm -R "${a}/nconvert.exe" "${a}/addon/Microsoft.VC90.CRT"
    rm "${a}"/{formats,license,plugins,readme,usage,whatsnew}.txt
}
