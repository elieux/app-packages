# Maintainer: David Macek <david.macek.0@gmail.com>

_realname=openjdk
_realver=b1e0dfa218384cb9959bdcb897162d4e/10
_choconame=openjdk
pkgbase="app-${_realname}"
pkgname=("app-x86_64-${_realname}")
epoch=1
pkgver=25.0.2
pkgrel=1
pkgdesc="Open Java Development Kit"
url="https://openjdk.org/"
arch=('any')
license=('custom')
replaces=("app-i686-jdk" "app-x86_64-jdk")
provides=("${pkgbase}")
options=(!strip)
source=("openjdk_windows-x64_bin.zip::https://download.java.net/java/GA/jdk${pkgver%.0.0}/${_realver}/GPL/openjdk-${pkgver%.0.0}_windows-x64_bin.zip")
sha512sums=('5e506597f01d10a2c7375cf5b1597fef89fd4914edddcbb1d99f1f2c82c400bb912656050591a105c1d57b0892fa734eb38079b9ea44b04514411faa9db0668b')

_rmmsdll() {
    local ms

    ms=("msvbvm[0-9]+"
        "ucrtbase"
        "mfcm?[0-9]+[a-z]+"
        "msvc[rp][0-9]+(_([0-9]|codecvt_ids|atomic_wait))?"
        "vc([ao]mp|corlib|runtime)[0-9]+(_([0-9]|threads))?"
        "concrt[0-9]+"
        "pgort[0-9]+"
        "api-ms-win-[0-9a-z-]+")
    find "${1}" -iname '*.dll' | grep -Eif <(for m in "${ms[@]}"; do echo "/${m}[.]dll$"; done) | xargs -r rm
}

package() {
    local a="${pkgdir}/apps/${_realname}"
    mkdir -p "${a}"
    cp -R "jdk-${pkgver%.0.0}"/* "${a}"
    _rmmsdll "${a}"
}
