# Maintainer: David Macek <david.macek.0@gmail.com>

_realname=mc
_realver=222
_choconame=mc
pkgbase="app-${_realname}"
pkgname=("app-i686-${_realname}")
epoch=1
pkgver=4.8.24.222
pkgrel=1
pkgdesc="GNU terminal two-pane file manager"
url="https://sourceforge.net/projects/mcwin32/"
arch=('any')
license=('GPL3')
provides=("${pkgbase}")
makedepends=("app-innounp")
options=(!strip)
source=("mcwin32-build-setup.exe::https://downloads.sourceforge.net/mcwin32/mcwin32-build${_realver}-setup.exe")
sha512sums=('8156f81d06e8ebed233ad00589c96a129107cff8752472b67ec8c986cb13e162a08618ed3003d4fa9c31b757a71d1bce62b46fd650f5f9800e5afe22bbdd8c4e')

prepare() {
    /apps/innounp/innounp -x -q -m -a -y "mcwin32-build-setup.exe" -d"mcwin32-build-setup.dir"
}

pkgver() {
    powershell -command '(Get-Item "mcwin32-build-setup.exe").VersionInfo.ProductVersion.ToString()' | sed -r 's/^([0-9]+[.][0-9]+[.][0-9]+) [(]build: [0-9]{8}-([0-9]+)[)] *$/\1.\2/'
}

package() {
    local a="${pkgdir}/apps/${_realname}"
    mkdir -p "${a}"
    cp -R "mcwin32-build-setup.dir/{app}"/* "${a}"
    rm "${a}"/*.map
}
