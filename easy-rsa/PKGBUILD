# Maintainer: David Macek <david.macek.0@gmail.com>

_realname=easy-rsa
pkgbase="app-${_realname}"
pkgname=("app-x86_64-${_realname}")
epoch=1
pkgver=3.2.0
pkgrel=1
pkgdesc="Simple shell based CA utility"
url="https://community.openvpn.net/openvpn#Easy-rsasubproject"
arch=('any')
license=('custom')
replaces=("app-i686-${_realname}")
provides=("${pkgbase}")
options=(!strip)
source=("EasyRSA-win64.zip::https://github.com/OpenVPN/easy-rsa/releases/download/v${pkgver}/EasyRSA-${pkgver}-win64.zip")
sha512sums=('b1eeef6484ae7fff3c334323b94c7a5a01b57e372f2c595fdfeeeee8de120e184e1652773be3f229be6c10f081e1e64db51af33596608324ec60e0e410beb261')

_lc() {
    local d f n

    for f in "${@}"
    do
        d="$(dirname "${f}")"
        f="$(basename "${f}")"
        n="$(echo "${f}" | tr 'A-Z' 'a-z')"
        if test "${f}" != "${n}"
        then
            mv "${d}/${f}" "${d}/${n}"
        fi
    done
}

package() {
    local a="${pkgdir}/apps/${_realname}"
    mkdir -p "${a}"
    cp -R "EasyRSA-${pkgver}"/* "${a}"
    _lc "${a}"/*
}
