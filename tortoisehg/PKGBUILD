# Maintainer: David Macek <david.macek.0@gmail.com>

_realname=tortoisehg
_choconame=tortoisehg
pkgbase="app-${_realname}"
pkgname=("app-x86_64-${_realname}")
epoch=1
pkgver=5.9.2
pkgrel=1
pkgdesc="Mercurial client implemented as a Windows shell extension"
url="https://www.mercurial-scm.org/wiki/TortoiseHg"
arch=('any')
license=('GPL2')
makedepends=("app-lessmsi")
provides=("${pkgbase}")
options=(!strip)
source=("tortoisehg-x64.msi::https://www.mercurial-scm.org/release/tortoisehg/windows/tortoisehg-${pkgver}-x64.msi")
sha512sums=('33bb1cf2fd022a8394d8dc7554e794ee34644069576ca636108ac69e0c8f2b256c0fa5120b98a71da6f811b556b3233dc58bfa415110d36f2babd61e58e02a8f')

_lc() {
    local d f n

    for f in "${@}"
    do
        d="$(dirname "${f}")"
        f="$(basename "${f}")"
        n="$(echo "${f}" | tr 'A-Z' 'a-z')"
        if test "${f}" != "${n}"
        then
            mv "${d}/${f}" "${d}/${n}"
        fi
    done
}

prepare() {
    rm -fR "tortoisehg-x64"
    /apps/lessmsi/lessmsi x "tortoisehg-x64.msi" > /dev/null
    _lc "tortoisehg-x64/SourceDir/PFiles/TortoiseHg"/*
    _lc "tortoisehg-x64/SourceDir/Programsx86/TortoiseHg"/*
}

package() {
    depends=("app-tortoise-overlays")

    local a="${pkgdir}/apps/${_realname}"
    mkdir -p "${a}"
    cp -R "tortoisehg-x64/SourceDir/PFiles/TortoiseHg"/* "${a}"
    cp -R "tortoisehg-x64/SourceDir/Programsx86/TortoiseHg"/* "${a}"
}
