# Maintainer: David Macek <david.macek.0@gmail.com>

_dirname=android-sdk
_realname=android-sdk-platform-tools
pkgbase="app-${_realname}"
pkgname=("app-i686-${_realname}")
epoch=1
pkgver=30.0.1
pkgrel=1
pkgdesc="Platform-Tools for Google Android SDK (adb and fastboot)"
url="https://developer.android.com/studio/releases/platform-tools.html"
arch=('any')
license=('custom')
provides=("${pkgbase}")
options=(!strip)
source=("platform-tools-latest-windows.zip::https://dl.google.com/android/repository/platform-tools-latest-windows.zip")
sha512sums=('8260ca7697f97f01524c64c797904af9db205e19fe63abba8c75dd612b6f94a27324a3a97296880a481046ff55cae3f10234e99909d1437485c18f2c494d7765')

_lc() {
    local d f n

    for f in "${@}"
    do
        d="$(dirname "${f}")"
        f="$(basename "${f}")"
        n="$(echo "${f}" | tr 'A-Z' 'a-z')"
        if test "${f}" != "${n}"
        then
            mv "${d}/${f}" "${d}/${n}"
        fi
    done
}

pkgver() {
    cat "platform-tools/source.properties" | grep "^Pkg.Revision" | cut -d= -f2
}

package() {
    local a="${pkgdir}/apps/${_dirname}"
    mkdir -p "${a}"
    cp -R "platform-tools" "${a}"
    _lc "${a}/platform-tools"/*
}
