# Maintainer: David Macek <david.macek.0@gmail.com>

_realname=slack
_apparch=32
pkgbase="app-${_realname}"
pkgname=("app-i686-${_realname}")
pkgver=2.4.1
pkgrel=1
pkgdesc="Client for the Slack team communication platform"
url="http://www.slack.com/"
arch=('any')
license=('custom')
makedepends=("p7zip" "unzip")
provides=("${pkgbase}")
options=(!strip)
source=("SlackSetup.exe::https://slack.com/ssb/download-win")
sha512sums=('aa4a6b957688fac329a565278ce3a2b591cb16eed056eb710ceec6a5e1532f907242faa0607c947cffe99f978eda630a887bfe746bfcf137caa2c28083e1a054')

_lc() {
    local d f n

    for f in "${@}"
    do
        d="$(dirname "${f}")"
        f="$(basename "${f}")"
        n="$(echo "${f}" | tr 'A-Z' 'a-z')"
        if test "${f}" != "${n}"
        then
            mv "${d}/${f}" "${d}/${n}"
        fi
    done
}

pkgver() {
    b="$(cygpath -wa "SlackSetup.exe" | sed 's/\\/\\\\/g')"
    wmic datafile where name=\""${b}"\" get version | grep 'Version' -A 1 | tail -1 | tr -d ' ' | sed -r 's/^([0-9]+[.][0-9]+[.][0-9]+)[.]0+$/\1/'
}

prepare() {
    rm -f "slack"-*-"full.nupkg"
    7z x -y -bd "SlackSetup.exe"
    unzip -o "slack"-*-"full.nupkg"
    _lc "lib/net45"/*
}

package() {
    a="${pkgdir}/apps${_apparch}/${_realname}"
    mkdir -p "${a}"
    cp -r "lib/net45"/* "${a}"
    rm "${a}"/{,resources/app.asar.unpacked/node_modules/edge-atom-shell/build/Release}/{{vccorlib,vcruntime,msvcp,concrt}140,ucrtbase}.dll "${a}"/{,resources/app.asar.unpacked/node_modules/edge-atom-shell/build/Release}/api-ms-win-*.dll

    d="${pkgdir}/docs${_apparch}/${_realname}"
    mkdir -p "${d}"
    mv "${a}/license" "${d}"
}
