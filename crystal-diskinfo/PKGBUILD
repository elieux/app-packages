# Maintainer: David Macek <david.macek.0@gmail.com>

_realname=crystaldiskinfo
_realver=64532
_dirname=crystal
pkgbase="app-${_realname}"
pkgname=("app-i686-${_realname}"
         "app-x86_64-${_realname}")
pkgver=6.7.5
pkgrel=1
pkgdesc="A disk utility (Crystal Dew World)"
url="http://crystalmark.info/software/CrystalDiskInfo/index-e.html"
arch=('any')
license=('MIT')
makedepends=("unzip")
provides=("${pkgbase}")
options=(!strip)
source=("CrystalDiskInfo.zip::http://osdn.jp/frs/redir.php?f=%2Fcrystaldiskinfo%2F${_realver}%2FCrystalDiskInfo${pkgver//./_}.zip")
sha512sums=('a1ff41219f554e407b0b2c4a79a18fd931bb2e5ce8f67f5b416878ce02f369d24266488ddb24e6046ea305c37e318a24bcdbb1ca9b1ff662c34586e675c8f9a1')

_lc() {
    for f in "${@}"
    do
        n="$(echo "${f}" | tr 'A-Z' 'a-z')"
        if test "${f}" != "${n}"
        then
            mv "${f}" "${n}"
        fi
    done
}

prepare() {
    _lc "License"/* *
}

_package() {
    a="${pkgdir}/apps${_apparch}/${_dirname}"
    mkdir -p "${a}"
    cp "diskinfo${1}.exe" "${a}/crystaldiskinfo.exe"
    cp -r "cdiresource" "${a}"

    d="${pkgdir}/docs${_apparch}/${_dirname}"
    mkdir -p "${d}/diskinfo"
    cp -r "license"/* "${d}/diskinfo"
}

package_app-i686-crystaldiskinfo() {
    _apparch=32
    _package ""
}

package_app-x86_64-crystaldiskinfo() {
    _apparch=64
    _package "X64"
}
