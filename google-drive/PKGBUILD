# Maintainer: David Macek <david.macek.0@gmail.com>

_realname=google-drive
_choconame=googledrive
pkgbase="app-${_realname}"
pkgname=("app-x86_64-${_realname}")
epoch=1
pkgver=83.0.2.0
pkgrel=1
pkgdesc="The Google Drive synchronization tool"
url="https://www.google.com/drive/"
arch=('any')
license=('custom')
replaces=("app-i686-${_realname}")
makedepends=("p7zip")
provides=("${pkgbase}")
options=(!strip)
source=("GoogleDriveSetup.exe::https://dl.google.com/drive-file-stream/GoogleDriveSetup.exe")
sha512sums=('2214e9bd9875c0642c06a17da0f5ca519a5a3a26228355a3312da4a84f323a6d121c203a72e0a2ace2c6513748724ea00f0c72c544bb740c2d6b857a8b7245fc')

prepare() {
    7z x -y -bd "GoogleDriveSetup.exe" -o"GoogleDriveSetup.dir" > /dev/null
}

pkgver() {
    powershell -command '(Get-Item "GoogleDriveSetup.dir/GoogleDriveFS.exe").VersionInfo.FileVersionRaw.ToString()'
}

package() {
    local a="${pkgdir}/apps/${_realname}"
    mkdir -p "${a}"
    cp -R "GoogleDriveSetup.dir"/* "${a}"
}
