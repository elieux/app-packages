# Maintainer: David Macek <david.macek.0@gmail.com>

_realname=autoit
_choconame=autoit
pkgbase="app-${_realname}"
pkgname=("app-x86_64-${_realname}")
epoch=1
pkgver=3.3.16.1
pkgrel=1
pkgdesc="A free BASIC-like scripting language for automating Windows GUIs"
url="https://www.autoitscript.com/"
arch=('any')
license=('custom')
replaces=("app-i686-${_realname}")
provides=("${pkgbase}")
options=(!strip)
source=("autoit.zip::https://www.autoitscript.com/files/autoit${pkgver%%.*}/autoit-v${pkgver%%.*}.zip")
sha512sums=('0c1678fbf26cacff13b00ddc1a6f5e8eb77336f79770931212de9de20f46b2fb62ee2b42fd5d9b1ea0a3b53ec6c55b0506ea689c72285eb7e8538df2507ac4b7')

_lc() {
    local d f n

    for f in "${@}"
    do
        d="$(dirname "${f}")"
        f="$(basename "${f}")"
        n="$(echo "${f}" | tr 'A-Z' 'a-z')"
        if test "${f}" != "${n}"
        then
            mv "${d}/${f}" "${d}/${n}"
        fi
    done
}

pkgver() {
    powershell -command '(Get-Item "install/Au3Check.exe").VersionInfo.ProductVersion'
}

package() {
    local a="${pkgdir}/apps/${_realname}"
    mkdir -p "${a}"
    cp -R "install"/* "${a}"
    cp -R "${a}/aut2exe"/* "${a}/autoitx"/* "${a}"
    rm -R "${a}/aut2exe" "${a}/autoitx" "${a}/upx.exe"
    mv "${a}/scite" "${a}/extras/Editors/SciTE"
    rm "${a}/extras/Editors/SciTE/SciTE.exe"
    for f in "${a}"/*_x64*
    do
        mv -f "${f}" "${f/_x64/}"
    done
    _lc "${a}"/*
}
