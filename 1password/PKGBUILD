# Maintainer: David Macek <david.macek.0@gmail.com>

_realname=1password
_choconame=1password
pkgbase="app-${_realname}"
pkgname=("app-x86_64-${_realname}")
epoch=1
pkgver=8.12.1.3
pkgrel=1
pkgdesc="Password manager and secure wallet"
url="https://1password.com/downloads"
arch=('any')
license=('custom')
makedepends=("app-lessmsi")
provides=("${pkgbase}")
options=(!strip)
source=("1PasswordSetup-latest.msi::https://downloads.1password.com/win/1PasswordSetup-latest.msi")
sha512sums=('9e66de4602f40fd3bf51185f7f049c0aeb02d7dc9a37ffc12db4067f88eba0001b54266627e61bac0d688bdc9c6b78e74a10c0aad0d250393c30d4d76b714d24')
noextract=("1PasswordSetup-latest.exe")

_lc() {
    local d f n

    for f in "${@}"
    do
        d="$(dirname "${f}")"
        f="$(basename "${f}")"
        n="$(echo "${f}" | tr 'A-Z' 'a-z')"
        if test "${f}" != "${n}"
        then
            mv "${d}/${f}" "${d}/${n}"
        fi
    done
}

prepare() {
    rm -fR "1PasswordSetup-latest"
    /apps/lessmsi/lessmsi x "1PasswordSetup-latest.msi" > /dev/null
}

pkgver() {
    powershell -command '(Get-Item "1PasswordSetup-latest/SourceDir/ProgramFiles64Folder/1Password/app/8/1Password.exe").VersionInfo.FileVersion'
}

package() {
    local a="${pkgdir}/apps/${_realname}"
    mkdir -p "${a}"
    cp -R "1PasswordSetup-latest/SourceDir/ProgramFiles64Folder/1Password/app/8"/* "${a}"
    _lc "${a}"/*
}
