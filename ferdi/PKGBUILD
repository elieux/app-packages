# Maintainer: David Macek <david.macek.0@gmail.com>

_realname=ferdi
_choconame=ferdi
pkgbase="app-${_realname}"
pkgname=("app-x86_64-${_realname}")
epoch=1
pkgver=5.7.0
pkgrel=1
pkgdesc="Combine your favourite messaging services into one application, community fork of Franz"
url="https://getferdi.com/"
arch=('any')
license=('Apache2')
makedepends=("p7zip")
provides=("${pkgbase}")
options=(!strip)
source=("Ferdi-Setup.exe::https://github.com/getferdi/ferdi/releases/download/v${pkgver}/Ferdi-Setup-${pkgver}.exe")
sha512sums=('c2807f307d036a061b8362325d984acb661f5d1a976bbceb95c9e08613f9239627ea2a59aef3fc70b847c5eec54b138b0b99dd2b199c70168649945ffe09d2e1')

_lc() {
    local d f n

    for f in "${@}"
    do
        d="$(dirname "${f}")"
        f="$(basename "${f}")"
        n="$(echo "${f}" | tr 'A-Z' 'a-z')"
        if test "${f}" != "${n}"
        then
            mv "${d}/${f}" "${d}/${n}"
        fi
    done
}

prepare() {
    7z x -y -bd "Ferdi-Setup.exe" -o"Ferdi-Setup.dir" > /dev/null
    7z x -y -bd "Ferdi-Setup.dir"/'$PLUGINSDIR'/"app-64.7z" -o"app-64" > /dev/null
}

package() {
    local a="${pkgdir}/apps/${_realname}"
    mkdir -p "${a}"
    cp -R "app-64"/* "${a}"
    _lc "${a}"/*
}
