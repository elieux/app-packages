# Maintainer: David Macek <david.macek.0@gmail.com>

_realname=teamviewer
_apparch=32
pkgbase="app-${_realname}"
pkgname=("app-i686-${_realname}")
pkgver=12.1.17833
pkgrel=1
pkgdesc="All-in-one software for remote support and online meetings"
url="http://www.teamviewer.com/"
arch=('any')
license=('custom')
makedepends=("unzip")
provides=("${pkgbase}")
options=(!strip)
source=("TeamViewerPortable.zip::http://download.teamviewer.com/download/TeamViewerPortable.zip")
sha512sums=('e533885e89ac5213d7f924454c80476aca4a6b3a6fc5d69132bc984db612bc7320bcced56ee89ddc8d40030f36de3812d9e5bd093972bcde0f37e66229657661')

_lc() {
    local d f n

    for f in "${@}"
    do
        d="$(dirname "${f}")"
        f="$(basename "${f}")"
        n="$(echo "${f}" | tr 'A-Z' 'a-z')"
        if test "${f}" != "${n}"
        then
            mv "${d}/${f}" "${d}/${n}"
        fi
    done
}

pkgver() {
    "${SYSTEMROOT}/System32/WindowsPowerShell/v1.0/powershell" -Command '(Get-Item "teamviewer.exe").VersionInfo.FileVersion' | sed -r 's/([0-9]+[.][0-9]+[.][0-9]+)[.]0$/\1/'
}

prepare() {
    _lc *
}

package() {
    a="${pkgdir}/apps${_apparch}/${_realname}"
    mkdir -p "${a}"
    cp *.{dll,exe,ini} "${a}"

    d="${pkgdir}/docs${_apparch}/${_realname}"
    mkdir -p "${d}"
    cp *.{txt,inf} "${d}"
}
