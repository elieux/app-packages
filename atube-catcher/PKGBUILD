# Maintainer: David Macek <david.macek.0@gmail.com>

_realname=atube-catcher
_apparch=32
pkgbase="app-${_realname}"
pkgname=("app-i686-${_realname}")
pkgver=3.8.0.9152
pkgrel=1
pkgdesc="Downloads videos from the Internet"
url="http://www.atube.me/"
arch=('any')
license=('custom')
makedepends=("app-i686-innounp")
provides=("${pkgbase}")
options=(!strip)
source=("aTubeCatcher.exe::http://files.dsnetwb.com/aTube_Catcher.exe")
sha512sums=('cf2923a5603339526290d1f26c51908c813e2e5f7faa11b1e0dd93dcffa0799bd1bb6cefc2119ffd2100d6efae4cbd0527f196c6f8553f3483d1c68e786ddab4')

_lc() {
    local d f n

    for f in "${@}"
    do
        d="$(dirname "${f}")"
        f="$(basename "${f}")"
        n="$(echo "${f}" | tr 'A-Z' 'a-z')"
        if test "${f}" != "${n}"
        then
            mv "${d}/${f}" "${d}/${n}"
        fi
    done
}

pkgver() {
    /apps32/innounp/innounp -e -q -m -a -y "aTubeCatcher.exe" "yct.exe" > /dev/null
    b="$(cygpath -wa "yct.exe" | sed 's/\\/\\\\/g')"
    wmic datafile where name=\""${b}"\" get version | grep 'Version' -A 1 | tail -1 | tr -d ' '
}

prepare() {
    /apps32/innounp/innounp -x -q -m -a -y "aTubeCatcher.exe"
    _lc "{app}"/*
}

package() {
    a="${pkgdir}/apps${_apparch}/${_realname}"
    mkdir -p "${a}"
    cp -r "{app}"/* "${a}"

    d="${pkgdir}/docs${_apparch}/${_realname}"
    mkdir -p "${d}"
    mv "${a}"/*.txt "${d}"
}
