# Maintainer: David Macek <david.macek.0@gmail.com>

_realname=chromium
_realver=464641
pkgbase="app-${_realname}"
pkgname=("app-i686-${_realname}"
         "app-x86_64-${_realname}")
epoch=1
pkgver=59.0.3071.104
pkgrel=1
pkgdesc="The open-source project behind Google Chrome, an attempt at creating a safer, faster, and more stable browser"
url="http://www.chromium.org/Home"
arch=('any')
license=('BSD')
makedepends=("unzip")
provides=("${pkgbase}")
options=(!strip)
source=("chrome-win32.zip::https://github.com/henrypp/chromium/releases/download/v${pkgver}-r${_realver}-win32/chromium-sync.zip"
        "chrome-win64.zip::https://github.com/henrypp/chromium/releases/download/v${pkgver}-r${_realver}-win64/chromium-sync.zip")
sha512sums=('0f838ce771c6102f6eac724927e920dd5cd98688bb536e7072f1d41045641aa03580607d1a1aaa6d0ec40b5683b293f6848022f3d839121a961ab658e2957170'
            '806ec9fcf11623289225eb9d2e52c3ec19633d6812730b322a9dade7576abe9e51b5031c3bc72acba918a8f7f77a4ad3954fec2fbcd7b9e2bcf588c7d39d4a01')
noextract=("chrome-win32.zip"
           "chrome-win64.zip")

_eqver() {
    local r v

    for v in "${@}"
    do
        if [ -n "${r}" -a "${r}" != "${v}" ]
        then
            echo "Version mismatch between ${r} and ${v}."
        fi
        r="${v}"
    done
    echo "${r}"
}

_lc() {
    local d f n

    for f in "${@}"
    do
        d="$(dirname "${f}")"
        f="$(basename "${f}")"
        n="$(echo "${f}" | tr 'A-Z' 'a-z')"
        if test "${f}" != "${n}"
        then
            mv "${d}/${f}" "${d}/${n}"
        fi
    done
}

pkgver() {
    _vers=( $(
        for _apparch in 32 64
        do
            unzip -l "chrome-win${_apparch}.zip" | tail -n +4 | head -n -2 | sed -r 's/[ ]+/\t/g' | cut -f 5 | sed 's~^chrome-win32/~~' | sed 's~.manifest$~~' | grep -P '[0-9]+[.][0-9]+[.][0-9]+[.][0-9]+' | sort | tail -1
        done
    ) )
    _eqver "${_vers[@]}"
}

prepare() {
    for _apparch in 32 64
    do
        unzip -o "chrome-win${_apparch}.zip" -d "chromium${_apparch}"
        _lc "chromium${_apparch}/chrome-win32"/*
    done
}

_package() {
    a="${pkgdir}/apps${_apparch}/${_realname}"
    mkdir -p "${a}"
    cp -r "chromium${_apparch}/chrome-win32"/* "${a}"
}

package_app-i686-chromium() {
    _apparch=32
    _package
}

package_app-x86_64-chromium() {
    _apparch=64
    _package
}
