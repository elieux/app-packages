# Maintainer: David Macek <david.macek.0@gmail.com>

_realname=crystaldiskinfo
_realver=65980
_dirname=crystal
pkgbase="app-${_realname}"
pkgname=("app-i686-${_realname}"
         "app-x86_64-${_realname}")
pkgver=7.0.3
pkgrel=1
pkgdesc="A disk utility (Crystal Dew World)"
url="http://crystalmark.info/software/CrystalDiskInfo/index-e.html"
arch=('any')
license=('MIT')
makedepends=("unzip")
provides=("${pkgbase}")
options=(!strip)
source=("CrystalDiskInfo.zip::http://osdn.jp/frs/redir.php?f=%2Fcrystaldiskinfo%2F${_realver}%2FCrystalDiskInfo${pkgver//./_}.zip")
sha512sums=('f1ba77a9df3566b0e4a227f9474eb16d34a2a27096b96181712e71cc7532854163a20290890f263bbd1fc75bab760435aed0a5c18bdf1348cc04cd2fba0822f8')

_lc() {
    for f in "${@}"
    do
        n="$(echo "${f}" | tr 'A-Z' 'a-z')"
        if test "${f}" != "${n}"
        then
            mv "${f}" "${n}"
        fi
    done
}

prepare() {
    _lc "License"/* *
}

_package() {
    a="${pkgdir}/apps${_apparch}/${_dirname}"
    mkdir -p "${a}"
    cp "diskinfo${_apparch}.exe" "${a}/crystaldiskinfo.exe"
    cp -r "cdiresource" "${a}"

    d="${pkgdir}/docs${_apparch}/${_dirname}"
    mkdir -p "${d}/diskinfo"
    cp -r "license"/* "${d}/diskinfo"
}

package_app-i686-crystaldiskinfo() {
    _apparch=32
    _package
}

package_app-x86_64-crystaldiskinfo() {
    _apparch=64
    _package
}
