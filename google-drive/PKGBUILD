# Maintainer: David Macek <david.macek.0@gmail.com>

_realname=google-drive
_choconame=google-drive
pkgbase="app-${_realname}"
pkgname=("app-x86_64-${_realname}")
epoch=1
pkgver=55.0.3.0
pkgrel=1
pkgdesc="The Google Drive synchronization tool"
url="https://www.google.com/drive/"
arch=('any')
license=('custom')
replaces=("app-i686-${_realname}")
makedepends=("p7zip")
provides=("${pkgbase}")
options=(!strip)
source=("GoogleDriveSetup.exe::https://dl.google.com/drive-file-stream/GoogleDriveSetup.exe")
sha512sums=('9b20529171353e3d7b588cfaddd501197ea810807b860c5e8476d122c3970be7bf56179fcb12b97326fe7e8986b28498d272ce489bff5da16d61f6d0a481495e')

prepare() {
    7z x -y -bd "GoogleDriveSetup.exe" -o"GoogleDriveSetup.dir" > /dev/null
    7z x -y -bd "GoogleDriveSetup.dir/.rsrc/0/RCDATA/101" -o"101" > /dev/null
}

pkgver() {
    powershell -command '(Get-Item "101/GoogleDriveFS.exe").VersionInfo.FileVersionRaw.ToString()'
}

package() {
    local a="${pkgdir}/apps/${_realname}"
    mkdir -p "${a}"
    cp -R "101"/* "${a}"
}
