# Maintainer: David Macek <david.macek.0@gmail.com>

_chocover() {
    # 6.3.0 -> 6.3
    # 6.3.1 -> 6.3.1
    echo "${1}" | sed -r 's/^([0-9]+)[.]([0-9]+)[.]0$/\1.\2/'
}

_realname=winscp
_choconame=winscp
pkgbase="app-${_realname}"
pkgname=("app-i686-${_realname}")
epoch=1
pkgver=6.3.3
pkgrel=1
pkgdesc="An open source two-pane SFTP/FTP/WebDAV/SCP client"
url="https://winscp.net/"
arch=('any')
license=('custom')
makedepends=("app-innounp")
provides=("${pkgbase}")
options=(!strip)
source=("WinSCP-Setup.exe::https://download.sourceforge.net/winscp/WinSCP-${pkgver}-Setup.exe")
sha512sums=('c5f888995f9faba0ac70e31943277c4d7e7a2bb47b842b11bc8b369c97ec89f6ecd1d44229ace927e12414e933c85cea39cd8fdf0da8494f9658a285020456fe')

_lc() {
    local d f n

    for f in "${@}"
    do
        d="$(dirname "${f}")"
        f="$(basename "${f}")"
        n="$(echo "${f}" | tr 'A-Z' 'a-z')"
        if test "${f}" != "${n}"
        then
            mv "${d}/${f}" "${d}/${n}"
        fi
    done
}

prepare() {
    /apps/innounp/innounp -x -q -m -a -y "WinSCP-Setup.exe" -d"WinSCP-Setup.dir"
}

package() {
    local a="${pkgdir}/apps/${_realname}"
    mkdir -p "${a}"
    cp -R "WinSCP-Setup.dir/{app}"/* "${a}"
    rm -R "${a}/winscp.map" "${a}/putty"
    _lc "${a}"/*
}
