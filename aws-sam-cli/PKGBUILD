# Maintainer: David Macek <david.macek.0@gmail.com>

_realname=aws-sam-cli
pkgbase="app-${_realname}"
pkgname=("app-x86_64-${_realname}")
epoch=1
pkgver=1.26.0
pkgrel=1
pkgdesc="CLI tool to build, test, debug, and deploy Serverless applications using AWS SAM"
url="https://aws.amazon.com/serverless/sam/"
arch=('any')
license=('Apache')
replaces=("app-i686-${_realname}")
makedepends=("app-lessmsi")
provides=("${pkgbase}")
options=(!strip)
source=("AWS_SAM_CLI_64_PY3.msi::https://github.com/awslabs/aws-sam-cli/releases/latest/download/AWS_SAM_CLI_64_PY3.msi")
sha512sums=('19183f0950dd12a8beb344b0e2634096ab0a684195acc6cc3def348a6c08f97b2c17e1bfe8a6c338151b3e8819f6441fa408655cddfe17d02523f9a71dd0da4e')

_rmmsdll() {
    local ms

    ms=("msvbvm[0-9]+"
        "ucrtbase"
        "mfcm?[0-9]+[a-z]+"
        "msvc[rp][0-9]+(_([0-9]|codecvt_ids|atomic_wait))?"
        "vc([ao]mp|corlib|runtime)[0-9]+(_[0-9])?"
        "concrt[0-9]+"
        "pgort[0-9]+"
        "api-ms-win-[0-9a-z-]+")
    find "${1}" -iname '*.dll' | grep -Eif <(for m in "${ms[@]}"; do echo "/${m}[.]dll$"; done) | xargs -r rm
}

prepare() {
    rm -fR "AWS_SAM_CLI_64_PY3"
    /apps/lessmsi/lessmsi x "AWS_SAM_CLI_64_PY3.msi" > /dev/null
}

pkgver() {
    cat "AWS_SAM_CLI_64_PY3/SourceDir/Amazon/AWSSAMCLI/Product.wxs" | grep '<Product' | grep Version= | sed -r 's/^.*Version="([0-9.]+)".*$/\1/'
}

package() {
    local a="${pkgdir}/apps/${_realname}"
    mkdir -p "${a}"
    cp -R "AWS_SAM_CLI_64_PY3/SourceDir/Amazon/AWSSAMCLI"/* "${a}"
    rm -R "${a}/resources" "${a}"/*.wxs
    _rmmsdll "${a}"
}
