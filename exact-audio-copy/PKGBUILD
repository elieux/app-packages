# Maintainer: David Macek <david.macek.0@gmail.com>

_realname=exact-audio-copy
_choconame=eac
pkgbase="app-${_realname}"
pkgname=("app-i686-${_realname}")
epoch=1
pkgver=1.6
pkgrel=1
pkgdesc="A very accurate CD audio grabber"
url="http://www.exactaudiocopy.de/en/"
arch=('any')
license=('custom')
makedepends=("p7zip")
provides=("${pkgbase}")
options=(!strip)
source=("eac.exe::http://www.exactaudiocopy.de/eac-${pkgver}.exe")
sha512sums=('0274dee707a8bc3cc2cecd3f4564f2f56ab26066aae466b5bae1200531d7c4db04b842d10b0f8c59e939072400ff9ce9675db24b84ffbd4da6138b6d190c3660')
noextract=("eac.exe")

_lc() {
    local d f n

    for f in "${@}"
    do
        d="$(dirname "${f}")"
        f="$(basename "${f}")"
        n="$(echo "${f}" | tr 'A-Z' 'a-z')"
        if test "${f}" != "${n}"
        then
            mv "${d}/${f}" "${d}/${n}"
        fi
    done
}

prepare() {
    7z x -y -bd "eac.exe" -o"eac.dir" > /dev/null
}

package() {
    local a="${pkgdir}/apps/${_realname}"
    mkdir -p "${a}"
    cp -R "eac.dir"/* "${a}"
    rm -R "${a}"/'$PLUGINSDIR' "${a}/Microsoft.VC80.CRT"
    _lc "${a}"/*
}
