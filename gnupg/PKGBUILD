# Maintainer: David Macek <david.macek.0@gmail.com>

_realname=gnupg
_realver=20260224
_choconame=gnupg
pkgbase="app-${_realname}"
pkgname=("app-i686-${_realname}")
epoch=1
pkgver=2.5.18
pkgrel=1
pkgdesc="Complete and free implementation of the OpenPGP standard"
url="https://www.gnupg.org/"
arch=('any')
license=('custom')
makedepends=("p7zip")
provides=("${pkgbase}")
options=(!strip)
source=("gnupg-w32.exe::https://www.gnupg.org/ftp/gcrypt/binary/gnupg-w32-${pkgver}_${_realver}.exe")
sha512sums=('1e6f94810a3609ac28e29b232d6d6953609df031c6891bb634ac66d5206a9356c5a6a5f812e086370a0edee079c2c27069ce9267fbc0db76fee6d1afc4ad5e3d')
noextract=("gnupg-w32.exe")

prepare() {
    7z x -y -bd "gnupg-w32.exe" -o"gnupg-w32.dir" > /dev/null
}

package() {
    local a="${pkgdir}/apps/${_realname}"
    mkdir -p "${a}"
    cp -R "gnupg-w32.dir"/* "${a}"
    rm -R "${a}"/'$PLUGINSDIR'
    find "${a}" -iname '*.tmp' -delete
}
