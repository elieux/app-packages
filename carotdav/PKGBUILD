# Maintainer: David Macek <david.macek.0@gmail.com>

_realname=carotdav
_choconame=carotdav
pkgbase="app-${_realname}"
pkgname=("app-i686-${_realname}")
epoch=1
pkgver=1.15.9
pkgrel=1
pkgdesc="A simple WebDAV, FTP, cloud drives and IMAP client"
url="http://rei.to/carotdav_en.html"
arch=('any')
license=('custom')
provides=("${pkgbase}")
options=(!strip)
source=("CarotDAV.portable.zip::http://rei.to/CarotDAV${pkgver}.portable.zip"
        "installed.txt")
sha512sums=('a61a76fc1f51c7de5ee6ec2228820c10d2f85bedffe7cfc31f359c6841576442b0601cb3d97ea5670a5dfae097d3ee0478e2772624181e52cebb357ccd4b8665'
            'd9263cc4a54c6ac861cb02126ef86f4c7d2d3a7b2cdcb6bcb80978dfd1e81597788e2c8bee16c1038ebf5e892ccbdd3cd2c0ee92a87e494560dfbc7b7c46ab0b')

_lc() {
    local d f n

    for f in "${@}"
    do
        d="$(dirname "${f}")"
        f="$(basename "${f}")"
        n="$(echo "${f}" | tr 'A-Z' 'a-z')"
        if test "${f}" != "${n}"
        then
            mv "${d}/${f}" "${d}/${n}"
        fi
    done
}

package() {
    local a="${pkgdir}/apps/${_realname}"
    mkdir -p "${a}"
    cp "installed.txt" "CarotDAV"/* "${a}"
    _lc "${a}"/*
}
