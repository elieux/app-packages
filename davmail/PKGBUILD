# Maintainer: David Macek <david.macek.0@gmail.com>

_realname=davmail
_realver=4.6.1-2343
pkgbase="app-${_realname}"
pkgname=("app-i686-${_realname}"
         "app-x86_64-${_realname}")
pkgver=${_realver//-/.}
pkgrel=1
pkgdesc="A gateway allowing usage of any mail/calendar client with an Exchange server"
url="http://davmail.sourceforge.net/"
arch=('any')
license=('GPL2')
makedepends=("unzip")
provides=("${pkgbase}")
source=("davmail-windows-noinstall.zip::http://downloads.sourceforge.net/${_realname}/${_realname}/${_realver%%-*}/${_realname}-${_realver}-windows-noinstall.zip")
md5sums=('ab970cb0ee0bae2b4384c4e943ad46f6')

package() {
    mkdir -p "${pkgdir}/apps${_apparch}/${_realname}"

    cp -R "${srcdir}/lib" "${pkgdir}/apps${_apparch}/${_realname}/"
    cp "${srcdir}/davmail.jar" "${pkgdir}/apps${_apparch}/${_realname}/"
    cp "${srcdir}/davmailconsole.exe" "${pkgdir}/apps${_apparch}/${_realname}/"
}

package_app-i686-davmail() {
    _apparch=32
    package
    for file in "davmail" "davmailservice"
    do
        cp "${srcdir}/${file}.exe" "${pkgdir}/apps${_apparch}/${_realname}/"
    done
}

package_app-x86_64-davmail() {
    _apparch=64
    package
    for file in "davmail" "davmailservice"
    do
        cp "${srcdir}/${file}64.exe" "${pkgdir}/apps${_apparch}/${_realname}/${file}.exe"
    done
}
