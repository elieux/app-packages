# Maintainer: David Macek <david.macek.0@gmail.com>

_realname=winmerge-plugin-7z
_dirname=winmerge
_apparch=32
pkgbase="app-${_realname}"
pkgname=("app-i686-${_realname}")
pkgver=0028.465.920
pkgrel=1
pkgdesc="A WinMerge plugin for 7-zip support"
url="http://winmerge.org/downloads/plugins.php"
arch=('any')
license=('custom')
depends=("app-i686-winmerge")
makedepends=("p7zip")
provides=("${pkgbase}")
options=(!strip)
source=("Merge7z.7z::http://downloads.sourceforge.net/winmerge/Merge7z${pkgver//./-}.7z")
sha512sums=('e868dc578f5c6b29ee58101f9d223ce4f707eee23c74be1c3724e1cb80e6030a40ceaa20e4b42fb2c0055eded0247402a7b746e29340e33dca3828c8e97532c4')

_lc() {
    local d f n

    for f in "${@}"
    do
        d="$(dirname "${f}")"
        f="$(basename "${f}")"
        n="$(echo "${f}" | tr 'A-Z' 'a-z')"
        if test "${f}" != "${n}"
        then
            mv "${d}/${f}" "${d}/${n}"
        fi
    done
}

prepare() {
    _lc *
}

package() {
    a="${pkgdir}/apps${_apparch}/${_dirname}"
    mkdir -p "${a}"
    cp -r "lang" "7z.dll" "7zip_pad.xml" "merge7z920u.dll" "${a}"
}
