# Maintainer: David Macek <david.macek.0@gmail.com>

_realname=obsidian
_choconame=obsidian
pkgbase="app-${_realname}"
pkgname=("app-x86_64-${_realname}")
epoch=1
pkgver=1.10.3
pkgrel=1
pkgdesc="A powerful knowledge base that works on top of a local folder of plain text Markdown files"
url="https://obsidian.md/"
arch=('any')
license=('custom')
makedepends=("p7zip")
provides=("${pkgbase}")
options=(!strip)
source=("Obsidian.exe::https://github.com/obsidianmd/obsidian-releases/releases/download/v${pkgver}/Obsidian-${pkgver}.exe")
sha512sums=('8b69e18702b9e9107b9d47b2914c0cc5d6ac92edaaeb496c53edc46cca1f394a2d1fbea133d6edd037b444b0a0312e38e59e82b4cee31989241d0f612ccda633')
noextract=("Obsidian.exe")

_lc() {
    local d f n

    for f in "${@}"
    do
        d="$(dirname "${f}")"
        f="$(basename "${f}")"
        n="$(echo "${f}" | tr 'A-Z' 'a-z')"
        if test "${f}" != "${n}"
        then
            mv "${d}/${f}" "${d}/${n}"
        fi
    done
}

prepare() {
    rm -fR "Obsidian.dir"
    7z x -y -bd "Obsidian.exe" -o"Obsidian.dir" > /dev/null
    7z x -y -bd "Obsidian.dir"/'$PLUGINSDIR'/"app-64.7z" -o"app-64" > /dev/null
}

package() {
    local a="${pkgdir}/apps/${_realname}"
    mkdir -p "${a}"
    cp -R "app-64"/* "${a}"
    _lc "${a}"/*
}
