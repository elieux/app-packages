# Maintainer: David Macek <david.macek.0@gmail.com>

_realname=winscp
pkgbase="app-${_realname}"
pkgname=("app-i686-${_realname}")
epoch=1
pkgver=5.17.8
pkgrel=1
pkgdesc="An open source two-pane SFTP/FTP/WebDAV/SCP client"
url="https://winscp.net/"
arch=('any')
license=('custom')
makedepends=("app-innounp")
provides=("${pkgbase}")
options=(!strip)
source=("WinSCP-Setup.exe::https://download.sourceforge.net/winscp/WinSCP-${pkgver}-Setup.exe")
sha512sums=('8ac6617da164f14f3ba2b7d9b913fc367a752fefa38032eaa104de98dc4c437cffcf28ce58c6e3a46d147b5cc67cd4f5c0619279994c55e53bbd4537dbb66ea0')

_lc() {
    local d f n

    for f in "${@}"
    do
        d="$(dirname "${f}")"
        f="$(basename "${f}")"
        n="$(echo "${f}" | tr 'A-Z' 'a-z')"
        if test "${f}" != "${n}"
        then
            mv "${d}/${f}" "${d}/${n}"
        fi
    done
}

prepare() {
    /apps/innounp/innounp -x -q -m -a -y "WinSCP-Setup.exe" -d"WinSCP-Setup.dir"
}

package() {
    local a="${pkgdir}/apps/${_realname}"
    mkdir -p "${a}"
    cp -R "WinSCP-Setup.dir/{app}"/* "${a}"
    rm -R "${a}/winscp.map" "${a}/putty"
    _lc "${a}"/*
}
