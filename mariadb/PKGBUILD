# Maintainer: David Macek <david.macek.0@gmail.com>

_realname=mariadb
_choconame=mariadb
pkgbase="app-${_realname}"
pkgname=("app-x86_64-${_realname}")
epoch=1
pkgver=11.2.1
pkgrel=1
pkgdesc="Fast SQL database server, drop-in replacement for MySQL"
url="https://mariadb.org/"
arch=('any')
license=('GPL')
replaces=("app-i686-${_realname}")
makedepends=("rsync")
provides=("${pkgbase}")
options=(!strip)
source=("mariadb-winx64.zip::rsync://rsync.osuosl.org/mariadb/mariadb-${pkgver}/winx64-packages/mariadb-${pkgver}-winx64.zip")
sha512sums=('aae5fcfd080823550560f1158780b36402c913962be765cdc9414873790b3bd2833887532c7dbc618308613181a49644d335a993ce41c56075a241516103eff6')

_rmmsdll() {
    local ms

    ms=("msvbvm[0-9]+"
        "ucrtbase"
        "mfcm?[0-9]+[a-z]+"
        "msvc[rp][0-9]+(_([0-9]|codecvt_ids|atomic_wait))?"
        "vc([ao]mp|corlib|runtime)[0-9]+(_([0-9]|threads))?"
        "concrt[0-9]+"
        "pgort[0-9]+"
        "api-ms-win-[0-9a-z-]+")
    find "${1}" -iname '*.dll' | grep -Eif <(for m in "${ms[@]}"; do echo "/${m}[.]dll$"; done) | xargs -r rm
}

package() {
    local a="${pkgdir}/apps/${_realname}"
    mkdir -p "${a}"
    cp -R "mariadb-${pkgver}-winx64"/* "${a}"
    find "${a}" -name '*.pdb' -delete
    _rmmsdll "${a}"
}
