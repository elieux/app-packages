# Maintainer: David Macek <david.macek.0@gmail.com>

_realname=far
_realver=30b5454-20190823
pkgbase="app-${_realname}"
pkgname=("app-x86_64-${_realname}")
epoch=1
pkgver="$(echo -n "${_realver}" | sed -r 's/^([0-9])([0-9])b([0-9]+)-[0-9]+$/\1.\2.\3/')"
pkgrel=1
pkgdesc="Extensible text mode file manager"
url="http://farmanager.com/index.php?l=en"
arch=('any')
license=('GPL2' 'BSD' 'custom')
replaces=("app-i686-${_realname}")
makedepends=("p7zip")
provides=("${pkgbase}")
options=(!strip)
source=("Far.x64.7z::http://farmanager.com/files/Far${_realver%%-*}.x64.${_realver##*-}.7z")
sha512sums=('a470a41776a41e05386f289648121f1037f592f5f7cc69bcccb15f41b4832f77798f30e92832ca58ee4d47c4fcd7787c7b93f100b47cb768c749e2d61181f277')
noextract=("${source[@]%%::*}")

_lc() {
    local d f n

    for f in "${@}"
    do
        d="$(dirname "${f}")"
        f="$(basename "${f}")"
        n="$(echo "${f}" | tr 'A-Z' 'a-z')"
        if test "${f}" != "${n}"
        then
            mv "${d}/${f}" "${d}/${n}"
        fi
    done
}

prepare() {
    7z x -y -bd "Far.x64.7z" -o"far"
    _lc "far"/*
}

package() {
    a="${pkgdir}/apps/${_realname}"
    mkdir -p "${a}"
    cp -r "far"/* "${a}"
    find "${a}" -name '*.map' -delete
}
