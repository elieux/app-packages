# Maintainer: David Macek <david.macek.0@gmail.com>

_realname=teamviewer
_apparch=32
pkgbase="app-${_realname}"
pkgname=("app-i686-${_realname}")
pkgver=10.0.39052
pkgrel=1
pkgdesc="All-in-one software for remote support and online meetings"
url="http://www.teamviewer.com/"
arch=('any')
license=('custom')
makedepends=("unzip")
provides=("${pkgbase}")
options=(!strip)
source=("http://download.teamviewer.com/download/TeamViewerPortable.zip")
md5sums=('c333dc1cdcb34a594631ae3f8704e080')

_lc() {
    for f in "${@}"
    do
        n="$(echo "${f}" | tr 'A-Z' 'a-z')"
        if test "${f}" != "${n}"
        then
            mv "${f}" "${n}"
        fi
    done
}

pkgver() {
    b="$(cygpath -wa "TeamViewer.exe" | sed 's/\\/\\\\/g')"
    wmic datafile where name=\""${b}"\" get version | grep 'Version' -A 1 | tail -1 | tr -d ' ' | sed -r 's/([0-9]+[.][0-9]+[.][0-9]+)[.]0$/\1/'
}

prepare() {
    _lc *
}

package() {
    a="${pkgdir}/apps${_apparch}/${_realname}"
    mkdir -p "${a}"
    cp *.{dll,exe,ini} "${a}"

    d="${pkgdir}/docs${_apparch}/${_realname}"
    mkdir -p "${d}"
    cp *.{txt,inf} "${d}"
}
