# Maintainer: David Macek <david.macek.0@gmail.com>

_realname=mysql
pkgbase="app-${_realname}"
pkgname=("app-i686-${_realname}"
         "app-x86_64-${_realname}")
pkgver=5.7.13
pkgrel=1
pkgdesc="Fast SQL database server, community edition"
url="https://www.mysql.com/products/community/"
arch=('any')
license=('GPL')
makedepends=("unzip")
provides=("${pkgbase}")
options=(!strip)
source=("mysql-win32.zip::https://dev.mysql.com/get/Downloads/MySQL-${pkgver%.*}/mysql-${pkgver}-win32.zip"
        "mysql-winx64.zip::https://dev.mysql.com/get/Downloads/MySQL-${pkgver%.*}/mysql-${pkgver}-winx64.zip")
sha512sums=('36e24613b77ec0fc9e946ea444086de4920f5dab2bc18749e8750947bc881d86bba5fad265f659507654e728b3c1ebfac9a58c6cb22b85f8f1a5994f1ce52d7b'
            'b539606da045561797723c85d719352723209070a803359762c91dbc8ab7fd2205320f51cc42247664dd069408a573fb7f9f7c804254c356badaee95c1d492b4')

_package() {
    a="${pkgdir}/apps${_apparch}/${_realname}"
    mkdir -p "${a}"
    cp -r "mysql-${pkgver}-win${1}"/* "${a}"
    find "${a}" -name 'debug' -exec rm -r '{}' \; -prune
    find "${a}" -name '*.pdb' -delete
    find "${a}" -name '*.lib' -delete

    d="${pkgdir}/docs${_apparch}/${_realname}"
    mkdir -p "${d}"
    mv "${a}"/{docs,COPYING,README} "${a}"/*.ini "${d}"
}

package_app-i686-mysql() {
    _apparch=32
    _package "32"
}

package_app-x86_64-mysql() {
    _apparch=64
    _package "x64"
}
