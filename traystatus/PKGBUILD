# Maintainer: David Macek <david.macek.0@gmail.com>

_realname=traystatus
_choconame=traystatus
pkgbase="app-${_realname}"
pkgname=("app-i686-${_realname}")
epoch=1
pkgver=4.4
pkgrel=1
pkgdesc="Show status of Caps Lock, Num Lock, Scroll Lock, and more in system tray"
url="https://www.traystatus.com/"
arch=('any')
license=('custom')
provides=("${pkgbase}")
options=(!strip)
source=("TrayStatus.zip::https://binaryfortressdownloads.com/Download/BFSFiles/114/TrayStatus-${pkgver}.zip")
sha512sums=('4ebb47d431cf59920a7bc79bb69bec678f2e12e10f55c91a937440bf8d1ae55f9c4903e71a47ad6c70adb6ffd26726a5e07d8859699a72d9e12a44102e0adaf4')

_lc() {
    local d f n

    for f in "${@}"
    do
        d="$(dirname "${f}")"
        f="$(basename "${f}")"
        n="$(echo "${f}" | tr 'A-Z' 'a-z')"
        if test "${f}" != "${n}"
        then
            mv "${d}/${f}" "${d}/${n}"
        fi
    done
}

package() {
    local a="${pkgdir}/apps/${_realname}"
    mkdir -p "${a}"
    cp -R "TrayStatus"/* "${a}"
    rm "${a}"/System.*.dll
    _lc "${a}"/*
}
