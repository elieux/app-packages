# Maintainer: David Macek <david.macek.0@gmail.com>

_realname=franz
pkgbase="app-${_realname}"
pkgname=("app-x86_64-${_realname}")
epoch=1
pkgver=5.1.0
pkgrel=1
pkgdesc="Franz is a free messaging app and combines chat & messaging services into one application"
url="http://meetfranz.com/"
arch=('any')
license=('custom')
replaces=("app-i686-${_realname}")
makedepends=("p7zip")
provides=("${pkgbase}")
options=(!strip)
source=("franz-setup.exe::https://github.com/meetfranz/franz/releases/download/v${pkgver}/franz-setup-${pkgver}.exe")
sha512sums=('bf3597281360fc60c23d2640f74a12ef09b094debdf58b1add4c34d48b197a373df3d7b1cf55c15bdca272180b4fda24253f4759f4e18aee2bbf3ab831f43cb7')

_lc() {
    local d f n

    for f in "${@}"
    do
        d="$(dirname "${f}")"
        f="$(basename "${f}")"
        n="$(echo "${f}" | tr 'A-Z' 'a-z')"
        if test "${f}" != "${n}"
        then
            mv "${d}/${f}" "${d}/${n}"
        fi
    done
}

prepare() {
    7z x -y -bd "franz-setup.exe" > /dev/null
     7z x -y -bd '$PLUGINSDIR'/"app-64.7z" -o"franz"
    _lc "franz"/*
}

package() {
    a="${pkgdir}/apps/${_realname}"
    mkdir -p "${a}"
    cp -r "franz"/* "${a}"
}
