# Maintainer: David Macek <david.macek.0@gmail.com>

_realname=winmerge
_choconame=winmerge
pkgbase="app-${_realname}"
pkgname=("app-x86_64-${_realname}")
epoch=1
pkgver=2.16.52
pkgrel=1
pkgdesc="A differencing and merging tool"
url="https://winmerge.org/"
arch=('any')
license=('GPL')
replaces=("app-i686-${_realname}" "app-i686-winmerge-plugin-7z")
makedepends=("app-innounp")
provides=("${pkgbase}" "app-winmerge-plugin-7z" "app-x86_64-winmerge-plugin-7z")
options=(!strip)
source=("WinMerge-Setup.exe::https://github.com/WinMerge/winmerge/releases/download/v${pkgver}/WinMerge-${pkgver}-Setup.exe"
        "winmerge-x64-exe.zip::https://github.com/WinMerge/winmerge/releases/download/v${pkgver}/winmerge-${pkgver}-x64-exe.zip")
sha512sums=('8f18f23836d5e4e106c626126ba8b18ee48818af0cc45f2df774068496a9e3044f7942c513865ac99bc6c9bf712549071123d74eaeef17a9568a9afd86e5cab3'
            'a4de5480ef7503df224ffad9a1e15416761f81e0e85a890404413dcb4dba500090d8015b61cb94614771c4dcbcd60e8827524d5f3b77b626c4492e9449fd73e2')

_lc() {
    local d f n

    for f in "${@}"
    do
        d="$(dirname "${f}")"
        f="$(basename "${f}")"
        n="$(echo "${f}" | tr 'A-Z' 'a-z')"
        if test "${f}" != "${n}"
        then
            mv "${d}/${f}" "${d}/${n}"
        fi
    done
}

prepare() {
    /apps/innounp/innounp -x -q -m -a -y "WinMerge-Setup.exe" -d"WinMerge-Setup.dir"
}

package() {
    local a="${pkgdir}/apps/${_realname}"
    mkdir -p "${a}"
    cp -R "WinMerge"/* "${a}"
    cp -R "WinMerge-Setup.dir/{app}/LogoImages" "WinMerge-Setup.dir/{app}/WinMergeU.VisualElementsManifest.xml" "${a}"
    rm "${a}/vcomp140.dll"
    _lc "${a}"/*
}
