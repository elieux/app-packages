# Maintainer: David Macek <david.macek.0@gmail.com>

_realname=notepad++
pkgbase="app-${_realname}"
pkgname=("app-i686-${_realname}"
         "app-x86_64-${_realname}")
pkgver=7.5.1
pkgrel=1
pkgdesc="A free Scintilla-based editor with plugin support"
url="https://notepad-plus-plus.org/"
arch=('any')
license=('GPL')
makedepends=("p7zip" "unzip")
provides=("${pkgbase}")
options=(!strip)
source=("npp.bin32.zip::https://notepad-plus-plus.org/repository/${pkgver%%.*}.x/${pkgver}/npp.${pkgver}.bin.zip"
        "npp.Installer32.exe::https://notepad-plus-plus.org/repository/${pkgver%%.*}.x/${pkgver}/npp.${pkgver}.Installer.exe"
        "npp.bin64.zip::https://notepad-plus-plus.org/repository/${pkgver%%.*}.x/${pkgver}/npp.${pkgver}.bin.x64.zip"
        "npp.Installer64.exe::https://notepad-plus-plus.org/repository/${pkgver%%.*}.x/${pkgver}/npp.${pkgver}.Installer.x64.exe")
sha512sums=('66445d57e38cafb7ddc16c36ff482bb99f2e8a7c5cba310b2b94351cc37d64ca89fc395758a9dc41dbda0b584d0e9c6a1840531ec71672fe9126874cb60bee44'
            '1b55db384377793ed76e619869f7edc05cc9e56f503728469944c9f4c18197da36b31fbc099011287c795f495e74f1c26a52450270a25b64d396a6cd2c4609ff'
            '16689c9d0e91091b8ce1a2acef5b91ed5d176f6b34fd41dbb7e1f9161aaab4fb2800f3adf19e12f1897aa3f3cf1e108f5c8e6899ac22942c8635dcdf4c151958'
            '12ed6528f2353751ce3932ca177ede403ac9fb85cab0b0e3930969dfbbb638d9171718d81619c50b1c4c5255b54694e126784bcc9af980b58a7719e0a266018f')
noextract=("npp.bin32.zip"
           "npp.bin64.zip")

_lc() {
    local d f n

    for f in "${@}"
    do
        d="$(dirname "${f}")"
        f="$(basename "${f}")"
        n="$(echo "${f}" | tr 'A-Z' 'a-z')"
        if test "${f}" != "${n}"
        then
            mv "${d}/${f}" "${d}/${n}"
        fi
    done
}

_prepare() {
    unzip -o "npp.bin${_apparch}.zip" -d "npp${_apparch}"
    rm -f "npp${_apparch}/nppshell_06*.dll"
    echo u | 7z x -bd -i!"NppShell_06.dll" -o"npp${_apparch}" "npp.Installer${_apparch}.exe"
    _lc "npp${_apparch}"/*
    for f in "npp${_apparch}"/nppshell_06*.dll
    do
        case "${_apparch}:$(file "${f}")" in
        32:*80386*|64:*x86-64*) mv "${f}" "npp${_apparch}"/"nppshell_06.dll" ;;
        32:*x86-64*|64:*80386*) rm "${f}" ;;
        esac
    done
}

prepare() {
    for _apparch in 32 64
    do
        _prepare
    done
}

_package() {
    a="${pkgdir}/apps${_apparch}/${_realname}"
    mkdir -p "${a}"
    cp -r "npp${_apparch}"/* "${a}"
    rm "${a}/dolocalconf.xml"

    d="${pkgdir}/docs${_apparch}/${_realname}"
    mkdir -p "${d}"
    mv "${a}"/*.{log,txt} "${d}"
}

# space before () required
package_app-i686-notepad++ () {
    _apparch=32
    _package
}

# space before () required
package_app-x86_64-notepad++ () {
    _apparch=64
    _package
}
