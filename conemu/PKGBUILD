# Maintainer: David Macek <david.macek.0@gmail.com>

_realname=conemu
pkgbase="app-${_realname}"
pkgname=("app-i686-${_realname}"
         "app-x86_64-${_realname}")
pkgver=18.03.09
pkgrel=2
pkgdesc="A powerful, highly customizable console emulator with tabs"
url="http://conemu.github.io/"
arch=('any')
license=('BSD')
makedepends=("p7zip")
provides=("${pkgbase}")
options=(!strip)
source=("ConEmuPack.7z::https://github.com/Maximus5/ConEmu/releases/download/v${pkgver}/ConEmuPack.${pkgver//./}.7z")
sha512sums=('e2039c7d80b12211e3d3ad8f3c96f8c2c811c9adca4703f6c2170ad2cc687c622e9da4fbb4793bc9de2ce0379afd551dea7bacefaf8cc0131a22c4ac302d900d')

_lc() {
    local d f n

    for f in "${@}"
    do
        d="$(dirname "${f}")"
        f="$(basename "${f}")"
        n="$(echo "${f}" | tr 'A-Z' 'a-z')"
        if test "${f}" != "${n}"
        then
            mv "${d}/${f}" "${d}/${n}"
        fi
    done
}

prepare() {
    _lc *
}

_package() {
    a="${pkgdir}/apps${_apparch}/${_realname}"
    mkdir -p "${a}"
    cp -r "conemu${1}.exe" "${a}/conemu.exe"
    cp -r "conemu" "plugins" "${a}"
}

package_app-i686-conemu() {
    _apparch=32
    _package ""
}

package_app-x86_64-conemu() {
    _apparch=64
    _package "64"
}
