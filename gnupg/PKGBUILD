# Maintainer: David Macek <david.macek.0@gmail.com>

_realname=gnupg
_realver=20200903
pkgbase="app-${_realname}"
pkgname=("app-i686-${_realname}")
epoch=1
pkgver=2.2.23
pkgrel=1
pkgdesc="Complete and free implementation of the OpenPGP standard"
url="https://www.gnupg.org/"
arch=('any')
license=('custom')
makedepends=("p7zip")
provides=("${pkgbase}")
options=(!strip)
source=("gnupg-w32.exe::https://www.gnupg.org/ftp/gcrypt/binary/gnupg-w32-${pkgver}_${_realver}.exe")
sha512sums=('87baf2ec27ed4ed8aab4944713e61abdcaf16fd064d06e6a4166fe660ec0352c5a701341284a1264e36cd868c1938a73a2d2512002cdecf6472a5e159a9f0da5')

prepare() {
    7z x -y -bd "gnupg-w32.exe" -o"gnupg-w32.dir" > /dev/null
}

package() {
    local a="${pkgdir}/apps/${_realname}"
    mkdir -p "${a}"
    cp -R "gnupg-w32.dir"/* "${a}"
    rm -R "${a}"/'$PLUGINSDIR'
    find "${a}" -iname '*.tmp' -delete
}
