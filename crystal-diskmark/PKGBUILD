# Maintainer: David Macek <david.macek.0@gmail.com>

_realname=crystaldiskmark
_realver=66553
_dirname=crystal
pkgbase="app-${_realname}"
pkgname=("app-i686-${_realname}"
         "app-x86_64-${_realname}")
pkgver=5.2.0
pkgrel=1
pkgdesc="A disk benchmarking software (Crystal Dew World)"
url="http://crystalmark.info/software/CrystalDiskMark/index-e.html"
arch=('any')
license=('MIT')
makedepends=("unzip")
provides=("${pkgbase}")
options=(!strip)
source=("CrystalDiskMark.zip::http://osdn.jp/frs/redir.php?f=%2Fcrystaldiskmark%2F${_realver}%2FCrystalDiskMark${pkgver//./_}.zip")
sha512sums=('9d99d6412e7c0235f4ab14352d13cdfe1e585452d56d87b2fa81b0c9c752a2bfc797ffc4899951838068cb8439879851fbd702792365d269f0686286775065d1')

_lc() {
    local d f n

    for f in "${@}"
    do
        d="$(dirname "${f}")"
        f="$(basename "${f}")"
        n="$(echo "${f}" | tr 'A-Z' 'a-z')"
        if test "${f}" != "${n}"
        then
            mv "${d}/${f}" "${d}/${n}"
        fi
    done
}

prepare() {
    _lc *
}

_package() {
    a="${pkgdir}/apps${_apparch}/${_dirname}"
    mkdir -p "${a}"
    cp "diskmark${_apparch}.exe" "${a}/crystaldiskmark.exe"
    cp -r "cdmresource" "${a}"
    rm "${a}/cdmresource/diskspd"/*
    cp "cdmresource/diskspd/diskspd${_apparch}.exe" "${a}/cdmresource/diskspd"
}

package_app-i686-crystaldiskmark() {
    _apparch=32
    _package
}

package_app-x86_64-crystaldiskmark() {
    _apparch=64
    _package
}
