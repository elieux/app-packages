# Maintainer: David Macek <david.macek.0@gmail.com>

_dirname=xdebug-dbgp-tools
_realname=xdebug-dbgp-client
pkgbase="app-${_realname}"
pkgname=("app-x86_64-${_realname}")
epoch=1
pkgver=0.5.0 # determined by running the .exe
pkgrel=1
pkgdesc="DBGp client from Xdebug"
url="https://xdebug.org/docs/dbgpClient"
arch=('any')
license=('unknown')
provides=("${pkgbase}")
conflicts=("xdebug-dbgp-tools")
options=(!strip)
source=("dbgpClient.exe::https://xdebug.org/files/binaries/dbgpClient.exe")
sha512sums=('f76f04743dce9b0ad098eadbfd95ce464473e47674ea8ffd1162b615186cbd0c6718d8840a2fe572e28125979b17b11c256d217874118488ac9cf66ee297e4cd')

_lc() {
    local d f n

    for f in "${@}"
    do
        d="$(dirname "${f}")"
        f="$(basename "${f}")"
        n="$(echo "${f}" | tr 'A-Z' 'a-z')"
        if test "${f}" != "${n}"
        then
            mv "${d}/${f}" "${d}/${n}"
        fi
    done
}

package() {
    local a="${pkgdir}/apps/${_dirname}"
    mkdir -p "${a}"
    cp "dbgpClient.exe" "${a}"
    _lc "${a}"/*
}
