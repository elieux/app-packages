# Maintainer: David Macek <david.macek.0@gmail.com>

_realname=tortoise-overlays
_realver=2.0.0.0
pkgbase="app-${_realname}"
pkgname=("app-x86_64-${_realname}")
epoch=1
pkgver=1.1.4.26626
pkgrel=1
pkgdesc="Overlays component for Tortoise tools"
url="https://tortoisesvn.net"
arch=('any')
license=('custom')
replaces=("app-i686-${_realname}")
makedepends=("app-lessmsi")
provides=("${pkgbase}")
options=(!strip)
source=("TortoiseGit-64bit.msi::https://download.tortoisegit.org/tgit/${_realver}/TortoiseGit-${_realver}-64bit.msi")
sha512sums=('0073ceb7c08fbc0e3c1506abb6c00ba925b6069807eb9a7d7bc4626cec6c0097ace4ebafb24dd0e5ffb47bb76b87aed03014933faf05777ba02ccb808b3cbdd5')

_lc() {
    local d f n

    for f in "${@}"
    do
        d="$(dirname "${f}")"
        f="$(basename "${f}")"
        n="$(echo "${f}" | tr 'A-Z' 'a-z')"
        if test "${f}" != "${n}"
        then
            mv "${d}/${f}" "${d}/${n}"
        fi
    done
}

prepare() {
    rm -fR "TortoiseGit-64bit"
    /apps/lessmsi/lessmsi x "TortoiseGit-64bit.msi"
    _lc "TortoiseGit-64bit/SourceDir/Program Files/TortoiseGit/Common/TortoiseOverlays"/*
}

pkgver() {
    "${SYSTEMROOT}/System32/WindowsPowerShell/v1.0/powershell" -command '(Get-Item "TortoiseGit-64bit/SourceDir/Program Files/TortoiseGit/Common/TortoiseOverlays/tortoiseoverlays.dll").VersionInfo.ProductVersion' | dos2unix | tr -d ' ' | tr ',' '.'
}

package() {
    a="${pkgdir}/apps/${_realname}"
    mkdir -p "${a}"
    cp -r "TortoiseGit-64bit/SourceDir/Program Files/TortoiseGit/Common/TortoiseOverlays"/* "${a}"

    for f in "TortoiseGit-64bit/SourceDir/Program Files/TortoiseGit/Common/TortoiseOverlays"/tortoiseoverlays.dll*
    do
        case "$(file "${f}")" in
        *x86-64*) cp -f "${f}" "${a}/tortoiseoverlays.dll" ;;
        *80386*) cp -f "${f}" "${a}/tortoiseoverlays32.dll" ;;
        esac
    done

    find "${a}" -name "*.duplicate*" -delete
}
