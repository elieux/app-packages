# Maintainer: David Macek <david.macek.0@gmail.com>

_realname=mysql
pkgbase="app-${_realname}"
pkgname=("app-i686-${_realname}"
         "app-x86_64-${_realname}")
pkgver=5.7.14
pkgrel=1
pkgdesc="Fast SQL database server, community edition"
url="https://www.mysql.com/products/community/"
arch=('any')
license=('GPL')
makedepends=("unzip")
provides=("${pkgbase}")
options=(!strip)
source=("mysql-win32.zip::https://dev.mysql.com/get/Downloads/MySQL-${pkgver%.*}/mysql-${pkgver}-win32.zip"
        "mysql-winx64.zip::https://dev.mysql.com/get/Downloads/MySQL-${pkgver%.*}/mysql-${pkgver}-winx64.zip")
sha512sums=('6853ef64f0d2357b8cdac27efca0433efd680ce9dec6c53f859227c881cde14e7b28e12695621fcbb5cd45d13ea0c1254e64906c667ba59427642ba58dc2a24d'
            '66d4cb7289f5acd3bfda449db30a76eef22da16bb54d19411ba939f7022f8a984ace878568c61d2f74bb2f5def003291afb27e2f9c116f188f57f5cd23475385')

_package() {
    a="${pkgdir}/apps${_apparch}/${_realname}"
    mkdir -p "${a}"
    cp -r "mysql-${pkgver}-win${1}"/* "${a}"
    find "${a}" -name 'debug' -exec rm -r '{}' \; -prune
    find "${a}" -name '*.pdb' -delete
    find "${a}" -name '*.lib' -delete

    d="${pkgdir}/docs${_apparch}/${_realname}"
    mkdir -p "${d}"
    mv "${a}"/{docs,COPYING,README} "${a}"/*.ini "${d}"
}

package_app-i686-mysql() {
    _apparch=32
    _package "32"
}

package_app-x86_64-mysql() {
    _apparch=64
    _package "x64"
}
