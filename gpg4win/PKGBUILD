# Maintainer: David Macek <david.macek.0@gmail.com>

_realname=gpg4win
_choconame=Gpg4win
pkgbase="app-${_realname}"
pkgname=("app-i686-${_realname}")
epoch=1
pkgver=4.0.0
pkgrel=1
pkgdesc="A secure solution for file and email encryption"
url="https://www.gpg4win.org/index.html"
arch=('any')
license=('custom')
makedepends=("p7zip")
provides=("${pkgbase}")
options=(!strip)
source=("gpg4win.exe::https://files.gpg4win.org/gpg4win-${pkgver}.exe")
sha512sums=('702c004db7e61dd29274f4f4df4228ba4441d370cfc7ad3c99f152305b9a88312771339b05b8664f17612d5bba81f2026612c23b947f8810b9eb6ff0a3ec570e')

_lc() {
    local d f n

    for f in "${@}"
    do
        d="$(dirname "${f}")"
        f="$(basename "${f}")"
        n="$(echo "${f}" | tr 'A-Z' 'a-z')"
        if test "${f}" != "${n}"
        then
            mv "${d}/${f}" "${d}/${n}"
        fi
    done
}

prepare() {
    7z x -y -bd "gpg4win.exe" -o"gpg4win.dir" > /dev/null
}

package() {
    local a="${pkgdir}/apps/${_realname}"
    mkdir -p "${a}"
    cp -R "gpg4win.dir"/* "${a}"
    find "${a}" -iname '*.tmp' -delete
    rm -R "${a}"/'$PLUGINSDIR' "${a}"/'$TEMP'  "${a}/gpg4win-uninstall.exe.nsis"
    rm "${a}/bin/pinentry.exe"
    cp "${a}/bin/pinentry-w32.exe" "${a}/bin/pinentry.exe"
    _lc "${a}"/*
}
