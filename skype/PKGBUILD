# Maintainer: David Macek <david.macek.0@gmail.com>

_realname=skype
pkgbase="app-${_realname}"
pkgname=("app-i686-${_realname}")
epoch=1
pkgver=8.54.0.91
pkgrel=1
pkgdesc="P2P software for high-quality voice communication"
url="https://www.skype.com/"
arch=('any')
license=('custom')
makedepends=("app-innounp")
provides=("${pkgbase}")
options=(!strip)
source=("Skype.exe::https://get.skype.com/go/getskype-skypeforwindows")
sha512sums=('2f4fb80aaa8e5587133e13f239867438ea9c2290aeda84234a55db65e77d95f13a78d792bfa54f912b0bbef4f10cc40eb9c87700c81fcbc33ec5b43ff557eaf7')

_lc() {
    local d f n

    for f in "${@}"
    do
        d="$(dirname "${f}")"
        f="$(basename "${f}")"
        n="$(echo "${f}" | tr 'A-Z' 'a-z')"
        if test "${f}" != "${n}"
        then
            mv "${d}/${f}" "${d}/${n}"
        fi
    done
}

_rmmsdll() {
    local ms

    ms=("msvbvm[0-9]+"
        "ucrtbase"
        "mfcm?[0-9]+[a-z]+"
        "msvc[rp][0-9]+"
        "vc([ao]mp|corlib|runtime)[0-9]+"
        "concrt[0-9]+"
        "pgort[0-9]+"
        "api-ms-win-[0-9a-z-]+")
    find "${1}" -iname '*.dll' | grep -Eif <(for m in "${ms[@]}"; do echo "/${m}[.]dll$"; done) | xargs -r rm
}

prepare() {
    /apps/innounp/innounp -x -q -m -a -y "Skype.exe"
    _lc "{app}"/*
}

pkgver() {
    "${SYSTEMROOT}/System32/WindowsPowerShell/v1.0/powershell" -command '(Get-Item "Skype.exe").VersionInfo.FileVersion' | dos2unix
}

package() {
    a="${pkgdir}/apps/${_realname}"
    mkdir -p "${a}"
    cp -r "{app}"/* "${a}"
    _rmmsdll "${a}"
}
