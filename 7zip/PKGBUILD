# Maintainer: David Macek <david.macek.0@gmail.com>

_realname=7zip
pkgbase="app-${_realname}"
pkgname=("app-i686-${_realname}"
         "app-x86_64-${_realname}")
pkgver=16.04
pkgrel=1
pkgdesc="A free file archiver for extremely high compression"
url="http://www.7-zip.org/"
arch=('any')
license=('LGPL2' 'custom')
makedepends=("p7zip")
provides=("${pkgbase}")
options=(!strip)
source=("7z32.exe::http://7-zip.org/a/7z${pkgver//./}.exe"
        "7z64.exe::http://7-zip.org/a/7z${pkgver//./}-x64.exe"
        "7zextra.7z::http://7-zip.org/a/7z${pkgver//./}-extra.7z")
sha512sums=('939869d9a60571c2f380ffe29ec40e7930d47e8295a2995e9ec56143319584b809fddd39a6e9df6e4b7c5e29b4925f588ca483e3c6f6758c5db54e13e2a3388c'
            'c5ec75c033970fefe0183285e35360308caa6094ded453bc5542761cd2b569a176183ca357bbdad764fa55ff42ed1d2f58f4bfec0be35b544becf32f427eb6ff'
            '8d2ffa07f21d5355f683573a114bfb4e4fc8dd95a4f6b07ca3cdd769fcc7aaadb7f3b6cd4b4e283007084ddb67632f0db3f77102870d700e43388febd78657d2')

_lc() {
    for f in "${@}"
    do
        n="$(echo "${f}" | tr 'A-Z' 'a-z')"
        if test "${f}" != "${n}"
        then
            mv "${f}" "${n}"
        fi
    done
}

_prepare() {
    7z x -y -bd "7z${_apparch}.exe" -o"7z${_apparch}"
    _lc "7z${_apparch}"/*
}

prepare() {
    _lc *
    for _apparch in 32 64
    do
        _prepare
    done
}

_package() {
    a="${pkgdir}/apps${_apparch}/${_realname}"
    mkdir -p "${a}"
    cp -r "${@}" "7z${_apparch}/lang" "7z${_apparch}"/*.{exe,dll,sfx} "${a}"
    rm "${a}/uninstall.exe"

    d="${pkgdir}/docs${_apparch}/${_realname}"
    mkdir -p "${d}" "${d}/extra"
    cp "7z${_apparch}"/*.{chm,txt,ion} "${d}"
    cp *.txt "${d}/extra"
}

package_app-i686-7zip() {
    _apparch=32
    _package *.{dll,exe}
    rm "${a}"/7z{32,64}.exe
}

package_app-x86_64-7zip() {
    _apparch=64
    _package "x64"/*.{dll,exe}
}
