# Maintainer: David Macek <david.macek.0@gmail.com>

_realname=unbound
pkgbase="app-${_realname}"
pkgname=("app-x86_64-${_realname}")
epoch=1
pkgver=1.13.1
pkgrel=1
pkgdesc="Validating, recursive, and caching DNS resolver"
arch=('any')
url="https://nlnetlabs.nl/projects/unbound/about/"
license=('custom:BSD')
replaces=("app-i686-${_realname}")
makedepends=("p7zip")
provides=("${pkgbase}")
options=(!strip)
source=("unbound_setup.exe::https://nlnetlabs.nl/downloads/unbound/unbound_setup_${pkgver}.exe")
sha512sums=('946cd991b90d83d001c1240eab95308df9cd8e74a4f6cf3e5c63b40f05cc5201a6324c44f9a77b099f06059087021788a071dbfd15ee04bb57c074717f9e0cdc')

_lc() {
    local d f n

    for f in "${@}"
    do
        d="$(dirname "${f}")"
        f="$(basename "${f}")"
        n="$(echo "${f}" | tr 'A-Z' 'a-z')"
        if test "${f}" != "${n}"
        then
            mv "${d}/${f}" "${d}/${n}"
        fi
    done
}

prepare() {
    7z x -bd "unbound_setup.exe" -o"unbound_setup.dir" > /dev/null
}

package() {
    local a="${pkgdir}/apps/${_realname}"
    mkdir -p "${a}"
    cp -R "unbound_setup.dir"/* "${a}"
    rm -R "${a}"/'$PLUGINSDIR'
    _lc "${a}"/*
}
