# Maintainer: David Macek <david.macek.0@gmail.com>

_realname=telegram
_choconame=telegram
pkgbase="app-${_realname}"
pkgname=("app-i686-${_realname}")
epoch=1
pkgver=4.0.2
pkgrel=1
pkgdesc="Desktop Telegram client"
url="https://desktop.telegram.org/"
arch=('any')
license=('GPL3')
provides=("${pkgbase}")
replaces=("app-x86_64-${_realname}")
options=(!strip)
source=("tportable.zip::https://github.com/telegramdesktop/tdesktop/releases/download/v${pkgver}/tportable.${pkgver}.zip")
sha512sums=('1c4853c02ed6599c6e4685f8fd16f75fcc6c57250ca7db9597ea723cef47215bd0a8f1ac6b760466273544cb664aae4eea6d44194131aa71088c1b74143d34c5')

_lc() {
    local d f n

    for f in "${@}"
    do
        d="$(dirname "${f}")"
        f="$(basename "${f}")"
        n="$(echo "${f}" | tr 'A-Z' 'a-z')"
        if test "${f}" != "${n}"
        then
            mv "${d}/${f}" "${d}/${n}"
        fi
    done
}

package() {
    local a="${pkgdir}/apps/${_realname}"
    mkdir -p "${a}"
    cp -R "Telegram"/* "${a}"
    _lc "${a}"/*
}
