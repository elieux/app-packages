# Maintainer: David Macek <david.macek.0@gmail.com>

_realname=7zip
pkgbase="app-${_realname}"
pkgname=("app-x86_64-${_realname}")
epoch=1
pkgver=18.01
pkgrel=1
pkgdesc="A free file archiver for extremely high compression"
url="http://www.7-zip.org/"
arch=('any')
license=('LGPL2' 'custom')
replaces=("app-i686-${_realname}")
makedepends=("p7zip")
provides=("${pkgbase}")
options=(!strip)
source=("7z-x64.exe::http://7-zip.org/a/7z${pkgver//./}-x64.exe"
        "7zextra.7z::http://7-zip.org/a/7z${pkgver//./}-extra.7z")
sha512sums=('0a1e8b876ecf4c58801743f6144e47951a60ade274a8bb7c3476ea70874e023b8c4e5e71e5fa25064dc7ec9208d8ea7a5e520107788fe06955fca0f71aa91b63'
            '9133fc551d76515e37fdd4dd8c1e28d464aea493548246b44565a42bba46715764f41f9cfa14d470d298c3a6e9829d200f8be5168cb67cf8f23d8042fca833bc')

_lc() {
    local d f n

    for f in "${@}"
    do
        d="$(dirname "${f}")"
        f="$(basename "${f}")"
        n="$(echo "${f}" | tr 'A-Z' 'a-z')"
        if test "${f}" != "${n}"
        then
            mv "${d}/${f}" "${d}/${n}"
        fi
    done
}

prepare() {
    7z x -y -bd "7z-x64.exe" -o"7z"
    _lc * "7z"/*
}

package() {
    a="${pkgdir}/apps/${_realname}"
    mkdir -p "${a}" "${a}/extra"
    cp -r "x64"/*.{dll,exe} "7z/lang" "7z"/*.{exe,dll,sfx} "${a}"
    rm "${a}/uninstall.exe"
    cp "7z"/*.{chm,txt,ion} "${a}"
    cp *.txt "${a}/extra"
}
