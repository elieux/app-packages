# Maintainer: David Macek <david.macek.0@gmail.com>

_realname=skype
_choconame=skype
pkgbase="app-${_realname}"
pkgname=("app-i686-${_realname}")
epoch=1
pkgver=8.69.0.77
pkgrel=1
pkgdesc="P2P software for high-quality voice communication"
url="https://www.skype.com/"
arch=('any')
license=('custom')
makedepends=("app-innounp")
provides=("${pkgbase}")
options=(!strip)
source=("Skype.exe::https://download.skype.com/s4l/download/win/Skype-${pkgver}.exe")
sha512sums=('95fde49a06b009372c426be8b1d67308a4b2d87b9cfcd0b0a7aab9969137ce81d26b5c2117dcf7fb5fca70ce2ff885cfcabd5d7b9a0fa6839567da3feed97ca4')

_lc() {
    local d f n

    for f in "${@}"
    do
        d="$(dirname "${f}")"
        f="$(basename "${f}")"
        n="$(echo "${f}" | tr 'A-Z' 'a-z')"
        if test "${f}" != "${n}"
        then
            mv "${d}/${f}" "${d}/${n}"
        fi
    done
}

_rmmsdll() {
    local ms

    ms=("msvbvm[0-9]+"
        "ucrtbase"
        "mfcm?[0-9]+[a-z]+"
        "msvc[rp][0-9]+(_([0-9]|codecvt_ids))?"
        "vc([ao]mp|corlib|runtime)[0-9]+(_[0-9])?"
        "concrt[0-9]+"
        "pgort[0-9]+"
        "api-ms-win-[0-9a-z-]+")
    find "${1}" -iname '*.dll' | grep -Eif <(for m in "${ms[@]}"; do echo "/${m}[.]dll$"; done) | xargs -r rm
}

prepare() {
    /apps/innounp/innounp -x -q -m -a -y "Skype.exe" -d"Skype.dir"
}

package() {
    local a="${pkgdir}/apps/${_realname}"
    mkdir -p "${a}"
    cp -R "Skype.dir/{app}"/* "${a}"
    _rmmsdll "${a}"
    _lc "${a}"/*
}
