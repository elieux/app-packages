# Maintainer: David Macek <david.macek.0@gmail.com>

_realname=exact-audio-copy
_apparch=32
pkgbase="app-${_realname}"
pkgname=("app-i686-${_realname}")
pkgver=1.2
pkgrel=1
pkgdesc="A very accurate CD audio grabber"
url="http://www.exactaudiocopy.de/en/"
arch=('any')
license=('custom')
makedepends=("p7zip")
provides=("${pkgbase}")
options=(!strip)
source=("eac.exe::http://www.exactaudiocopy.de/eac-${pkgver}.exe")
sha512sums=('e2b98b0c827f97b31ff26a28208c829b6c230746d27c4dcb5767f95ec4bf6fd389e51b5d03bd4e4cd7b93623f4eef760d7bc16e42be5ea5386b1a605423a1af5')

_lc() {
    local d f n

    for f in "${@}"
    do
        d="$(dirname "${f}")"
        f="$(basename "${f}")"
        n="$(echo "${f}" | tr 'A-Z' 'a-z')"
        if test "${f}" != "${n}"
        then
            mv "${d}/${f}" "${d}/${n}"
        fi
    done
}

prepare() {
    7z x -y -bd "eac.exe" -o"eac"
    _lc "eac"/*
}

package() {
    a="${pkgdir}/apps${_apparch}/${_realname}"
    mkdir -p "${a}"
    cp -r "eac"/* "${a}"
    rm -r "${a}"/*.nsi "${a}"/'$pluginsdir' "${a}/microsoft.vc80.crt" "${a}/uninst.exe.nsis"

    d="${pkgdir}/docs${_apparch}/${_realname}"
    mkdir -p "${d}"
    mv "${a}/cdrdao/GPL license.txt" "${d}/cdrdao-license.txt"
    mv "${a}"/'[LICENSE].rtf' "${d}/license.rtf"
    mv "${a}"/*.rtf "${a}/documentation"/* "${d}"
    rm -r "${a}/documentation"
}
