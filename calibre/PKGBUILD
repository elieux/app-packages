# Maintainer: David Macek <david.macek.0@gmail.com>

_realname=calibre
_choconame=calibre
pkgbase="app-${_realname}"
pkgname=("app-x86_64-${_realname}")
epoch=1
pkgver=9.3.1
pkgrel=1
pkgdesc="Ebook management application"
url="https://calibre-ebook.com/"
arch=('any')
license=('GPL3')
replaces=("app-i686-${_realname}")
makedepends=("app-lessmsi")
provides=("${pkgbase}")
options=(!strip)
source=("calibre-64bit.msi::https://download.calibre-ebook.com/${pkgver}/calibre-64bit-${pkgver}.msi")
sha512sums=('1bbfd9e2f51581de1d7ca88dc857c050e3f10fffb4d22be5d3b4dc1dfdd441de60884fa4b9e974808b8573326f28aa80cd4ef302a9937e77cd143ecd36d65466')

_rmmsdll() {
    local ms

    ms=("msvbvm[0-9]+"
        "ucrtbase"
        "mfcm?[0-9]+[a-z]+"
        "msvc[rp][0-9]+(_([0-9]|codecvt_ids|atomic_wait))?"
        "vc([ao]mp|corlib|runtime)[0-9]+(_([0-9]|threads))?"
        "concrt[0-9]+"
        "pgort[0-9]+"
        "api-ms-win-[0-9a-z-]+")
    find "${1}" -iname '*.dll' | grep -Eif <(for m in "${ms[@]}"; do echo "/${m}[.]dll$"; done) | xargs -r rm
}

prepare() {
    rm -fR "calibre-64bit"
    /apps/lessmsi/lessmsi x "calibre-64bit.msi" > /dev/null
}

package() {
    local a="${pkgdir}/apps/${_realname}"
    mkdir -p "${a}"
    cp -R "calibre-64bit/SourceDir/PFiles64/Calibre2"/* "${a}"
    _rmmsdll "${a}"
}
