# Maintainer: David Macek <david.macek.0@gmail.com>

_realname=wireshark
pkgbase="app-${_realname}"
pkgname=("app-i686-${_realname}"
         "app-x86_64-${_realname}")
pkgver=2.2.4
pkgrel=1
pkgdesc="A free network protocol analyzer"
url="https://www.wireshark.org/"
arch=('any')
license=('GPL2')
makedepends=("p7zip")
provides=("${pkgbase}")
options=(!strip)
source=("Wireshark-win32.exe::https://1.eu.dl.wireshark.org/win32/Wireshark-win32-${pkgver}.exe"
        "Wireshark-win64.exe::https://1.eu.dl.wireshark.org/win64/Wireshark-win64-${pkgver}.exe")
sha512sums=('b2ab6263bd6ccc36c5b1944a84672111f2434799390cded2a58755942b954c7177f5e6426229bf9b8a3047ab0fdd6bbd8d6219899c8be5f5e2e3fd47718228b1'
            'a9c969143aae2495162b5f475702d5c5c67344e99fe928f35ed3e97ae8df61885ae18c74f0782c436a36138c706cd45b1fac31c6495db607248130875c5670c5')

_lc() {
    local d f n

    for f in "${@}"
    do
        d="$(dirname "${f}")"
        f="$(basename "${f}")"
        n="$(echo "${f}" | tr 'A-Z' 'a-z')"
        if test "${f}" != "${n}"
        then
            mv "${d}/${f}" "${d}/${n}"
        fi
    done
}

_prepare() {
    7z x -y -bd "Wireshark-win${_apparch}.exe" -o"Wireshark-win${_apparch}"
    _lc "Wireshark-win${_apparch}"/*
}

prepare() {
    for _apparch in 32 64
    do
        _prepare
    done
}

_package() {
    a="${pkgdir}/apps${_apparch}/${_realname}"
    mkdir -p "${a}"
    cp -r "Wireshark-win${_apparch}"/* "${a}"
    rm -r "${a}"/*.nsi "${a}"/winpcap_*.exe "${a}"/usbpcap*.exe "${a}/vcredist_${1}.exe" "${a}/uninstall.exe" "${a}"/'$pluginsdir'

    d="${pkgdir}/docs${_apparch}/${_realname}"
    mkdir -p "${d}"
    mv "${a}/help" "${a}"/*.{txt,chm,html,css} "${d}"
    mv "${d}/[license].txt" "${d}/license.txt"
}

package_app-i686-wireshark() {
    _apparch=32
    _package "x86"
}

package_app-x86_64-wireshark() {
    _apparch=64
    _package "x64"
}
