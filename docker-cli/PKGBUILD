# Maintainer: David Macek <david.macek.0@gmail.com>

_realver=46911
_realname=docker-cli
_dirname=docker
pkgbase="app-${_realname}"
pkgname=("app-x86_64-${_realname}")
epoch=1
pkgver=19.03.12
pkgrel=1
pkgdesc="Pack, ship and run any application as a lightweight container"
url="https://docs.docker.com/engine/reference/commandline/cli/"
arch=('any')
license=('Apache')
makedepends=("p7zip")
provides=("${pkgbase}")
replaces=("app-x86_64-docker")
conflicts=("app-x86_64-docker")
options=(!strip)
source=("DockerDesktopInstaller.exe::https://download.docker.com/win/stable/${_realver}/Docker%20Desktop%20Installer.exe")
sha512sums=('ddd84d4285bd5f2f4ff986282764fa0b4384bb0cdf7c739023171a9930c4bab27ff01dd7ae80c50a3cb7d28fa5c80f6da7a9a78c461d93d3129251310bab6548')

prepare() {
    7z x -y -bd "DockerDesktopInstaller.exe" -o"DockerDesktopInstaller.dir" > /dev/null
    7z x -y -bd "DockerDesktopInstaller.dir/.text" -o"text" > /dev/null
}

pkgver() {
    powershell -command '(Get-Item "text/resources/bin/docker.exe").VersionInfo.ProductVersion'
}

package() {
    local a="${pkgdir}/apps/${_dirname}"
    mkdir -p "${a}"
    cp "text/resources/bin/docker.exe" "${a}"
}
