# Maintainer: David Macek <david.macek.0@gmail.com>

_realname=xnview
_apparch=32
pkgbase="app-${_realname}"
pkgname=("app-i686-${_realname}")
pkgver=2.43
pkgrel=1
pkgdesc="An efficient multimedia viewer, browser and converter"
url="https://www.xnview.com/en/xnview/"
arch=('any')
license=('custom')
depends=("app-i686-nconvert")
makedepends=("unzip")
provides=("${pkgbase}")
options=(!strip)
source=("XnView-win-full.zip::http://download3.xnview.com/XnView-win-full.zip")
sha512sums=('4ba2bd347090a4fe1912f0c7abfb1b290dd72326b54854a2f7906dc0a014d7c95052ca57e5cbfa97b3b9b927a56c827cf155a4ca9463327aa4ca383ca1babd98')

_lc() {
    local d f n

    for f in "${@}"
    do
        d="$(dirname "${f}")"
        f="$(basename "${f}")"
        n="$(echo "${f}" | tr 'A-Z' 'a-z')"
        if test "${f}" != "${n}"
        then
            mv "${d}/${f}" "${d}/${n}"
        fi
    done
}

pkgver() {
    "${SYSTEMROOT}/System32/WindowsPowerShell/v1.0/powershell" -command '(Get-Item "XnView/xnview.exe").VersionInfo.ProductVersion'
}

prepare() {
    _lc "XnView"/*
}

package() {
    a="${pkgdir}/apps${_apparch}/${_realname}"
    mkdir -p "${a}"
    cp -R "XnView"/* "${a}"
    rm -R "${a}/nconvert.exe" "${a}/addon/Microsoft.VC90.CRT"

    d="${pkgdir}/docs${_apparch}/${_realname}"
    mkdir -p "${d}"
    mv "${a}"/*.{txt,url} "${a}/addon"/*.txt "${a}/filters"/*.txt "${a}/plugins"/*.txt "${d}"
    rm "${d}"/{formats,license,plugins,readme,usage,whatsnew}.txt
}
