# Maintainer: David Macek <david.macek.0@gmail.com>

DLAGENTS=('https::/usr/bin/curl -k -qb "" -fLC - -o %o %u')

_realname=dropbox
_apparch=32
pkgbase="app-${_realname}"
pkgname=("app-i686-${_realname}")
pkgver=3.10.8
pkgrel=1
pkgdesc="The Dropbox synchronization tool"
url="http://www.dropbox.com/"
arch=('any')
license=('custom')
makedepends=("p7zip" "curl")
provides=("${pkgbase}")
options=(!strip)
source=("DropboxOfflineInstaller.exe::https://www.dropbox.com/download?full=1&plat=win")
md5sums=('ada80329fb7e024b5f679deb3867e565')

_lc() {
    for f in "${@}"
    do
        n="$(echo "${f}" | tr 'A-Z' 'a-z')"
        if test "${f}" != "${n}"
        then
            mv "${f}" "${n}"
        fi
    done
}

pkgver() {
    7z x -y -bd "DropboxOfflineInstaller.exe" > /dev/null
    b="$(cygpath -wa "Installer.exe".* | sed 's/\\/\\\\/g')"
    wmic datafile where name=\""${b}"\" get version | grep 'Version' -A 1 | tail -1 | tr -d ' ' | sed -r 's/([0-9]+[.][0-9]+[.][0-9]+)[.]0$/\1/'
}

prepare() {
    7z x -y -bd "DropboxOfflineInstaller.exe"
    7z x -y -bd "Installer.exe".*
    _lc '$0'/*
}

package() {
    a="${pkgdir}/apps${_apparch}/${_realname}"
    mkdir -p "${a}"
    cp -r '$0'/* "${a}"
    rm -r "${a}/dropboxuninstaller.exe" "${a}/gdiplus.dll" "${a}"/msvc{p,r}120.dll "${a}"/{msvcp,vccorlib,vcruntime}140.dll
}
