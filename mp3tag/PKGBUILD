# Maintainer: David Macek <david.macek.0@gmail.com>

_realname=mp3tag
_choconame=mp3tag
pkgbase="app-${_realname}"
pkgname=("app-i686-${_realname}")
epoch=1
pkgver=3.08
pkgrel=1
pkgdesc="Powerful audio metadata editor"
arch=('any')
url="https://mp3tag.de/en/index.html"
license=('custom')
makedepends=("p7zip")
provides=("${pkgbase}")
options=(!strip)
source=("mp3tagsetup.exe::https://download.mp3tag.de/mp3tagv${pkgver//./}setup.exe")
sha512sums=('90ecb229dc49fba7e1765747b4a56ce3e0ed28f67f505078991f86a8709ef1e54722c6b943a953c7e96084d95b64484879193414b2988f50ad6a0401db094c3e')

_lc() {
    local d f n

    for f in "${@}"
    do
        d="$(dirname "${f}")"
        f="$(basename "${f}")"
        n="$(echo "${f}" | tr 'A-Z' 'a-z')"
        if test "${f}" != "${n}"
        then
            mv "${d}/${f}" "${d}/${n}"
        fi
    done
}

prepare() {
    local w f
    w="*license*.txt"
    7z x -y -bd -x!"${w}" "mp3tagsetup.exe" -o"mp3tagsetup.dir" > /dev/null
    for f in $(7z l -y -bd -i!"${w}" "mp3tagsetup.exe" | grep 'license.*txt' | sed -r 's/[ ]+/\t/g' | cut -f 5 | sed 's/^.*\\//')
    do
        7z e -y -bd -so -i!"*${f}" "mp3tagsetup.exe" > "mp3tagsetup.dir/${f}"
    done
}

package() {
    local a="${pkgdir}/apps/${_realname}"
    mkdir -p "${a}" "${a}/help"
    cp -R "mp3tagsetup.dir"/* "${a}"
    touch "${a}/help/helpmap.idx"
    rm -R "${a}"/'$PLUGINSDIR' "${a}"/'$R0' "${a}/mp3taguninstall.exe.nsis"
    _lc "${a}"/*
}
