# Maintainer: David Macek <david.macek.0@gmail.com>

_realname=opera
pkgbase="app-${_realname}"
pkgname=("app-i686-${_realname}"
         "app-x86_64-${_realname}")
pkgver=43.0.2442.1144
pkgrel=1
pkgdesc="A fast and free Blink-based web browser"
url="http://www.opera.com/computer"
arch=('any')
license=('custom')
makedepends=("p7zip")
provides=("${pkgbase}")
options=(!strip)
source=("Opera_Setup_x32.exe::http://ftp.opera.com/pub/opera/desktop/${pkgver}/win/Opera_${pkgver}_Setup.exe"
        "Opera_Setup_x64.exe::http://ftp.opera.com/pub/opera/desktop/${pkgver}/win/Opera_${pkgver}_Setup_x64.exe")
sha512sums=('cbe0d08397825bca7c303bf3336cae9396c511e4f963703d572322f892df3149b3b10e6c57e656dd14cb52db8760360bd36b3a6292e8f2a6747f5f532b6dc8f7'
            '79bce59a5cd7a927a6f134282e9fe37ab16450541b60822d405f4590cf9e3147c58d818e38ec85ab6f40371d3c7007e2f221772b4d1d2938ee93f179d95869bf')

_lc() {
    local d f n

    for f in "${@}"
    do
        d="$(dirname "${f}")"
        f="$(basename "${f}")"
        n="$(echo "${f}" | tr 'A-Z' 'a-z')"
        if test "${f}" != "${n}"
        then
            mv "${d}/${f}" "${d}/${n}"
        fi
    done
}

_prepare() {
    7z x -y -bd "Opera_Setup_x${_apparch}.exe" -o"Opera${_apparch}"
    _lc "Opera${_apparch}"/*
}

prepare() {
    for _apparch in 32 64
    do
        _prepare
    done
}

_package() {
    a="${pkgdir}/apps${_apparch}/${_realname}"
    mkdir -p "${a}"
    cp -r "Opera${_apparch}"/* "${a}"
    rm -r "${a}"/{assets,root_files_list,files_list,launcher.exe,launcher.visualelementsmanifest.xml,resources.pri} "${a}"/{msvcp100.dll,msvcr100.dll} "${a}"/{installer.exe,opera_autoupdate.exe,opera_autoupdate.licenses,opera_autoupdate.version}
    rm "${a}"/{msvcp,pgort,vccorlib,vcruntime}140.dll "${a}/ucrtbase.dll"
}

package_app-i686-opera() {
    replaces=("app-i686-opera2")
    conflicts=("app-i686-${_realname}<15")
    _apparch=32
    _package
}

package_app-x86_64-opera() {
    replaces=("app-x86_64-opera2")
    conflicts=("app-x86_64-${_realname}<15")
    _apparch=64
    _package
}
