# Maintainer: David Macek <david.macek.0@gmail.com>

_realname=wireshark
pkgbase="app-${_realname}"
pkgname=("app-x86_64-${_realname}")
epoch=1
pkgver=3.3.0
pkgrel=1
pkgdesc="A free network protocol analyzer"
url="https://www.wireshark.org/"
arch=('any')
license=('GPL2')
replaces=("app-i686-${_realname}")
makedepends=("p7zip")
provides=("${pkgbase}")
options=(!strip)
source=("Wireshark-win64.exe::https://1.eu.dl.wireshark.org/win64/Wireshark-win64-${pkgver}.exe")
sha512sums=('362ba0849e351f6cb0ae27de8ced64304a9df061ecd99d04975f6c4378e31f772e072e924e342790cae41e5daa47fed6126d8c1f03cae8f90a229cc82de95d93')

_lc() {
    local d f n

    for f in "${@}"
    do
        d="$(dirname "${f}")"
        f="$(basename "${f}")"
        n="$(echo "${f}" | tr 'A-Z' 'a-z')"
        if test "${f}" != "${n}"
        then
            mv "${d}/${f}" "${d}/${n}"
        fi
    done
}

prepare() {
    7z x -y -bd "Wireshark-win64.exe" -o"Wireshark-win64.dir" > /dev/null
}

package() {
    local a="${pkgdir}/apps/${_realname}"
    mkdir -p "${a}"
    cp -R "Wireshark-win64.dir"/* "${a}"
    rm -R "${a}"/npcap-*.exe "${a}"/USBPcapSetup-*.exe "${a}/vcredist_x64.exe" "${a}/uninstall.exe" "${a}"/'$PLUGINSDIR'
    _lc "${a}"/*
}
