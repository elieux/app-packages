# Maintainer: David Macek <david.macek.0@gmail.com>

_realname=hwmonitor
_dirname=cpuid
pkgbase="app-${_realname}"
pkgname=("app-i686-${_realname}"
         "app-x86_64-${_realname}")
pkgver=1.30
pkgrel=1
pkgdesc="Reads PC systems main health sensors (CPUID)"
url="http://www.cpuid.com/softwares/hwmonitor.html"
arch=('any')
license=('custom')
makedepends=("unzip")
provides=("${pkgbase}")
options=(!strip)
source=("hwmonitor.zip::http://download.cpuid.com/${_realname}/${_realname}_${pkgver}.zip")
sha512sums=('c8271e5a3a467f5f79b6304e15cc6d3108c799dc6ff4599a943a6ce5fc616809b510301a95c928ee820fd5371e91025aefdbd568c8fd0c9e4726ddca59fe573d')

_lc() {
    local d f n

    for f in "${@}"
    do
        d="$(dirname "${f}")"
        f="$(basename "${f}")"
        n="$(echo "${f}" | tr 'A-Z' 'a-z')"
        if test "${f}" != "${n}"
        then
            mv "${d}/${f}" "${d}/${n}"
        fi
    done
}

prepare() {
    _lc *
}

_package() {
    a="${pkgdir}/apps${_apparch}/${_dirname}"
    mkdir -p "${a}"
    cp "hwmonitor_x${_apparch}.exe" "${a}/hwmonitor.exe"
}

package_app-i686-hwmonitor() {
    _apparch=32
    _package
}

package_app-x86_64-hwmonitor() {
    _apparch=64
    _package
}
