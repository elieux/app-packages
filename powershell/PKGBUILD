# Maintainer: David Macek <david.macek.0@gmail.com>

_realname=powershell
pkgbase="app-${_realname}"
pkgname=("app-x86_64-${_realname}")
epoch=1
pkgver=6.2.3
pkgrel=1
pkgdesc="A cross-platform automation and configuration tool/framework"
url="https://github.com/PowerShell/PowerShell"
arch=('any')
license=('MIT')
makedepends=("unzip")
provides=("${pkgbase}")
options=(!strip)
source=("PowerShell-win-x64.zip::https://github.com/PowerShell/PowerShell/releases/download/v${pkgver}/PowerShell-${pkgver}-win-x64.zip")
sha512sums=('cb274f3f3a7273f8574d95e1d5ea647007513c206ceddc1aa56f7b1e4e906179ad5a5e2a5e788d57652053975be9202453e5dd32d1d8640bcb6e267296eef747')

_rmmsdll() {
    local ms

    ms=("msvbvm[0-9]+"
        "ucrtbase"
        "mfcm?[0-9]+[a-z]+"
        "msvc[rp][0-9]+"
        "vc([ao]mp|corlib|runtime)[0-9]+"
        "concrt[0-9]+"
        "pgort[0-9]+"
        "api-ms-win-[0-9a-z-]+")
    find "${1}" -iname '*.dll' | grep -Eif <(for m in "${ms[@]}"; do echo "/${m}[.]dll$"; done) | xargs -r rm
}

package() {
    a="${pkgdir}/apps/${_realname}"
    mkdir -p "${a}"
    cp -R * "${a}"
    rm "${a}/PowerShell-win-x64.zip"
    cat pwsh.deps.json | sed -r '/(api-ms-win|API-MS-Win|ucrtbase)/,/[}]/d' | sed -r '/[}],/{ N; s/[}],(\n +[}])/}\1/ }' | unix2dos > "${a}/pwsh.deps.json"
    _rmmsdll "${a}"
}
