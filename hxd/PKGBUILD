# Maintainer: David Macek <david.macek.0@gmail.com>

_realname=hxd
pkgbase="app-${_realname}"
pkgname=("app-x86_64-${_realname}")
epoch=1
pkgver=2.4.0.0
pkgrel=1
pkgdesc="Freeware hex editor and disk editor"
arch=('any')
url="https://mh-nexus.de/en/hxd/"
license=('custom')
replaces=("app-i686-${_realname}")
makedepends=("app-innounp")
provides=("${pkgbase}")
options=(!strip)
source=("HxDSetup.zip::https://mh-nexus.de/downloads/HxDSetup.zip")
sha512sums=('33714bf909de383b2df6c7b860634cad0f1c0f6954753076c417feddc1477f506bc887b1b867a9ca2945fe90e6fd0ca70bbf74a63999a818e394d60cff59df99')

_lc() {
    local d f n

    for f in "${@}"
    do
        d="$(dirname "${f}")"
        f="$(basename "${f}")"
        n="$(echo "${f}" | tr 'A-Z' 'a-z')"
        if test "${f}" != "${n}"
        then
            mv "${d}/${f}" "${d}/${n}"
        fi
    done
}

prepare() {
    /apps/innounp/innounp -x -q -m -a -y "HxDSetup.exe" -d"HxDSetup.dir"
}

pkgver() {
    powershell -command '(Get-Item "HxDSetup.exe").VersionInfo.ProductVersionRaw.ToString()'
}

package() {
    local a="${pkgdir}/apps/${_realname}"
    mkdir -p "${a}"
    cp "HxDSetup.dir/{app}"/* "${a}"

    # work around a runtime error because the program thinks it's in portable mode
    mkdir "${a}/Settings"
    touch "${a}/Settings/HxD Hex Editor.ini"
    echo -n "ENU" > "${a}/Settings/HxD Hex Editor.lang"

    local exe
    exe="$(cat "HxDSetup.dir/install_script.iss" | grep '^Source: ' | grep 'Languages: "enu"' | grep 'Check: "not IsInPortableMode and IsX64InstallMode"' | sed -r 's/^Source: "[{]app[}]\\(HxD[0-9,]+.exe)".*$/\1/')"
    rm "${a}"/HxD*.exe
    cp "HxDSetup.dir/{app}/${exe}" "${a}/HxD.exe"

    _lc "${a}"/*
}
