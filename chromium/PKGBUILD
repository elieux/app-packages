# Maintainer: David Macek <david.macek.0@gmail.com>

_realname=chromium
_choconame=chromium
_realver=1036826
pkgbase="app-${_realname}"
pkgname=("app-x86_64-${_realname}")
epoch=1
pkgver=106.0.5249.91
pkgrel=1
pkgdesc="The open-source project behind Google Chrome, an attempt at creating a safer, faster, and more stable browser"
url="https://www.chromium.org/Home"
arch=('any')
license=('BSD')
replaces=("app-i686-${_realname}")
provides=("${pkgbase}")
options=(!strip)
source=("chrome.sync.7z::https://github.com/Hibbiki/chromium-win64/releases/download/v${pkgver}-r${_realver}/chrome.sync.7z")
sha512sums=('c0e41a700af10eab5767b42f5d5f9a8fbb4e223565b1f3b126ee071ee28a6c6d8826d72b04b9a9ccedd9116d2af58d2a9018b1ac68ec896d7d03ec9b16c3f5c9')

_lc() {
    local d f n

    for f in "${@}"
    do
        d="$(dirname "${f}")"
        f="$(basename "${f}")"
        n="$(echo "${f}" | tr 'A-Z' 'a-z')"
        if test "${f}" != "${n}"
        then
            mv "${d}/${f}" "${d}/${n}"
        fi
    done
}

package() {
    local a="${pkgdir}/apps/${_realname}"
    mkdir -p "${a}"
    cp -R "Chrome-bin"/* "${a}"
    mv "${a}/${pkgver}"/* "${a}"
    rm -R "${a}/${pkgver}"
    _lc "${a}"/*
}
