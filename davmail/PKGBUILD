# Maintainer: David Macek <david.macek.0@gmail.com>

_realname=davmail
_realver=4.7.2-2427
pkgbase="app-${_realname}"
pkgname=("app-i686-${_realname}"
         "app-x86_64-${_realname}")
pkgver=${_realver//-/.}
pkgrel=1
pkgdesc="A gateway allowing usage of any mail/calendar client with an Exchange server"
url="http://davmail.sourceforge.net/"
arch=('any')
license=('GPL2')
makedepends=("unzip")
provides=("${pkgbase}")
options=(!strip)
source=("davmail-windows-noinstall.zip::http://downloads.sourceforge.net/${_realname}/${_realname}/${_realver%%-*}/${_realname}-${_realver}-windows-noinstall.zip")
sha512sums=('f7efe5b7a54821138f969f89fa3a1d179d3d91c5fd89c8d868fd189575d5d7df838d3725aecd5de2371cc31f6dcd5cfdaccbb45ad4afa745ccc2719494e97ef9')

_package() {
    a="${pkgdir}/apps${_apparch}/${_realname}"
    mkdir -p "${a}"
    cp -r "lib" "davmail.jar" "davmailconsole.exe" "${a}"
    for f in "davmail" "davmailservice"
    do
        cp "${f}${1}.exe" "${a}/${f}.exe"
    done
}

package_app-i686-davmail() {
    _apparch=32
    _package ""
}

package_app-x86_64-davmail() {
    _apparch=64
    _package "64"
}
