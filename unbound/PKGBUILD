# Maintainer: David Macek <david.macek.0@gmail.com>

_realname=unbound
pkgbase="app-${_realname}"
pkgname=("app-x86_64-${_realname}")
epoch=1
pkgver=1.9.4
pkgrel=1
pkgdesc="Validating, recursive, and caching DNS resolver"
arch=('any')
url="https://nlnetlabs.nl/projects/unbound/about/"
license=('custom:BSD')
replaces=("app-i686-${_realname}")
makedepends=("p7zip")
provides=("${pkgbase}")
options=(!strip)
source=("unbound_setup.exe::https://nlnetlabs.nl/downloads/unbound/unbound_setup_${pkgver}.exe")
sha512sums=('9b90fde1d82ddfe6d1567f36824df7d29792c0bc1dda97798c49be018cab3fa2c4b3c155dfd8417856d0bc419f080b105197deaae4764d32132bcc3fa103051f')

_lc() {
    local d f n

    for f in "${@}"
    do
        d="$(dirname "${f}")"
        f="$(basename "${f}")"
        n="$(echo "${f}" | tr 'A-Z' 'a-z')"
        if test "${f}" != "${n}"
        then
            mv "${d}/${f}" "${d}/${n}"
        fi
    done
}

prepare() {
    7z x -bd "unbound_setup.exe" -o"unbound_setup"
    _lc "unbound_setup"/*
}

package() {
    a="${pkgdir}/apps/${_realname}"
    mkdir -p "${a}"
    cp -R "unbound_setup"/* "${a}"
    rm -R "${a}"/'$pluginsdir'
}
