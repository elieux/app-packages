# Maintainer: David Macek <david.macek.0@gmail.com>

_realname=musescore
pkgbase="app-${_realname}"
pkgname=("app-x86_64-${_realname}")
epoch=1
pkgver=3.5.1
pkgrel=1
pkgdesc="Create, play and print beautiful sheet music"
url="https://musescore.org/"
arch=('any')
license=('GPL')
replaces=("app-i686-${_realname}")
makedepends=("app-lessmsi")
provides=("${pkgbase}")
options=(!strip)
source=("MuseScore-x86_64.msi::https://github.com/musescore/MuseScore/releases/download/v${pkgver%.0}/MuseScore-${pkgver}-x86_64.msi")
sha512sums=('0031e3e86cbe439a7ac9344912bea52bfb0259d408d83f9f5ccd456a30fd3117247e4423413bf76091bb2910e6c86d4037f26e323b3a6eabae66f3b113d879b4')

_rmmsdll() {
    local ms

    ms=("msvbvm[0-9]+"
        "ucrtbase"
        "mfcm?[0-9]+[a-z]+"
        "msvc[rp][0-9]+(_([0-9]|codecvt_ids))?"
        "vc([ao]mp|corlib|runtime)[0-9]+(_[0-9])?"
        "concrt[0-9]+"
        "pgort[0-9]+"
        "api-ms-win-[0-9a-z-]+")
    find "${1}" -iname '*.dll' | grep -Eif <(for m in "${ms[@]}"; do echo "/${m}[.]dll$"; done) | xargs -r rm
}

prepare() {
    rm -fR "MuseScore-x86_64"
    /apps/lessmsi/lessmsi x "MuseScore-x86_64.msi" > /dev/null
}

package() {
    local a="${pkgdir}/apps/${_realname}"
    mkdir -p "${a}"
    cp -R "MuseScore-x86_64/SourceDir/MuseScore 3"/* "${a}"
    _rmmsdll "${a}"
}
