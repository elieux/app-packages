# Maintainer: David Macek <david.macek.0@gmail.com>

_dirname=android-sdk
_realname=android-sdk-platform-tools
pkgbase="app-${_realname}"
pkgname=("app-i686-${_realname}")
epoch=1
pkgver=36.0.0
pkgrel=1
pkgdesc="Platform-Tools for Google Android SDK (adb and fastboot)"
url="https://developer.android.com/studio/releases/platform-tools.html"
arch=('any')
license=('custom')
provides=("${pkgbase}")
options=(!strip)
source=("platform-tools-latest-windows.zip::https://dl.google.com/android/repository/platform-tools-latest-windows.zip")
sha512sums=('85811868169617ed6cc3104502cf48a71c835ddcb18cd5590ca06a6ecb3ebcd9f7ff5a038b3c9095ddf7a561d92aaec29d1dd580dd32f10871bc901d8e5627e2')

_lc() {
    local d f n

    for f in "${@}"
    do
        d="$(dirname "${f}")"
        f="$(basename "${f}")"
        n="$(echo "${f}" | tr 'A-Z' 'a-z')"
        if test "${f}" != "${n}"
        then
            mv "${d}/${f}" "${d}/${n}"
        fi
    done
}

pkgver() {
    cat "platform-tools/source.properties" | grep "^Pkg.Revision" | cut -d= -f2
}

package() {
    local a="${pkgdir}/apps/${_dirname}"
    mkdir -p "${a}"
    cp -R "platform-tools" "${a}"
    _lc "${a}/platform-tools"/*
}
