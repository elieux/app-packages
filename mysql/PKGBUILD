# Maintainer: David Macek <david.macek.0@gmail.com>

_realname=mysql
pkgbase="app-${_realname}"
pkgname=("app-i686-${_realname}"
         "app-x86_64-${_realname}")
pkgver=5.7.16
pkgrel=1
pkgdesc="Fast SQL database server, community edition"
url="https://www.mysql.com/products/community/"
arch=('any')
license=('GPL')
makedepends=("unzip")
provides=("${pkgbase}")
options=(!strip)
source=("mysql-win32.zip::https://dev.mysql.com/get/Downloads/MySQL-${pkgver%.*}/mysql-${pkgver}-win32.zip"
        "mysql-winx64.zip::https://dev.mysql.com/get/Downloads/MySQL-${pkgver%.*}/mysql-${pkgver}-winx64.zip")
sha512sums=('3be73c9be35c63b8250f71301dc79a28022576ab2a6134dcc191d0093f32c8efd7664547c61e55b3f5bb9604af02fb2e6c01e90efe988589208af96e24e00c32'
            '6a735f6161a1e354e5818c192cdc74e831956ce0d584ff99bcc3a676abedd2bfaa7d16a7ab1a03824c1a391df37ff6684815b6ef1b3c549a1a7426754f4eed07')

_package() {
    a="${pkgdir}/apps${_apparch}/${_realname}"
    mkdir -p "${a}"
    cp -r "mysql-${pkgver}-win${1}"/* "${a}"
    find "${a}" -name 'debug' -exec rm -r '{}' \; -prune
    find "${a}" -name '*.pdb' -delete
    find "${a}" -name '*.lib' -delete

    d="${pkgdir}/docs${_apparch}/${_realname}"
    mkdir -p "${d}"
    mv "${a}"/{docs,COPYING,README} "${a}"/*.ini "${d}"
}

package_app-i686-mysql() {
    _apparch=32
    _package "32"
}

package_app-x86_64-mysql() {
    _apparch=64
    _package "x64"
}
