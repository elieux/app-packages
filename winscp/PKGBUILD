# Maintainer: David Macek <david.macek.0@gmail.com>

_realname=winscp
_apparch=32
pkgbase="app-${_realname}"
pkgname=("app-i686-${_realname}")
pkgver=5.9.5
pkgrel=1
pkgdesc="An open source two-pane SFTP/FTP/WebDAV/SCP client"
url="http://winscp.net/"
arch=('any')
license=('custom')
makedepends=("app-i686-innounp"
             "unzip")
provides=("${pkgbase}")
options=(!strip)
source=("winscpsetup.exe::http://download.sourceforge.net/winscp/WinSCP-${pkgver}-Setup.exe"
        "winscp.zip::http://download.sourceforge.net/winscp/WinSCP-${pkgver}-Portable.zip"
        "winscpautomation.zip::http://download.sourceforge.net/winscp/WinSCP-${pkgver}-Automation.zip")
sha512sums=('48bf991e148b2e6beb3205b1cc4c85b08d33a2afd42af1c5815e470db4759dd8f1e7fdfc6e4f130164ffcadf974b58bd9ae98a6fcd33f791033cb96e8e5b580d'
            '7f2278ec0321585a0c4414085213899932b6871df9fab5d483d2859cd83333b8e71f565ca4c5108e20608272df99047a64520862f4b4b3618dc2100c8d5cf29f'
            'c9d6dd8a74bd7de24bf9810ea81a58e24583cc884cc376700640628e72fd8b3d6bab3c9a1954bc08f23c1ea12f92d042b63cfd41fae4959b059da749cc350a8a')

_lc() {
    local d f n

    for f in "${@}"
    do
        d="$(dirname "${f}")"
        f="$(basename "${f}")"
        n="$(echo "${f}" | tr 'A-Z' 'a-z')"
        if test "${f}" != "${n}"
        then
            mv "${d}/${f}" "${d}/${n}"
        fi
    done
}

prepare() {
    /apps32/innounp/innounp -x -q -m -a -y "winscpsetup.exe"
    _lc '{app}'/* *
}

package() {
    a="${pkgdir}/apps${_apparch}/${_realname}"
    mkdir -p "${a}"
    cp -r "winscpnet.dll" '{app}'/dragext*.dll '{app}'/winscp.* '{app}'/"extensions"  "${a}"
    rm "${a}/winscp.map"

    d="${pkgdir}/docs${_apparch}/${_realname}"
    mkdir -p "${d}"
    cp *.txt "${d}"
}
