# Maintainer: David Macek <david.macek.0@gmail.com>

_realname=docker-cli
_realver=78933
_choconame=docker-cli
_dirname=docker
pkgbase="app-${_realname}"
pkgname=("app-x86_64-${_realname}")
epoch=1
pkgver=20.10.14
pkgrel=1
pkgdesc="Pack, ship and run any application as a lightweight container"
url="https://docs.docker.com/engine/reference/commandline/cli/"
arch=('any')
license=('Apache')
makedepends=("p7zip")
provides=("${pkgbase}")
replaces=("app-x86_64-docker")
conflicts=("app-x86_64-docker")
options=(!strip)
source=("DockerDesktopInstaller.exe::https://desktop.docker.com/win/main/amd64/${_realver}/Docker%20Desktop%20Installer.exe")
sha512sums=('e3d42e4f69d199a76eb5fc4298a510c71990451409ac1299109a63593dbd06c62d42948b8e03a9c186fb49dfd3470b211229b119cefac7a2ddd86417aff9de02')

prepare() {
    7z x -y -bd "DockerDesktopInstaller.exe" -o"DockerDesktopInstaller.dir" > /dev/null
}

pkgver() {
    powershell -command '(Get-Item "DockerDesktopInstaller.dir/resources/bin/com.docker.cli.exe").VersionInfo.ProductVersion'
}

package() {
    local a="${pkgdir}/apps/${_dirname}"
    mkdir -p "${a}"
    cp "DockerDesktopInstaller.dir/resources/bin/com.docker.cli.exe" "${a}/docker.exe"
}
