# Maintainer: David Macek <david.macek.0@gmail.com>

_realname=pc-wizard
_dirname=cpuid
_apparch=32
pkgbase="app-${_realname}"
pkgname=("app-i686-${_realname}")
pkgver=2014.02.13
pkgrel=1
pkgdesc="Analyzes and benchmarks your computer system (CPUID)"
url="http://www.cpuid.com/softwares/pc-wizard.html"
arch=('any')
license=('custom')
makedepends=("unzip")
provides=("${pkgbase}")
options=(!strip) # digital signatures
source=("pc-wizard.zip::http://www.cpuid.com/medias/files/softwares/${_realname}/${_realname}_${pkgver//.0/.}.zip")
md5sums=('f0002f6e691ea0edd2760f09ebf69af3')

prepare() {
    cd "${srcdir}"

    for f in *
    do
        n=$(echo "${f}" | tr 'A-Z' 'a-z')
        if test "${f}" != "${n}"
        then
            mv "${f}" "${n}"
        fi
    done
}

package() {
    mkdir -p "${pkgdir}/apps${_apparch}/${_dirname}"
    mkdir -p "${pkgdir}/docs${_apparch}/${_dirname}/${_realname}"
    cp -r "${srcdir}"/{data,extras,language,mark,partner,web} "${pkgdir}/apps${_apparch}/${_dirname}/"
    cp "${srcdir}/pc wizard.exe" "${pkgdir}/apps${_apparch}/${_dirname}/pc-wizard.exe"
    cp "${srcdir}"/*.{sys,dll,vxd} "${pkgdir}/apps${_apparch}/${_dirname}/"
    rm "${pkgdir}/apps${_apparch}/${_dirname}/msvbvm60.dll"
    cp "${srcdir}"/*.txt "${pkgdir}/docs${_apparch}/${_dirname}/${_realname}/"
}
