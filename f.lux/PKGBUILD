# Maintainer: David Macek <david.macek.0@gmail.com>

_realname=f.lux
_choconame=f.lux
pkgbase="app-${_realname}"
pkgname=("app-i686-${_realname}")
epoch=1
pkgver=4.120
pkgrel=1
pkgdesc="Changes the display color temperature according to the time of the day"
url="https://justgetflux.com/"
arch=('any')
license=('custom')
makedepends=("p7zip")
provides=("${pkgbase}")
replaces=("app-i686-f-lux")
options=(!strip)
source=("flux-setup.exe::https://justgetflux.com/flux-setup.exe")
sha512sums=('63ca2946efb48a281f583eab29dcf9fe177eae4c05157d314842504ae7ab5c94dcbf263fd5a6037a3cdbfda29b3475115b765b77bbb5f9955567eab4dae83d9a')

prepare() {
    7z x -y -bd "flux-setup.exe" -o"flux-setup.dir" > /dev/null
}

pkgver() {
    powershell -command '(Get-Item "flux-setup.dir/flux.exe").VersionInfo.FileVersionRaw.ToString()' | sed -r 's/^([0-9]+[.][0-9]+)[.]0[.]0$/\1/'
}

package() {
    local a="${pkgdir}/apps/${_realname}"
    mkdir -p "${a}"
    cp -R "flux-setup.dir"/* "${a}"
    rm -R "${a}"/'$PLUGINSDIR' "${a}/runtime/uninstall.exe.nsis"
}
