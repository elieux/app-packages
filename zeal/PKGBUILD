# Maintainer: David Macek <david.macek.0@gmail.com>

_realname=zeal
_choconame=zeal
pkgbase="app-${_realname}"
pkgname=("app-x86_64-${_realname}")
epoch=1
pkgver=0.7.2
pkgrel=1
pkgdesc="An offline documentation browser for software developers"
url="https://zealdocs.org/"
arch=('any')
license=('GPL3')
replaces=("app-i686-${_realname}")
makedepends=("app-lessmsi")
provides=("${pkgbase}")
options=(!strip)
source=("zeal-windows-x64.msi::https://github.com/zealdocs/zeal/releases/download/v${pkgver}/zeal-${pkgver}-windows-x64.msi")
sha512sums=('fe3f06506ebd028a0beb524d2388a2c251a634014690220d3ca6d4ff9b118803a7c868d982dc60b888fec9ca89d995afef0cdff978409307915261d741bb580e')

_lc() {
    local d f n

    for f in "${@}"
    do
        d="$(dirname "${f}")"
        f="$(basename "${f}")"
        n="$(echo "${f}" | tr 'A-Z' 'a-z')"
        if test "${f}" != "${n}"
        then
            mv "${d}/${f}" "${d}/${n}"
        fi
    done
}

prepare() {
    rm -fR "zeal-windows-x64"
    /apps/lessmsi/lessmsi x "zeal-windows-x64.msi" > /dev/null
}

package() {
    local a="${pkgdir}/apps/${_realname}"
    mkdir -p "${a}"
    cp -R "zeal-windows-x64/SourceDir/Zeal"/* "${a}"
    _lc "${a}"/*
}
