# Maintainer: David Macek <david.macek.0@gmail.com>

_realname=caprine
_choconame=caprine
pkgbase="app-${_realname}"
pkgname=("app-i686-${_realname}")
epoch=1
pkgver=2.61.0
pkgrel=1
pkgdesc="Elegant Facebook Messenger desktop app."
url="https://sindresorhus.com/caprine"
arch=('any')
license=('MIT')
makedepends=("p7zip")
provides=("${pkgbase}")
options=(!strip)
source=("Caprine-Setup.exe::https://github.com/sindresorhus/caprine/releases/download/v${pkgver}/Caprine-Setup-${pkgver}.exe")
sha512sums=('73ab2352856ded09ab2cb372e8655221fcffd9978f93d3f265485e71f837650d0b630d70ab9759cdc2bfe5580f04939cd2b7121eecf0729e2ad5875712f7f649')
noextract=("Caprine-Setup.exe")

_lc() {
    local d f n

    for f in "${@}"
    do
        d="$(dirname "${f}")"
        f="$(basename "${f}")"
        n="$(echo "${f}" | tr 'A-Z' 'a-z')"
        if test "${f}" != "${n}"
        then
            mv "${d}/${f}" "${d}/${n}"
        fi
    done
}

prepare() {
    7z x -bd "Caprine-Setup.exe" -o"Caprine-Setup" > /dev/null
    7z x -bd "Caprine-Setup"/'$PLUGINSDIR'/"app-64.7z" -o"app-64" > /dev/null
}

package() {
    local a="${pkgdir}/apps/${_realname}"
    mkdir -p "${a}"
    cp -R "app-64"/* "${a}"
    _lc "${a}"/*
}
