# Maintainer: David Macek <david.macek.0@gmail.com>

_realname=franz
_choconame=franz
pkgbase="app-${_realname}"
pkgname=("app-x86_64-${_realname}")
epoch=1
pkgver=5.10.0
pkgrel=1
pkgdesc="Franz is a free messaging app and combines chat & messaging services into one application"
url="https://meetfranz.com/"
arch=('any')
license=('custom')
replaces=("app-i686-${_realname}")
makedepends=("p7zip")
provides=("${pkgbase}")
options=(!strip)
source=("franz-setup.exe::https://github.com/meetfranz/franz/releases/download/v${pkgver}/franz-setup-${pkgver}.exe")
sha512sums=('cb77cb0f64f8be2e8caad2c34399090cd13b392b9d9acb3359d3b38b4840ecfc74ff29b2309422825c9d80ecc8e265062901b67dfbdd4f4be7082f45c74cfe74')
noextract=("franz-setup.exe")

_lc() {
    local d f n

    for f in "${@}"
    do
        d="$(dirname "${f}")"
        f="$(basename "${f}")"
        n="$(echo "${f}" | tr 'A-Z' 'a-z')"
        if test "${f}" != "${n}"
        then
            mv "${d}/${f}" "${d}/${n}"
        fi
    done
}

prepare() {
    7z x -y -bd "franz-setup.exe" -o"franz-setup.dir" > /dev/null
    7z x -y -bd "franz-setup.dir"/'$PLUGINSDIR'/"app-64.7z" -o"app-64" > /dev/null
}

package() {
    local a="${pkgdir}/apps/${_realname}"
    mkdir -p "${a}"
    cp -R "app-64"/* "${a}"
    _lc "${a}"/*
}
