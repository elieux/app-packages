# Maintainer: David Macek <david.macek.0@gmail.com>

_realname=youtube-music
_choconame=th-ch-youtube-music
pkgbase="app-${_realname}"
pkgname=("app-x86_64-${_realname}")
epoch=1
pkgver=1.12.2
pkgrel=1
pkgdesc="Unofficial YouTube Music Desktop App with built-in ad blocker and downloader"
url="https://th-ch.github.io/youtube-music/"
arch=('any')
license=('MIT')
makedepends=("p7zip")
provides=("${pkgbase}")
options=(!strip)
source=("YouTube-Music-Setup.exe::https://github.com/th-ch/youtube-music/releases/download/v${pkgver}/YouTube-Music-Setup-${pkgver}.exe")
sha512sums=('a9cc85536051aafb097f91d5c586638596e2e429a494422c995c8d43762661852287534a776400907880f62e534d4a7004fb05c8c2bfdafb649851fec5469c54')

_lc() {
    local d f n

    for f in "${@}"
    do
        d="$(dirname "${f}")"
        f="$(basename "${f}")"
        n="$(echo "${f}" | tr 'A-Z' 'a-z')"
        if test "${f}" != "${n}"
        then
            mv "${d}/${f}" "${d}/${n}"
        fi
    done
}

prepare() {
    7z x -bd "YouTube-Music-Setup.exe" -o"YouTube-Music-Setup" > /dev/null
    7z x -bd "YouTube-Music-Setup"/'$PLUGINSDIR'/"app-64.7z" -o"app-64" > /dev/null
}

package() {
    local a="${pkgdir}/apps/${_realname}"
    mkdir -p "${a}"
    cp -R "app-64"/* "${a}"
    mv "${a}/YouTube Music.exe" "${a}/YouTube-Music.exe"
    _lc "${a}"/*
}
