# Maintainer: David Macek <david.macek.0@gmail.com>

_realname=ketarin
pkgbase="app-${_realname}"
pkgname=("app-i686-${_realname}")
epoch=1
pkgver=1.8.11
pkgrel=1
pkgdesc="A small application which automatically updates setup packages"
url="https://ketarin.org"
arch=('any')
license=('custom')
makedepends=("unzip")
provides=("${pkgbase}")
options=(!strip)
source=("Ketarin.zip::https://ketarin.org/downloads/Ketarin/Ketarin-${pkgver}.zip?noredirect")
sha512sums=('e34a7d07d7891bf70e3331862afcdf63136a7cc74682d39035b7d3ce0608609e1cfa225ba9e9b4cc72b735562921eddb67ed94b46deb9c6af3931d20b229e0bb')

_lc() {
    local d f n

    for f in "${@}"
    do
        d="$(dirname "${f}")"
        f="$(basename "${f}")"
        n="$(echo "${f}" | tr 'A-Z' 'a-z')"
        if test "${f}" != "${n}"
        then
            mv "${d}/${f}" "${d}/${n}"
        fi
    done
}

prepare() {
    _lc *
}

package() {
    a="${pkgdir}/apps/${_realname}"
    mkdir -p "${a}"
    cp *.{exe,dll,config} "${a}"
}
