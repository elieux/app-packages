# Maintainer: David Macek <david.macek.0@gmail.com>

_realname=winmerge
pkgbase="app-${_realname}"
pkgname=("app-x86_64-${_realname}")
epoch=1
pkgver=2.16.6
pkgrel=1
pkgdesc="A differencing and merging tool"
url="https://winmerge.org/"
arch=('any')
license=('GPL')
replaces=("app-i686-${_realname}" "app-i686-winmerge-plugin-7z")
makedepends=("app-innounp")
provides=("${pkgbase}" "app-winmerge-plugin-7z" "app-x86_64-winmerge-plugin-7z")
options=(!strip)
source=("WinMerge-Setup.exe::https://downloads.sourceforge.net/winmerge/WinMerge-${pkgver}-Setup.exe"
        "winmerge-x64-exe.zip::https://downloads.sourceforge.net/winmerge/winmerge-${pkgver}-x64-exe.zip")
sha512sums=('99b3a758f147661a20e59c59167bacfee0514a6898473ab639a139c1e2b17211355b3e8417a9bbd708df5093602dc1dc2f6a18a5dc248f78b8a0e759150245fe'
            '3858083254db0eabf8a225804230151a0a1d7cb62d3dc148f19406c16b9c8a099081f657ae01421c755939a2ae9822d05bcb5deceae3b77bc4e928ba25c2e134')

_lc() {
    local d f n

    for f in "${@}"
    do
        d="$(dirname "${f}")"
        f="$(basename "${f}")"
        n="$(echo "${f}" | tr 'A-Z' 'a-z')"
        if test "${f}" != "${n}"
        then
            mv "${d}/${f}" "${d}/${n}"
        fi
    done
}

prepare() {
    /apps/innounp/innounp -x -q -m -a -y "WinMerge-Setup.exe" -d"WinMerge-Setup.dir"
}

package() {
    local a="${pkgdir}/apps/${_realname}"
    mkdir -p "${a}"
    cp -R "WinMerge"/* "${a}"
    cp -R "WinMerge-Setup.dir/{app}/LogoImages" "WinMerge-Setup.dir/{app}/WinMergeU.VisualElementsManifest.xml" "${a}"
    rm "${a}/vcomp140.dll"
    _lc "${a}"/*
}
