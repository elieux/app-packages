# Maintainer: David Macek <david.macek.0@gmail.com>

_realname=carotdav
_apparch=32
pkgbase="app-${_realname}"
pkgname=("app-i686-${_realname}")
pkgver=1.11.11
pkgrel=3
pkgdesc="A simple WebDAV, FTP, cloud drives and IMAP client"
url="http://rei.to/carotdav_en.html"
arch=('any')
license=('custom')
makedepends=("unzip")
provides=("${pkgbase}")
options=(!strip)
source=("CarotDAV.portable.zip::http://rei.to/CarotDAV${pkgver}.portable.zip"
        "installed.txt")
md5sums=('4b49839783a6495c3bc238a89ebbede7'
         'ea6667b547144212c03bb2b498fcea8a')

_lc() {
    for f in "${@}"
    do
        n="$(echo "${f}" | tr 'A-Z' 'a-z')"
        if test "${f}" != "${n}"
        then
            mv "${f}" "${n}"
        fi
    done
}

prepare() {
    _lc "CarotDAV"/*
}

package() {
    a="${pkgdir}/apps${_apparch}/${_realname}"
    mkdir -p "${a}"
    cp "installed.txt" "CarotDAV"/*.{dll,exe,xml,config} "${a}"

    d="${pkgdir}/docs${_apparch}/${_realname}"
    mkdir -p "${d}"
    cp "CarotDAV"/*.txt "${d}"
}
