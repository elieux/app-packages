# Maintainer: David Macek <david.macek.0@gmail.com>

DLAGENTS=('http::/usr/bin/curl -qb "" -RfLC - -o %o %u')

_realname=ltrdata
pkgbase="app-${_realname}"
pkgname=("app-i686-${_realname}"
         "app-x86_64-${_realname}")
pkgver=2016.02.26
pkgrel=1
pkgdesc="Collection of small and useful freeware utilities by Lagerkvist Teknisk Radgivnings"
url="http://www.ltr-data.se/opencode.html"
arch=('any')
license=('custom')
makedepends=("unzip" "curl")
provides=("${pkgbase}")
options=(!strip)
source=("exefiles.zip::http://www.ltr-data.se/files/exefiles.zip"
        "exefiles64.zip::http://www.ltr-data.se/files/win64/exefiles.zip"
        "scripts.zip::http://ltr-data.se/files/scripts.zip"
        "scripts64.zip::http://ltr-data.se/files/scripts.zip"

        "checksum.zip::http://www.ltr-data.se/files/checksum.zip"
        "coordtool.zip::http://www.ltr-data.se/files/coordtool.zip"
        "netcheck.zip::http://www.ltr-data.se/files/netcheck.zip"
        
        "devio.exe::http://www.ltr-data.se/files/devio.exe"

        # installers
        # "zerodrv.exe::http://www.ltr-data.se/files/zerodrv.exe"
        # "imdiskinst.exe::http://www.ltr-data.se/files/imdiskinst.exe"
        # "ImDiskNet.7z::http://www.ltr-data.se/files/ImDiskNet.7z"
)
md5sums=('a1fee96e07b5c047ec6660860417d6e1'
         '33c6b5dcd67ea81b30c86c24eab00227'
         'a007910bb85b614021fe9d493bc3cf48'
         'a007910bb85b614021fe9d493bc3cf48'
         '866b2d82b75294c7eec74ebd2c6ea87c'
         'ede1d9f5ab8a30927738ec6cb71b5b69'
         '9d08d1b245c2f64513738712e1184e9b'
         '1e56d5235adaaaa03638971efa016b9d')
noextract=("${source[@]##*/}")

_lc() {
    for f in "${@}"
    do
        n="$(echo "${f}" | tr 'A-Z' 'a-z')"
        if test "${f}" != "${n}"
        then
            mv "${f}" "${n}"
        fi
    done
}

pkgver() {
    #find . -mindepth 1 -maxdepth 2 -name '*.exe' -printf '%TY.%Tm.%Td\n' | sort | tail -1
    find . -mindepth 1 -maxdepth 1 -name '*.zip' -execdir bash -c 'unzip -l "{}" | tail -n +4 | head -n -2 | sed -r "s/[ ]+/\t/g" | cut -f 3 | tr "-" "."' \; \
      -o -name '*.exe' -printf '%TY.%Tm.%Td\n' \
      | sort | tail -1
}

prepare() {
    for f in *.zip
    do
        unzip -o "${f}" -d "${f%%.zip}"
        _lc "${f%%.zip}"/*
    done
}

_package() {
    a="${pkgdir}/apps${_apparch}/${_realname}"
    mkdir -p "${a}"
    d="${pkgdir}/docs${_apparch}/${_realname}"
    mkdir -p "${d}"
    for s in $1
    do
        s="${s%%.zip}"
        u="${s##*/}"
        u="${u%%64}"
        echo "${u}"
        find "${s}" -regextype posix-extended -regex ".+\.(exe|dll|exe\.config|cmd|vbs|ps1)" -print0 | xargs -0 cp -t "${a}"
        find "${s}" -regextype posix-extended -regex ".+\.(txt|cnt|hlp)" -print0 | xargs -0r cp -t "${d}"
    done
}

package_app-i686-ltrdata() {
    _apparch=32
    _package "$(find . -mindepth 1 -maxdepth 1 -name '*.zip' -not -name '*64*')"
    cp "devio.exe" "${pkgdir}/apps${_apparch}/${_realname}"
}

package_app-x86_64-ltrdata() {
    _apparch=64
    _package "$(find . -mindepth 1 -maxdepth 1 -name '*64*.zip')"
}
