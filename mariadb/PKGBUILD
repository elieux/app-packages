# Maintainer: David Macek <david.macek.0@gmail.com>

_realname=mariadb
pkgbase="app-${_realname}"
pkgname=("app-i686-${_realname}"
         "app-x86_64-${_realname}")
pkgver=10.1.12
pkgrel=1
pkgdesc="Fast SQL database server, drop-in replacement for MySQL"
url="http://mariadb.org/"
arch=('any')
license=('GPL')
makedepends=("unzip")
provides=("${pkgbase}")
options=(!strip)
source=("mariadb-win32.zip::https://downloads.mariadb.org/f/mariadb-${pkgver}/win32-packages/mariadb-${pkgver}-win32.zip"
        "mariadb-winx64.zip::https://downloads.mariadb.org/f/mariadb-${pkgver}/winx64-packages/mariadb-${pkgver}-winx64.zip")
sha512sums=('48437e7a0acedaa90eee600292916df61b9bd75be99298b99a0c9a451a5833759bff8a7174025880164f311b61b352579a1aae3e102c6f12a559777243907eb8'
            'e51a51929793084d7f0026c27a7461f572b3bd3d9965df69e40d05d15c6cca175c8b533acb530fd40f683514f5845df96b787410cc74257a4a06cabd50efb266')

_package() {
    a="${pkgdir}/apps${_apparch}/${_realname}"
    mkdir -p "${a}"
    cp -r "mariadb-${pkgver}-win${1}"/* "${a}"
    rm -r "${a}"/{data,mysql-test,sql-bench}
    find "${a}" -name '*.pdb' -delete
    find "${a}" -name '*.lib' -delete

    d="${pkgdir}/docs${_apparch}/${_realname}"
    mkdir -p "${d}"
    mv "${a}"/COPYING* "${a}"/*.ini "${a}"/{CREDITS,EXCEPTIONS-CLIENT,README} "${d}"
}

package_app-i686-mariadb() {
    _apparch=32
    _package "32"
}

package_app-x86_64-mariadb() {
    _apparch=64
    _package "x64"
}
