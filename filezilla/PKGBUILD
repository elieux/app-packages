# Maintainer: David Macek <david.macek.0@gmail.com>

_realname=filezilla
pkgbase="app-${_realname}"
pkgname=("app-i686-${_realname}"
         "app-x86_64-${_realname}")
pkgver=3.25.1
pkgrel=1
pkgdesc="Fast and reliable FTP, FTPS and SFTP client"
url="http://filezilla-project.org/"
arch=('any')
license=('GPL')
makedepends=("unzip")
provides=("${pkgbase}")
options=(!strip)
source=("FileZilla_win32.zip::http://downloads.sourceforge.net/${_realname}/FileZilla_Client/${pkgver}/FileZilla_${pkgver}_win32.zip"
        "FileZilla_win64.zip::http://downloads.sourceforge.net/${_realname}/FileZilla_Client/${pkgver}/FileZilla_${pkgver}_win64.zip")
sha512sums=('e9c576819603c28c8cdc96b6c55c80dc6afeba484fb398a3147cda3be5d468aa608405e530bad0fc28e276d76b0be566c8af1f994bcf7d27c3b0f2df557c40e7'
            '322466cf8013dcb0e8a2cabab6f9cf11c86c29f8b7d2d2c8315f72e7cc0975145d86befecbbffc11c764fe5b6f39615690bee7f3fe0ac805fa8e8143f15b79a6')
noextract=("FileZilla_win32.zip"
           "FileZilla_win64.zip")

prepare() {
    for _apparch in 32 64
    do
        unzip -o "FileZilla_win${_apparch}.zip" -d "filezilla${_apparch}"
    done
}

_package() {
    a="${pkgdir}/apps${_apparch}/${_realname}"
    mkdir -p "${a}"
    cp -r "filezilla${_apparch}/FileZilla-${pkgver}"/* "${a}"

    d="${pkgdir}/docs${_apparch}/${_realname}"
    mkdir -p "${d}"
    mv "${a}/docs" "${a}/AUTHORS" "${a}/NEWS" "${a}/GPL.html" "${d}"
}

package_app-i686-filezilla() {
    _apparch=32
    _package
}

package_app-x86_64-filezilla() {
    _apparch=64
    _package
}
