# Maintainer: David Macek <david.macek.0@gmail.com>

_realname=atube-catcher
_choconame=atubecatcher
pkgbase="app-${_realname}"
pkgname=("app-i686-${_realname}")
epoch=1
pkgver=3.8.0.9831
pkgrel=1
pkgdesc="Downloads videos from the Internet"
url="https://www.atube.me/"
arch=('any')
license=('custom')
makedepends=("app-innounp")
provides=("${pkgbase}")
options=(!strip)
source=("aTube_Catcher.exe::http://files.dsnetwb.com/aTube_Catcher.exe")
sha512sums=('558c82ebccca6cf8df26296ff765774c23d7e8e97de30b502dd478129d1661d00ca10e6c7f59217a384ba200781954d5824cfd7fafb52e19194bef2baec7291e')

_lc() {
    local d f n

    for f in "${@}"
    do
        d="$(dirname "${f}")"
        f="$(basename "${f}")"
        n="$(echo "${f}" | tr 'A-Z' 'a-z')"
        if test "${f}" != "${n}"
        then
            mv "${d}/${f}" "${d}/${n}"
        fi
    done
}

prepare() {
    /apps/innounp/innounp -x -q -m -a -y "aTube_Catcher.exe" -d"aTube_Catcher.dir"
}

pkgver() {
    powershell -command '(Get-Item "aTube_Catcher.dir/{app}/yct.exe").VersionInfo.ProductVersionRaw.ToString()'
}

package() {
    local a="${pkgdir}/apps/${_realname}"
    mkdir -p "${a}"
    cp -R "aTube_Catcher.dir/{app}"/* "${a}"
    _lc "${a}"/*
}
