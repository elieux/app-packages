# Maintainer: David Macek <david.macek.0@gmail.com>

_realname=consolez
pkgbase="app-${_realname}"
pkgname=("app-i686-${_realname}"
         "app-x86_64-${_realname}")
pkgver=1.17.0.16129
pkgrel=1
pkgdesc="A console wrapper with tabs, easy copy-pasting and pretty visuals"
url="http://github.com/cbucher/console"
arch=('any')
license=('GPL2')
makedepends=("unzip")
provides=("${pkgbase}")
options=(!strip)
source=("ConsoleZ32.zip::https://github.com/cbucher/console/releases/download/${pkgver%.*}/ConsoleZ.x86.${pkgver}.zip"
        "ConsoleZ64.zip::https://github.com/cbucher/console/releases/download/${pkgver%.*}/ConsoleZ.x64.${pkgver}.zip")
sha512sums=('f76b2ecb84ccbb6209295a831ba5c6c92950c660ba4e2e8a95038e7ef3c89886d93558b147c2a077d8111eea1a5853041c2b85d2e31188164d2c0dda6e775183'
            '3c26028adaee3465d13f5a1d3ac9d53b2c2738094160d7f875c0492ff9cca56378c8fe0fdefd8491a3a4bb4de5b3abaf7e810b23d8467e090d534ab625224ccd')
noextract=("ConsoleZ32.zip"
           "ConsoleZ64.zip")

_lc() {
    for f in "${@}"
    do
        n="$(echo "${f}" | tr 'A-Z' 'a-z')"
        if test "${f}" != "${n}"
        then
            mv "${f}" "${n}"
        fi
    done
}

prepare() {
    for _apparch in 32 64
    do
        unzip -o "ConsoleZ${_apparch}.zip" -d "consolez${_apparch}"
        _lc "consolez${_apparch}"/*
    done
}

_package() {
    a="${pkgdir}/apps${_apparch}/${_realname}"
    mkdir -p "${a}"
    cp "consolez${_apparch}"/*.{exe,dll} "${a}"

    d="${pkgdir}/docs${_apparch}/${_realname}"
    mkdir -p "${d}"
    cp "consolez${_apparch}"/*.chm "${d}"
}

package_app-i686-consolez() {
    _apparch=32
    _package
}

package_app-x86_64-consolez() {
    _apparch=64
    _package
}
