# Maintainer: David Macek <david.macek.0@gmail.com>

_realname=musescore
_apparch=32
pkgbase="app-${_realname}"
pkgname=("app-i686-${_realname}")
pkgver=2.0.3
pkgrel=1
pkgdesc="Create, play and print beautiful sheet music"
url="http://musescore.org/"
arch=('any')
license=('GPL')
provides=("${pkgbase}")
options=(!strip)
source=("MuseScore.msi::ftp://ftp.osuosl.org/pub/musescore/releases/MuseScore-${pkgver}/MuseScore-${pkgver}.msi")
sha512sums=('cc649408b67aef23905419d24fec82dc3e1c8f5665adfa1f119265c671cf92a8ff2000d448c5ef3562845fbbeb1343978aaa8377f5dceefb43709187095d2069')

_lc() {
    local d f n

    for f in "${@}"
    do
        d="$(dirname "${f}")"
        f="$(basename "${f}")"
        n="$(echo "${f}" | tr 'A-Z' 'a-z')"
        if test "${f}" != "${n}"
        then
            mv "${d}/${f}" "${d}/${n}"
        fi
    done
}

build() {
    msiexec -a "MuseScore.msi" "TARGETDIR=$(cygpath -wa "install")" -qn

    s="install/MuseScore 2"
    _lc "${s}/bin"/*
}

package() {
    s="install/MuseScore 2"

    a="${pkgdir}/apps${_apparch}/${_realname}"
    mkdir -p "${a}"
    cp -r "${s}"/* "${a}"

    d="${pkgdir}/docs${_apparch}/${_realname}"
    mkdir -p "${d}"
    mv "${a}"/{demos,manual} "${a}/sound/FluidR3Mono_License.md" "${d}"
}
