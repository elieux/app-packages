# Maintainer: David Macek <david.macek.0@gmail.com>

_realname=google-drive
_apparch=32
pkgbase="app-${_realname}"
pkgname=("app-i686-${_realname}")
pkgver=1.30.2170.0459
pkgrel=1
pkgdesc="The Google Drive synchronization tool"
url="http://www.google.com/drive/"
arch=('any')
license=('custom')
provides=("${pkgbase}")
options=(!strip)
source=("gsync.msi::http://dl.google.com/drive/${pkgver}/gsync.msi")
sha512sums=('e6e220fdbc67819f8f7eabb450191a272e158c25bfc1b58adceddc4660a426bf089641136ae8aae5a967618c82ed519891659955c6d6c16fafa8e0c048581d60')

_lc() {
    for f in "${@}"
    do
        n="$(echo "${f}" | tr 'A-Z' 'a-z')"
        if test "${f}" != "${n}"
        then
            mv "${f}" "${n}"
        fi
    done
}

build() {
    msiexec -a "gsync.msi" "TARGETDIR=$(cygpath -wa "msi")" -qn
    _lc "msi/Google/Drive"/*
}

package() {
    s="msi/Google/Drive"
    a="${pkgdir}/apps${_apparch}/${_realname}"
    mkdir -p "${a}"
    cp -r "${s}"/*.{dll,exe} "${s}/languages" "${a}"
}
