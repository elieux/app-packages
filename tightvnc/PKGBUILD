# Maintainer: David Macek <david.macek.0@gmail.com>

_realname=tightvnc
pkgbase="app-${_realname}"
pkgname=("app-x86_64-${_realname}")
epoch=1
pkgver=2.8.8
pkgrel=1
pkgdesc="Light weight VNC implementation"
url="http://www.tightvnc.com/"
arch=('any')
license=('GPL2')
replaces=("app-i686-${_realname}")
provides=("${pkgbase}")
options=(!strip)
source=("tightvnc-setup-64bit.msi::http://www.tightvnc.com/download/${pkgver}/tightvnc-${pkgver}-gpl-setup-64bit.msi")
sha512sums=('0379d2fbc645388f9935828f05803c1c83e3e952067c6c357246779d4ef14d6227ba45801f689680639b6b89778c1af2012276447a52478fc34b675e6092f7cf')

_lc() {
    local d f n

    for f in "${@}"
    do
        d="$(dirname "${f}")"
        f="$(basename "${f}")"
        n="$(echo "${f}" | tr 'A-Z' 'a-z')"
        if test "${f}" != "${n}"
        then
            mv "${d}/${f}" "${d}/${n}"
        fi
    done
}

build() {
    msiexec -a "tightvnc-setup-64bit.msi" "TARGETDIR=$(cygpath -wa "install")" -qn
}

package() {
    s="install/PFiles/TightVNC"
    a="${pkgdir}/apps/${_realname}"
    mkdir -p "${a}"
    cp -r "${s}"/*.{exe,dll} "${a}"
    cp "${s}"/*.{txt,url} "${a}"
}
