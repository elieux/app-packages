# Maintainer: David Macek <david.macek.0@gmail.com>

_realname=winscp
_choconame=winscp
pkgbase="app-${_realname}"
pkgname=("app-i686-${_realname}")
epoch=1
pkgver=5.21.2
pkgrel=1
pkgdesc="An open source two-pane SFTP/FTP/WebDAV/SCP client"
url="https://winscp.net/"
arch=('any')
license=('custom')
makedepends=("app-innounp")
provides=("${pkgbase}")
options=(!strip)
source=("WinSCP-Setup.exe::https://download.sourceforge.net/winscp/WinSCP-${pkgver}-Setup.exe")
sha512sums=('14f1b6e8c90f6a9b68c6a9af52416c7eeb5dcd38c8374fa3df4d604029e8bd7454238483fec116f034c731b17199ab99ec7a738d16e52628509003dd5006c3c9')

_lc() {
    local d f n

    for f in "${@}"
    do
        d="$(dirname "${f}")"
        f="$(basename "${f}")"
        n="$(echo "${f}" | tr 'A-Z' 'a-z')"
        if test "${f}" != "${n}"
        then
            mv "${d}/${f}" "${d}/${n}"
        fi
    done
}

prepare() {
    /apps/innounp/innounp -x -q -m -a -y "WinSCP-Setup.exe" -d"WinSCP-Setup.dir"
}

package() {
    local a="${pkgdir}/apps/${_realname}"
    mkdir -p "${a}"
    cp -R "WinSCP-Setup.dir/{app}"/* "${a}"
    rm -R "${a}/winscp.map" "${a}/putty"
    _lc "${a}"/*
}
