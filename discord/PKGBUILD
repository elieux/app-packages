# Maintainer: David Macek <david.macek.0@gmail.com>

_realname=discord
_apparch=32
pkgbase="app-${_realname}"
pkgname=("app-i686-${_realname}")
pkgver=0.0.295
pkgrel=1
pkgdesc="All-in-one voice and text chat for gamers"
url="https://www.discordapp.com/"
arch=('any')
license=('custom')
makedepends=("p7zip" "unzip")
provides=("${pkgbase}")
options=(!strip)
source=("DiscordSetup.exe::https://discordapp.com/api/download?platform=win")
sha512sums=('8939dd9b40fbd2c2d16cf05f11ff91e959fd152d1c059f7279f5b0c360efffa5a7d3db62d74487f09843c4326b9749aef2fe1e9cd795737c50df0c889c86432a')

_lc() {
    for f in "${@}"
    do
        n="$(echo "${f}" | tr 'A-Z' 'a-z')"
        if test "${f}" != "${n}"
        then
            mv "${f}" "${n}"
        fi
    done
}

pkgver() {
    b="$(cygpath -wa "DiscordSetup.exe" | sed 's/\\/\\\\/g')"
    powershell -command '(Get-Item "DiscordSetup.exe").VersionInfo.ProductVersion'
}

prepare() {
    rm -f "Discord"-*-"full.nupkg"
    7z x -y -bd "DiscordSetup.exe"
    unzip -o "Discord"-*-"full.nupkg"
    _lc "lib/net45"/*
}

package() {
    a="${pkgdir}/apps${_apparch}/${_realname}"
    mkdir -p "${a}"
    cp -r "lib/net45"/* "${a}"
}
