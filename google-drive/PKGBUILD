# Maintainer: David Macek <david.macek.0@gmail.com>

_realname=google-drive
pkgbase="app-${_realname}"
pkgname=("app-i686-${_realname}")
epoch=1
pkgver=3.49.9760.2421
pkgrel=1
pkgdesc="The Google Drive synchronization tool"
url="https://www.google.com/drive/"
arch=('any')
license=('custom')
makedepends=("app-lessmsi")
provides=("${pkgbase}")
options=(!strip)
source=("gsync_enterprise.msi::https://dl.google.com/drive/gsync_enterprise.msi")
sha512sums=('c47d6b312281e988f025a4eb5b7f7bf34d8662da5b38529b426f70fb4d935b10c3b11e13bd2e690f93a3579f1c33f0d50f374a325370ed7800ae0751f0793a14')

prepare() {
    rm -fR "gsync_enterprise"
    /apps/lessmsi/lessmsi x "gsync_enterprise.msi"
}

pkgver() {
    powershell -command '(Get-Item "gsync_enterprise/SourceDir/Google/Drive/googledrivesync.exe").VersionInfo.FileVersionRaw.ToString()'
}

package() {
    local a="${pkgdir}/apps/${_realname}"
    mkdir -p "${a}"
    cp -R "gsync_enterprise/SourceDir/Google/Drive"/* "${a}"
    rm -fR "${a}"/Microsoft.VC90.{ATL,CRT,MFC}
}
