# Maintainer: David Macek <david.macek.0@gmail.com>

_realname=davmail
_realver=4.8.0-2479
pkgbase="app-${_realname}"
pkgname=("app-i686-${_realname}"
         "app-x86_64-${_realname}")
pkgver=${_realver//-/.}
pkgrel=1
pkgdesc="A gateway allowing usage of any mail/calendar client with an Exchange server"
url="http://davmail.sourceforge.net/"
arch=('any')
license=('GPL2')
makedepends=("unzip")
provides=("${pkgbase}")
options=(!strip)
source=("davmail-windows-noinstall.zip::http://downloads.sourceforge.net/${_realname}/${_realname}/${_realver%%-*}/${_realname}-${_realver}-windows-noinstall.zip")
sha512sums=('a191681c66e283973a541f612c8d7b460e98a9bc81390ef12e5b9e563f1d78cc46e36ea6ed8155aa11cedb6d0f5680a33518c5fc5907684648bac31b2e1919e1')

_package() {
    a="${pkgdir}/apps${_apparch}/${_realname}"
    mkdir -p "${a}"
    cp -r "lib" "davmail.jar" "davmailconsole.exe" "${a}"
    for f in "davmail" "davmailservice"
    do
        cp "${f}${1}.exe" "${a}/${f}.exe"
    done
}

package_app-i686-davmail() {
    _apparch=32
    _package ""
}

package_app-x86_64-davmail() {
    _apparch=64
    _package "64"
}
