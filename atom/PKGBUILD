# Maintainer: David Macek <david.macek.0@gmail.com>

_realname=atom
pkgbase="app-${_realname}"
pkgname=("app-i686-${_realname}"
         "app-x86_64-${_realname}")
pkgver=1.22.1
pkgrel=1
pkgdesc="A hackable text editor for the 21st Century"
arch=('any')
url="https://atom.io/"
license=('custom')
makedepends=("unzip")
provides=("${pkgbase}")
options=(!strip)
source=("atom-x32-windows.zip::https://github.com/atom/atom/releases/download/v${pkgver}/atom-windows.zip"
        "atom-x64-windows.zip::https://github.com/atom/atom/releases/download/v${pkgver}/atom-x64-windows.zip")
sha512sums=('962c930acc680a5cf0bbdf79c54d9c97b7c79c971519580972e24db5e17e2a47936f25fe8d13ad6a5850fb32c7b53cc900fcb0848517c3b56b154c36da9ea49a'
            '9adeb5cbdd2ac21965e9aac4e941fb185bd10ca9a0cd0f906df65b191b4f68f193d0d7355399f8e7983a52cc39001a8a6a87dc94eec98a44e8cd5cffb56ea997')

_lc() {
    local d f n

    for f in "${@}"
    do
        d="$(dirname "${f}")"
        f="$(basename "${f}")"
        n="$(echo "${f}" | tr 'A-Z' 'a-z')"
        if test "${f}" != "${n}"
        then
            mv "${d}/${f}" "${d}/${n}"
        fi
    done
}

_prepare() {
    _lc "Atom x${_apparch}"/*
}

prepare() {
    mv "Atom" "Atom x32"

    for _apparch in 32 64
    do
        _prepare
    done
}

_package() {
    a="${pkgdir}/apps${_apparch}/${_realname}"
    mkdir -p "${a}"
    cp -r "Atom x${_apparch}"/* "${a}"

    d="${pkgdir}/docs${_apparch}/${_realname}"
    mkdir -p "${d}"
    mv "${a}"/{version,license} "${a}"/*.html "${a}/resources/LICENSE.md" "${d}"
}

package_app-i686-atom() {
    _apparch=32
    _package
}

package_app-x86_64-atom() {
    _apparch=64
    _package
}
