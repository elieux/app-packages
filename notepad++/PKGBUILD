# Maintainer: David Macek <david.macek.0@gmail.com>

_realname=notepad++
pkgbase="app-${_realname}"
pkgname=("app-x86_64-${_realname}")
epoch=1
pkgver=7.8.1
pkgrel=1
pkgdesc="A free Scintilla-based editor with plugin support"
url="https://notepad-plus-plus.org/"
arch=('any')
license=('GPL')
replaces=("app-i686-${_realname}")
makedepends=("p7zip" "unzip")
provides=("${pkgbase}")
options=(!strip)
source=("npp.bin.x64.zip::http://download.notepad-plus-plus.org/repository/${pkgver%%.*}.x/${pkgver}/npp.${pkgver}.bin.x64.zip"
        "npp.Installer.x64.exe::http://download.notepad-plus-plus.org/repository/${pkgver%%.*}.x/${pkgver}/npp.${pkgver}.Installer.x64.exe")
sha512sums=('958db8b97de511dc2a1595f33a49d2bd0d592b558e2be1cb7a1507f5c67579d29ea1c31a9cad32fade117e124ced1447b999ac81129d7cd8fec21a0faef84aad'
            '4993faaf453d6a982385c0b9e2368d21cfdbaf6b861d2d5f080f4523f628cdc91c91096d5a9461d9160f862c1d5a7e36309bb383f13bb208e84e6805a9499c2b')
noextract=("npp.bin.x64.zip")

_lc() {
    local d f n

    for f in "${@}"
    do
        d="$(dirname "${f}")"
        f="$(basename "${f}")"
        n="$(echo "${f}" | tr 'A-Z' 'a-z')"
        if test "${f}" != "${n}"
        then
            mv "${d}/${f}" "${d}/${n}"
        fi
    done
}

prepare() {
    unzip -o "npp.bin.x64.zip" -d "npp"
    rm -f "npp/nppshell_06*.dll"
    echo u | 7z x -bd -i!"NppShell_06.dll" -o"npp" "npp.Installer.x64.exe"
    _lc "npp"/*
}

package() {
    a="${pkgdir}/apps/${_realname}"
    mkdir -p "${a}"
    cp -r "npp"/* "${a}"
    rm "${a}/dolocalconf.xml"
}
