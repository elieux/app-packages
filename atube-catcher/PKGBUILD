# Maintainer: David Macek <david.macek.0@gmail.com>

_realname=atube-catcher
_apparch=32
pkgbase="app-${_realname}"
pkgname=("app-i686-${_realname}")
pkgver=3.8.0.9003
pkgrel=1
pkgdesc="Downloads videos from the Internet"
url="http://www.atube.me/"
arch=('any')
license=('custom')
makedepends=("app-i686-innounp")
provides=("${pkgbase}")
options=(!strip)
source=("aTubeCatcher.exe::http://files3.dsnetwb.com/aTubeCatcher.exe")
sha512sums=('11945678acfa0b0864119ee7959877a4d89a5bccaffd0d1a0c33d61eb1e52804c2d99eb5335cba05796c4ef9c55a6589e3798062fd3db731d7675b51ad491bef')

_lc() {
    for f in "${@}"
    do
        n="$(echo "${f}" | tr 'A-Z' 'a-z')"
        if test "${f}" != "${n}"
        then
            mv "${f}" "${n}"
        fi
    done
}

pkgver() {
    /apps32/innounp/innounp -e -q -m -a -y "aTubeCatcher.exe" "yct.exe" > /dev/null
    b="$(cygpath -wa "yct.exe" | sed 's/\\/\\\\/g')"
    wmic datafile where name=\""${b}"\" get version | grep 'Version' -A 1 | tail -1 | tr -d ' '
}

prepare() {
    /apps32/innounp/innounp -x -q -m -a -y "aTubeCatcher.exe"
    _lc "{app}"/*
}

package() {
    a="${pkgdir}/apps${_apparch}/${_realname}"
    mkdir -p "${a}"
    cp -r "{app}"/* "${a}"
    rm "${a}/offercast391_atu3_.exe"

    d="${pkgdir}/docs${_apparch}/${_realname}"
    mkdir -p "${d}"
    mv "${a}"/*.txt "${d}"
}
