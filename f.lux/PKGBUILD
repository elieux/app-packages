# Maintainer: David Macek <david.macek.0@gmail.com>

_realname=f.lux
_choconame=f.lux
pkgbase="app-${_realname}"
pkgname=("app-i686-${_realname}")
epoch=1
pkgver=4.124
pkgrel=1
pkgdesc="Changes the display color temperature according to the time of the day"
url="https://justgetflux.com/"
arch=('any')
license=('custom')
makedepends=("p7zip")
provides=("${pkgbase}")
replaces=("app-i686-f-lux")
options=(!strip)
source=("flux-setup.exe::https://justgetflux.com/flux-setup.exe")
sha512sums=('179e692a73d74cc7ff0610969987bc07056ac68f11f47b706778526d19c3aca3fb55a8f517a579719cd7a298507b2a8cb3668e14b819840f688698240fd99b40')

prepare() {
    7z x -y -bd "flux-setup.exe" -o"flux-setup.dir" > /dev/null
}

pkgver() {
    powershell -command '(Get-Item "flux-setup.dir/flux.exe").VersionInfo.FileVersionRaw.ToString()' | sed -r 's/^([0-9]+[.][0-9]+)[.]0[.]0$/\1/'
}

package() {
    local a="${pkgdir}/apps/${_realname}"
    mkdir -p "${a}"
    cp -R "flux-setup.dir"/* "${a}"
    rm -R "${a}"/'$PLUGINSDIR' "${a}/runtime/uninstall.exe.nsis"
}
