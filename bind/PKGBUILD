# Maintainer: David Macek <david.macek.0@gmail.com>

_realname=bind
_realver=9.10.4-P2
pkgbase="app-${_realname}"
pkgname=("app-i686-${_realname}"
         "app-x86_64-${_realname}")
pkgver=${_realver//-/.}
pkgrel=1
pkgdesc="The ISC BIND nameserver"
url="https://www.isc.org/downloads/bind/"
arch=('any')
license=('custom:ISC')
makedepends=("unzip")
provides=("${pkgbase}")
options=(!strip)
source=("BIND32.zip::ftp://ftp.isc.org/isc/bind9/${_realver}/BIND${_realver}.x86.zip"
        "BIND64.zip::ftp://ftp.isc.org/isc/bind9/${_realver}/BIND${_realver}.x64.zip")
sha512sums=('d65afd2382e6cfdf52f3a909c93eb35943a8d2fe6912067038d13b9888b9eedb45abb0945f2291b1d95af19aa7022c78c730afa4a68366f7d889e2926f420f58'
            '0c18594aa60f97224819e3e1c9afc2b824baf1face5e8d8312a13a5ea23aee890f9fd84bf39f69baeacc8f9a791c92fc4945a62731f8d394b5e17d88166d9920')
noextract=("BIND32.zip"
           "BIND64.zip")

_lc() {
    for f in "${@}"
    do
        n="$(echo "${f}" | tr 'A-Z' 'a-z')"
        if test "${f}" != "${n}"
        then
            mv "${f}" "${n}"
        fi
    done
}

prepare() {
    for _apparch in 32 64
    do
        unzip -o "BIND${_apparch}.zip" -d "bind${_apparch}"
        _lc "bind${_apparch}"/*
    done
}

_package() {
    a="${pkgdir}/apps${_apparch}/${_realname}"
    mkdir -p "${a}"
    cp "bind${_apparch}"/*.{exe,dll} "${a}"
    rm "${a}"/vcredist*.exe "${a}/bindinstall.exe"

    d="${pkgdir}/docs${_apparch}/${_realname}"
    mkdir -p "${d}"
    cp "bind${_apparch}"/*.{html,pdf,txt} "bind${_apparch}"/{changes,copyright,faq,history,migration,migration-4to9,openssl-license,readme,sha256} "${d}"
}

package_app-i686-bind() {
    _apparch=32
    _package
}

package_app-x86_64-bind() {
    _apparch=64
    _package
}
