# Maintainer: David Macek <david.macek.0@gmail.com>

_realname=7zip
pkgname=("app-i686-${_realname}"
         "app-x86_64-${_realname}")
pkgver=9.22
pkgrel=1
pkgdesc="A free file archiver for extremely high compression"
url="http://www.7-zip.org/"
arch=('any')
license=('LGPL2' 'custom')
makedepends=("p7zip")
source=("7z32.msi::http://downloads.sourceforge.net/sevenzip/7-Zip/${pkgver}/7z922.msi"
        "7z64.msi::http://downloads.sourceforge.net/sevenzip/7-Zip/${pkgver}/7z922-x64.msi"
        "7zextra.7z::http://downloads.sourceforge.net/sevenzip/7-Zip/${pkgver}/7z922_extra.7z")
md5sums=('c4217c54ee6e927478367a1a2797db05'
         '5670ad185cd6c7609046e436e6603aa5'
         'f6e704bad4e0441789d6afcd0078dfb5')

build() {
    for apparch in 32 64
    do
        cd "${srcdir}"
        msiexec -a "7z${apparch}.msi" "TARGETDIR=$(cygpath -w "${srcdir}/msi${apparch}")" -qn
        cd "${srcdir}/msi${apparch}/Files/7-Zip"
        for f in *
        do
            n=$(echo "${f}" | tr 'A-Z' 'a-z')
            if test "${f}" != "${n}"
            then
                mv "${f}" "${n}"
            fi
        done
    done
}

package_app-i686-7zip() {
    apparch=32
    mkdir -p "${pkgdir}/apps${apparch}/${_realname}"
    mkdir -p "${pkgdir}/docs${apparch}/${_realname}"
    cp "${srcdir}"/*.{dll,exe} "${pkgdir}/apps${apparch}/${_realname}"
    #cp "${srcdir}"/*.{chm,txt} "${pkgdir}/docs${apparch}/${_realname}"
    cp -R "${srcdir}/msi${apparch}/Files/7-Zip"/* "${pkgdir}/apps${apparch}/${_realname}"
    mv "${pkgdir}/apps${apparch}/${_realname}"/*.{chm,txt,ion} "${pkgdir}/docs${apparch}/${_realname}"
}

package_app-x86_64-7zip() {
    apparch=64
    mkdir -p "${pkgdir}/apps${apparch}/${_realname}"
    mkdir -p "${pkgdir}/docs${apparch}/${_realname}"
    cp "${srcdir}/x64"/*.dll "${pkgdir}/apps${apparch}/${_realname}"
    #cp "${srcdir}"/*.{chm,txt} "${pkgdir}/docs${apparch}/${_realname}"
    cp -R "${srcdir}/msi${apparch}/Files/7-Zip"/* "${pkgdir}/apps${apparch}/${_realname}"
    mv "${pkgdir}/apps${apparch}/${_realname}"/*.{chm,txt,ion} "${pkgdir}/docs${apparch}/${_realname}"
}
