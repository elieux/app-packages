# Maintainer: David Macek <david.macek.0@gmail.com>

_realname=chromium
_choconame=chromium
_realver=843830
pkgbase="app-${_realname}"
pkgname=("app-x86_64-${_realname}")
epoch=1
pkgver=89.0.4389.82
pkgrel=1
pkgdesc="The open-source project behind Google Chrome, an attempt at creating a safer, faster, and more stable browser"
url="https://www.chromium.org/Home"
arch=('any')
license=('BSD')
replaces=("app-i686-${_realname}")
provides=("${pkgbase}")
options=(!strip)
source=("chrome.sync.7z::https://github.com/Hibbiki/chromium-win64/releases/download/v${pkgver}-r${_realver}/chrome.sync.7z")
sha512sums=('8112ec7da1da66bf6680cce771458e1cd3e8b22b3a30e248c8196ad05836a67a6ed3a2b364f51ff585a79bc155aebbcff3b59faafc8d3df2be115acf84ad9025')

_lc() {
    local d f n

    for f in "${@}"
    do
        d="$(dirname "${f}")"
        f="$(basename "${f}")"
        n="$(echo "${f}" | tr 'A-Z' 'a-z')"
        if test "${f}" != "${n}"
        then
            mv "${d}/${f}" "${d}/${n}"
        fi
    done
}

package() {
    local a="${pkgdir}/apps/${_realname}"
    mkdir -p "${a}"
    cp -R "Chrome-bin"/* "${a}"
    mv "${a}/${pkgver}"/* "${a}"
    rm -R "${a}/${pkgver}"
    _lc "${a}"/*
}
