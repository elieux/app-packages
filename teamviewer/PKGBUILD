# Maintainer: David Macek <david.macek.0@gmail.com>

_realname=teamviewer
pkgbase="app-${_realname}"
pkgname=("app-i686-${_realname}")
epoch=1
pkgver=15.0.8397
pkgrel=1
pkgdesc="All-in-one software for remote support and online meetings"
url="https://www.teamviewer.com/"
arch=('any')
license=('custom')
makedepends=("unzip")
provides=("${pkgbase}")
options=(!strip)
source=("TeamViewerPortable.zip::https://download.teamviewer.com/download/TeamViewerPortable.zip")
sha512sums=('bee1371f3aeabea2f85d14d506635a18992f92e584271f6097e3b8a86364e999bbf5b1c1df7d3c893447fd915575d8fc4b262b73b0f98fdf33c24b5c13d0bbb3')

_lc() {
    local d f n

    for f in "${@}"
    do
        d="$(dirname "${f}")"
        f="$(basename "${f}")"
        n="$(echo "${f}" | tr 'A-Z' 'a-z')"
        if test "${f}" != "${n}"
        then
            mv "${d}/${f}" "${d}/${n}"
        fi
    done
}

prepare() {
    _lc *
}

pkgver() {
    "${SYSTEMROOT}/System32/WindowsPowerShell/v1.0/powershell" -Command '(Get-Item "teamviewer.exe").VersionInfo.FileVersion' | sed -r 's/([0-9]+[.][0-9]+[.][0-9]+)[.]0$/\1/'
}

package() {
    a="${pkgdir}/apps/${_realname}"
    mkdir -p "${a}"
    cp * "${a}"
    rm "${a}/teamviewerportable.zip"
}
