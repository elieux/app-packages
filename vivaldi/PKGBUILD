# Maintainer: David Macek <david.macek.0@gmail.com>

_realname=vivaldi
_apparch=32
pkgbase="app-${_realname}"
pkgname=("app-i686-${_realname}")
pkgver=1.0.303.52
pkgrel=1
pkgdesc="A new browser for our friends"
url="https://vivaldi.com/"
arch=('any')
license=('custom')
makedepends=("p7zip")
provides=("${pkgbase}")
options=(!strip)
source=("Vivaldi.exe::https://vivaldi.com/download/Vivaldi_Beta.${pkgver}.exe")
md5sums=('682a6f0de5af657440a1965d7359c78a')

_lc() {
    for f in "${@}"
    do
        n="$(echo "${f}" | tr 'A-Z' 'a-z')"
        if test "${f}" != "${n}"
        then
            mv "${f}" "${n}"
        fi
    done
}

prepare() {
    7z x -y -bd "Vivaldi.exe"
    7z x -y -bd "vivaldi.7z"
    _lc "Vivaldi-bin"/* "Vivaldi-bin/${pkgver}"/*
}

package() {
    a="${pkgdir}/apps${_apparch}/${_realname}"
    mkdir -p "${a}"
    cp -r "Vivaldi-bin"/*.exe "Vivaldi-bin/${pkgver}"/* "${a}"
}
