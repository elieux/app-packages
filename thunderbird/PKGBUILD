# Maintainer: David Macek <david.macek.0@gmail.com>

_realname=thunderbird
_choconame=thunderbird
pkgbase="app-${_realname}"
pkgname=("app-x86_64-${_realname}")
epoch=1
pkgver=115.3.1
pkgrel=1
pkgdesc="Standalone, easy to customize mail/news reader"
url="https://www.thunderbird.net/"
arch=('any')
license=('GPL' 'MPL')
replaces=("app-i686-${_realname}")
makedepends=("p7zip")
provides=("${pkgbase}")
options=(!strip)
source=("ThunderbirdSetup.exe::https://ftp.mozilla.org/pub/thunderbird/releases/${pkgver}/win64/en-US/Thunderbird%20Setup%20${pkgver}.exe")
sha512sums=('f0ee25427b876e091a9c3f27f2d4c0eee63a8a377c136ad9bac7195d9bb82ad8fe5a679ba32f741be817e7c5e5e472be1f77dea05cb6c66263f6db811ee8ec71')
noextract=("ThunderbirdSetup.exe")

_lc() {
    local d f n

    for f in "${@}"
    do
        d="$(dirname "${f}")"
        f="$(basename "${f}")"
        n="$(echo "${f}" | tr 'A-Z' 'a-z')"
        if test "${f}" != "${n}"
        then
            mv "${d}/${f}" "${d}/${n}"
        fi
    done
}

_rmmsdll() {
    local ms

    ms=("msvbvm[0-9]+"
        "ucrtbase"
        "mfcm?[0-9]+[a-z]+"
        "msvc[rp][0-9]+(_([0-9]|codecvt_ids|atomic_wait))?"
        "vc([ao]mp|corlib|runtime)[0-9]+(_([0-9]|threads))?"
        "concrt[0-9]+"
        "pgort[0-9]+"
        "api-ms-win-[0-9a-z-]+")
    find "${1}" -iname '*.dll' | grep -Eif <(for m in "${ms[@]}"; do echo "/${m}[.]dll$"; done) | xargs -r rm
}

prepare() {
    7z x -y -bd "ThunderbirdSetup.exe" -o"ThunderbirdSetup.dir" > /dev/null
}

package() {
    local a="${pkgdir}/apps/${_realname}"
    mkdir -p "${a}"
    cp -R "ThunderbirdSetup.dir/core"/* "${a}"
    rm "${a}"/maintenanceservice{,_installer}.exe

    # remove system DLLs
    _rmmsdll "${a}"
    sed -r -e '/(vcruntime|msvcp)140(_1)?.dll/d' -e '/api-ms-win-[0-9a-z-]+.dll/d' -i "${a}/dependentlibs.list"

    mkdir "${a}/extensions"

    _lc "${a}"/*
}
