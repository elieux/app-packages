# Maintainer: David Macek <david.macek.0@gmail.com>

_realname=calibre
pkgbase="app-${_realname}"
pkgname=("app-i686-${_realname}"
         "app-x86_64-${_realname}")
pkgver=2.63.0
pkgrel=1
pkgdesc="Ebook management application"
url="http://calibre-ebook.com/"
arch=('any')
license=('GPL3')
provides=("${pkgbase}")
options=(!strip)
source=("calibre32.msi::http://download.calibre-ebook.com/${pkgver}/${_realname}-${pkgver}.msi"
        "calibre64.msi::http://download.calibre-ebook.com/${pkgver}/${_realname}-64bit-${pkgver}.msi")
sha512sums=('881f5f247e419fa38ab915f2f75bbead9fe0a8290da2527419faa413c247207120c7eac690abad1d41b369a526465de05c6ccd9c695df62a6acc48a046e10b40'
            'f8fd34d2b3a05b914ab65eae7f06da36e25853bf3a100805c14f71a9b937d20596a5ba987d5d9e41dd8634f90d5c079a853727769103c30d3710bf45c7859c8c')

_lc() {
    for f in "${@}"
    do
        n="$(echo "${f}" | tr 'A-Z' 'a-z')"
        if test "${f}" != "${n}"
        then
            mv "${f}" "${n}"
        fi
    done
}

_build() {
    msiexec -a "calibre${_apparch}.msi" "TARGETDIR=$(cygpath -wa "install${_apparch}")" -qn
    attrib -r //s
    _lc "install${_apparch}/PFiles"/*
    _lc "install${_apparch}/PFiles"/calibre*/*
}

build() {
    for _apparch in 32 64
    do
        _build
    done
}

_package() {
    a="${pkgdir}/apps${_apparch}/${_realname}"
    mkdir -p "${a}"
    cp -r "install${_apparch}/PFiles"/calibre*/* "${a}"
    rm -rf "${a}/microsoft.vc90.crt" # write-protected

    d="${pkgdir}/docs${_apparch}/${_realname}"
    mkdir -p "${d}"
    mv "${a}/license" "${d}"
}

package_app-i686-calibre() {
    _apparch=32
    _package
}

package_app-x86_64-calibre() {
    _apparch=64
    _package
}
