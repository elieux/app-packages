# Maintainer: David Macek <david.macek.0@gmail.com>

_realname=xnconvert
_choconame=xnconvert
pkgbase="app-${_realname}"
pkgname=("app-x86_64-${_realname}")
epoch=1
pkgver=1.96.0
pkgrel=1
pkgdesc="A bulk image converter"
url="https://www.xnview.com/en/xnconvert/"
arch=('any')
license=('custom')
provides=("${pkgbase}")
options=(!strip)
source=("XnConvert-win-x64.zip::https://download.xnview.com/XnConvert-win-x64.zip")
sha512sums=('b34ccf1f64e3a857a567ea85ee7f2a5f6fd0a2a74969ee79aca8300970c88b013c058e4434dba1198fc7dcc2fc8551b824cdc600dda449593a0f4074f5a7e380')

_lc() {
    local d f n

    for f in "${@}"
    do
        d="$(dirname "${f}")"
        f="$(basename "${f}")"
        n="$(echo "${f}" | tr 'A-Z' 'a-z')"
        if test "${f}" != "${n}"
        then
            mv "${d}/${f}" "${d}/${n}"
        fi
    done
}

_rmmsdll() {
    local ms

    ms=("msvbvm[0-9]+"
        "ucrtbase"
        "mfcm?[0-9]+[a-z]+"
        "msvc[rp][0-9]+(_([0-9]|codecvt_ids|atomic_wait))?"
        "vc([ao]mp|corlib|runtime)[0-9]+(_[0-9])?"
        "concrt[0-9]+"
        "pgort[0-9]+"
        "api-ms-win-[0-9a-z-]+")
    find "${1}" -iname '*.dll' | grep -Eif <(for m in "${ms[@]}"; do echo "/${m}[.]dll$"; done) | xargs -r rm
}

pkgver() {
    powershell -command '(Get-Item "XnConvert/xnconvert.exe").VersionInfo.ProductVersion'
}

package() {
    local a="${pkgdir}/apps/${_realname}"
    mkdir -p "${a}"
    cp -R "XnConvert"/* "${a}"
    _rmmsdll "${a}"
    rm "${a}/The latest supported Visual C++ downloads.URL"
    _lc "${a}"/*
}
