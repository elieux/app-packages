# Maintainer: David Macek <david.macek.0@gmail.com>

_realname=telegram
_choconame=telegram
pkgbase="app-${_realname}"
pkgname=("app-i686-${_realname}")
epoch=1
pkgver=2.6.1
pkgrel=1
pkgdesc="Desktop Telegram client"
url="https://desktop.telegram.org/"
arch=('any')
license=('GPL3')
provides=("${pkgbase}")
replaces=("app-x86_64-${_realname}")
options=(!strip)
source=("tportable.zip::https://github.com/telegramdesktop/tdesktop/releases/download/v${pkgver}/tportable.${pkgver}.zip")
sha512sums=('0a1080d3b0a838b53f7d6557b2018bc01d4065bf9f931364b043769b937efdb2401daeda907071ded6e5f2f0847a0342e92719a328dadcf6aa15470ec0ad6aad')

_lc() {
    local d f n

    for f in "${@}"
    do
        d="$(dirname "${f}")"
        f="$(basename "${f}")"
        n="$(echo "${f}" | tr 'A-Z' 'a-z')"
        if test "${f}" != "${n}"
        then
            mv "${d}/${f}" "${d}/${n}"
        fi
    done
}

package() {
    local a="${pkgdir}/apps/${_realname}"
    mkdir -p "${a}"
    cp "Telegram"/* "${a}"
    _lc "${a}"/*
}
