# Maintainer: David Macek <david.macek.0@gmail.com>

_chocover() {
    # choco version format: 20.11.20
    echo "${1}" | sed -r 's/^([0-9]{2})[.]([0-9]{2})[.]([0-9]{2})$/20\1.\2.\3/'
}

_realname=imdisk-toolkit
_choconame=ImDisk-Toolkit
pkgbase="app-${_realname}"
pkgname=("app-x86_64-${_realname}")
epoch=1
pkgver=2020.07.27
pkgrel=1
pkgdesc="Ramdisk for Windows and mounting of image files"
arch=('any')
url="https://sourceforge.net/projects/imdisk-toolkit/"
license=('unknown')
makedepends=("p7zip")
provides=("${pkgbase}")
options=(!strip)
source=("ImDiskTk-x64.zip::https://downloads.sourceforge.net/imdisk-toolkit/${pkgver//./}/ImDiskTk-x64.zip")
sha512sums=('900755dbc41cbe696920d614ed35b5ff3bda9d99c41f310a99d291c372a2cdda93899f79c3e07d4f2f6dbcb322cc01089d79636e0f13ffe3c87fab872576e0d8')

_lc() {
    local d f n

    for f in "${@}"
    do
        d="$(dirname "${f}")"
        f="$(basename "${f}")"
        n="$(echo "${f}" | tr 'A-Z' 'a-z')"
        if test "${f}" != "${n}"
        then
            mv "${d}/${f}" "${d}/${n}"
        fi
    done
}

prepare() {
    7z x -y -bd "ImDiskTk${pkgver//./}/files.cab" -o"files" > /dev/null
}

package() {
    depends=("app-ltrdata")

    local a="${pkgdir}/apps/${_realname}"
    mkdir -p "${a}"
    cp -R "files"/* "${a}"
    rm -R "${a}/driver"
    _lc "${a}"/*
}
