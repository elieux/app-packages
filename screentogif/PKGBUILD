# Maintainer: David Macek <david.macek.0@gmail.com>

_realname=screentogif
_choconame=screentogif
pkgbase="app-${_realname}"
pkgname=("app-i686-${_realname}")
epoch=1
pkgver=2.28.2
pkgrel=1
pkgdesc="Screen, webcam and sketchboard recorder with an integrated editor"
url="https://www.screentogif.com/"
arch=('any')
license=('MS-PL')
provides=("${pkgbase}")
options=(!strip)
source=("ScreenToGif.Portable.zip::https://github.com/NickeManarin/ScreenToGif/releases/download/${pkgver}/ScreenToGif.${pkgver}.Portable.zip")
sha512sums=('dca164395f8fc7827b4b80964156d5e5d516a49619de7a1a80dacc5a7b82eae4ee2b7bb47cf5e3f28c65bf082eb64eba88e3bd8d976fd307fccd9e00d0a4c6b4')

_lc() {
    local d f n

    for f in "${@}"
    do
        d="$(dirname "${f}")"
        f="$(basename "${f}")"
        n="$(echo "${f}" | tr 'A-Z' 'a-z')"
        if test "${f}" != "${n}"
        then
            mv "${d}/${f}" "${d}/${n}"
        fi
    done
}

package() {
    local a="${pkgdir}/apps/${_realname}"
    mkdir -p "${a}"
    cp "ScreenToGif.exe" "${a}"
    _lc "${a}"/*
}
