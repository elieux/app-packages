# Maintainer: David Macek <david.macek.0@gmail.com>

_realname=winscp
_apparch=32
pkgbase="app-${_realname}"
pkgname=("app-i686-${_realname}")
pkgver=5.9.2
pkgrel=1
pkgdesc="An open source two-pane SFTP/FTP/WebDAV/SCP client"
url="http://winscp.net/"
arch=('any')
license=('custom')
makedepends=("app-i686-innounp"
             "unzip")
provides=("${pkgbase}")
options=(!strip)
source=("winscpsetup.exe::http://download.sourceforge.net/winscp/WinSCP-${pkgver}-Setup.exe"
        "winscp.zip::http://download.sourceforge.net/winscp/WinSCP-${pkgver}-Portable.zip"
        "winscpautomation.zip::http://download.sourceforge.net/winscp/WinSCP-${pkgver}-Automation.zip")
sha512sums=('726aa0cb99d0d41af04b8ec9841ca013ac3fb2fa004414c9f869e8d5f3c534fad99f346e27af939668907c4511f35d79c36d9479c4c6cbbbe1245fb985cf62fb'
            'a0d91c0d89355454af6327139ca913eeb227e3c58dc7aa6836f9b2937cd85edf6d8eb1c36674256da0e34ac3d95d20f3255975c7c0aa5797c5a9c660bca82438'
            '2b92ed2051a323ba04c9052161eec782b54481ef6151dd0a9251725bc6325a09a512917c8598f145927a79a54c76fbfc8f10b7f8704f16f32fd0903432146413')

_lc() {
    local d f n

    for f in "${@}"
    do
        d="$(dirname "${f}")"
        f="$(basename "${f}")"
        n="$(echo "${f}" | tr 'A-Z' 'a-z')"
        if test "${f}" != "${n}"
        then
            mv "${d}/${f}" "${d}/${n}"
        fi
    done
}

prepare() {
    /apps32/innounp/innounp -x -q -m -a -y "winscpsetup.exe"
    _lc '{app}'/* *
}

package() {
    a="${pkgdir}/apps${_apparch}/${_realname}"
    mkdir -p "${a}"
    cp -r "winscpnet.dll" '{app}'/dragext*.dll '{app}'/winscp.* '{app}'/"extensions"  "${a}"
    rm "${a}/winscp.map"

    d="${pkgdir}/docs${_apparch}/${_realname}"
    mkdir -p "${d}"
    cp *.txt "${d}"
}
