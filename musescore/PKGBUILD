# Maintainer: David Macek <david.macek.0@gmail.com>

_realname=musescore
_apparch=32
pkgbase="app-${_realname}"
pkgname=("app-i686-${_realname}")
pkgver=2.0.2
pkgrel=1
pkgdesc="Create, play and print beautiful sheet music"
url="http://musescore.org/"
arch=('any')
license=('GPL')
provides=("${pkgbase}")
options=(!strip)
source=("MuseScore.msi::ftp://ftp.osuosl.org/pub/musescore/releases/MuseScore-${pkgver}/MuseScore-${pkgver}.msi")
sha512sums=('c67dd2ec8d1a4706f130666627a7f5a789707defaee99858f103d2e1e5032f56bd539bc158e6dd474db05f65075a2be55cf2c04c2aec4e2e6bcdab49d602bcf1')

_lc() {
    for f in "${@}"
    do
        n="$(echo "${f}" | tr 'A-Z' 'a-z')"
        if test "${f}" != "${n}"
        then
            mv "${f}" "${n}"
        fi
    done
}

build() {
    msiexec -a "MuseScore.msi" "TARGETDIR=$(cygpath -wa "install")" -qn

    s="install/MuseScore 2"
    _lc "${s}/bin"/*
}

package() {
    s="install/MuseScore 2"

    a="${pkgdir}/apps${_apparch}/${_realname}"
    mkdir -p "${a}"
    cp -r "${s}"/* "${a}"

    d="${pkgdir}/docs${_apparch}/${_realname}"
    mkdir -p "${d}"
    mv "${a}"/{demos,manual} "${a}/sound/FluidR3Mono_License.md" "${d}"
}
