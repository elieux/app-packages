# Maintainer: David Macek <david.macek.0@gmail.com>

_realname=vivaldi
_apparch=32
pkgbase="app-${_realname}"
pkgname=("app-i686-${_realname}")
pkgver=1.4.589.38
pkgrel=1
pkgdesc="A new browser for our friends"
url="https://vivaldi.com/"
arch=('any')
license=('custom')
makedepends=("p7zip")
provides=("${pkgbase}")
options=(!strip)
source=("Vivaldi.exe::https://downloads.vivaldi.com/stable/Vivaldi.${pkgver}.exe")
sha512sums=('3e6086178a34fcd0237120bededf81014d2e078d8fa66bdaa646d17e5fadbddc52c6d5345f5a40fce17162b836c05fa4ee5421a1c5d1786edfc59e5497cc2b8e')

_lc() {
    for f in "${@}"
    do
        n="$(echo "${f}" | tr 'A-Z' 'a-z')"
        if test "${f}" != "${n}"
        then
            mv "${f}" "${n}"
        fi
    done
}

prepare() {
    7z x -y -bd "Vivaldi.exe"
    7z x -y -bd "vivaldi.7z"
    _lc "Vivaldi-bin"/* "Vivaldi-bin/${pkgver}"/*
}

package() {
    a="${pkgdir}/apps${_apparch}/${_realname}"
    mkdir -p "${a}"
    cp -r "Vivaldi-bin"/*.exe "Vivaldi-bin/${pkgver}"/* "${a}"
}
