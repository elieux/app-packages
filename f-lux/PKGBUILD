# Maintainer: David Macek <david.macek.0@gmail.com>

_realname=f-lux
_apparch=32
pkgbase="app-${_realname}"
pkgname=("app-i686-${_realname}")
pkgver=4.55.0.0
pkgrel=1
pkgdesc="Changes the display color temperature according to the time of the day"
url="http://justgetflux.com/"
arch=('any')
license=('custom')
makedepends=("p7zip")
provides=("${pkgbase}")
options=(!strip)
source=("flux-setup.exe::https://justgetflux.com/flux-setup.exe")
sha512sums=('ae14b319a7c910e5225e2704d9c6241e2657779361c26cf1311dae6ea18830c20502ae3289274e7bb9fdf0c04fcc3844036ac71014494e5a4f898130099d2ddc')

pkgver() {
    7z x -y -bd "flux-setup.exe" "flux.exe" > /dev/null
    b="$(cygpath -wa "flux.exe" | sed 's/\\/\\\\/g')"
    wmic datafile where name=\""${b}"\" get version | grep 'Version' -A 1 | tail -1 | tr -d ' '
}

prepare() {
    7z x -y -bd "flux-setup.exe"
}

package() {
    a="${pkgdir}/apps${_apparch}/${_realname}"
    mkdir -p "${a}"
    cp -r "flux.exe" "runtime" "${a}"
}
