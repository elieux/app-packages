# Maintainer: David Macek <david.macek.0@gmail.com>

_realname=slack
_apparch=32
pkgbase="app-${_realname}"
pkgname=("app-i686-${_realname}")
pkgver=2.2.1
pkgrel=1
pkgdesc="Client for the Slack team communication platform"
url="http://www.slack.com/"
arch=('any')
license=('custom')
makedepends=("p7zip" "unzip")
provides=("${pkgbase}")
options=(!strip)
source=("SlackSetup.exe::https://slack.com/ssb/download-win")
sha512sums=('451351886f3d4b701e66cac7a4cfe226b7761416b440730dfc05e495ec63217917ec17a959673ecfaa069bf7daf6c363dbae6b1f5c3f9b451dad6040c682f367')

_lc() {
    for f in "${@}"
    do
        n="$(echo "${f}" | tr 'A-Z' 'a-z')"
        if test "${f}" != "${n}"
        then
            mv "${f}" "${n}"
        fi
    done
}

pkgver() {
    b="$(cygpath -wa "SlackSetup.exe" | sed 's/\\/\\\\/g')"
    wmic datafile where name=\""${b}"\" get version | grep 'Version' -A 1 | tail -1 | tr -d ' ' | sed -r 's/^([0-9]+[.][0-9]+[.][0-9]+)[.]0+$/\1/'
}

prepare() {
    rm -f "slack"-*-"full.nupkg"
    7z x -y -bd "SlackSetup.exe"
    unzip -o "slack"-*-"full.nupkg"
    _lc "lib/net45"/*
}

package() {
    a="${pkgdir}/apps${_apparch}/${_realname}"
    mkdir -p "${a}"
    cp -r "lib/net45"/* "${a}"
    rm "${a}"/{,resources/app.asar.unpacked/node_modules/edge-atom-shell/build/Release}/{{vccorlib,vcruntime,msvcp,concrt}140,ucrtbase}.dll "${a}"/{,resources/app.asar.unpacked/node_modules/edge-atom-shell/build/Release}/api-ms-win-*.dll

    d="${pkgdir}/docs${_apparch}/${_realname}"
    mkdir -p "${d}"
    mv "${a}/license" "${d}"
}
