# Maintainer: David Macek <david.macek.0@gmail.com>

_realname=ferdi
_choconame=ferdi
pkgbase="app-${_realname}"
pkgname=("app-x86_64-${_realname}")
epoch=1
pkgver=5.6.8
pkgrel=1
pkgdesc="Combine your favourite messaging services into one application, community fork of Franz"
url="https://getferdi.com/"
arch=('any')
license=('Apache2')
makedepends=("p7zip")
provides=("${pkgbase}")
options=(!strip)
source=("Ferdi-Setup.exe::https://github.com/getferdi/ferdi/releases/download/v${pkgver}/Ferdi-Setup-${pkgver}.exe")
sha512sums=('5e5d3d826cbd6cb2cd60857074f27a204540b9ad20e52109ca05a59cf14116399cbdd5ec70a3a6dfb14493e14805dafc666b6ff7324c3b5b7513f920685024f7')

_lc() {
    local d f n

    for f in "${@}"
    do
        d="$(dirname "${f}")"
        f="$(basename "${f}")"
        n="$(echo "${f}" | tr 'A-Z' 'a-z')"
        if test "${f}" != "${n}"
        then
            mv "${d}/${f}" "${d}/${n}"
        fi
    done
}

prepare() {
    7z x -y -bd "Ferdi-Setup.exe" -o"Ferdi-Setup.dir" > /dev/null
    7z x -y -bd "Ferdi-Setup.dir"/'$PLUGINSDIR'/"app-64.7z" -o"app-64" > /dev/null
}

package() {
    local a="${pkgdir}/apps/${_realname}"
    mkdir -p "${a}"
    cp -R "app-64"/* "${a}"
    _lc "${a}"/*
}
