# Maintainer: David Macek <david.macek.0@gmail.com>

_realname=franz
_choconame=franz
pkgbase="app-${_realname}"
pkgname=("app-x86_64-${_realname}")
epoch=1
pkgver=5.8.0
pkgrel=1
pkgdesc="Franz is a free messaging app and combines chat & messaging services into one application"
url="https://meetfranz.com/"
arch=('any')
license=('custom')
replaces=("app-i686-${_realname}")
makedepends=("p7zip")
provides=("${pkgbase}")
options=(!strip)
source=("franz-setup.exe::https://github.com/meetfranz/franz/releases/download/v${pkgver}/franz-setup-${pkgver}.exe")
sha512sums=('c1c658f05b415c1f6183de3d6d63d1920d8dee79cc02b6872e81e8f2182d7aaaf1b4c729afa6db4b56b3b6490d173086cf910c28f888ba70d09327f86473f094')

_lc() {
    local d f n

    for f in "${@}"
    do
        d="$(dirname "${f}")"
        f="$(basename "${f}")"
        n="$(echo "${f}" | tr 'A-Z' 'a-z')"
        if test "${f}" != "${n}"
        then
            mv "${d}/${f}" "${d}/${n}"
        fi
    done
}

prepare() {
    7z x -y -bd "franz-setup.exe" -o"franz-setup.dir" > /dev/null
    7z x -y -bd "franz-setup.dir"/'$PLUGINSDIR'/"app-64.7z" -o"app-64" > /dev/null
}

package() {
    local a="${pkgdir}/apps/${_realname}"
    mkdir -p "${a}"
    cp -R "app-64"/* "${a}"
    _lc "${a}"/*
}
