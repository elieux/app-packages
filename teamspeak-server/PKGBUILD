# Maintainer: David Macek <david.macek.0@gmail.com>

_realname=teamspeak-server
pkgbase="app-${_realname}"
pkgname=("app-i686-${_realname}"
         "app-x86_64-${_realname}")
pkgver=3.0.12.2
pkgrel=1
pkgdesc="A proprietary VoIP conference software"
url="http://www.teamspeak.com/"
arch=('any')
license=('custom')
makedepends=("unzip")
provides=("${pkgbase}")
options=(!strip)
source=("teamspeak3-server_win32.zip::http://dl.4players.de/ts/releases/${pkgver}/teamspeak3-server_win32-${pkgver}.zip"
        "teamspeak3-server_win64.zip::http://dl.4players.de/ts/releases/${pkgver}/teamspeak3-server_win64-${pkgver}.zip")
sha512sums=('eb8766671ddce7d4cefb56d92754af6d600ccece836402edb94f8066709fa716fbd0a3b7038294dfff5c1df44a8d958603a7e7021e6e96e45dbbc7a351a0d119'
            'd5fb6dd8602cc9963103bce91ec0159fbb9c2c6a4ded61bc1ff01f9bd91a980a23d6e8f05f84dd5733a54d6a09db945dba016a0e298e119ab352f4b79be38305')

_package() {
    a="${pkgdir}/apps${_apparch}/${_realname}"
    mkdir -p "${a}"
    cp -r "teamspeak3-server_win${_apparch}/redist/libmariadb.dll" "teamspeak3-server_win${_apparch}/tsdns/tsdnsserver.exe" "teamspeak3-server_win${_apparch}"/*.{exe,dll} "${a}"

    d="${pkgdir}/docs${_apparch}/${_realname}"
    mkdir -p "${d}" "${d}/tsdns"
    cp -r "teamspeak3-server_win${_apparch}"/*.txt "teamspeak3-server_win${_apparch}"/{doc,sql,serverquerydocs} "${d}"
    cp "teamspeak3-server_win${_apparch}/tsdns"/*.{txt,sample} "${d}/tsdns"
}

package_app-i686-teamspeak-server() {
    _apparch=32
    _package
}

package_app-x86_64-teamspeak-server() {
    _apparch=64
    _package
}
