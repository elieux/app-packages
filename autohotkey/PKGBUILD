# Maintainer: David Macek <david.macek.0@gmail.com>

_chocover() {
    # choco version format: 1.1.37
    # choco version format: 1.1.37.1
    echo "${1}" | sed -r -e 's/^([0-9]+)[.]([0-9]+)[.]([0-9]+)$/\1.\2.\3.00/' -e 's/^([0-9]+)[.]([0-9]+)[.]([0-9]+)[.]([0-9])$/\1.\2.\3.0\4/'
}

_realname=autohotkey
_choconame=autohotkey
pkgbase="app-${_realname}"
pkgname=("app-x86_64-${_realname}")
epoch=1
pkgver=1.1.37.01
pkgrel=1
pkgdesc="Macro-creation and automation-oriented scripting utility for Windows"
url="https://www.autohotkey.com/"
arch=('any')
license=('GPL2')
provides=("${pkgbase}")
makedepends=("p7zip")
options=(!strip)
source=("AutoHotkey_setup.exe::https://github.com/AutoHotkey/AutoHotkey/releases/download/v${pkgver}/AutoHotkey_${pkgver}_setup.exe")
sha512sums=('5fc75ae915ce44a69411c81c9b852475acc6a954fd20d3a2f4850dda7b938e921b8a9ebd79ac3987c2a6c7ec9969b5905c73ecc5341e243ce451d625c9bb2626')

_lc() {
    local d f n

    for f in "${@}"
    do
        d="$(dirname "${f}")"
        f="$(basename "${f}")"
        n="$(echo "${f}" | tr 'A-Z' 'a-z')"
        if test "${f}" != "${n}"
        then
            mv "${d}/${f}" "${d}/${n}"
        fi
    done
}

prepare() {
    7z x -y -bd "AutoHotkey_setup.exe" -o"AutoHotkey_setup.dir" > /dev/null
}

package() {
    local a="${pkgdir}/apps/${_realname}"
    mkdir -p "${a}"
    cp -R "AutoHotkey_setup.dir"/* "${a}"
    mv "${a}/Compiler"/* "${a}"
    rm -R "${a}/Compiler" "${a}/setup.exe" "${a}/Installer.ahk"
    _lc "${a}"/*
}
