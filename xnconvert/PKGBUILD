# Maintainer: David Macek <david.macek.0@gmail.com>

_realname=xnconvert
pkgbase="app-${_realname}"
pkgname=("app-x86_64-${_realname}")
epoch=1
pkgver=1.82
pkgrel=1
pkgdesc="A bulk image converter"
url="https://www.xnview.com/en/xnconvert/"
arch=('any')
license=('custom')
makedepends=("unzip")
provides=("${pkgbase}")
options=(!strip)
source=("XnConvert-win-x64.zip::https://download.xnview.com/XnConvert-win-x64.zip")
sha512sums=('50502db4371d0039a59ef37f5c3c9b4246eb6a645ff437414717b07339701ee22716b7eb52d7ea6aeb825ead78940160d2a2f701e51c564a71e5939e3ae38fea')

_lc() {
    local d f n

    for f in "${@}"
    do
        d="$(dirname "${f}")"
        f="$(basename "${f}")"
        n="$(echo "${f}" | tr 'A-Z' 'a-z')"
        if test "${f}" != "${n}"
        then
            mv "${d}/${f}" "${d}/${n}"
        fi
    done
}

_rmmsdll() {
    local ms

    ms=("msvbvm[0-9]+"
        "ucrtbase"
        "mfcm?[0-9]+[a-z]+"
        "msvc[rp][0-9]+"
        "vc([ao]mp|corlib|runtime)[0-9]+"
        "concrt[0-9]+"
        "pgort[0-9]+"
        "api-ms-win-[0-9a-z-]+")
    find "${1}" -iname '*.dll' | grep -Eif <(for m in "${ms[@]}"; do echo "/${m}[.]dll$"; done) | xargs -r rm
}

prepare() {
    _lc "XnConvert"/*
}

pkgver() {
    "${SYSTEMROOT}/System32/WindowsPowerShell/v1.0/powershell" -command '(Get-Item "XnConvert/xnconvert.exe").VersionInfo.ProductVersion'
}

package() {
    a="${pkgdir}/apps/${_realname}"
    mkdir -p "${a}"
    cp -R "XnConvert"/* "${a}"
    _rmmsdll "${a}"
}
