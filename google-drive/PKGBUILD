# Maintainer: David Macek <david.macek.0@gmail.com>

_realname=google-drive
pkgbase="app-${_realname}"
pkgname=("app-i686-${_realname}")
epoch=1
pkgver=3.47.7654.300
pkgrel=1
pkgdesc="The Google Drive synchronization tool"
url="http://www.google.com/drive/"
arch=('any')
license=('custom')
makedepends=("app-lessmsi")
provides=("${pkgbase}")
options=(!strip)
source=("gsync_enterprise.msi::https://dl.google.com/drive/gsync_enterprise.msi")
sha512sums=('813fcae7ad7253378ee7050e6457880d859cbd672837d667086d42fe14efa3e77e8d89b2294d5f8efe2cdb59562542b98afae759d90e7361e0ffeaa538779374')

prepare() {
    rm -fR "gsync_enterprise"
    /apps/lessmsi/lessmsi x "gsync_enterprise.msi"
}

pkgver() {
    "${SYSTEMROOT}/System32/WindowsPowerShell/v1.0/powershell" -command '(Get-Item "gsync_enterprise/SourceDir/Google/Drive/googledrivesync.exe").VersionInfo.FileVersionRaw.ToString()' | dos2unix
}

package() {
    a="${pkgdir}/apps/${_realname}"
    mkdir -p "${a}"
    cp -R "gsync_enterprise/SourceDir/Google/Drive"/* "${a}"
    rm -fR "${a}"/Microsoft.VC90.{ATL,CRT,MFC}
}
