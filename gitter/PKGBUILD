# Maintainer: David Macek <david.macek.0@gmail.com>

_realname=gitter
pkgbase="app-${_realname}"
pkgname=("app-i686-${_realname}")
epoch=1
pkgver=4.1.0
pkgrel=1
pkgdesc="Client for the Gitter communication platform"
url="https://gitter.im/"
arch=('any')
license=('custom')
makedepends=("app-innounp")
provides=("${pkgbase}")
options=(!strip)
source=("GitterSetup.exe::https://update.gitter.im/win/GitterSetup-${pkgver}.exe")
sha512sums=('f55367b2d559a8d3828daf9b50d749ed96e34fffbc93c38a15bc4c6fbf1d7efb132d52adc9554b793b96af39c9f84ed47d9130405dab1b07ed1c0313a567c335')

_lc() {
    local d f n

    for f in "${@}"
    do
        d="$(dirname "${f}")"
        f="$(basename "${f}")"
        n="$(echo "${f}" | tr 'A-Z' 'a-z')"
        if test "${f}" != "${n}"
        then
            mv "${d}/${f}" "${d}/${n}"
        fi
    done
}

prepare() {
    /apps/innounp/innounp -x -q -m -a -y "GitterSetup.exe" -d"GitterSetup.dir"
}

package() {
    local a="${pkgdir}/apps/${_realname}"
    mkdir -p "${a}"
    cp -R "GitterSetup.dir/{app}"/* "${a}"
    local f
    for f in "${a}"/Gitter*.exe
    do
        mv -f "${f}" "${a}/Gitter.exe"
    done
    _lc "${a}"/*
}
