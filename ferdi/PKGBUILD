# Maintainer: David Macek <david.macek.0@gmail.com>

_realname=ferdi
_choconame=ferdi
pkgbase="app-${_realname}"
pkgname=("app-x86_64-${_realname}")
epoch=1
pkgver=5.8.1
pkgrel=1
pkgdesc="Combine your favourite messaging services into one application, community fork of Franz"
url="https://getferdi.com/"
arch=('any')
license=('Apache2')
makedepends=("p7zip")
provides=("${pkgbase}")
options=(!strip)
source=("Ferdi-Setup.exe::https://downloads.sourceforge.net/ferdi.mirror/v${pkgver}/Ferdi-Setup-${pkgver}.exe")
sha512sums=('5b684e2d4eda8e495d14784e119083e1bf913116ee1ed66d0cb3928cdce4cc593dc1ef4083af22a7d4568e46239168f69202b97c3e589395c27f29e5ba38e3f5')
noextract=("Ferdi-Setup.exe")

_lc() {
    local d f n

    for f in "${@}"
    do
        d="$(dirname "${f}")"
        f="$(basename "${f}")"
        n="$(echo "${f}" | tr 'A-Z' 'a-z')"
        if test "${f}" != "${n}"
        then
            mv "${d}/${f}" "${d}/${n}"
        fi
    done
}

prepare() {
    7z x -y -bd "Ferdi-Setup.exe" -o"Ferdi-Setup.dir" > /dev/null
    7z x -y -bd "Ferdi-Setup.dir"/'$PLUGINSDIR'/"app-64.7z" -o"app-64" > /dev/null
}

package() {
    local a="${pkgdir}/apps/${_realname}"
    mkdir -p "${a}"
    cp -R "app-64"/* "${a}"
    _lc "${a}"/*
}
