# Maintainer: David Macek <david.macek.0@gmail.com>

_realname=perfmonitor
_dirname=cpuid
pkgbase="app-${_realname}"
pkgname=("app-i686-${_realname}"
         "app-x86_64-${_realname}")
pkgver=2.04
pkgrel=3
pkgdesc="Monitors processor performance and other values (CPUID)"
url="http://www.cpuid.com/softwares/perfmonitor-2.html"
arch=('any')
license=('custom')
makedepends=("unzip")
provides=("${pkgbase}")
options=(!strip)
source=("perfmonitor-2.zip::http://download.cpuid.com/${_realname}-2/${_realname}-2_${pkgver}.zip")
sha512sums=('0688224b3ccf87ff3ef90ea5bc8dc615b581fec29ec9a2c8683ebf45f44a58207b29aa663d8f36edecb768d6a7f617265ad59de4466974fa89870f25bdee9630')

_lc() {
    local d f n

    for f in "${@}"
    do
        d="$(dirname "${f}")"
        f="$(basename "${f}")"
        n="$(echo "${f}" | tr 'A-Z' 'a-z')"
        if test "${f}" != "${n}"
        then
            mv "${d}/${f}" "${d}/${n}"
        fi
    done
}

prepare() {
    _lc *
}

_package() {
    a="${pkgdir}/apps${_apparch}/${_dirname}"
    mkdir -p "${a}"
    cp "perfmonitor2_x${_apparch}.exe" "${a}/perfmonitor2.exe"
    cp "perfmonitor2.pdf" "${a}"
}

package_app-i686-perfmonitor() {
    _apparch=32
    _package
}

package_app-x86_64-perfmonitor() {
    _apparch=64
    _package
}
