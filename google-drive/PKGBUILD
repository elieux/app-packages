# Maintainer: David Macek <david.macek.0@gmail.com>

_realname=google-drive
_choconame=googledrive
pkgbase="app-${_realname}"
pkgname=("app-x86_64-${_realname}")
epoch=1
pkgver=74.0.3.0
pkgrel=1
pkgdesc="The Google Drive synchronization tool"
url="https://www.google.com/drive/"
arch=('any')
license=('custom')
replaces=("app-i686-${_realname}")
makedepends=("p7zip")
provides=("${pkgbase}")
options=(!strip)
source=("GoogleDriveSetup.exe::https://dl.google.com/drive-file-stream/GoogleDriveSetup.exe")
sha512sums=('de685b36d2753f29562f08cb539c62464c54a7aad8c4b967b48136434a402cab47ac2ea267dca136b51b72cc06334fb809b07b628edc1b66ab26f73f1708779a')

prepare() {
    7z x -y -bd "GoogleDriveSetup.exe" -o"GoogleDriveSetup.dir" > /dev/null
    7z x -y -bd "GoogleDriveSetup.dir/.rsrc/RCDATA/101" -o"101" > /dev/null
}

pkgver() {
    powershell -command '(Get-Item "101/GoogleDriveFS.exe").VersionInfo.FileVersionRaw.ToString()'
}

package() {
    local a="${pkgdir}/apps/${_realname}"
    mkdir -p "${a}"
    cp -R "101"/* "${a}"
}
