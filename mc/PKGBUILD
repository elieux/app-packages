# Maintainer: David Macek <david.macek.0@gmail.com>

_realname=mc
_realver=230
_choconame=mc
pkgbase="app-${_realname}"
pkgname=("app-i686-${_realname}")
epoch=1
pkgver=4.8.31.230
pkgrel=1
pkgdesc="GNU terminal two-pane file manager"
url="https://sourceforge.net/projects/mcwin32/"
arch=('any')
license=('GPL3')
provides=("${pkgbase}")
makedepends=("app-innounp")
options=(!strip)
source=("mcwin32-build-setup.exe::https://downloads.sourceforge.net/mcwin32/mcwin32-build${_realver}-setup.exe")
sha512sums=('af17a4d99e9d11fad1af94d98fab4feac726fb4fd5bfc2152fc93a7c600cde077701943d44ed6513323eeba9812763bfd39a48595f12d51d29d4cb844843476d')

prepare() {
    /apps/innounp/innounp -x -q -m -a -y "mcwin32-build-setup.exe" -d"mcwin32-build-setup.dir"
}

pkgver() {
    powershell -command '(Get-Item "mcwin32-build-setup.exe").VersionInfo.ProductVersion.ToString()' | sed -r 's/^([0-9]+[.][0-9]+[.][0-9]+) [(]build: [0-9]{8}-([0-9]+)[)] *$/\1.\2/'
}

package() {
    local a="${pkgdir}/apps/${_realname}"
    mkdir -p "${a}"
    cp -R "mcwin32-build-setup.dir/{app}"/* "${a}"
}
