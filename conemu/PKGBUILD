# Maintainer: David Macek <david.macek.0@gmail.com>

_realname=conemu
pkgbase="app-${_realname}"
pkgname=("app-i686-${_realname}"
         "app-x86_64-${_realname}")
pkgver=16.03.01
pkgrel=1
pkgdesc="A powerful, highly customizable console emulator with tabs"
url="http://conemu.github.io/"
arch=('any')
license=('BSD')
makedepends=("p7zip")
provides=("${pkgbase}")
options=(!strip)
source=("ConEmuPack.7z::https://github.com/Maximus5/ConEmu/releases/download/v${pkgver}/ConEmuPack.${pkgver//./}.7z")
sha512sums=('931eac5109008f1cd8db0e90a2448f31b3c24c75db4fd443afb2c8e1e791e60dc14c5ab198a853efad706234050ab1452fd1bd0da9418a83ee8a88aa9823d328')

_lc() {
    for f in "${@}"
    do
        n="$(echo "${f}" | tr 'A-Z' 'a-z')"
        if test "${f}" != "${n}"
        then
            mv "${f}" "${n}"
        fi
    done
}

prepare() {
    _lc *
}

_package() {
    a="${pkgdir}/apps${_apparch}/${_realname}"
    mkdir -p "${a}"
    cp -r "conemu${1}.exe" "${a}/conemu.exe"
    cp -r "conemu" "plugins" "${a}"

    d="${pkgdir}/docs${_apparch}/${_realname}"
    mkdir -p "${d}"
    mv "${a}/conemu"/*.txt "${d}"
}

package_app-i686-conemu() {
    _apparch=32
    _package ""
}

package_app-x86_64-conemu() {
    _apparch=64
    _package "64"
}
