# Maintainer: David Macek <david.macek.0@gmail.com>

_realname=coretemp
pkgbase="app-${_realname}"
pkgname=("app-x86_64-${_realname}")
epoch=1
pkgver=1.11.0
pkgrel=1
pkgdesc="A compact program to monitor processor temperature"
arch=('any')
url="http://www.alcpu.com/CoreTemp/"
license=('custom')
replaces=("app-i686-${_realname}")
makedepends=("unzip")
provides=("${pkgbase}")
options=(!strip)
source=("CoreTemp64.zip::http://www.alcpu.com/CoreTemp/CoreTemp64.zip"
        "CoreTempTranslator.zip::http://www.alcpu.com/CoreTemp/CoreTempTranslator.zip")
sha512sums=('b7804c6d48d611a54007140cae94aa107c06e0fbc7fb82731077b899caa4d6e77c5a61b33cec2e85f5b7f56e726423dc281d92e8a2aa03646a547b295f2e2ded'
            'be6a08b8e797dbf662ae88aff592b29d720c3069ad6fcc255446bc41ae15cec8b4755953e1c2b7e00aea982f04c4de7c4930af55d64ab6601819643abe373d81')
noextract=("CoreTemp64.zip")

_lc() {
    local d f n

    for f in "${@}"
    do
        d="$(dirname "${f}")"
        f="$(basename "${f}")"
        n="$(echo "${f}" | tr 'A-Z' 'a-z')"
        if test "${f}" != "${n}"
        then
            mv "${d}/${f}" "${d}/${n}"
        fi
    done
}

pkgver() {
    b="$(cygpath -wa "coretemp/core temp.exe" | sed 's/\\/\\\\/g')"
    wmic datafile where name=\""${b}"\" get version | grep 'Version' -A 1 | tail -1 | tr -d ' ' | sed -r 's/([0-9]+[.][0-9]+[.][0-9]+)[.]0$/\1/'
}

prepare() {
    unzip -o "CoreTemp64.zip" -d "CoreTemp"
    _lc "CoreTemp"/* "languages"/* *
}

package() {
    a="${pkgdir}/apps/${_realname}"
    mkdir -p "${a}" "${a}/translator"
    cp -r *.{dll,exe,lng} "languages" "coretemp"/*.exe "${a}"
    mv "${a}/core temp.exe" "${a}/coretemp.exe"
    cp "coretemp"/*.{url,txt} "${a}"
    cp *.{txt,png} "${a}/translator"
}
