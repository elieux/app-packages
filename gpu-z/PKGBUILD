# Maintainer: David Macek <david.macek.0@gmail.com>

_realver=1565

DLAGENTS=("https::/usr/bin/curl -qb '' -fLC - -d id=${_realver}&server_id=8 -o %o %u")

_realname=gpu-z
pkgbase="app-${_realname}"
pkgname=("app-i686-${_realname}")
epoch=1
pkgver=2.33.0
pkgrel=1
pkgdesc="Lightweight utility providing GPU and graphics card information"
arch=('any')
url="https://www.techpowerup.com/gpuz/"
license=('custom')
provides=("${pkgbase}")
options=(!strip)
source=("GPU-Z.exe::https://www.techpowerup.com/download/techpowerup-gpu-z/")
sha512sums=('8f47a59d2f3ea330c2e63d3f010e1a8dbd419e5bae1dd189db0aceb88a9c1ae85671b345ecce3475a28e32aed0730db21b5cd77be434f3d893ee288054baf181')

pkgver() {
    powershell -command '(Get-Item "GPU-Z.exe").VersionInfo.ProductVersion' | sed -r 's/^([0-9]+[.][0-9]+[.][0-9]+)[.][0-9]+$/\1/'
}

package() {
    local a="${pkgdir}/apps/${_realname}"
    mkdir -p "${a}"
    cp "GPU-Z.exe" "${a}/gpu-z.exe"
}
