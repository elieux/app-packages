# Maintainer: David Macek <david.macek.0@gmail.com>

_realname=docker-cli
_realver=83626
_choconame=docker-cli
_dirname=docker
pkgbase="app-${_realname}"
pkgname=("app-x86_64-${_realname}")
epoch=1
pkgver=20.10.17
pkgrel=1
pkgdesc="Pack, ship and run any application as a lightweight container"
url="https://docs.docker.com/engine/reference/commandline/cli/"
arch=('any')
license=('Apache')
makedepends=("p7zip")
provides=("${pkgbase}")
replaces=("app-x86_64-docker")
conflicts=("app-x86_64-docker")
options=(!strip)
source=("DockerDesktopInstaller.exe::https://desktop.docker.com/win/main/amd64/${_realver}/Docker%20Desktop%20Installer.exe")
sha512sums=('b8fe048681215683015d061d6b0c23463fba04133e0da676a83fbb21f5c6343fb890fd9ccb6db1661c5e253817367f65e827faa40e2c92fa04d8173b0cb6e9e9')

prepare() {
    7z x -y -bd "DockerDesktopInstaller.exe" -o"DockerDesktopInstaller.dir" > /dev/null
}

pkgver() {
    powershell -command '(Get-Item "DockerDesktopInstaller.dir/resources/bin/com.docker.cli.exe").VersionInfo.ProductVersion'
}

package() {
    local a="${pkgdir}/apps/${_dirname}"
    mkdir -p "${a}"
    cp "DockerDesktopInstaller.dir/resources/bin/com.docker.cli.exe" "${a}/docker.exe"
}
