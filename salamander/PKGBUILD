# Maintainer: David Macek <david.macek.0@gmail.com>

_realname=salamander
_choconame=altap-salamander
pkgbase="app-${_realname}"
pkgname=("app-x86_64-${_realname}")
epoch=1
pkgver=4.0
pkgrel=1
pkgdesc="Powerful two panel file manager"
url="https://www.altap.cz/"
arch=('any')
license=('custom')
provides=("${pkgbase}")
makedepends=("p7zip")
options=(!strip)
source=("asx64.exe::ftp://ftp.altap.cz/pub/altap/salamand/as${pkgver/./}x64.exe")
sha512sums=('d119f06e29de6ca082a2688588729a69b0d141e6b55268466ccd476684c52f8e7e2e381c5459196c1c4f7b59a188cc92395658d9c7e11b64ecac73389e6acbf3')

_rmmsdll() {
    local ms

    ms=("msvbvm[0-9]+"
        "ucrtbase"
        "mfcm?[0-9]+[a-z]+"
        "msvc[rp][0-9]+(_([0-9]|codecvt_ids))?"
        "vc([ao]mp|corlib|runtime)[0-9]+(_[0-9])?"
        "concrt[0-9]+"
        "pgort[0-9]+"
        "api-ms-win-[0-9a-z-]+")
    find "${1}" -iname '*.dll' | grep -Eif <(for m in "${ms[@]}"; do echo "/${m}[.]dll$"; done) | xargs -r rm
}

prepare() {
    7z x -y -bd "asx64.exe" -o"asx64.dir" > /dev/null
}

package() {
    local a="${pkgdir}/apps/${_realname}"
    mkdir -p "${a}"
    cp -R "asx64.dir"/* "${a}"
    rm -R "${a}/remove" "${a}/x64" "${a}"/setup.{exe,inf}
    _rmmsdll "${a}"
}
