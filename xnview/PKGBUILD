# Maintainer: David Macek <david.macek.0@gmail.com>

_realname=xnview
_choconame=XnView
pkgbase="app-${_realname}"
pkgname=("app-i686-${_realname}")
epoch=1
pkgver=2.50.4
pkgrel=1
pkgdesc="An efficient multimedia viewer, browser and converter"
url="https://www.xnview.com/en/xnview/"
arch=('any')
license=('custom')
provides=("${pkgbase}")
options=(!strip)
source=("XnView-win-full.zip::https://download3.xnview.com/XnView-win-full.zip")
sha512sums=('04c0140564bfe17c4c19cde7d50d219a9c6f40a6373120a474bedf4479ec4be9b17b7b9d51e5d7d3564bc18b889ba1feafea6cd09badcb185f6eac98df868c8b')

_lc() {
    local d f n

    for f in "${@}"
    do
        d="$(dirname "${f}")"
        f="$(basename "${f}")"
        n="$(echo "${f}" | tr 'A-Z' 'a-z')"
        if test "${f}" != "${n}"
        then
            mv "${d}/${f}" "${d}/${n}"
        fi
    done
}

pkgver() {
    powershell -command '(Get-Item "XnView/xnview.exe").VersionInfo.ProductVersion'
}

package() {
    depends=("app-nconvert" "app-xnshell")

    local a="${pkgdir}/apps/${_realname}"
    mkdir -p "${a}"
    cp -R "XnView"/* "${a}"
    rm -R "${a}/AddOn/Microsoft.VC90.CRT"

    # the separate app-xnshell package contains newer versions as well as a 64-bit version
    rm -R "${a}/ShellEx"

    # the separate app-nconvert package contains newer versions
    rm -R "${a}/nconvert.exe" "${a}"/{Formats,license,Plugins,ReadMe,Usage,WhatsNew}.txt

    _lc "${a}"/*
}
