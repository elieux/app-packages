# Maintainer: David Macek <david.macek.0@gmail.com>

_realname=vscode
pkgbase="app-${_realname}"
pkgname=("app-x86_64-${_realname}")
epoch=1
pkgver=1.39.2
pkgrel=1
pkgdesc="Editor for building and debugging modern web and cloud applications"
arch=('any')
url="https://code.visualstudio.com/"
license=('custom')
replaces=("app-i686-${_realname}")
makedepends=("unzip")
provides=("${pkgbase}")
options=(!strip)
source=("VSCode-win64.zip::https://vscode-update.azurewebsites.net/${pkgver}/win32-x64-archive/stable")
sha512sums=('c58f84ff12a79274202d21597b99622de05d4f9717be2b87f58628b46e48c1209451381a73a8cc7c6ef332d9f6ca29c05fc9228e53a8f0ae9e5800faa767e97e')
noextract=("VSCode-win64.zip")

_lc() {
    local d f n

    for f in "${@}"
    do
        d="$(dirname "${f}")"
        f="$(basename "${f}")"
        n="$(echo "${f}" | tr 'A-Z' 'a-z')"
        if test "${f}" != "${n}"
        then
            mv "${d}/${f}" "${d}/${n}"
        fi
    done
}

_rmmsdll() {
    local ms

    ms=("msvbvm[0-9]+"
        "ucrtbase"
        "mfcm?[0-9]+[a-z]+"
        "msvc[rp][0-9]+"
        "vc([ao]mp|corlib|runtime)[0-9]+"
        "concrt[0-9]+"
        "pgort[0-9]+"
        "api-ms-win-[0-9a-z-]+")
    find "${1}" -iname '*.dll' | grep -Eif <(for m in "${ms[@]}"; do echo "/${m}[.]dll$"; done) | xargs -r rm
}

prepare() {
    unzip -o "VSCode-win64.zip" -d "vscode"
    _lc "vscode"/*
}

package() {
    a="${pkgdir}/apps/${_realname}"
    mkdir -p "${a}"
    cp -r "vscode"/* "${a}"
    _rmmsdll "${a}"
}
