# Maintainer: David Macek <david.macek.0@gmail.com>

_realname=chromium
_realver=414607
pkgbase="app-${_realname}"
pkgname=("app-i686-${_realname}"
         "app-x86_64-${_realname}")
epoch=1
pkgver=54.0.2840.99
pkgrel=1
pkgdesc="The open-source project behind Google Chrome, an attempt at creating a safer, faster, and more stable browser"
url="http://www.chromium.org/Home"
arch=('any')
license=('BSD')
makedepends=("unzip")
provides=("${pkgbase}")
options=(!strip)
source=("chrome-win32.zip::https://github.com/henrypp/chromium/releases/download/v${pkgver}-r${_realver}-win32/chromium-sync.zip"
        "chrome-win64.zip::https://github.com/henrypp/chromium/releases/download/v${pkgver}-r${_realver}-win64/chromium-sync.zip")
sha512sums=('36dbfed051b0d1accc0deeeb9de0ef14dd6df1cad0a6c7586410bbbe4cb1be4b36d684d7b5fc46760d4f1153e4fdf07fa16b8ebe7f41b29b4d15ae2b2f043571'
            'b896104b7e4ddf61cac085c341c5b2e9bebf036cc15c32e976ab138ad9841c2f03246125ddcce0a471474e1eb08ee446c92c9b58e06ce263c491c0964d59a3dc')
noextract=("chrome-win32.zip"
           "chrome-win64.zip")

_lc() {
    for f in "${@}"
    do
        n="$(echo "${f}" | tr 'A-Z' 'a-z')"
        if test "${f}" != "${n}"
        then
            mv "${f}" "${n}"
        fi
    done
}

pkgver() {
    for _apparch in 32 64
    do
        eval _ver${_apparch}="$(unzip -l "chrome-win${_apparch}.zip" | tail -n +4 | head -n -2 | sed -r 's/[ ]+/\t/g' | cut -f 5 | sed 's~^chrome-win32/~~' | sed 's~.manifest$~~' | grep -P '[0-9]+[.][0-9]+[.][0-9]+[.][0-9]+' | sort | tail -1)"
    done
    if [ "${_ver32}" != "${_ver64}" ]
    then
        echo "32-bit version number is different from 64-bit."
    else
        echo "${_ver32}"
    fi
}

prepare() {
    for _apparch in 32 64
    do
        unzip -o "chrome-win${_apparch}.zip" -d "chromium${_apparch}"
        _lc "chromium${_apparch}"/*
    done
}

_package() {
    a="${pkgdir}/apps${_apparch}/${_realname}"
    mkdir -p "${a}"
    cp -r "chromium${_apparch}/chrome-win32"/* "${a}"
}

package_app-i686-chromium() {
    _apparch=32
    _package
}

package_app-x86_64-chromium() {
    _apparch=64
    _package
}
