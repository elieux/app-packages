# Maintainer: David Macek <david.macek.0@gmail.com>

_realname=autohotkey
_choconame=autohotkey
pkgbase="app-${_realname}"
pkgname=("app-x86_64-${_realname}")
epoch=1
pkgver=1.1.34.02
pkgrel=1
pkgdesc="Macro-creation and automation-oriented scripting utility for Windows"
url="https://www.autohotkey.com/"
arch=('any')
license=('GPL2')
provides=("${pkgbase}")
makedepends=("p7zip")
options=(!strip)
source=("AutoHotkey_setup.exe::https://github.com/Lexikos/AutoHotkey_L/releases/download/v${pkgver}/AutoHotkey_${pkgver}_setup.exe")
sha512sums=('ebe03cd71200f1e4faec0b04929d58d33b049d588d907c27908fbfc65f6b5b9731bc14a4d4fa64725b715ca31a6ae76d4e43e3fafd78e431f80c082397539aba')

_lc() {
    local d f n

    for f in "${@}"
    do
        d="$(dirname "${f}")"
        f="$(basename "${f}")"
        n="$(echo "${f}" | tr 'A-Z' 'a-z')"
        if test "${f}" != "${n}"
        then
            mv "${d}/${f}" "${d}/${n}"
        fi
    done
}

prepare() {
    7z x -y -bd "AutoHotkey_setup.exe" -o"AutoHotkey_setup.dir" > /dev/null
}

package() {
    local a="${pkgdir}/apps/${_realname}"
    mkdir -p "${a}"
    cp -R "AutoHotkey_setup.dir"/* "${a}"
    mv "${a}/Compiler"/* "${a}"
    rm -R "${a}/Compiler" "${a}/setup.exe" "${a}/Installer.ahk"
    _lc "${a}"/*
}
