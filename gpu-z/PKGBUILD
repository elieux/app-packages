# Maintainer: David Macek <david.macek.0@gmail.com>

_realver=2160

DLAGENTS=("https::/usr/bin/curl -qb '' -fLC - -d id=${_realver}&server_id=8 -o %o %u")

_realname=gpu-z
_choconame=gpu-z
pkgbase="app-${_realname}"
pkgname=("app-i686-${_realname}")
epoch=1
pkgver=2.46.0
pkgrel=1
pkgdesc="Lightweight utility providing GPU and graphics card information"
arch=('any')
url="https://www.techpowerup.com/gpuz/"
license=('custom')
provides=("${pkgbase}")
options=(!strip)
source=("GPU-Z.exe::https://www.techpowerup.com/download/techpowerup-gpu-z/")
sha512sums=('33990c88336aa07db19520a02e92dec068e2e2a8cfdf7be8d212eab27c7306827f70d55f66e0f1fa5cb3b0feb3cb6038bbf5a5c72e28b38c2c33fb3da0fe6f34')

pkgver() {
    powershell -command '(Get-Item "GPU-Z.exe").VersionInfo.ProductVersion' | sed -r 's/^([0-9]+[.][0-9]+[.][0-9]+)[.][0-9]+$/\1/'
}

package() {
    local a="${pkgdir}/apps/${_realname}"
    mkdir -p "${a}"
    cp "GPU-Z.exe" "${a}/gpu-z.exe"
}
