# Maintainer: David Macek <david.macek.0@gmail.com>

_realname=carotdav
_apparch=32
pkgbase="app-${_realname}"
pkgname=("app-i686-${_realname}")
pkgver=1.15.2
pkgrel=2
pkgdesc="A simple WebDAV, FTP, cloud drives and IMAP client"
url="http://rei.to/carotdav_en.html"
arch=('any')
license=('custom')
makedepends=("unzip")
provides=("${pkgbase}")
options=(!strip)
source=("CarotDAV.portable.zip::http://rei.to/CarotDAV${pkgver}.portable.zip"
        "installed.txt")
sha512sums=('9cdac734c636555f8efb3341a267c06f33e402fb463341c869dceff40c9220119274e468b72605344bc7531a370d664805e75f77fbbd89530cc71418a722c135'
            'd9263cc4a54c6ac861cb02126ef86f4c7d2d3a7b2cdcb6bcb80978dfd1e81597788e2c8bee16c1038ebf5e892ccbdd3cd2c0ee92a87e494560dfbc7b7c46ab0b')

_lc() {
    local d f n

    for f in "${@}"
    do
        d="$(dirname "${f}")"
        f="$(basename "${f}")"
        n="$(echo "${f}" | tr 'A-Z' 'a-z')"
        if test "${f}" != "${n}"
        then
            mv "${d}/${f}" "${d}/${n}"
        fi
    done
}

prepare() {
    _lc "CarotDAV"/*
}

package() {
    a="${pkgdir}/apps${_apparch}/${_realname}"
    mkdir -p "${a}"
    cp "installed.txt" "CarotDAV"/*.{dll,exe,xml,config} "${a}"
    cp "CarotDAV"/*.txt "${a}"
}
