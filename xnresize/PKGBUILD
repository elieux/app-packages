# Maintainer: David Macek <david.macek.0@gmail.com>

_realname=xnresize
pkgbase="app-${_realname}"
pkgname=("app-x86_64-${_realname}")
epoch=1
pkgver=1.10
pkgrel=1
pkgdesc="Fast, powerful and free cross-platform image resizer"
url="https://www.xnview.com/en/xnresize/"
arch=('any')
license=('custom')
provides=("${pkgbase}")
options=(!strip)
source=("XnResize-win-x64.zip::https://download.xnview.com/XnResize-win-x64.zip")
sha512sums=('fec3d2ef032693a9ee6df5941ea9b6b3d2c969491317bd4bf3e67609c0c68f1bb82ab68870b17ef70843b03277f0a2a4fea2ce551719913ff6bb1ff665a2c571')

_lc() {
    local d f n

    for f in "${@}"
    do
        d="$(dirname "${f}")"
        f="$(basename "${f}")"
        n="$(echo "${f}" | tr 'A-Z' 'a-z')"
        if test "${f}" != "${n}"
        then
            mv "${d}/${f}" "${d}/${n}"
        fi
    done
}

_rmmsdll() {
    local ms

    ms=("msvbvm[0-9]+"
        "ucrtbase"
        "mfcm?[0-9]+[a-z]+"
        "msvc[rp][0-9]+(_([0-9]|codecvt_ids|atomic_wait))?"
        "vc([ao]mp|corlib|runtime)[0-9]+(_([0-9]|threads))?"
        "concrt[0-9]+"
        "pgort[0-9]+"
        "api-ms-win-[0-9a-z-]+")
    find "${1}" -iname '*.dll' | grep -Eif <(for m in "${ms[@]}"; do echo "/${m}[.]dll$"; done) | xargs -r rm
}

pkgver() {
    powershell -command '(Get-Item "XnResize/XnResize.exe").VersionInfo.ProductVersion'
}

package() {
    local a="${pkgdir}/apps/${_realname}"
    mkdir -p "${a}"
    cp -R "XnResize"/* "${a}"
    _rmmsdll "${a}"
    rm "${a}/The latest supported Visual C++ downloads.website"
    _lc "${a}"/*
}
