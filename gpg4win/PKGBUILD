# Maintainer: David Macek <david.macek.0@gmail.com>

_realname=gpg4win
_choconame=Gpg4win
pkgbase="app-${_realname}"
pkgname=("app-i686-${_realname}")
epoch=1
pkgver=4.4.1
pkgrel=1
pkgdesc="A secure solution for file and email encryption"
url="https://www.gpg4win.org/index.html"
arch=('any')
license=('custom')
makedepends=("p7zip")
provides=("${pkgbase}")
options=(!strip)
source=("gpg4win.exe::https://files.gpg4win.org/gpg4win-${pkgver}.exe")
sha512sums=('2674a4f54dc31f21d30a27412f79d9799cfe1086c3ae06a7ebd15a9f1ff7351ea2eae34188d5928055a0576b4c7cb7b7ec9021077e010338973fdc44a004c163')
noextract=("gpg4win.exe")

_lc() {
    local d f n

    for f in "${@}"
    do
        d="$(dirname "${f}")"
        f="$(basename "${f}")"
        n="$(echo "${f}" | tr 'A-Z' 'a-z')"
        if test "${f}" != "${n}"
        then
            mv "${d}/${f}" "${d}/${n}"
        fi
    done
}

prepare() {
    7z x -y -bd "gpg4win.exe" -o"gpg4win.dir" > /dev/null
}

package() {
    local a="${pkgdir}/apps/${_realname}"
    mkdir -p "${a}"
    cp -R "gpg4win.dir"/* "${a}"
    find "${a}" -iname '*.tmp' -delete
    rm -R "${a}"/'$PLUGINSDIR' "${a}"/'$TEMP'
    rm "${a}/bin/pinentry.exe"
    cp "${a}/bin/pinentry-w32.exe" "${a}/bin/pinentry.exe"
    _lc "${a}"/*
}
