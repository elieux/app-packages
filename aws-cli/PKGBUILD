# Maintainer: David Macek <david.macek.0@gmail.com>

_realname=aws-cli
_choconame=awscli
pkgbase="app-${_realname}"
pkgname=("app-x86_64-${_realname}")
epoch=1
pkgver=2.15.46
pkgrel=1
pkgdesc="Universal Command Line Interface for Amazon Web Services"
url="https://aws.amazon.com/cli/"
arch=('any')
license=('Apache')
makedepends=("app-lessmsi")
provides=("${pkgbase}")
options=(!strip)
source=("AWSCLIV2.msi::https://awscli.amazonaws.com/AWSCLIV2.msi")
sha512sums=('a2dd695e62257c226d5ff9cf79db1415b797d85c8ac347e1b14cf3a4f5eeb4f42038750d2fb907a55b2700d98f04ff4c35287e0f097ee864278f8dc9de72cafd')

_rmmsdll() {
    local ms

    ms=("msvbvm[0-9]+"
        "ucrtbase"
        "mfcm?[0-9]+[a-z]+"
        "msvc[rp][0-9]+(_([0-9]|codecvt_ids|atomic_wait))?"
        "vc([ao]mp|corlib|runtime)[0-9]+(_([0-9]|threads))?"
        "concrt[0-9]+"
        "pgort[0-9]+"
        "api-ms-win-[0-9a-z-]+")
    find "${1}" -iname '*.dll' | grep -Eif <(for m in "${ms[@]}"; do echo "/${m}[.]dll$"; done) | xargs -r rm
}

prepare() {
    rm -fR "AWSCLIV2"
    /apps/lessmsi/lessmsi x "AWSCLIV2.msi" > /dev/null
}

pkgver() {
    /apps/lessmsi/lessmsi v "AWSCLIV2.msi" | sed -r 's/[.]0$//'
}

package() {
    local a="${pkgdir}/apps/${_realname}"
    mkdir -p "${a}"
    cp -R "AWSCLIV2/SourceDir/Amazon/AWSCLIV2"/* "${a}"
    _rmmsdll "${a}"
}
