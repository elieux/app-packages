# Maintainer: David Macek <david.macek.0@gmail.com>

_realname=openvpn
_choconame=openvpn
pkgbase="app-${_realname}"
pkgname=("app-x86_64-${_realname}")
epoch=1
pkgver=2.6.6.001
pkgrel=1
pkgdesc="A full-featured SSL VPN solution"
url="https://openvpn.net/community/"
arch=('any')
license=('custom')
makedepends=("app-lessmsi")
provides=("${pkgbase}")
options=(!strip)
source=("OpenVPN-amd64.msi::https://swupdate.openvpn.org/community/releases/OpenVPN-${pkgver%.*}-I${pkgver##*.}-amd64.msi")
sha512sums=('579f10fee7cd34348e68c039b987a9e02f3d571d25aff7e0b2a5dde12190bd501df8d4b456c95e5fb00fea4d851a32b0ae4e004a2dd31d8d1f92bd5ed0edece1')

_rmmsdll() {
    local ms

    ms=("msvbvm[0-9]+"
        "ucrtbase"
        "mfcm?[0-9]+[a-z]+"
        "msvc[rp][0-9]+(_([0-9]|codecvt_ids|atomic_wait))?"
        "vc([ao]mp|corlib|runtime)[0-9]+(_([0-9]|threads))?"
        "concrt[0-9]+"
        "pgort[0-9]+"
        "api-ms-win-[0-9a-z-]+")
    find "${1}" -iname '*.dll' | grep -Eif <(for m in "${ms[@]}"; do echo "/${m}[.]dll$"; done) | xargs -r rm
}

prepare() {
    rm -fR "OpenVPN-amd64"
    /apps/lessmsi/lessmsi x "OpenVPN-amd64.msi" > /dev/null
}

package() {
    local a="${pkgdir}/apps/${_realname}"
    mkdir -p "${a}"
    cp -R "OpenVPN-amd64/SourceDir/OpenVPN"/* "${a}"
    rm -R "${a}"/{easy-rsa,include,config,config-auto,log}
    _rmmsdll "${a}"
}
