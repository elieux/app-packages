# Maintainer: David Macek <david.macek.0@gmail.com>

_realname=teamspeak-server
pkgbase="app-${_realname}"
pkgname=("app-i686-${_realname}"
         "app-x86_64-${_realname}")
pkgver=3.0.13.8
pkgrel=1
pkgdesc="A proprietary VoIP conference software"
url="http://www.teamspeak.com/"
arch=('any')
license=('custom')
makedepends=("unzip")
provides=("${pkgbase}")
options=(!strip)
source=("teamspeak3-server_win32.zip::http://dl.4players.de/ts/releases/${pkgver}/teamspeak3-server_win32-${pkgver}.zip"
        "teamspeak3-server_win64.zip::http://dl.4players.de/ts/releases/${pkgver}/teamspeak3-server_win64-${pkgver}.zip")
sha512sums=('1340bd4c849578e4f229aed14b4102df34ae5c15539804a9d19cbd2c56a609f838ca836e72a3d64c058b989a464df812c1777e716ff2734f6ea4396f9a5cb730'
            '97c97a5471bbf1cbae07bc980d5ad32a83fb9a3744a7be3a877f1db5a532d9f3efa0ec8cddb515b9b41ea46c778db6926d41057c1a9695b915c83fded1b87a5b')

_package() {
    a="${pkgdir}/apps${_apparch}/${_realname}"
    mkdir -p "${a}"
    cp -r "teamspeak3-server_win${_apparch}/redist/libmariadb.dll" "teamspeak3-server_win${_apparch}/tsdns/tsdnsserver.exe" "teamspeak3-server_win${_apparch}"/*.{exe,dll} "${a}"

    d="${pkgdir}/docs${_apparch}/${_realname}"
    mkdir -p "${d}" "${d}/tsdns"
    cp -r "teamspeak3-server_win${_apparch}"/*.txt "teamspeak3-server_win${_apparch}"/{doc,sql,serverquerydocs} "${d}"
    cp "teamspeak3-server_win${_apparch}/tsdns"/*.{txt,sample} "${d}/tsdns"
}

package_app-i686-teamspeak-server() {
    _apparch=32
    _package
}

package_app-x86_64-teamspeak-server() {
    _apparch=64
    _package
}
