# Maintainer: David Macek <david.macek.0@gmail.com>

_realname=wireshark
pkgbase="app-${_realname}"
pkgname=("app-i686-${_realname}"
         "app-x86_64-${_realname}")
pkgver=2.4.2
pkgrel=1
pkgdesc="A free network protocol analyzer"
url="https://www.wireshark.org/"
arch=('any')
license=('GPL2')
makedepends=("p7zip")
provides=("${pkgbase}")
options=(!strip)
source=("Wireshark-win32.exe::https://1.eu.dl.wireshark.org/win32/Wireshark-win32-${pkgver}.exe"
        "Wireshark-win64.exe::https://1.eu.dl.wireshark.org/win64/Wireshark-win64-${pkgver}.exe")
sha512sums=('8c5cfeb46c02ff7a8d00fbdf9690035e50194ccb41ce01a3e920854edabe00383db1ceaa9f4da8325e278cd8f65287aa4f6de30110d30425f177ffe46b9789e6'
            '06a1da4e71b11aadc1582833e35c7c72d39d390bb18d32d13b84549f21d8c7c315248cb9883e710c32889dad4e0cc4ed7c92121927f3199bb842912f57186f18')

_lc() {
    local d f n

    for f in "${@}"
    do
        d="$(dirname "${f}")"
        f="$(basename "${f}")"
        n="$(echo "${f}" | tr 'A-Z' 'a-z')"
        if test "${f}" != "${n}"
        then
            mv "${d}/${f}" "${d}/${n}"
        fi
    done
}

_prepare() {
    7z x -y -bd "Wireshark-win${_apparch}.exe" -o"Wireshark-win${_apparch}"
    _lc "Wireshark-win${_apparch}"/*
}

prepare() {
    for _apparch in 32 64
    do
        _prepare
    done
}

_package() {
    a="${pkgdir}/apps${_apparch}/${_realname}"
    mkdir -p "${a}"
    cp -r "Wireshark-win${_apparch}"/* "${a}"
    rm -r "${a}"/winpcap_*.exe "${a}"/usbpcap*.exe "${a}/vcredist_${1}.exe" "${a}/uninstall.exe" "${a}"/'$pluginsdir'

    d="${pkgdir}/docs${_apparch}/${_realname}"
    mkdir -p "${d}"
    mv "${a}/help" "${a}"/*.{txt,chm,html,css} "${d}"
}

package_app-i686-wireshark() {
    _apparch=32
    _package "x86"
}

package_app-x86_64-wireshark() {
    _apparch=64
    _package "x64"
}
