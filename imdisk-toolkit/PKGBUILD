# Maintainer: David Macek <david.macek.0@gmail.com>

_chocover() {
    # 20.11.20 -> 2020.11.20
    echo "${1}" | sed -r 's/^([0-9]{2})[.]([0-9]{2})[.]([0-9]{2})$/20\1.\2.\3/'
}

_realname=imdisk-toolkit
_choconame=ImDisk-Toolkit
pkgbase="app-${_realname}"
pkgname=("app-x86_64-${_realname}")
epoch=1
pkgver=2024.02.10
pkgrel=1
pkgdesc="Ramdisk for Windows and mounting of image files"
arch=('any')
url="https://sourceforge.net/projects/imdisk-toolkit/"
license=('unknown')
makedepends=("p7zip")
provides=("${pkgbase}")
options=(!strip)
source=("ImDiskTk-x64.zip::https://downloads.sourceforge.net/imdisk-toolkit/${pkgver//./}/ImDiskTk-x64.zip")
sha512sums=('9af6a1265e4a92d5dbb193daece0c51e6cad55c86bd9ccf52f76b09396bac35b1a6b4542ac0fd6259014ac29012d467e4ed630f75a863687255d5053d2a423f5')

_lc() {
    local d f n

    for f in "${@}"
    do
        d="$(dirname "${f}")"
        f="$(basename "${f}")"
        n="$(echo "${f}" | tr 'A-Z' 'a-z')"
        if test "${f}" != "${n}"
        then
            mv "${d}/${f}" "${d}/${n}"
        fi
    done
}

prepare() {
    7z x -y -bd "ImDiskTk${pkgver//./}/files.cab" -o"files" > /dev/null
}

package() {
    depends=("app-ltrdata")

    local a="${pkgdir}/apps/${_realname}"
    mkdir -p "${a}"
    cp -R "files"/* "${a}"
    rm -R "${a}/driver"
    _lc "${a}"/*
}
