# Maintainer: David Macek <david.macek.0@gmail.com>

_realname=vortex
_choconame=vortex
pkgbase="app-${_realname}"
pkgname=("app-x86_64-${_realname}")
epoch=1
pkgver=1.16.1
pkgrel=1
pkgdesc="The elegant, powerful, and open-source mod manager from Nexus Mods"
url="https://www.nexusmods.com/about/vortex/"
arch=('any')
license=('GPL3')
makedepends=("p7zip")
provides=("${pkgbase}")
options=(!strip)
source=("vortex-setup.exe::https://github.com/Nexus-Mods/Vortex/releases/download/v${pkgver}/vortex-setup-${pkgver}.exe")
sha512sums=('31c82d0b92b14490066760302b60d1e8eceeebd2105a9248b8b0ff5d9f78a05d08e0eade2b6bf9413d6b9a28b06b4fac48a2aabfc053cd00f8d52304d1d56d3a')
noextract=("vortex-setup.exe")

_lc() {
    local d f n

    for f in "${@}"
    do
        d="$(dirname "${f}")"
        f="$(basename "${f}")"
        n="$(echo "${f}" | tr 'A-Z' 'a-z')"
        if test "${f}" != "${n}"
        then
            mv "${d}/${f}" "${d}/${n}"
        fi
    done
}

_rmmsdll() {
    local ms

    ms=("msvbvm[0-9]+"
        "ucrtbase"
        "mfcm?[0-9]+[a-z]+"
        "msvc[rp][0-9]+(_([0-9]|codecvt_ids|atomic_wait))?"
        "vc([ao]mp|corlib|runtime)[0-9]+(_([0-9]|threads))?"
        "concrt[0-9]+"
        "pgort[0-9]+"
        "api-ms-win-[0-9a-z-]+")
    find "${1}" -iname '*.dll' | grep -Eif <(for m in "${ms[@]}"; do echo "/${m}[.]dll$"; done) | xargs -r rm
}

prepare() {
    7z x -y -bd "vortex-setup.exe" -o"vortex-setup.dir" > /dev/null
    7z x -y -bd "vortex-setup.dir"/'$PLUGINSDIR'/"app-64.7z" -o"app-64" > /dev/null
}

package() {
    local a="${pkgdir}/apps/${_realname}"
    mkdir -p "${a}"
    cp -R "app-64"/* "${a}"
    rm -R "${a}/resources/build"
    _rmmsdll "${a}"
    _lc "${a}"/*
}
