# Maintainer: David Macek <david.macek.0@gmail.com>

_realname=crystaldiskinfo
_dirname=crystal
pkgbase="app-${_realname}"
pkgname=("app-i686-${_realname}"
         "app-x86_64-${_realname}")
pkgver=7.5.0
pkgrel=1
pkgdesc="A disk utility (Crystal Dew World)"
url="http://crystalmark.info/software/CrystalDiskInfo/index-e.html"
arch=('any')
license=('MIT')
makedepends=("unzip")
provides=("${pkgbase}")
options=(!strip)
source=("CrystalDiskInfo.zip::https://osdn.net/dl/crystaldiskinfo/CrystalDiskInfo${pkgver//./_}.zip")
sha512sums=('c9659293ab405a210a73f8a339cd3176fb1ddfd68206fbd105194f14a4f2535dfe36fa74128875ff1a72a02dec2544810be623b96a3ef1172ca2eff2e96c6cae')

_lc() {
    local d f n

    for f in "${@}"
    do
        d="$(dirname "${f}")"
        f="$(basename "${f}")"
        n="$(echo "${f}" | tr 'A-Z' 'a-z')"
        if test "${f}" != "${n}"
        then
            mv "${d}/${f}" "${d}/${n}"
        fi
    done
}

prepare() {
    _lc "License"/* *
}

_package() {
    a="${pkgdir}/apps${_apparch}/${_dirname}"
    mkdir -p "${a}"
    cp "diskinfo${_apparch}.exe" "${a}/crystaldiskinfo.exe"
    cp -r "cdiresource" "${a}"

    d="${pkgdir}/docs${_apparch}/${_dirname}"
    mkdir -p "${d}/diskinfo"
    cp -r "license"/* "${d}/diskinfo"
}

package_app-i686-crystaldiskinfo() {
    _apparch=32
    _package
}

package_app-x86_64-crystaldiskinfo() {
    _apparch=64
    _package
}
