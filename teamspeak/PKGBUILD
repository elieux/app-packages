# Maintainer: David Macek <david.macek.0@gmail.com>

_realname=teamspeak
_choconame=teamspeak
pkgbase="app-${_realname}"
pkgname=("app-x86_64-${_realname}")
epoch=1
pkgver=3.6.2
pkgrel=1
pkgdesc="Quality voice communication via the Internet"
url="https://www.teamspeak.com/"
arch=('any')
license=('custom')
replaces=("app-i686-${_realname}")
makedepends=("p7zip")
provides=("${pkgbase}")
options=(!strip)
source=("TeamSpeak3-Client-win64.exe::https://files.teamspeak-services.com/releases/client/${pkgver}/TeamSpeak3-Client-win64-${pkgver}.exe")
sha512sums=('54d48fd972ed79200201c14d0d1cff26aa1a1aca306ae7730d42f708d589d9339b53ffb16fbd85fd35f09d86094d711fc305d91c9bd3ada0b9ee9b862a8dcf32')
noextract=("TeamSpeak3-Client-win64.exe")

_lc() {
    local d f n

    for f in "${@}"
    do
        d="$(dirname "${f}")"
        f="$(basename "${f}")"
        n="$(echo "${f}" | tr 'A-Z' 'a-z')"
        if test "${f}" != "${n}"
        then
            mv "${d}/${f}" "${d}/${n}"
        fi
    done
}

prepare() {
    local w f
    w="*license*.rtf"
    7z x -y -bd -x!"${w}" "TeamSpeak3-Client-win64.exe" -o"TeamSpeak3-Client-win64.dir" > /dev/null
    for f in $(7z l -y -bd -i!"${w}" "TeamSpeak3-Client-win64.exe" | grep 'license.*rtf' | sed -r 's/[ ]+/\t/g' | cut -f 5 | sed 's/^.*\\//')
    do
        7z e -y -bd -so -i!"*${f}" "TeamSpeak3-Client-win.exe" > "TeamSpeak3-Client-win64.dir/${f}"
    done
}

package() {
    local a="${pkgdir}/apps/${_realname}"
    mkdir -p "${a}"
    cp -R "TeamSpeak3-Client-win64.dir"/* "${a}"
    rm -R "${a}/update.exe" "${a}"/'$PLUGINSDIR'
    local f
    for f in "${a}/ts3client_win64".*
    do
        mv "${f}" "${f/_win64/}"
    done
    _lc "${a}"/*
}
