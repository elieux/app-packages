# Maintainer: David Macek <david.macek.0@gmail.com>

_realname=mariadb
pkgbase="app-${_realname}"
pkgname=("app-i686-${_realname}"
         "app-x86_64-${_realname}")
pkgver=10.2.9
pkgrel=1
pkgdesc="Fast SQL database server, drop-in replacement for MySQL"
url="http://mariadb.org/"
arch=('any')
license=('GPL')
makedepends=("unzip")
provides=("${pkgbase}")
options=(!strip)
source=("mariadb-win32.zip::https://downloads.mariadb.org/f/mariadb-${pkgver}/win32-packages/mariadb-${pkgver}-win32.zip"
        "mariadb-winx64.zip::https://downloads.mariadb.org/f/mariadb-${pkgver}/winx64-packages/mariadb-${pkgver}-winx64.zip")
sha512sums=('4dc53f8f9aaf27e96368f547dad7e738d480659c753b42dfce50da707f2ee81ba3e6decaf0ecaad1a6b593744ea9816d477dc6df0dce0f8a0e9533ba0be1d7bc'
            '5f0705c1fc9b20caef51b4adb808c072e77cf88c66ca96750508746effc80803ca90117f44a36366764e4f4c3b4f2f7f261cd3069f93a551589075c4518e9bfa')

_package() {
    a="${pkgdir}/apps${_apparch}/${_realname}"
    mkdir -p "${a}"
    cp -r "mariadb-${pkgver}-win${1}"/* "${a}"
    rm -r "${a}"/{data,mysql-test,sql-bench}
    find "${a}" -name 'debug' -exec rm -r '{}' \; -prune
    find "${a}" -name '*.pdb' -delete
    find "${a}" -name '*.lib' -delete

    d="${pkgdir}/docs${_apparch}/${_realname}"
    mkdir -p "${d}"
    mv "${a}"/COPYING* "${a}"/*.ini "${a}"/{CREDITS,EXCEPTIONS-CLIENT,README.md} "${d}"
}

package_app-i686-mariadb() {
    _apparch=32
    _package "32"
}

package_app-x86_64-mariadb() {
    _apparch=64
    _package "x64"
}
