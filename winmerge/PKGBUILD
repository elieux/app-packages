# Maintainer: David Macek <david.macek.0@gmail.com>

_realname=winmerge
_choconame=winmerge
pkgbase="app-${_realname}"
pkgname=("app-x86_64-${_realname}")
epoch=1
pkgver=2.16.30
pkgrel=1
pkgdesc="A differencing and merging tool"
url="https://winmerge.org/"
arch=('any')
license=('GPL')
replaces=("app-i686-${_realname}" "app-i686-winmerge-plugin-7z")
makedepends=("app-innounp")
provides=("${pkgbase}" "app-winmerge-plugin-7z" "app-x86_64-winmerge-plugin-7z")
options=(!strip)
source=("WinMerge-Setup.exe::https://github.com/WinMerge/winmerge/releases/download/v${pkgver}/WinMerge-${pkgver}-Setup.exe"
        "winmerge-x64-exe.zip::https://github.com/WinMerge/winmerge/releases/download/v${pkgver}/winmerge-${pkgver}-x64-exe.zip")
sha512sums=('52d215f13e7cb9d4b90b07c757dad6af12358b4976767d4fe4559933db841e3d8549783f8028d4a38eb5493cbd61a0f9266000819986cc18211b1e4220b8e68c'
            'e59607f0a9d7804cbf8d7a9e81a2af7d28e53d95f0c7eb586cd857ace107de83cafcb8ee26d18cc8ece8adddb141873255dfade637a2baa1485147c9edfdc85f')

_lc() {
    local d f n

    for f in "${@}"
    do
        d="$(dirname "${f}")"
        f="$(basename "${f}")"
        n="$(echo "${f}" | tr 'A-Z' 'a-z')"
        if test "${f}" != "${n}"
        then
            mv "${d}/${f}" "${d}/${n}"
        fi
    done
}

prepare() {
    /apps/innounp/innounp -x -q -m -a -y "WinMerge-Setup.exe" -d"WinMerge-Setup.dir"
}

package() {
    local a="${pkgdir}/apps/${_realname}"
    mkdir -p "${a}"
    cp -R "WinMerge"/* "${a}"
    cp -R "WinMerge-Setup.dir/{app}/LogoImages" "WinMerge-Setup.dir/{app}/WinMergeU.VisualElementsManifest.xml" "${a}"
    rm "${a}/vcomp140.dll"
    _lc "${a}"/*
}
