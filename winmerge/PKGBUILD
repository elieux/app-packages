# Maintainer: David Macek <david.macek.0@gmail.com>

_realname=winmerge
_apparch=32
pkgbase="app-${_realname}"
pkgname=("app-i686-${_realname}")
pkgver=2.14.0
pkgrel=1
pkgdesc="A differencing and merging tool"
url="http://winmerge.org/"
arch=('any')
license=('GPL')
makedepends=("unzip" "app-i686-innounp")
provides=("${pkgbase}")
options=(!strip)
source=("WinMerge-Setup.exe::http://downloads.sourceforge.net/${_realname}/WinMerge-${pkgver}-Setup.exe"
        "WinMerge-exe.zip::http://downloads.sourceforge.net/${_realname}/WinMerge-${pkgver}-exe.zip")
sha512sums=('367d28f97ce2292d7da12cc066afc9339a7b39d7fc2e75140d7285516dd17b801886c00800dcd32348bf71836e89b1338782e947d1b8ec2e178c7dd11a128c04'
            'b1fc1d1d4e4e944fa8d57e5784043c073a69eec214b49b18abcd498a350215538f15c47302020a85760a6d7c14ea7d6ccf23211661da0fea66e7f59781f64feb')

_lc() {
    for f in "${@}"
    do
        n="$(echo "${f}" | tr 'A-Z' 'a-z')"
        if test "${f}" != "${n}"
        then
            mv "${f}" "${n}"
        fi
    done
}

prepare() {
    /apps32/innounp/innounp -x -q -m -a -y "WinMerge-Setup.exe"
    cp -r "{app}"/* "WinMerge-${pkgver}-exe"
    _lc "WinMerge-${pkgver}-exe"/* "WinMerge-${pkgver}-exe"/*
}

package() {
    a="${pkgdir}/apps${_apparch}/${_realname}"
    mkdir -p "${a}"
    cp -r "WinMerge-${pkgver}-exe"/* "${a}"
    rm "${a}/readme.txt"

    d="${pkgdir}/docs${_apparch}/${_realname}"
    mkdir -p "${d}"
    mv "${a}"/*.txt "${a}/docs"/* "${d}"
    rm -r "${a}/docs"
}
