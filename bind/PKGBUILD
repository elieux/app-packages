# Maintainer: David Macek <david.macek.0@gmail.com>

_realname=bind
_realver=9.10.3-P3
pkgbase="app-${_realname}"
pkgname=("app-i686-${_realname}"
         "app-x86_64-${_realname}")
pkgver=${_realver//-/.}
pkgrel=1
pkgdesc="The ISC BIND nameserver"
url="https://www.isc.org/downloads/bind/"
arch=('any')
license=('custom:ISC')
makedepends=("unzip")
provides=("${pkgbase}")
options=(!strip)
source=("BIND32.zip::ftp://ftp.isc.org/isc/bind9/${_realver}/BIND${_realver}.x86.zip"
        "BIND64.zip::ftp://ftp.isc.org/isc/bind9/${_realver}/BIND${_realver}.x64.zip")
sha512sums=('ad85eeff4ae1ef7db2400f88906423a50b4066232e10f0050cceacbc7196af2e57591dc0ea5c736f557d7bcf2668b4c898f25ecb7651274f2db07811e0828537'
            '3939d7ed183d4ea58402c127a641c66f33a94576c3089c1c0d4a61354d8ab3455a3af91e34d356f6bcd38d8cd1764b2173341b41c8b9e321ef538728ee72d5cc')
noextract=("BIND32.zip"
           "BIND64.zip")

_lc() {
    for f in "${@}"
    do
        n="$(echo "${f}" | tr 'A-Z' 'a-z')"
        if test "${f}" != "${n}"
        then
            mv "${f}" "${n}"
        fi
    done
}

prepare() {
    for _apparch in 32 64
    do
        unzip -o "BIND${_apparch}.zip" -d "bind${_apparch}"
        _lc "bind${_apparch}"/*
    done
}

_package() {
    a="${pkgdir}/apps${_apparch}/${_realname}"
    mkdir -p "${a}"
    cp "bind${_apparch}"/*.{exe,dll} "${a}"
    rm "${a}"/vcredist*.exe "${a}/bindinstall.exe"

    d="${pkgdir}/docs${_apparch}/${_realname}"
    mkdir -p "${d}"
    cp "bind${_apparch}"/*.{html,pdf,txt} "bind${_apparch}"/{changes,copyright,faq,history,migration,migration-4to9,openssl-license,readme,sha256} "${d}"
}

package_app-i686-bind() {
    _apparch=32
    _package
}

package_app-x86_64-bind() {
    _apparch=64
    _package
}
