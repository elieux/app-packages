# Maintainer: David Macek <david.macek.0@gmail.com>

_realname=keepass-plugin-certkeyprovider
_realver=2009-09-06T23:00:34Z
_dirname=keepass
_apparch=32
pkgbase="app-${_realname}"
pkgname=("app-i686-${_realname}")
pkgver=2.0.8.3
pkgrel=1
pkgdesc="A KeePass plugin that allows to use Windows certificate store as a master key source"
url="http://keepass.info/plugins.html#certkeyprov"
arch=('any')
license=('unknown')
depends=("app-i686-keepass")
provides=("${pkgbase}")
options=(!strip)
source=("CertKeyProviderPlugin.plgx::http://dl.getdropbox.com/u/1457829/CertKeyProviderPlugin.plgx")
sha512sums=('b6430c3f5f0d2029f5d52b3b3fdbddab752c7fd7aedc10b9360e0fdbcdd3a747a881e00cf74a015cf4cecbce932f14456f3aa6ab6a9f23de69758edebf56f1d0')

pkgver() {
    v="$(dd if="CertKeyProviderPlugin.plgx" bs=1 skip=67 count=20 status=none)"
    if [ "${v}" == "${_realver}" ]
    then
        echo "${pkgver}"
    else
        echo "Timestamp has changed. Fix version number."
    fi
}

package() {
    a="${pkgdir}/apps${_apparch}/${_dirname}"
    mkdir -p "${a}/plugins"
    cp *.plgx "${a}/plugins"
}
