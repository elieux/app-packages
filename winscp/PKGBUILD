# Maintainer: David Macek <david.macek.0@gmail.com>

_realname=winscp
_apparch=32
pkgbase="app-${_realname}"
pkgname=("app-i686-${_realname}")
pkgver=5.11.1
pkgrel=1
pkgdesc="An open source two-pane SFTP/FTP/WebDAV/SCP client"
url="http://winscp.net/"
arch=('any')
license=('custom')
makedepends=("app-i686-innounp"
             "unzip")
provides=("${pkgbase}")
options=(!strip)
source=("winscpsetup.exe::http://download.sourceforge.net/winscp/WinSCP-${pkgver}-Setup.exe"
        "winscp.zip::http://download.sourceforge.net/winscp/WinSCP-${pkgver}-Portable.zip"
        "winscpautomation.zip::http://download.sourceforge.net/winscp/WinSCP-${pkgver}-Automation.zip")
sha512sums=('6dc2667c5e089df6b941a486a70115fec3b78e4a5e7a7017ca7ecc24b2fa216d9120861a9eabf25c7b925d793d53c5eeeb8cc010dd8efbc1ecd5b7c96850fca5'
            '0d89862941a964dbedb8576b2c1342ed68e1982c10886c19d381092f7c6da28e19ba0b23053cb92a31a27866fd530830c89af59c410a8c586bda9192717a8585'
            'b88d5ed720adf70182b75d56d015741ba7eb54281a4dda21cd2bd30015ca70a03b8d1b456f198a7ce4de17c508ecde15ac48fff3a1dd4d2c4e3d3b540a183fbd')

_lc() {
    local d f n

    for f in "${@}"
    do
        d="$(dirname "${f}")"
        f="$(basename "${f}")"
        n="$(echo "${f}" | tr 'A-Z' 'a-z')"
        if test "${f}" != "${n}"
        then
            mv "${d}/${f}" "${d}/${n}"
        fi
    done
}

prepare() {
    /apps32/innounp/innounp -x -q -m -a -y "winscpsetup.exe"
    _lc '{app}'/* *
}

package() {
    a="${pkgdir}/apps${_apparch}/${_realname}"
    mkdir -p "${a}"
    cp -r "winscpnet.dll" '{app}'/dragext*.dll '{app}'/winscp.* '{app}'/"extensions" '{app}'/"translations"  "${a}"
    rm "${a}/winscp.map"

    d="${pkgdir}/docs${_apparch}/${_realname}"
    mkdir -p "${d}"
    cp *.txt "${d}"
}
