# Maintainer: David Macek <david.macek.0@gmail.com>

_realname=nexus-mod-manager
_choconame=nmm
pkgbase="app-${_realname}"
pkgname=("app-i686-${_realname}")
epoch=1
pkgver=0.83.9
pkgrel=1
pkgdesc="Game modifications manager originally from Nexus Mods"
url="https://www.nexusmods.com/site/mods/4"
arch=('any')
license=('GPL2')
makedepends=("app-innounp")
provides=("${pkgbase}")
options=(!strip)
source=("NMM.exe::https://github.com/Nexus-Mods/Nexus-Mod-Manager/releases/download/${pkgver}/NMM-${pkgver}.exe")
sha512sums=('820caad0f1da14371d4f6f21efcffdddbb019584c04a7de349846100ef7ddfd0d4cd7cece1f7f7410db7872fef8f6f59755f65444ecc3759e5f9ded51f91970a')

_lc() {
    local d f n

    for f in "${@}"
    do
        d="$(dirname "${f}")"
        f="$(basename "${f}")"
        n="$(echo "${f}" | tr 'A-Z' 'a-z')"
        if test "${f}" != "${n}"
        then
            mv "${d}/${f}" "${d}/${n}"
        fi
    done
}

prepare() {
    /apps/innounp/innounp -x -q -m -a -y "NMM.exe" -d"NMM.dir"
}

package() {
    local a="${pkgdir}/apps/${_realname}"
    mkdir -p "${a}"
    cp -R "NMM.dir/{app}"/* "${a}"
    _lc "${a}"/*
}
