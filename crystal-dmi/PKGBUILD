# Maintainer: David Macek <david.macek.0@gmail.com>

_realname=crystaldmi
_dirname=crystal
pkgbase="app-${_realname}"
pkgname=("app-i686-${_realname}")
epoch=1
pkgver=1.06
pkgrel=1
pkgdesc="A DMI utility (Crystal Dew World)"
url="http://crystalmark.info/software/CrystalDMI/index-e.html"
arch=('any')
license=('BSD')
makedepends=("unzip")
provides=("${pkgbase}")
options=(!strip)
source=("CrystalDMI.zip::http://crystalmark.info/download/archive/CrystalDMI/CrystalDMI.zip")
sha512sums=('6f5de3bae8eb8d924f718a5bea90cd5f90439ec4cfaaadc4d1e92da5fd4eabdb4d970c66bf0d9a5211a6639b0f2b0869978537c5c208b186a22aafb288cea477')

_lc() {
    local d f n

    for f in "${@}"
    do
        d="$(dirname "${f}")"
        f="$(basename "${f}")"
        n="$(echo "${f}" | tr 'A-Z' 'a-z')"
        if test "${f}" != "${n}"
        then
            mv "${d}/${f}" "${d}/${n}"
        fi
    done
}

pkgver() {
    b="$(cygpath -wa "CrystalDMI.exe" | sed 's/\\/\\\\/g')"
    wmic datafile where name=\""${b}"\" get version | grep 'Version' -A 1 | tail -1 | tr -d ' ' | sed -r 's/([0-9]+)[.]([0-9]+)[.]([0-9]+).0$/\1.\2\3/'
}

prepare() {
    _lc *
}

package() {
    a="${pkgdir}/apps/${_dirname}"
    mkdir -p "${a}" "${a}/dmi"
    cp "crystaldmi.exe" "${a}/crystaldmi.exe"
    cp *.{dll,vxd,sys} "${a}"
    cp -r *.txt "${a}/dmi"
}
