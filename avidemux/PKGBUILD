# Maintainer: David Macek <david.macek.0@gmail.com>

_realname=avidemux
_choconame=avidemux
pkgbase="app-${_realname}"
pkgname=("app-x86_64-${_realname}")
epoch=1
pkgver=2.8.2r240531
pkgrel=1
pkgdesc="Graphical tool to edit video (filter/re-encode/split)"
url="http://www.avidemux.org/"
arch=('any')
license=('GPL2')
makedepends=("p7zip")
provides=("${pkgbase}")
options=(!strip)
source=("avidemux_win64.exe::http://www.avidemux.org/nightly/win64/avidemux_${pkgver%r*}%20r${pkgver#*r}_win64.exe")
sha512sums=('6566e1514b4ac8fe16b2d8ae19598dd1621c5e214511297f3edd4de0ec6f700695a29d4955dd0b465dac7bdf764c8140ea2970b4fbd90554e3c3e75d165213c5')

prepare() {
    rm -fR "avidemux_win64.dir"
    echo u | 7z x -bd "avidemux_win64.exe" -o"avidemux_win64.dir" > /dev/null
}

package() {
    local a="${pkgdir}/apps/${_realname}"
    mkdir -p "${a}"
    cp -R "avidemux_win64.dir"/* "${a}"
    rm -R "${a}"/'$PLUGINSDIR'
}
