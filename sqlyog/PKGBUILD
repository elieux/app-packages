# Maintainer: David Macek <david.macek.0@gmail.com>

_realname=sqlyog
_choconame=sqlyog
pkgbase="app-${_realname}"
pkgname=("app-x86_64-${_realname}")
epoch=1
pkgver=13.2.1
pkgrel=1
pkgdesc="MySQL manager and admin tool"
url="https://github.com/webyog/sqlyog-community"
arch=('any')
license=('GPL2')
replaces=("app-i686-${_realname}")
makedepends=("p7zip" "curl")
provides=("${pkgbase}")
options=(!strip)
source=("SQLyog.x64Community.exe::https://s3.amazonaws.com/SQLyog_Community/SQLyog+${pkgver}/SQLyog-${pkgver}-0.x64Community.exe")
sha512sums=('25e6b8d7c2721bcb13a0f6072c395b319aa743731c199b6afac8e0702713afeace014b7760b171ee3ee0adf1f1d65bb95be56e076595653699f548baabf97f9a')
noextract=("SQLyog.x64Community.exe")

_lc() {
    local d f n

    for f in "${@}"
    do
        d="$(dirname "${f}")"
        f="$(basename "${f}")"
        n="$(echo "${f}" | tr 'A-Z' 'a-z')"
        if test "${f}" != "${n}"
        then
            mv "${d}/${f}" "${d}/${n}"
        fi
    done
}

_rmmsdll() {
    local ms

    ms=("msvbvm[0-9]+"
        "ucrtbase"
        "mfcm?[0-9]+[a-z]+"
        "msvc[rp][0-9]+(_([0-9]|codecvt_ids|atomic_wait))?"
        "vc([ao]mp|corlib|runtime)[0-9]+(_([0-9]|threads))?"
        "concrt[0-9]+"
        "pgort[0-9]+"
        "api-ms-win-[0-9a-z-]+")
    find "${1}" -iname '*.dll' | grep -Eif <(for m in "${ms[@]}"; do echo "/${m}[.]dll$"; done) | xargs -r rm
}

prepare() {
    7z x -y -bd "SQLyog.x64Community.exe" -o"SQLyog.x64Community.dir" > /dev/null
}

package() {
    local a="${pkgdir}/apps/${_realname}"
    mkdir -p "${a}"
    cp -R "SQLyog.x64Community.dir"/* "${a}"
    rm -R "${a}/uninst.exe" "${a}"/'$PLUGINSDIR'
    _rmmsdll "${a}"
    _lc "${a}"/*
}
