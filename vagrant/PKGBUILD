# Maintainer: David Macek <david.macek.0@gmail.com>

_realname=vagrant
_choconame=vagrant
pkgbase="app-${_realname}"
pkgname=("app-x86_64-${_realname}")
epoch=1
pkgver=2.3.7
pkgrel=1
pkgdesc="Building and distributing development environments"
url="https://www.vagrantup.com/"
arch=('any')
license=('MIT')
makedepends=("app-lessmsi")
provides=("${pkgbase}")
options=(!strip)
source=("vagrant_windows_amd64.msi::https://releases.hashicorp.com/vagrant/${pkgver}/vagrant_${pkgver}_windows_amd64.msi")
sha512sums=('f7fec5955cbc361ccc5cdb42fa0e4e2642e6f3c6de3d02271ccaff4a713c9c69e418a3d5337f3eca08c37aa3c1b9e6bc7b431d3e9f3cac0e55d85c156debc62f')

prepare() {
    rm -fR "vagrant_windows_amd64"
    /apps/lessmsi/lessmsi x "vagrant_windows_amd64.msi" > /dev/null
}

package() {
    local a="${pkgdir}/apps/${_realname}"
    mkdir -p "${a}"
    cp -R "vagrant_windows_amd64/SourceDir/HashiCorp/Vagrant"/* "${a}"
    rm -f "${a}/embedded/bin/vcruntime140.dll"

    # remove some of the largest MSYS2 left-overs
    rm -R "${a}/embedded"/{usr/{lib/*.a,include},mingw64/{x86_64-w64-mingw32,lib/gcc,lib/*.a,include}}
}
