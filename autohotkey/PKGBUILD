# Maintainer: David Macek <david.macek.0@gmail.com>

_realname=autohotkey
_choconame=autohotkey
pkgbase="app-${_realname}"
pkgname=("app-x86_64-${_realname}")
epoch=1
pkgver=1.1.33.05
pkgrel=1
pkgdesc="Macro-creation and automation-oriented scripting utility for Windows"
url="https://www.autohotkey.com/"
arch=('any')
license=('GPL2')
provides=("${pkgbase}")
makedepends=("p7zip")
options=(!strip)
source=("AutoHotkey_setup.exe::https://github.com/Lexikos/AutoHotkey_L/releases/download/v${pkgver}/AutoHotkey_${pkgver}_setup.exe")
sha512sums=('8fb0f8e30a71f03f4c020e1c33f73d8a65d693097bd99c86d57e3efcefedfc6017cf252ec747c46ffd6c9e2197f1624b38eca39e1334a610347b4841f99aefa9')

_lc() {
    local d f n

    for f in "${@}"
    do
        d="$(dirname "${f}")"
        f="$(basename "${f}")"
        n="$(echo "${f}" | tr 'A-Z' 'a-z')"
        if test "${f}" != "${n}"
        then
            mv "${d}/${f}" "${d}/${n}"
        fi
    done
}

prepare() {
    7z x -y -bd "AutoHotkey_setup.exe" -o"AutoHotkey_setup.dir" > /dev/null
}

package() {
    local a="${pkgdir}/apps/${_realname}"
    mkdir -p "${a}"
    cp -R "AutoHotkey_setup.dir"/* "${a}"
    mv "${a}/Compiler"/* "${a}"
    rm -R "${a}/Compiler" "${a}/setup.exe" "${a}/Installer.ahk"
    _lc "${a}"/*
}
