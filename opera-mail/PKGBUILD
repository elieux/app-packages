# Maintainer: David Macek <david.macek.0@gmail.com>

_realname=opera-mail
_realver=39133
_apparch=32
pkgbase="app-${_realname}"
pkgname=("app-i686-${_realname}")
pkgver=1.0.1044
pkgrel=1
pkgdesc="A lightweight, customizable mail client"
url="http://www.opera.com/computer/mail"
arch=('any')
license=('custom')
makedepends=("p7zip")
provides=("${pkgbase}")
options=(!strip)
source=("Opera-Mail.i386.exe::http://www.opera.com/download/get/?id=${_realver}&location=360&nothanks=yes&sub=marine")
sha512sums=('9048a51fecda9a8694bfc00936eda758d20cbd7fb342f43118f73be86920cf59da062cf00f23bef50382ba87841e6592051fca05b4fad55a855ef4ff02765e92')

_lc() {
    for f in "${@}"
    do
        n="$(echo "${f}" | tr 'A-Z' 'a-z')"
        if test "${f}" != "${n}"
        then
            mv "${f}" "${n}"
        fi
    done
}

pkgver() {
    _lc *
    7z e -y -bd "opera-mail.i386.exe" "operamail.exe" > /dev/null
    b="$(cygpath -wa "operamail.exe" | sed 's/\\/\\\\/g')"
    wmic datafile where name=\""${b}"\" get version | grep 'Version' -A 1 | tail -1 | tr -d ' ' | sed -r 's/^([0-9]+[.][0-9]+[.][0-9]+)[.]0+$/\1/'
}

prepare() {
    _lc *
    7z x -y -bd "opera-mail.i386.exe"
    _lc *
}

package() {
    a="${pkgdir}/apps${_apparch}/${_realname}"
    mkdir -p "${a}"
    cp -r * "${a}"
    rm -r "${a}/updatechecker" "${a}"/files* "${a}/opera-mail.i386.exe"

    d="${pkgdir}/docs${_apparch}/${_realname}"
    mkdir -p "${d}" "${d}/gstreamer"
    mv "${a}/help" "${d}"
    mv "${a}/gstreamer"/*.txt "${d}/gstreamer"
}
