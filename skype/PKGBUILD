# Maintainer: David Macek <david.macek.0@gmail.com>

_realname=skype
_apparch=32
pkgbase="app-${_realname}"
pkgname=("app-i686-${_realname}")
pkgver=7.2.32.103
pkgrel=1
pkgdesc="P2P software for high-quality voice communication"
url="http://www.skype.com/"
arch=('any')
license=('custom')
provides=("${pkgbase}")
options=(!strip)
source=("SkypeSetup.msi::http://www.skype.com/go/getskype-msi")
md5sums=('913ebbf257b73fa6c8ba4fb7c97ad4e8')

_lc() {
    for f in "${@}"
    do
        n="$(echo "${f}" | tr 'A-Z' 'a-z')"
        if test "${f}" != "${n}"
        then
            mv "${f}" "${n}"
        fi
    done
}

pkgver() {
    msg "Starting build() to determine package version :("
    build > /dev/null
    b="install/Program Files/Skype/Phone/skype.exe"
    if [ -f "${b}" ]
    then
        b="$(cygpath -wa "${b}" | sed 's/\\/\\\\/g')"
        wmic datafile where name=\""${b}"\" get version | grep 'Version' -A 1 | tail -1 | tr -d ' '
    fi
}

build() {
    msiexec -a "SkypeSetup.msi" "TARGETDIR=$(cygpath -wa "install")" -qn

    s="install/Program Files/Skype"
    _lc "${s}/Phone"/* "${s}/Redist/Skype"/* "${s}/PFiles/Skype/Browser"/*
}

package() {
    s="install/Program Files/Skype"

    a="${pkgdir}/apps${_apparch}/${_realname}"
    mkdir -p "${a}"
    cp "${s}/Phone"/* "${s}/PFiles/Skype/Browser/skypebrowserhost.exe" "${s}/Redist/Skype/skype4com.dll" "${a}"

    d="${pkgdir}/docs${_apparch}/${_realname}"
    mkdir -p "${d}"
    cp "${s}/third-party_attributions.txt" "${d}"
}
