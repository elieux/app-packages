# Maintainer: David Macek <david.macek.0@gmail.com>

_realname=crystaldiskinfo
_dirname=crystal
pkgbase="app-${_realname}"
pkgname=("app-i686-${_realname}"
         "app-x86_64-${_realname}")
pkgver=7.5.2
pkgrel=2
pkgdesc="A disk utility (Crystal Dew World)"
url="http://crystalmark.info/software/CrystalDiskInfo/index-e.html"
arch=('any')
license=('MIT')
makedepends=("unzip")
provides=("${pkgbase}")
options=(!strip)
source=("CrystalDiskInfo.zip::https://osdn.net/dl/crystaldiskinfo/CrystalDiskInfo${pkgver//./_}.zip")
sha512sums=('10cc865fd53c9f1070a8e0da3c750f7f97f41f705cb77e0b3175d0ce18c6ee14f9385b6d1d447212b94be32addb04e5a47956c3f6cc66fd9973d9ad5ba012db4')

_lc() {
    local d f n

    for f in "${@}"
    do
        d="$(dirname "${f}")"
        f="$(basename "${f}")"
        n="$(echo "${f}" | tr 'A-Z' 'a-z')"
        if test "${f}" != "${n}"
        then
            mv "${d}/${f}" "${d}/${n}"
        fi
    done
}

prepare() {
    _lc "License"/* *
}

_package() {
    a="${pkgdir}/apps${_apparch}/${_dirname}"
    mkdir -p "${a}" "${a}/diskinfo"
    cp "diskinfo${_apparch}.exe" "${a}/crystaldiskinfo.exe"
    cp -r "cdiresource" "${a}"
    cp -r "license"/* "${a}/diskinfo"
}

package_app-i686-crystaldiskinfo() {
    _apparch=32
    _package
}

package_app-x86_64-crystaldiskinfo() {
    _apparch=64
    _package
}
