# Maintainer: David Macek <david.macek.0@gmail.com>

_realname=sysinternals-deprecated
_dirname=sysinternals
pkgbase="app-${_realname}"
pkgname=("app-i686-${_realname}")
epoch=1
pkgver=2006.11.01
pkgrel=1
pkgdesc="Advanced system utilities for Windows (deprecated)"
url="https://web.archive.org/web/20091002072805/http://technet.microsoft.com:80/en-us/sysinternals/bb545027.aspx"
arch=('any')
license=('custom')
makedepends=("unzip")
provides=("${pkgbase}")
options=(!strip)
source=("NewSid.zip::https://web.archive.org/web/20070322035140if_/http://download.sysinternals.com:80/Files/NewSid.zip"
        "ProcFeatures.zip::https://web.archive.org/web/20070222172736if_/http://download.sysinternals.com:80/Files/ProcFeatures.zip")
sha512sums=('a279ccdb94a43f0e20278a534dbb7db0b931f00dd8a1dc45a93d4400e819e7a81126d34dd3dd3c7bd6b3e7da18b761e756c5f9166b2f86f92837a71128bdb1ab'
            '2c7e6e84beb528f72f2048d4b7dab9e7ccf4658d091b9ef44da89e37b2670a7198bfc6e902e98db680e493884b068fc5c4bdbce1a65e02113b1fb0ac2bd2130b')

_lc() {
    local d f n

    for f in "${@}"
    do
        d="$(dirname "${f}")"
        f="$(basename "${f}")"
        n="$(echo "${f}" | tr 'A-Z' 'a-z')"
        if test "${f}" != "${n}"
        then
            mv "${d}/${f}" "${d}/${n}"
        fi
    done
}

prepare() {
    _lc *
}

pkgver() {
    find . -mindepth 1 -maxdepth 1 -name '*.exe' -printf '%TY.%Tm.%Td\n' | sort | tail -1
}

package() {
    a="${pkgdir}/apps/${_dirname}"
    mkdir -p "${a}"
    cp *.exe "${a}"
}
