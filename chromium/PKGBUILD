# Maintainer: David Macek <david.macek.0@gmail.com>

_realname=chromium
_realver=499098
pkgbase="app-${_realname}"
pkgname=("app-i686-${_realname}"
         "app-x86_64-${_realname}")
epoch=1
pkgver=62.0.3202.62
pkgrel=1
pkgdesc="The open-source project behind Google Chrome, an attempt at creating a safer, faster, and more stable browser"
url="http://www.chromium.org/Home"
arch=('any')
license=('BSD')
makedepends=("unzip")
provides=("${pkgbase}")
options=(!strip)
source=("chrome-win32.zip::https://github.com/henrypp/chromium/releases/download/v${pkgver}-r${_realver}-win32/chromium-sync.zip"
        "chrome-win64.zip::https://github.com/henrypp/chromium/releases/download/v${pkgver}-r${_realver}-win64/chromium-sync.zip")
sha512sums=('a75b073340dd565d5b4341a3583b46cc5904bd7bdafb655a88b7d52876c98add0aeb89fb88f529155a637750bf773add32a82e53205453428a770d296fae43b3'
            'dd30a2eee9f2227f8138064d21b1ddf73bdb4acd0461f4ee4aa81062041e281b52a1843d8aec1506258cfce5653cd0ab9a927ce6747b060dbcf0fb8e6569a61f')
noextract=("chrome-win32.zip"
           "chrome-win64.zip")

_eqver() {
    local r v

    for v in "${@}"
    do
        if [ -n "${r}" -a "${r}" != "${v}" ]
        then
            echo "Version mismatch between ${r} and ${v}."
        fi
        r="${v}"
    done
    echo "${r}"
}

_lc() {
    local d f n

    for f in "${@}"
    do
        d="$(dirname "${f}")"
        f="$(basename "${f}")"
        n="$(echo "${f}" | tr 'A-Z' 'a-z')"
        if test "${f}" != "${n}"
        then
            mv "${d}/${f}" "${d}/${n}"
        fi
    done
}

pkgver() {
    _vers=( $(
        for _apparch in 32 64
        do
            unzip -l "chrome-win${_apparch}.zip" | tail -n +4 | head -n -2 | sed -r 's/[ ]+/\t/g' | cut -f 5 | sed 's~^chrome-win32/~~' | sed 's~.manifest$~~' | grep -P '[0-9]+[.][0-9]+[.][0-9]+[.][0-9]+' | sort | tail -1
        done
    ) )
    _eqver "${_vers[@]}"
}

prepare() {
    for _apparch in 32 64
    do
        unzip -o "chrome-win${_apparch}.zip" -d "chromium${_apparch}"
        _lc "chromium${_apparch}/chrome-win32"/*
    done
}

_package() {
    a="${pkgdir}/apps${_apparch}/${_realname}"
    mkdir -p "${a}"
    cp -r "chromium${_apparch}/chrome-win32"/* "${a}"
}

package_app-i686-chromium() {
    _apparch=32
    _package
}

package_app-x86_64-chromium() {
    _apparch=64
    _package
}
