# Maintainer: David Macek <david.macek.0@gmail.com>

_realname=consolez
pkgbase="app-${_realname}"
pkgname=("app-i686-${_realname}"
         "app-x86_64-${_realname}")
pkgver=1.16.1.16068
pkgrel=1
pkgdesc="A console wrapper with tabs, easy copy-pasting and pretty visuals"
url="http://github.com/cbucher/console"
arch=('any')
license=('GPL2')
makedepends=("unzip")
provides=("${pkgbase}")
options=(!strip)
source=("ConsoleZ32.zip::https://github.com/cbucher/console/releases/download/${pkgver%.*}/ConsoleZ.x86.${pkgver}.zip"
        "ConsoleZ64.zip::https://github.com/cbucher/console/releases/download/${pkgver%.*}/ConsoleZ.x64.${pkgver}.zip")
sha512sums=('e246027413f42118d2a34724c0d21afcf4aaa8d12398cb4101dcca93c4eb9f2c5259369a3d6126fe5af925c130ec399b4ec9d52ab420aa577a6f792b6edc4b27'
            '737d3daaadc39b79e012b9e82435950a735e3eff59faeb31be837ccf255f696158ed32bdf757c2453ca3d312e4ae2b81f58d2e713a69d0c814e63e9a0b568df8')
noextract=("ConsoleZ32.zip"
           "ConsoleZ64.zip")

_lc() {
    for f in "${@}"
    do
        n="$(echo "${f}" | tr 'A-Z' 'a-z')"
        if test "${f}" != "${n}"
        then
            mv "${f}" "${n}"
        fi
    done
}

prepare() {
    for _apparch in 32 64
    do
        unzip -o "ConsoleZ${_apparch}.zip" -d "consolez${_apparch}"
        _lc "consolez${_apparch}"/*
    done
}

_package() {
    a="${pkgdir}/apps${_apparch}/${_realname}"
    mkdir -p "${a}"
    cp "consolez${_apparch}"/*.{exe,dll} "${a}"

    d="${pkgdir}/docs${_apparch}/${_realname}"
    mkdir -p "${d}"
    cp "consolez${_apparch}"/*.chm "${d}"
}

package_app-i686-consolez() {
    _apparch=32
    _package
}

package_app-x86_64-consolez() {
    _apparch=64
    _package
}
