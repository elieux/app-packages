# Maintainer: David Macek <david.macek.0@gmail.com>

_realname=slack
_apparch=32
pkgbase="app-${_realname}"
pkgname=("app-i686-${_realname}")
pkgver=2.0.0
pkgrel=1
pkgdesc="Client for the Slack team communication platform"
url="http://www.slack.com/"
arch=('any')
license=('custom')
makedepends=("p7zip" "unzip")
provides=("${pkgbase}")
options=(!strip)
source=("SlackSetup.exe::https://slack.com/ssb/download-win"
        "en_US.dic::http://slack-ssb-updates.s3.amazonaws.com/dictionaries/en_US.dic"
        "en_US.aff::http://slack-ssb-updates.s3.amazonaws.com/dictionaries/en_US.aff")
md5sums=('b4679c73e2300bd7439504c7dc4108e9'
         '4515fbb1b055337dfd1b95a92c1b7e4f'
         '948412697f6fe862d4bc17517011f46e')

_lc() {
    for f in "${@}"
    do
        n="$(echo "${f}" | tr 'A-Z' 'a-z')"
        if test "${f}" != "${n}"
        then
            mv "${f}" "${n}"
        fi
    done
}

pkgver() {
    b="$(cygpath -wa "SlackSetup.exe" | sed 's/\\/\\\\/g')"
    wmic datafile where name=\""${b}"\" get version | grep 'Version' -A 1 | tail -1 | tr -d ' ' | sed -r 's/^([0-9]+[.][0-9]+[.][0-9]+)[.]0+$/\1/'
}

prepare() {
    7z x -y -bd "SlackSetup.exe"
    unzip -o "slack-${pkgver}-full.nupkg"
    _lc "lib/net45"/*
}

package() {
    a="${pkgdir}/apps${_apparch}/${_realname}"
    mkdir -p "${a}" "${a}/app" "${a}/dictionaries"
    cp -r "lib/net45"/* "${a}/app"
    rm "${a}/app"/msvc{p,r}120.dll "${a}/app/vccorlib120.dll"
    cp en_US.* "${a}/dictionaries"

    d="${pkgdir}/docs${_apparch}/${_realname}"
    mkdir -p "${d}"
    mv "${a}/app/license" "${d}"
}
