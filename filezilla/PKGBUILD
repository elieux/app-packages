# Maintainer: David Macek <david.macek.0@gmail.com>

_realname=filezilla
pkgbase="app-${_realname}"
pkgname=("app-i686-${_realname}"
         "app-x86_64-${_realname}")
pkgver=3.22.2.2
pkgrel=1
pkgdesc="Fast and reliable FTP, FTPS and SFTP client"
url="http://filezilla-project.org/"
arch=('any')
license=('GPL')
makedepends=("unzip")
provides=("${pkgbase}")
options=(!strip)
source=("FileZilla_win32.zip::http://downloads.sourceforge.net/${_realname}/FileZilla_Client/${pkgver}/FileZilla_${pkgver}_win32.zip"
        "FileZilla_win64.zip::http://downloads.sourceforge.net/${_realname}/FileZilla_Client/${pkgver}/FileZilla_${pkgver}_win64.zip")
sha512sums=('47cb6469c44444cb46b70afaf0de4c2ddbfd4c1fd96c6d43e0c4429048f593b134f7b9e44554639cb775b80cd11e5766ccacbffab4a02916d876fef7e1b55dbc'
            '58013f9da08e20c36e23fa58d5d5fff9fd1144cbe7611694d10353715aeee4b036277f579d53175c69495e8606accedb6a522092a9b1679a772a30a2375bfa07')
noextract=("FileZilla_win32.zip"
           "FileZilla_win64.zip")

prepare() {
    for _apparch in 32 64
    do
        unzip -o "FileZilla_win${_apparch}.zip" -d "filezilla${_apparch}"
    done
}

_package() {
    a="${pkgdir}/apps${_apparch}/${_realname}"
    mkdir -p "${a}"
    cp -r "filezilla${_apparch}/FileZilla-${pkgver}"/* "${a}"

    d="${pkgdir}/docs${_apparch}/${_realname}"
    mkdir -p "${d}"
    mv "${a}/docs" "${a}/AUTHORS" "${a}/NEWS" "${a}/GPL.html" "${d}"
}

package_app-i686-filezilla() {
    _apparch=32
    _package
}

package_app-x86_64-filezilla() {
    _apparch=64
    _package
}
