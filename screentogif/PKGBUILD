# Maintainer: David Macek <david.macek.0@gmail.com>

_realname=screentogif
_choconame=screentogif
pkgbase="app-${_realname}"
pkgname=("app-i686-${_realname}")
epoch=1
pkgver=2.29.1
pkgrel=1
pkgdesc="Screen, webcam and sketchboard recorder with an integrated editor"
url="https://www.screentogif.com/"
arch=('any')
license=('MS-PL')
provides=("${pkgbase}")
options=(!strip)
source=("ScreenToGif.Portable.zip::https://github.com/NickeManarin/ScreenToGif/releases/download/${pkgver}/ScreenToGif.${pkgver}.Portable.zip")
sha512sums=('1daf5063aa703bd121a9127398f1073fe8f099b33bc185f727a55f8c01d84211ba270cb9cee4eceb26dd4048f0ae3a8eec9bd091213df397359e4ece4375e135')

_lc() {
    local d f n

    for f in "${@}"
    do
        d="$(dirname "${f}")"
        f="$(basename "${f}")"
        n="$(echo "${f}" | tr 'A-Z' 'a-z')"
        if test "${f}" != "${n}"
        then
            mv "${d}/${f}" "${d}/${n}"
        fi
    done
}

package() {
    local a="${pkgdir}/apps/${_realname}"
    mkdir -p "${a}"
    cp "ScreenToGif.exe" "${a}"
    _lc "${a}"/*
}
