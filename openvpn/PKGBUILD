# Maintainer: David Macek <david.macek.0@gmail.com>

_realname=openvpn
_choconame=openvpn
pkgbase="app-${_realname}"
pkgname=("app-x86_64-${_realname}")
epoch=1
pkgver=2.5.2.601
pkgrel=1
pkgdesc="A full-featured SSL VPN solution"
url="https://openvpn.net/community/"
arch=('any')
license=('custom')
makedepends=("app-lessmsi")
provides=("${pkgbase}")
options=(!strip)
source=("OpenVPN-amd64.msi::https://swupdate.openvpn.org/community/releases/OpenVPN-${pkgver%.*}-I${pkgver##*.}-amd64.msi")
sha512sums=('cdac3cadc05a6c4605f3f6cbf9c1ce9db15d1d5aa3f92fec66deccc5b216f4b3ebbddb25224ea5135336b836e0dedd6c3c092d3b77477319c3a4de8705e239d7')

prepare() {
    rm -fR "OpenVPN-amd64"
    /apps/lessmsi/lessmsi x "OpenVPN-amd64.msi" > /dev/null
}

package() {
    local a="${pkgdir}/apps/${_realname}"
    mkdir -p "${a}"
    cp -R "OpenVPN-amd64/SourceDir/OpenVPN"/* "${a}"
    rm -R "${a}"/{easy-rsa,include,config,config-auto,log}
}
