# Maintainer: David Macek <david.macek.0@gmail.com>

_realname=crystalcpuid
_dirname=crystal
pkgbase="app-${_realname}"
pkgname=("app-x86_64-${_realname}")
epoch=1
pkgver=4.1.5
pkgrel=1
pkgdesc="A CPU utility (Crystal Dew World)"
url="http://crystalmark.info/software/CrystalCPUID/index-e.html"
arch=('any')
license=('custom')
replaces=("app-i686-${_realname}")
depends=("app-x86_64-crystalsysinfo")
makedepends=("unzip")
provides=("${pkgbase}")
options=(!strip)
source=("CrystalCPUID64.zip::http://crystalmark.info/download/archive/CrystalCPUID/CrystalCPUID${pkgver//./}x64.zip")
sha512sums=('955a7f19b4ec651a7d07e5e800e2b40a1735056dfe2ed72d12ac93b75530d5b9973c707607a2015bc970c4904a34048d3f1c637c49599247a938d827fcf13d67')

_lc() {
    local d f n

    for f in "${@}"
    do
        d="$(dirname "${f}")"
        f="$(basename "${f}")"
        n="$(echo "${f}" | tr 'A-Z' 'a-z')"
        if test "${f}" != "${n}"
        then
            mv "${d}/${f}" "${d}/${n}"
        fi
    done
}

prepare() {
    _lc *
    rm -r "data" *sysinfo*
}

package() {
    a="${pkgdir}/apps/${_dirname}"
    mkdir -p "${a}" "${a}/cpuid"
    cp "crystalcpuidx64.exe" "${a}/crystalcpuid.exe"
    cp -r *.txt "${a}/cpuid"
}
