# Maintainer: David Macek <david.macek.0@gmail.com>

_realname=filezilla
pkgbase="app-${_realname}"
pkgname=("app-i686-${_realname}"
         "app-x86_64-${_realname}")
pkgver=3.20.0
pkgrel=1
pkgdesc="Fast and reliable FTP, FTPS and SFTP client"
url="http://filezilla-project.org/"
arch=('any')
license=('GPL')
makedepends=("unzip")
provides=("${pkgbase}")
options=(!strip)
source=("FileZilla_win32.zip::http://downloads.sourceforge.net/${_realname}/FileZilla_Client/${pkgver}/FileZilla_${pkgver}_win32.zip"
        "FileZilla_win64.zip::http://downloads.sourceforge.net/${_realname}/FileZilla_Client/${pkgver}/FileZilla_${pkgver}_win64.zip")
sha512sums=('1d474a8826d45b27c25277de642d14973ced7de64de8ba15b9c65143e5c5881bcc13fd4da8b8efb0390a290add3b94cb40650db19abf3ddaf734359b427b48dc'
            '5066b0fa7b443c65f7709e8548d76da08d643c819d7fa2343adb4d8da8fa68400e2d9cbab0392ce33e98a3649f40dc015765ae7d94062f8c1c1d1ed19f695f23')
noextract=("FileZilla_win32.zip"
           "FileZilla_win64.zip")

prepare() {
    for _apparch in 32 64
    do
        unzip -o "FileZilla_win${_apparch}.zip" -d "filezilla${_apparch}"
    done
}

_package() {
    a="${pkgdir}/apps${_apparch}/${_realname}"
    mkdir -p "${a}"
    cp -r "filezilla${_apparch}/FileZilla-${pkgver}"/* "${a}"

    d="${pkgdir}/docs${_apparch}/${_realname}"
    mkdir -p "${d}"
    mv "${a}/docs" "${a}/AUTHORS" "${a}/NEWS" "${a}/GPL.html" "${d}"
}

package_app-i686-filezilla() {
    _apparch=32
    _package
}

package_app-x86_64-filezilla() {
    _apparch=64
    _package
}
