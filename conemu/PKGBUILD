# Maintainer: David Macek <david.macek.0@gmail.com>

_realname=conemu
pkgbase="app-${_realname}"
pkgname=("app-x86_64-${_realname}")
epoch=1
pkgver=19.10.12
pkgrel=1
pkgdesc="A powerful, highly customizable console emulator with tabs"
url="http://conemu.github.io/"
arch=('any')
license=('BSD')
replaces=("app-i686-${_realname}")
makedepends=("p7zip")
provides=("${pkgbase}")
options=(!strip)
source=("ConEmuPack.7z::https://github.com/Maximus5/ConEmu/releases/download/v${pkgver}/ConEmuPack.${pkgver//./}.7z")
sha512sums=('dc081a10a513d710b121eb38af3e45eafb8fd82ba217a11107a6d56127db476073f66b42df10ac1a80c1490d9778ae085a311e3a7d3f95b4f0e16a496c64a6b3')

_lc() {
    local d f n

    for f in "${@}"
    do
        d="$(dirname "${f}")"
        f="$(basename "${f}")"
        n="$(echo "${f}" | tr 'A-Z' 'a-z')"
        if test "${f}" != "${n}"
        then
            mv "${d}/${f}" "${d}/${n}"
        fi
    done
}

prepare() {
    _lc *
}

package() {
    a="${pkgdir}/apps/${_realname}"
    mkdir -p "${a}"
    cp -r "conemu" "plugins" "${a}"
    cp -r "conemu64.exe" "${a}/conemu.exe"
    find "${a}" -name '*.map' -delete
}
