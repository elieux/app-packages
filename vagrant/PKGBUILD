# Maintainer: David Macek <david.macek.0@gmail.com>

_realname=vagrant
pkgbase="app-${_realname}"
pkgname=("app-x86_64-${_realname}")
epoch=1
pkgver=2.2.7
pkgrel=1
pkgdesc="Building and distributing development environments"
url="https://www.vagrantup.com/"
arch=('any')
license=('MIT')
makedepends=("app-lessmsi")
provides=("${pkgbase}")
options=(!strip)
source=("vagrant_x86_64.msi::https://releases.hashicorp.com/vagrant/${pkgver}/vagrant_${pkgver}_x86_64.msi")
sha512sums=('160a619808fe40435acbecfb163e119b28c0a83370b8dd6630f719c871d3d9758ae6f1e251be9eda781f5680fbda2d6b03eca1fef7d80f515cffae386230589c')

prepare() {
    rm -fR "vagrant_x86_64"
    /apps/lessmsi/lessmsi x "vagrant_x86_64.msi" > /dev/null
}

package() {
    local a="${pkgdir}/apps/${_realname}"
    mkdir -p "${a}"
    cp -R "vagrant_x86_64/SourceDir/HashiCorp/Vagrant"/* "${a}"
    rm -f "${a}/embedded/bin/vcruntime140.dll"

    # remove some of the largest MSYS2 left-overs
    rm -R "${a}/embedded"/{var/*,lib,include,usr/{lib/*.a,include},mingw64/{x86_64-w64-mingw32,lib/gcc,lib/*.a,include}}
}
