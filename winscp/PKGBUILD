# Maintainer: David Macek <david.macek.0@gmail.com>

_realname=winscp
_apparch=32
pkgbase="app-${_realname}"
pkgname=("app-i686-${_realname}")
pkgver=5.9
pkgrel=1
pkgdesc="An open source two-pane SFTP/FTP/WebDAV/SCP client"
url="http://winscp.net/"
arch=('any')
license=('custom')
makedepends=("app-i686-innounp"
             "unzip")
provides=("${pkgbase}")
options=(!strip)
source=("winscpsetup.exe::http://download.sourceforge.net/winscp/WinSCP-${pkgver}-Setup.exe"
        "winscp.zip::http://download.sourceforge.net/winscp/WinSCP-${pkgver}-Portable.zip"
        "winscpautomation.zip::http://download.sourceforge.net/winscp/WinSCP-${pkgver}-Automation.zip")
sha512sums=('cd8d5d13619a8559ba3e009d8baeaaa28f47525002e0835a738863641d6a4f5b7adb350639ae4b2e60717468a18e8b77921ea3b10ea32bc2f05cce9d434deff2'
            '8cba639651bd45e061eaf50d588572ae7be890427291fce554f316197d76b2038f000a8310c2d67e062bd6b5480c91e4cc3c5d325afd23660bf88d08a00a9adb'
            '622569b98296a33145050b58d7fdd12c22b2cbcdb315069cb5c192ea8c0fb4b4b2814f92023228297ba15ad6e4bc1c66cfe07c0e78c2a222abd031d6d716aa0e')

_lc() {
    for f in "${@}"
    do
        n="$(echo "${f}" | tr 'A-Z' 'a-z')"
        if test "${f}" != "${n}"
        then
            mv "${f}" "${n}"
        fi
    done
}

prepare() {
    /apps32/innounp/innounp -x -q -m -a -y "winscpsetup.exe"
    _lc '{app}'/* *
}

package() {
    a="${pkgdir}/apps${_apparch}/${_realname}"
    mkdir -p "${a}"
    cp "winscpnet.dll" '{app}'/dragext*.dll '{app}'/winscp.* "${a}"
    rm "${a}/winscp.map"

    d="${pkgdir}/docs${_apparch}/${_realname}"
    mkdir -p "${d}"
    cp *.txt "${d}"
}
