# Maintainer: David Macek <david.macek.0@gmail.com>

_realname=winscp
_choconame=winscp
pkgbase="app-${_realname}"
pkgname=("app-i686-${_realname}")
epoch=1
pkgver=5.19.6
pkgrel=1
pkgdesc="An open source two-pane SFTP/FTP/WebDAV/SCP client"
url="https://winscp.net/"
arch=('any')
license=('custom')
makedepends=("app-innounp")
provides=("${pkgbase}")
options=(!strip)
source=("WinSCP-Setup.exe::https://download.sourceforge.net/winscp/WinSCP-${pkgver}-Setup.exe")
sha512sums=('67d4bc94b62977f47aa05813b40b8f4aa23f94d399931f816763779561bf78eeb14b0b976df6e2cf4ae9c15ca950f30393499c3c28095eaa5228e0c38d06ea58')

_lc() {
    local d f n

    for f in "${@}"
    do
        d="$(dirname "${f}")"
        f="$(basename "${f}")"
        n="$(echo "${f}" | tr 'A-Z' 'a-z')"
        if test "${f}" != "${n}"
        then
            mv "${d}/${f}" "${d}/${n}"
        fi
    done
}

prepare() {
    /apps/innounp/innounp -x -q -m -a -y "WinSCP-Setup.exe" -d"WinSCP-Setup.dir"
}

package() {
    local a="${pkgdir}/apps/${_realname}"
    mkdir -p "${a}"
    cp -R "WinSCP-Setup.dir/{app}"/* "${a}"
    rm -R "${a}/winscp.map" "${a}/putty"
    _lc "${a}"/*
}
