# Maintainer: David Macek <david.macek.0@gmail.com>

_chocover() {
    # choco version format: 3.17.4.20191026
    echo "${1}" | sed -r 's/^([0-9]+)[.]([0-9]+)[.]([0-9]+)[.]([0-9]{8})$/\1.\2.\3/'
}

_realname=claws-mail
_choconame=claws-mail
pkgbase="app-${_realname}"
pkgname=("app-x86_64-${_realname}")
epoch=1
pkgver=3.17.8
pkgrel=1
pkgdesc="User-friendly, lightweight, and fast email client"
url="https://www.claws-mail.org/"
arch=('any')
license=('GPL3')
provides=("${pkgbase}")
makedepends=("p7zip")
options=(!strip)
source=("claws-mail-64bit.exe::https://www.claws-mail.org/win32/claws-mail-${pkgver}-1-64bit.exe")
sha512sums=('ca05d38a879d3c8c43e39661b8f996de30467ee5fd9090b923821eed81cee3f5a0184f5e61ca9907c516c2103d8b37baea29c2224d45ddc5f571ce4f3f30621c')

prepare() {
    7z x -y -bd "claws-mail-64bit.exe" -o"claws-mail-64bit.dir" > /dev/null
}

package() {
    local a="${pkgdir}/apps/${_realname}"
    mkdir -p "${a}"
    cp -R "claws-mail-64bit.dir"/* "${a}"
    rm -R "${a}"/'$PLUGINSDIR' "${a}"/*.tmp
}
