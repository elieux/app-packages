# Maintainer: David Macek <david.macek.0@gmail.com>

_realname=mp3tag
_choconame=mp3tag
pkgbase="app-${_realname}"
pkgname=("app-x86_64-${_realname}")
epoch=1
pkgver=3.18
pkgrel=1
pkgdesc="Powerful audio metadata editor"
arch=('any')
url="https://mp3tag.de/en/index.html"
license=('custom')
replaces=("app-i686-${_realname}")
makedepends=("p7zip")
provides=("${pkgbase}")
options=(!strip)
source=("mp3tag-x64-setup.exe::https://download.mp3tag.de/mp3tagv${pkgver//./}-x64-setup.exe")
sha512sums=('4e48738b2c8ad2ebd3a7bcaaed39ce9389dddb0a706d034f043686a883d0ac260afc27e6510bcf7eb7829aa451a25f3fae767db2ce57598def8cdf4c872feb16')

_lc() {
    local d f n

    for f in "${@}"
    do
        d="$(dirname "${f}")"
        f="$(basename "${f}")"
        n="$(echo "${f}" | tr 'A-Z' 'a-z')"
        if test "${f}" != "${n}"
        then
            mv "${d}/${f}" "${d}/${n}"
        fi
    done
}

prepare() {
    local w f
    w="*license*.txt"
    7z x -y -bd -x!"${w}" "mp3tag-x64-setup.exe" -o"mp3tag-x64-setup.dir" > /dev/null
    for f in $(7z l -y -bd -i!"${w}" "mp3tag-x64-setup.exe" | grep 'license.*txt' | sed -r 's/[ ]+/\t/g' | cut -f 5 | sed 's/^.*\\//')
    do
        7z e -y -bd -so -i!"*${f}" "mp3tag-x64-setup.exe" > "mp3tag-x64-setup.dir/${f}"
    done
}

package() {
    local a="${pkgdir}/apps/${_realname}"
    mkdir -p "${a}" "${a}/help"
    cp -R "mp3tag-x64-setup.dir"/* "${a}"
    touch "${a}/help/helpmap.idx"
    rm -R "${a}"/'$PLUGINSDIR' "${a}"/'$R0' "${a}"/'$R2'
    _lc "${a}"/*
}
