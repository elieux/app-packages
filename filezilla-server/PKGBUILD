# Maintainer: David Macek <david.macek.0@gmail.com>

_realname=filezilla-server
_choconame=filezilla.server
pkgbase="app-${_realname}"
pkgname=("app-x86_64-${_realname}")
epoch=1
pkgver=1.8.2
pkgrel=1
pkgdesc="A popular FTP server"
url="https://filezilla-project.org/"
arch=('any')
license=('GPL')
makedepends=("p7zip")
provides=("${pkgbase}")
replaces=("app-i686-${_realname}")
options=(!strip)
source=("FileZilla_Server_win64-setup.exe::https://download.filezilla-project.org/server/FileZilla_Server_${pkgver}_win64-setup.exe")
sha512sums=('0004fcb1485f3ac3f065efee7be82dd4bf47a1c33dd6cd9f260228714bf00993aad14f2ee023d12f9fd5a2da456488564755afb1196c9dd82d8ab5561260485e')
noextract=("FileZilla_Server_win64-setup.exe")

prepare() {
    7z x -y -bd "FileZilla_Server_win64-setup.exe" -o"FileZilla_Server_win64-setup.dir" > /dev/null
}

package() {
    local a="${pkgdir}/apps/${_realname}"
    mkdir -p "${a}"
    cp -R "FileZilla_Server_win64-setup.dir"/* "${a}"
    rm -R "${a}"/'$PLUGINSDIR' "${a}/Uninstall.exe.nsis"
}
