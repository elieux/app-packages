# Maintainer: David Macek <david.macek.0@gmail.com>

_realname=rhino
pkgbase="app-${_realname}"
pkgname=("app-i686-${_realname}"
         "app-x86_64-${_realname}")
pkgver=1.7.7.1
pkgrel=1
pkgdesc="Open-source implementation of JavaScript written in Java"
url="https://developer.mozilla.org/en-US/docs/Mozilla/Projects/Rhino"
arch=('any')
license=('MPL2')
makedepends=("unzip")
provides=("${pkgbase}")
options=(!strip)
source=("rhino.zip::https://github.com/mozilla/rhino/releases/download/Rhino${pkgver//./_}_RELEASE/rhino-${pkgver}.zip"
        "rhino.cmd")
sha512sums=('fba60e83db6e6dd7ef0a834e8aa6858af41b973e01a54392f11ed1d7d152b9641a48e5d473b434e062e8b8d5d19281da1445751e2b0e297ecd25d2680efc435a'
            'fa81013a80ff525b3f218e986de7ad1074b8019de65f162c0982aa2edc62e0543d54ca125ab797aade876fbfc1ae6295d42b14fed32abbbb1f11b31352fe6d94')

_package() {
    a="${pkgdir}/apps${_apparch}/${_realname}"
    mkdir -p "${a}"
    cp "rhino${pkgver}/lib/rhino-${pkgver}.jar" "${a}/rhino.jar"
    cp "rhino.cmd" "${a}"

    d="${pkgdir}/docs${_apparch}/${_realname}"
    mkdir -p "${d}"
    cp -r "rhino${pkgver}/docs"/* "${d}"
}

package_app-i686-rhino() {
    _apparch=32
    _package
}

package_app-x86_64-rhino() {
    _apparch=64
    _package
}
