# Maintainer: David Macek <david.macek.0@gmail.com>

_realname=notepad++
_apparch=32
pkgbase="app-${_realname}"
pkgname=("app-i686-${_realname}")
pkgver=6.9
pkgrel=1
pkgdesc="A free Scintilla-based editor with plugin support"
url="https://notepad-plus-plus.org/"
arch=('any')
license=('GPL')
makedepends=("p7zip" "unzip")
provides=("${pkgbase}")
options=(!strip)
source=("npp.Installer.exe::https://notepad-plus-plus.org/repository/${pkgver%%.*}.x/${pkgver}/npp.${pkgver}.Installer.exe"
        "npp.bin.zip::https://notepad-plus-plus.org/repository/${pkgver%%.*}.x/${pkgver}/npp.${pkgver}.bin.zip")
sha512sums=('844a13e3d841b4327d89a7b236ffbdfc8636898b764d130aed45390ce1cd964af079d33f0909185a2fb44b4aab0e6b3fb87cadff950d84d148e4b32bbce1b7d1'
            'c3ba79a68167137b79c504cf552262a9d0c58028b6032e393447f78dc305837ddd17ed3e9c428425605ef94944a0b007114df402d527b87e50076a53a37190a7')

_lc() {
    for f in "${@}"
    do
        n="$(echo "${f}" | tr 'A-Z' 'a-z')"
        if test "${f}" != "${n}"
        then
            mv "${f}" "${n}"
        fi
    done
}

prepare() {
    rm -f NppShell_06*.dll
    echo u | 7z x -bd -i!"NppShell_06.dll" "npp.Installer.exe"
    _lc *
    for f in nppshell_06*.dll
    do
        case $(file "${f}") in
        *80386*) mv "${f}" "nppshell_06.32.dll" ;;
        *x86-64*) mv "${f}" "nppshell_06.64.dll" ;;
        esac
    done
}

package() {
    a="${pkgdir}/apps${_apparch}/${_realname}"
    mkdir -p "${a}"
    cp -r * "${a}"
    rm "${a}/dolocalconf.xml" "${a}"/npp.{installer.exe,bin.zip}

    d="${pkgdir}/docs${_apparch}/${_realname}"
    mkdir -p "${d}"
    mv "${a}"/*.{log,txt} "${d}"
}
