# Maintainer: David Macek <david.macek.0@gmail.com>

_realname=tor
_realver=7.0.6/0.3.1.7
_apparch=32
pkgbase="app-${_realname}"
pkgname=("app-i686-${_realname}")
pkgver=${_realver#*/}
pkgrel=1
pkgdesc="Anonymizing overlay network"
url="http://www.torproject.org/"
arch=('any')
license=('BSD')
makedepends=("unzip")
provides=("${pkgbase}")
options=(!strip)
source=("tor-win32.zip::https://dist.torproject.org/torbrowser/${_realver%/*}/tor-win32-${pkgver}.zip")
sha512sums=('fdae45c4d769cfc7b7b3029c6ecc1018c33bf61efdd677f91a23de6d0b5a96ad04ed3dce8de77f9708b5fad26e832241addc6d27621a633dad1e524fff54fb50')

_lc() {
    local d f n

    for f in "${@}"
    do
        d="$(dirname "${f}")"
        f="$(basename "${f}")"
        n="$(echo "${f}" | tr 'A-Z' 'a-z')"
        if test "${f}" != "${n}"
        then
            mv "${d}/${f}" "${d}/${n}"
        fi
    done
}

prepare() {
    _lc "data"/* *
}

package() {
    a="${pkgdir}/apps${_apparch}/${_realname}"
    mkdir -p "${a}"
    cp -r "data" "tor" "${a}"
}
