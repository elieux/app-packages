# Maintainer: David Macek <david.macek.0@gmail.com>

_dirname=xdebug-dbgp-tools
_realname=xdebug-dbgp-client
pkgbase="app-${_realname}"
pkgname=("app-x86_64-${_realname}")
epoch=1
pkgver=0.4.2 # determined by running the .exe
pkgrel=1
pkgdesc="DBGp client from Xdebug"
url="https://xdebug.org/docs/dbgpClient"
arch=('any')
license=('unknown')
provides=("${pkgbase}")
conflicts=("xdebug-dbgp-tools")
options=(!strip)
source=("dbgpClient.exe::https://xdebug.org/files/binaries/dbgpClient.exe")
sha512sums=('c072a2c51bb468383cbed3c523226b3d876fa8e7f814cd6e4f378b0fa5739936aae2e1196d4f2e83dee1367fe27cf92fe104e1b39d0689c481de1fdc178f4521')

_lc() {
    local d f n

    for f in "${@}"
    do
        d="$(dirname "${f}")"
        f="$(basename "${f}")"
        n="$(echo "${f}" | tr 'A-Z' 'a-z')"
        if test "${f}" != "${n}"
        then
            mv "${d}/${f}" "${d}/${n}"
        fi
    done
}

package() {
    local a="${pkgdir}/apps/${_dirname}"
    mkdir -p "${a}"
    cp "dbgpClient.exe" "${a}"
    _lc "${a}"/*
}
