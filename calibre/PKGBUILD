# Maintainer: David Macek <david.macek.0@gmail.com>

_realname=calibre
pkgbase="app-${_realname}"
pkgname=("app-x86_64-${_realname}")
epoch=1
pkgver=4.23.0
pkgrel=1
pkgdesc="Ebook management application"
url="https://calibre-ebook.com/"
arch=('any')
license=('GPL3')
replaces=("app-i686-${_realname}")
makedepends=("app-lessmsi")
provides=("${pkgbase}")
options=(!strip)
source=("calibre-64bit.msi::https://download.calibre-ebook.com/${pkgver}/calibre-64bit-${pkgver}.msi")
sha512sums=('dd5f8a6891e2cb96cc725b2af9ffb7fc2db4dad506d64741bc423670bd84e6e7a86f6201835879a9bb0e46e59a81dae75fedbbe301450f5f58f20eb12fbd8027')

_rmmsdll() {
    local ms

    ms=("msvbvm[0-9]+"
        "ucrtbase"
        "mfcm?[0-9]+[a-z]+"
        "msvc[rp][0-9]+(_([0-9]|codecvt_ids))?"
        "vc([ao]mp|corlib|runtime)[0-9]+(_[0-9])?"
        "concrt[0-9]+"
        "pgort[0-9]+"
        "api-ms-win-[0-9a-z-]+")
    find "${1}" -iname '*.dll' | grep -Eif <(for m in "${ms[@]}"; do echo "/${m}[.]dll$"; done) | xargs -r rm
}

prepare() {
    rm -fR "calibre-64bit"
    /apps/lessmsi/lessmsi x "calibre-64bit.msi" > /dev/null
}

package() {
    local a="${pkgdir}/apps/${_realname}"
    mkdir -p "${a}"
    cp -R "calibre-64bit/SourceDir/PFiles/Calibre2"/* "${a}"
    _rmmsdll "${a}"
}
