# Maintainer: David Macek <david.macek.0@gmail.com>

_realname=filezilla
pkgbase="app-${_realname}"
pkgname=("app-i686-${_realname}"
         "app-x86_64-${_realname}")
pkgver=3.21.0
pkgrel=1
pkgdesc="Fast and reliable FTP, FTPS and SFTP client"
url="http://filezilla-project.org/"
arch=('any')
license=('GPL')
makedepends=("unzip")
provides=("${pkgbase}")
options=(!strip)
source=("FileZilla_win32.zip::http://downloads.sourceforge.net/${_realname}/FileZilla_Client/${pkgver}/FileZilla_${pkgver}_win32.zip"
        "FileZilla_win64.zip::http://downloads.sourceforge.net/${_realname}/FileZilla_Client/${pkgver}/FileZilla_${pkgver}_win64.zip")
sha512sums=('e8dea1cd8f303ac139fef7f9ed057cf1372e2bf2cb8e82a1998ffb14aef410c85bf29a27fd01e948a4f822800497153d098f3c7e838daaf83cd6ae41e9eeef3f'
            'b5994926e02e4094511fab1b1557388dab759d9fe08ed4cdadc608836c115eadf363c6f71fac859f38c7c9977d1bf15b75f53b8a8e2d46987abe1a1a4cac76c8')
noextract=("FileZilla_win32.zip"
           "FileZilla_win64.zip")

prepare() {
    for _apparch in 32 64
    do
        unzip -o "FileZilla_win${_apparch}.zip" -d "filezilla${_apparch}"
    done
}

_package() {
    a="${pkgdir}/apps${_apparch}/${_realname}"
    mkdir -p "${a}"
    cp -r "filezilla${_apparch}/FileZilla-${pkgver}"/* "${a}"

    d="${pkgdir}/docs${_apparch}/${_realname}"
    mkdir -p "${d}"
    mv "${a}/docs" "${a}/AUTHORS" "${a}/NEWS" "${a}/GPL.html" "${d}"
}

package_app-i686-filezilla() {
    _apparch=32
    _package
}

package_app-x86_64-filezilla() {
    _apparch=64
    _package
}
