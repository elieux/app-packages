# Maintainer: David Macek <david.macek.0@gmail.com>

_realname=discord
_apparch=32
pkgbase="app-${_realname}"
pkgname=("app-i686-${_realname}")
pkgver=0.0.297
pkgrel=1
pkgdesc="All-in-one voice and text chat for gamers"
url="https://www.discordapp.com/"
arch=('any')
license=('custom')
makedepends=("p7zip" "unzip")
provides=("${pkgbase}")
options=(!strip)
source=("DiscordSetup.exe::https://discordapp.com/api/download?platform=win")
sha512sums=('0115bdadef958736cea06ded096cdad3b66cd9d9cc04281e4bd8f87681a7661661f9199e3725b9db2d93d71ed2d2466a61a9546be0336c7c5669673de16b81ef')

_lc() {
    local d f n

    for f in "${@}"
    do
        d="$(dirname "${f}")"
        f="$(basename "${f}")"
        n="$(echo "${f}" | tr 'A-Z' 'a-z')"
        if test "${f}" != "${n}"
        then
            mv "${d}/${f}" "${d}/${n}"
        fi
    done
}

pkgver() {
    b="$(cygpath -wa "DiscordSetup.exe" | sed 's/\\/\\\\/g')"
    powershell -command '(Get-Item "DiscordSetup.exe").VersionInfo.ProductVersion'
}

prepare() {
    rm -f "Discord"-*-"full.nupkg"
    7z x -y -bd "DiscordSetup.exe"
    unzip -o "Discord"-*-"full.nupkg"
    _lc "lib/net45"/*
}

package() {
    a="${pkgdir}/apps${_apparch}/${_realname}"
    mkdir -p "${a}"
    cp -r "lib/net45"/* "${a}"
}
