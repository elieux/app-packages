# Maintainer: David Macek <david.macek.0@gmail.com>

_realname=tor
_realver=7.5.2/0.3.2.10
_apparch=32
pkgbase="app-${_realname}"
pkgname=("app-i686-${_realname}")
pkgver=${_realver#*/}
pkgrel=1
pkgdesc="Anonymizing overlay network"
url="http://www.torproject.org/"
arch=('any')
license=('BSD')
makedepends=("unzip")
provides=("${pkgbase}")
options=(!strip)
source=("tor-win32.zip::https://dist.torproject.org/torbrowser/${_realver%/*}/tor-win32-${pkgver}.zip")
sha512sums=('615c87e0185d96cf9408c0c677b4d088531d35240da85ac101d16cd93c78687756ebbff5ccb4c3d4b2539fb4305ee70e5c3d716b9ec4b1d5209107f558f10344')

_lc() {
    local d f n

    for f in "${@}"
    do
        d="$(dirname "${f}")"
        f="$(basename "${f}")"
        n="$(echo "${f}" | tr 'A-Z' 'a-z')"
        if test "${f}" != "${n}"
        then
            mv "${d}/${f}" "${d}/${n}"
        fi
    done
}

prepare() {
    _lc "data"/* *
}

package() {
    a="${pkgdir}/apps${_apparch}/${_realname}"
    mkdir -p "${a}"
    cp -r "data" "tor" "${a}"
}
