# Maintainer: David Macek <david.macek.0@gmail.com>

_realname=sysinternals
_apparch=32
pkgbase="app-${_realname}"
pkgname=("app-i686-${_realname}")
pkgver=2015.02.09
pkgrel=2
pkgdesc="Advanced system utilities for Windows"
url="http://technet.microsoft.com/en-us/sysinternals/bb545021.aspx"
arch=('any')
license=('custom')
makedepends=("unzip")
provides=("${pkgbase}")
options=(!strip) # digital signatures
source=("http://download.sysinternals.com/files/SysinternalsSuite.zip"
        "http://download.sysinternals.com/files/BlueScreen.zip")
md5sums=('5b1d576c3df4b0a7d39d5c006370ad95'
         'e985acdd52b7bccdd6feb85ebd74c722')

_lc() {
    for f in "${@}"
    do
        n="$(echo "${f}" | tr 'A-Z' 'a-z')"
        if test "${f}" != "${n}"
        then
            mv "${f}" "${n}"
        fi
    done
}

pkgver() {
    #find . -mindepth 1 -maxdepth 1 -name '*.exe' -printf '%TY.%Tm.%Td\n' | sort | tail -1
    find . -mindepth 1 -maxdepth 1 -name '*.zip' -execdir bash -c 'unzip -l "{}" | tail -n +4 | head -n -2' \; | sed -r 's/[ ]+/\t/g' | cut -f 3 | tr '-' '.' | sort | tail -1
}

prepare() {
    _lc *
}

package() {
    a="${pkgdir}/apps${_apparch}/${_realname}"
    mkdir -p "${a}"
    cp *.{exe,sys,scr} "${pkgdir}/apps${_apparch}/${_realname}/"

    d="${pkgdir}/docs${_apparch}/${_realname}"
    mkdir -p "${d}"
    cp *.{chm,txt,hlp} "${pkgdir}/docs${_apparch}/${_realname}/"
}
