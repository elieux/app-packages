# Maintainer: David Macek <david.macek.0@gmail.com>

_realname=mp3tag
_apparch=32
pkgbase="app-${_realname}"
pkgname=("app-i686-${_realname}")
pkgver=2.79
pkgrel=1
pkgdesc="Powerful audio metadata editor"
arch=('any')
url="http://mp3tag.de/en/index.html"
license=('custom')
makedepends=("p7zip")
provides=("${pkgbase}")
options=(!strip)
source=("mp3tagsetup.exe::http://download.mp3tag.de/mp3tagv${pkgver//./}setup.exe")
sha512sums=('1cec2e2d67c18c0590f9bcf595e0879b7207a482efe3a670bf87d5e78e311d8e8369c04697faffd248cced5e058eab12807bf54e2be1d703b084f98934910895')

_lc() {
    for f in "${@}"
    do
        n="$(echo "${f}" | tr 'A-Z' 'a-z')"
        if test "${f}" != "${n}"
        then
            mv "${f}" "${n}"
        fi
    done
}

prepare() {
    w="*license*.txt"
    7z x -y -bd -x!"${w}" "mp3tagsetup.exe"
    for f in $(7z l -y -bd -i!"${w}" "mp3tagsetup.exe" | grep 'license.*txt' | sed -r 's/[ ]+/\t/g' | cut -f 5 | sed 's/^.*\\//')
    do
        7z e -y -bd -so -i!"*${f}" "mp3tagsetup.exe" > "${f}"
    done
    _lc *
}

package() {
    a="${pkgdir}/apps${_apparch}/${_realname}"
    mkdir -p "${a}" "${a}/help"
    cp -r "data" "export" "lang" *.{exe,dll} "${a}"
    touch "${a}/help/helpmap.idx"
    rm "${a}/mp3tagsetup.exe"

    d="${pkgdir}/docs${_apparch}/${_realname}"
    mkdir -p "${d}"
    cp -r "help" *.txt "${d}"
}
