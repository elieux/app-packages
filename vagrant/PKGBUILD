# Maintainer: David Macek <david.macek.0@gmail.com>

_chocover() {
    # choco version format: 2.2.18.20210807
    echo "${1}" | sed -r 's/^([0-9]+)[.]([0-9]+)[.]([0-9]+)[.]([0-9]{8})$/\1.\2.\3/'
}

_realname=vagrant
_choconame=vagrant
pkgbase="app-${_realname}"
pkgname=("app-x86_64-${_realname}")
epoch=1
pkgver=2.2.18
pkgrel=1
pkgdesc="Building and distributing development environments"
url="https://www.vagrantup.com/"
arch=('any')
license=('MIT')
makedepends=("app-lessmsi")
provides=("${pkgbase}")
options=(!strip)
source=("vagrant_x86_64.msi::https://releases.hashicorp.com/vagrant/${pkgver}/vagrant_${pkgver}_x86_64.msi")
sha512sums=('c1da2cc3ac34dbf8cbc9033ce8e90034ac1a9b892bc5479586443bceea870eef62a6744962b323e2a0bc32495c73689350e55a02b43a62032a25758fd183d7ff')

prepare() {
    rm -fR "vagrant_x86_64"
    /apps/lessmsi/lessmsi x "vagrant_x86_64.msi" > /dev/null
}

package() {
    local a="${pkgdir}/apps/${_realname}"
    mkdir -p "${a}"
    cp -R "vagrant_x86_64/SourceDir/HashiCorp/Vagrant"/* "${a}"
    rm -f "${a}/embedded/bin/vcruntime140.dll"

    # remove some of the largest MSYS2 left-overs
    rm -R "${a}/embedded"/{var/*,lib,include,usr/{lib/*.a,include},mingw64/{x86_64-w64-mingw32,lib/gcc,lib/*.a,include}}
}
