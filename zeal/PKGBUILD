# Maintainer: David Macek <david.macek.0@gmail.com>

_realname=zeal
_apparch=64
pkgbase="app-${_realname}"
pkgname=("app-x86_64-${_realname}")
pkgver=0.5.0
pkgrel=1
pkgdesc="An offline documentation browser for software developers"
url="https://zealdocs.org/"
arch=('any')
license=('GPL3')
provides=("${pkgbase}")
options=(!strip)
source=("zeal-windows-x64.msi::http://dl.bintray.com/zealdocs/windows/zeal-${pkgver}-windows-x64.msi")
sha512sums=('5da09ff1bf520485f17a09c52d62124c9839b2b8d6723dddfabb71e87bd747a7d5aa7aff60f6e0e001dca86ea84ab46d11dd16ffacb7661b3e8645e8aa1da31d')

_lc() {
    local d f n

    for f in "${@}"
    do
        d="$(dirname "${f}")"
        f="$(basename "${f}")"
        n="$(echo "${f}" | tr 'A-Z' 'a-z')"
        if test "${f}" != "${n}"
        then
            mv "${d}/${f}" "${d}/${n}"
        fi
    done
}

build() {
    msiexec -a "zeal-windows-x64.msi" "TARGETDIR=$(cygpath -wa "install")" -qn
    attrib -r //s
    s="install/Zeal"
    _lc "${s}"/*
}

package() {
    s="install/Zeal"

    a="${pkgdir}/apps${_apparch}/${_realname}"
    mkdir -p "${a}"
    cp -r "${s}"/* "${a}"
}
