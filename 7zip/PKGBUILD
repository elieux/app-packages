# Maintainer: David Macek <david.macek.0@gmail.com>

_realname=7zip
pkgname=("app-i686-${_realname}"
         "app-x86_64-${_realname}")
pkgver=9.38
pkgrel=1
pkgdesc="A free file archiver for extremely high compression"
url="http://www.7-zip.org/"
arch=('any')
license=('LGPL2' 'custom')
makedepends=("p7zip")
source=("7z32.msi::http://downloads.sourceforge.net/sevenzip/7-Zip/${pkgver}/7z${pkgver//./}.msi"
        "7z64.msi::http://downloads.sourceforge.net/sevenzip/7-Zip/${pkgver}/7z${pkgver//./}-x64.msi"
        "7zextra.7z::http://downloads.sourceforge.net/sevenzip/7-Zip/${pkgver}/7z${pkgver//./}-extra.7z")
md5sums=('8e8e360994cfddcc2504fa473c977272'
         '9f4efa767708af6badca6f60ad2006ae'
         '3b95291e4a5db4f0dc1907462e9a5b57')

prepare() {
    cd "${srcdir}"
    for f in *
    do
        n=$(echo "${f}" | tr 'A-Z' 'a-z')
        if test "${f}" != "${n}"
        then
            mv "${f}" "${n}"
        fi
    done
}

build() {
    for _apparch in 32 64
    do
        cd "${srcdir}"
        msiexec -a "7z${_apparch}.msi" "TARGETDIR=$(cygpath -w "${srcdir}/install${_apparch}")" -qn
        cd "${srcdir}/install${_apparch}/Files/7-Zip"
        for f in *
        do
            n=$(echo "${f}" | tr 'A-Z' 'a-z')
            if test "${f}" != "${n}"
            then
                mv "${f}" "${n}"
            fi
        done
    done
}

package() {
    mkdir -p "${pkgdir}/apps${_apparch}/${_realname}"
    mkdir -p "${pkgdir}/docs${_apparch}/${_realname}"
    mkdir -p "${pkgdir}/docs${_apparch}/${_realname}-extra"

    cp "${srcdir}"/*.txt "${pkgdir}/docs${_apparch}/${_realname}-extra"
    cp -R "${srcdir}/install${_apparch}/Files/7-Zip/lang" "${pkgdir}/apps${_apparch}/${_realname}/"
    cp "${srcdir}/install${_apparch}/Files/7-Zip"/*.{exe,dll,sfx} "${pkgdir}/apps${_apparch}/${_realname}"
    cp "${srcdir}/install${_apparch}/Files/7-Zip"/*.{chm,txt,ion} "${pkgdir}/docs${_apparch}/${_realname}"
}

package_app-i686-7zip() {
    _apparch=32
    package
    cp "${srcdir}"/*.{dll,exe} "${pkgdir}/apps${_apparch}/${_realname}"
}

package_app-x86_64-7zip() {
    _apparch=64
    package
    cp "${srcdir}/x64"/*.{dll,exe} "${pkgdir}/apps${_apparch}/${_realname}"
}
