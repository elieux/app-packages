# Maintainer: David Macek <david.macek.0@gmail.com>

_realver=916

DLAGENTS=("https::/usr/bin/curl -qb '' -fLC - -d id=${_realver}&server_id=8 -o %o %u")

_realname=gpu-z
_apparch=32
pkgbase="app-${_realname}"
pkgname=("app-i686-${_realname}")
pkgver=2.8.0
pkgrel=1
pkgdesc="Lightweight utility providing GPU and graphics card information"
arch=('any')
url="http://www.techpowerup.com/gpuz/"
license=('custom')
provides=("${pkgbase}")
options=(!strip)
source=("GPU-Z.exe::https://www.techpowerup.com/download/techpowerup-gpu-z/")
sha512sums=('79823cb3ac7d174976569664e8925cc5bdaf988700d4895938e8366d92e11e6b9328ecd3d897fa499d7553155b8b921405e266e63504989d7545b5ac48dee20f')

_lc() {
    local d f n

    for f in "${@}"
    do
        d="$(dirname "${f}")"
        f="$(basename "${f}")"
        n="$(echo "${f}" | tr 'A-Z' 'a-z')"
        if test "${f}" != "${n}"
        then
            mv "${d}/${f}" "${d}/${n}"
        fi
    done
}

pkgver() {
    "${SYSTEMROOT}/System32/WindowsPowerShell/v1.0/powershell" -command '(Get-Item "GPU-Z.exe").VersionInfo.ProductVersion' | sed -r 's/^([0-9]+[.][0-9]+[.][0-9]+)[.][0-9]+$/\1/'
}

prepare() {
    _lc *
}

package() {
    a="${pkgdir}/apps${_apparch}/${_realname}"
    mkdir -p "${a}"
    cp -r *.exe "${a}"
}
