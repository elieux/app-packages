# Maintainer: David Macek <david.macek.0@gmail.com>

_realname=screentogif
pkgbase="app-${_realname}"
pkgname=("app-i686-${_realname}")
epoch=1
pkgver=2.24
pkgrel=1
pkgdesc="Screen, webcam and sketchboard recorder with an integrated editor"
url="https://www.screentogif.com/"
arch=('any')
license=('MS-PL')
replaces=("app-i686-${_realname}")
provides=("${pkgbase}")
options=(!strip)
source=("ScreenToGif.Portable.zip::https://github.com/NickeManarin/ScreenToGif/releases/download/${pkgver}/ScreenToGif.${pkgver}.Portable.zip")
sha512sums=('d5116e975d08ea367e044c44d61f96346f7475197486b528ea4d0b3722854cd91810f766e2ead7fed2928d3c9a333dee080d0e65d691db7fd2fdab0635951402')

_lc() {
    local d f n

    for f in "${@}"
    do
        d="$(dirname "${f}")"
        f="$(basename "${f}")"
        n="$(echo "${f}" | tr 'A-Z' 'a-z')"
        if test "${f}" != "${n}"
        then
            mv "${d}/${f}" "${d}/${n}"
        fi
    done
}

package() {
    local a="${pkgdir}/apps/${_realname}"
    mkdir -p "${a}"
    cp "ScreenToGif.exe" "${a}"
    _lc "${a}"/*
}
