# Maintainer: David Macek <david.macek.0@gmail.com>

_realname=tor
_realver=9.0.1
pkgbase="app-${_realname}"
pkgname=("app-x86_64-${_realname}")
epoch=1
pkgver=0.4.1.6
pkgrel=1
pkgdesc="Anonymizing overlay network"
url="https://www.torproject.org/"
arch=('any')
license=('BSD')
replaces=("app-i686-${_realname}")
makedepends=("unzip")
provides=("${pkgbase}")
options=(!strip)
source=("tor-win64.zip::https://dist.torproject.org/torbrowser/${_realver}/tor-win64-${pkgver}.zip")
sha512sums=('36e518aba9b5db9114d72dd4c3b9235702b9a97767f28c5060704deee2d9eb0d6005f044e8738c76962999628e9e3644354907e541b0a906f93119ef4b72a0a9')

_lc() {
    local d f n

    for f in "${@}"
    do
        d="$(dirname "${f}")"
        f="$(basename "${f}")"
        n="$(echo "${f}" | tr 'A-Z' 'a-z')"
        if test "${f}" != "${n}"
        then
            mv "${d}/${f}" "${d}/${n}"
        fi
    done
}

prepare() {
    _lc "data"/* *
}

package() {
    a="${pkgdir}/apps/${_realname}"
    mkdir -p "${a}"
    cp -R "data" "tor" "${a}"
}
