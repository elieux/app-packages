# Maintainer: David Macek <david.macek.0@gmail.com>

_realname=crystaldiskinfo
_realver=65980
_dirname=crystal
pkgbase="app-${_realname}"
pkgname=("app-i686-${_realname}"
         "app-x86_64-${_realname}")
pkgver=7.0.4
pkgrel=1
pkgdesc="A disk utility (Crystal Dew World)"
url="http://crystalmark.info/software/CrystalDiskInfo/index-e.html"
arch=('any')
license=('MIT')
makedepends=("unzip")
provides=("${pkgbase}")
options=(!strip)
source=("CrystalDiskInfo.zip::http://osdn.jp/frs/redir.php?f=%2Fcrystaldiskinfo%2F${_realver}%2FCrystalDiskInfo${pkgver//./_}.zip")
sha512sums=('8b579e33ef36e47154c402567f4fc32e35506ab34b1378f17215df7404ac57d9566deb56129562c2d60d63478df29b990396eee7eab40b986b61b907b1c03ace')

_lc() {
    local d f n

    for f in "${@}"
    do
        d="$(dirname "${f}")"
        f="$(basename "${f}")"
        n="$(echo "${f}" | tr 'A-Z' 'a-z')"
        if test "${f}" != "${n}"
        then
            mv "${d}/${f}" "${d}/${n}"
        fi
    done
}

prepare() {
    _lc "License"/* *
}

_package() {
    a="${pkgdir}/apps${_apparch}/${_dirname}"
    mkdir -p "${a}"
    cp "diskinfo${_apparch}.exe" "${a}/crystaldiskinfo.exe"
    cp -r "cdiresource" "${a}"

    d="${pkgdir}/docs${_apparch}/${_dirname}"
    mkdir -p "${d}/diskinfo"
    cp -r "license"/* "${d}/diskinfo"
}

package_app-i686-crystaldiskinfo() {
    _apparch=32
    _package
}

package_app-x86_64-crystaldiskinfo() {
    _apparch=64
    _package
}
