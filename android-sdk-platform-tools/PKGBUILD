# Maintainer: David Macek <david.macek.0@gmail.com>

_apparch=32
_dirname=android-sdk
_realname=android-sdk-platform-tools
pkgbase="app-${_realname}"
pkgname=("app-i686-${_realname}")
pkgver=26.0.1
pkgrel=1
pkgdesc="Platform-Tools for Google Android SDK (adb and fastboot)"
url="https://developer.android.com/studio/releases/platform-tools.html"
arch=('any')
license=('custom')
makedepends=("unzip")
provides=("${pkgbase}")
options=(!strip)
source=("platform-tools-latest-windows.zip::https://dl.google.com/android/repository/platform-tools-latest-windows.zip")
sha512sums=('15bd54ba08fd869db7389002359d828df3bfd6cdc780d48659649cdcdb67fbad667474476fd89e70ab5485ab4e3936609fb08f6047b2590b64c9add6cb843a27')

pkgver() {
    unzip -p "platform-tools-latest-windows.zip" "platform-tools/source.properties" | grep "^Pkg.Revision" | cut -d= -f2
}

_lc() {
    local d f n

    for f in "${@}"
    do
        d="$(dirname "${f}")"
        f="$(basename "${f}")"
        n="$(echo "${f}" | tr 'A-Z' 'a-z')"
        if test "${f}" != "${n}"
        then
            mv "${d}/${f}" "${d}/${n}"
        fi
    done
}

prepare() {
    _lc "platform-tools"/*
}

package() {
    a="${pkgdir}/apps${_apparch}/${_dirname}"
    mkdir -p "${a}"
    cp -r "platform-tools" "${a}"

    d="${pkgdir}/docs${_apparch}/${_dirname}"
    mkdir -p "${d}" "${d}/platform-tools" "${d}/platform-tools/systrace"
    mv "${a}/platform-tools"/*.{properties,txt} "${d}/platform-tools"
    mv "${a}/platform-tools/systrace/NOTICE" "${d}/platform-tools/systrace"
}
