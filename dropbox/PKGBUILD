# Maintainer: David Macek <david.macek.0@gmail.com>

DLAGENTS=('https::/usr/bin/curl -k -qb "" -fLC - -o %o %u')

_realname=dropbox
_apparch=32
pkgbase="app-${_realname}"
pkgname=("app-i686-${_realname}")
pkgver=10.4.25
pkgrel=1
pkgdesc="The Dropbox synchronization tool"
url="http://www.dropbox.com/"
arch=('any')
license=('custom')
makedepends=("p7zip" "curl")
provides=("${pkgbase}")
options=(!strip)
source=("DropboxOfflineInstaller.exe::https://www.dropbox.com/download?full=1&plat=win")
sha512sums=('1490d495af5e006053a5ad5a20f9581a4cdc6d53cc76aa4514f906cfdd4c2288fed360c376ab4b30da978354bcc29df2914e5d847f01a15bea8443a782fc7c92')

_lc() {
    for f in "${@}"
    do
        n="$(echo "${f}" | tr 'A-Z' 'a-z')"
        if test "${f}" != "${n}"
        then
            mv "${f}" "${n}"
        fi
    done
}

pkgver() {
    7z x -y -bd "DropboxOfflineInstaller.exe" > /dev/null
    b="$(cygpath -wa "Installer.exe".* | sed 's/\\/\\\\/g')"
    wmic datafile where name=\""${b}"\" get version | grep 'Version' -A 1 | tail -1 | tr -d ' ' | sed -r 's/([0-9]+[.][0-9]+[.][0-9]+)[.]0$/\1/'
}

prepare() {
    7z x -y -bd "DropboxOfflineInstaller.exe"
    7z x -y -bd "Installer.exe".*
    _lc '$0'/*
}

package() {
    a="${pkgdir}/apps${_apparch}/${_realname}"
    mkdir -p "${a}"
    cp -r '$0'/* "${a}"
    rm -r "${a}/dropboxuninstaller.exe" "${a}"/msvc{p,r}120.dll "${a}"/{concrt,msvcp,vccorlib,vcruntime}140.dll
}
