# Maintainer: David Macek <david.macek.0@gmail.com>

_realname=openjdk
_realver=14.0.2+12
pkgbase="app-${_realname}"
pkgname=("app-x86_64-${_realname}")
epoch=1
pkgver=${_realver/+/.}
pkgrel=1
pkgdesc="Open Java Development Kit"
url="https://adoptopenjdk.net/"
arch=('any')
license=('custom')
replaces=("app-i686-jdk" "app-x86_64-jdk")
provides=("${pkgbase}")
options=(!strip)
source=("OpenJDK-jdk_x64_windows_hotspot.zip::https://github.com/AdoptOpenJDK/openjdk${_realver%%.*}-binaries/releases/download/jdk-${_realver/+/%2B}/OpenJDK${_realver%%.*}U-jdk_x64_windows_hotspot_${_realver/+/_}.zip")
sha512sums=('461e84ecaf17b9a992c8b2a43b32ddb0772a1002a4ca04abb51f9b44a8b43c8a167dea92c6a58844037ea0562c7cb5ea9e33b087e4cc99a099c7a90b203d31f5')

_rmmsdll() {
    local ms

    ms=("msvbvm[0-9]+"
        "ucrtbase"
        "mfcm?[0-9]+[a-z]+"
        "msvc[rp][0-9]+(_([0-9]|codecvt_ids))?"
        "vc([ao]mp|corlib|runtime)[0-9]+(_[0-9])?"
        "concrt[0-9]+"
        "pgort[0-9]+"
        "api-ms-win-[0-9a-z-]+")
    find "${1}" -iname '*.dll' | grep -Eif <(for m in "${ms[@]}"; do echo "/${m}[.]dll$"; done) | xargs -r rm
}

package() {
    local a="${pkgdir}/apps/${_realname}"
    mkdir -p "${a}"
    cp -R "jdk-${_realver}"/* "${a}"
    _rmmsdll "${a}"
}
