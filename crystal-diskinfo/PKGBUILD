# Maintainer: David Macek <david.macek.0@gmail.com>

_realname=crystaldiskinfo
_dirname=crystal
pkgbase="app-${_realname}"
pkgname=("app-x86_64-${_realname}")
epoch=1
pkgver=8.3.1
pkgrel=1
pkgdesc="A disk utility (Crystal Dew World)"
url="http://crystalmark.info/software/CrystalDiskInfo/index-e.html"
arch=('any')
license=('MIT')
replaces=("app-i686-${_realname}")
makedepends=("unzip")
provides=("${pkgbase}")
options=(!strip)
source=("CrystalDiskInfo.zip::https://osdn.net/dl/crystaldiskinfo/CrystalDiskInfo${pkgver//./_}.zip")
sha512sums=('3a896e145cf4c02e81de1d4a422e12fde705f5cd1d58ba50fb808dcf850ce7dda6c928d9fff8191edcd32b61750c7637f3d55452e095429075fcddf1d917ba0b')

_lc() {
    local d f n

    for f in "${@}"
    do
        d="$(dirname "${f}")"
        f="$(basename "${f}")"
        n="$(echo "${f}" | tr 'A-Z' 'a-z')"
        if test "${f}" != "${n}"
        then
            mv "${d}/${f}" "${d}/${n}"
        fi
    done
}

prepare() {
    _lc "License"/* *
}

package() {
    a="${pkgdir}/apps/${_dirname}"
    mkdir -p "${a}"
    cp "diskinfo64.exe" "${a}/crystaldiskinfo.exe"
    cp -r "cdiresource" "${a}"
    cp -r "license" "${a}/cdilicense"
}
