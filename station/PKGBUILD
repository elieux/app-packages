# Maintainer: David Macek <david.macek.0@gmail.com>

_realname=station
pkgbase="app-${_realname}"
pkgname=("app-x86_64-${_realname}")
epoch=1
pkgver=3.2.0
pkgrel=1
pkgdesc="First open-source smart browser for busy people. A single place for all of your web applications."
url="https://getstation.com/"
arch=('any')
license=('Apache-2.0')
makedepends=("p7zip")
provides=("${pkgbase}")
options=(!strip)
source=("Station-Setup.exe::https://github.com/getstation/desktop-app/releases/download/v${pkgver}/Station-Setup.exe")
sha512sums=('5cf612192c74b9c283bf7b91dadab2a39cc18aa9e4625a99535c9e53dd6239ae4823d939285c927d2acf51c1e3650b63f5d120fb73f3f0cf90ce4e31d6122535')
noextract=("Station-Setup.exe")

_lc() {
    local d f n

    for f in "${@}"
    do
        d="$(dirname "${f}")"
        f="$(basename "${f}")"
        n="$(echo "${f}" | tr 'A-Z' 'a-z')"
        if test "${f}" != "${n}"
        then
            mv "${d}/${f}" "${d}/${n}"
        fi
    done
}

_rmmsdll() {
    local ms

    ms=("msvbvm[0-9]+"
        "ucrtbase"
        "mfcm?[0-9]+[a-z]+"
        "msvc[rp][0-9]+(_([0-9]|codecvt_ids|atomic_wait))?"
        "vc([ao]mp|corlib|runtime)[0-9]+(_([0-9]|threads))?"
        "concrt[0-9]+"
        "pgort[0-9]+"
        "api-ms-win-[0-9a-z-]+")
    find "${1}" -iname '*.dll' | grep -Eif <(for m in "${ms[@]}"; do echo "/${m}[.]dll$"; done) | xargs -r rm
}

prepare() {
    rm -fR "Station-Setup.dir"
    7z x -y -bd "Station-Setup.exe" -o"Station-Setup.dir" > /dev/null
    7z x -y -bd "Station-Setup.dir"/'$PLUGINSDIR'/"app-64.7z" -o"app-64" > /dev/null
}

package() {
    local a="${pkgdir}/apps/${_realname}"
    mkdir -p "${a}"
    cp -R "app-64"/* "${a}"
    _rmmsdll "${a}"
    _lc "${a}"/*
}
