# Maintainer: David Macek <david.macek.0@gmail.com>

DLAGENTS=('https::/usr/bin/curl -qb "" -fLC - -A "Chrome" -e "https://filezilla-project.org/" -o %o %u')

_realname=filezilla-server
_choconame=filezilla.server
pkgbase="app-${_realname}"
pkgname=("app-x86_64-${_realname}")
epoch=1
pkgver=1.12.0
pkgrel=1
pkgdesc="A popular FTP server"
url="https://filezilla-project.org/"
arch=('any')
license=('GPL')
makedepends=("openssl" "p7zip")
replaces=("app-i686-${_realname}")
provides=("${pkgbase}")
options=(!strip)
source=()
sha512sums=()
_sha512sum='b216ffaac8fd320f44f4970fdd6638f1f682ecddc249474e51fdb6d7fce37a53de805d7992eb60138146a15173b0cd7864cd607dc7cdd3441bfbcfe1815b1f40'
noextract=("FileZilla_Server_win64-setup.exe")

_dl() {
    (
        cd "${SRCDEST}"
        download_file "${1}"
    )
}

_dlread() {
    local tmp

    tmp=".tmp"
    rm -f "${SRCDEST}/${tmp}"
    _dl "${tmp}::${1}" &> /dev/null
    cat "${SRCDEST}/${tmp}"
    rm "${SRCDEST}/${tmp}"
}

prepare() {
    local localname remotename contentwrapper cipher iv rawkey a plain link remotename
    localname="FileZilla_Server_win64-setup.exe"
    remotename="FileZilla_Server_${pkgver}_win64-setup.exe"
    contentwrapper="$(_dlread "https://filezilla-project.org/download.php?show_all=1&type=server" | grep 'id="contentwrapper"')"
    cipher="$(<<< "${contentwrapper}" sed -r 's/^.*>([^<]+)<.*$/\1/')"
    iv="$(<<< "${contentwrapper}" sed -r 's/^.*v1="([^"]+)".*$/\1/' | openssl base64 -d -A | hexdump -e '16/1 "%02x"')"
    rawkey="$(<<< "${contentwrapper}" sed -r 's/^.*v2="([^"]+)".*$/\1/' | openssl base64 -d -A | hexdump -e '16/1 "%02x"')"
    a="$(<<< "${contentwrapper}" sed -r 's/^.*v3="([^"]+)".*$/\1/' | openssl base64 -d -A)"
    test "${a}" = "AES-CBC"
    plain="$(<<< "${cipher}" openssl aes-256-cbc -d -a -A -K "${rawkey}" -iv "${iv}")"
    link="$(<<< "${plain}" grep -F "/${remotename}?" | sed -r 's/^.*href="([^"]+)".*$/\1/')"
    _dl "${localname}::${link}"
    source+=("${localname}")
    sha512sums+=("${_sha512sum}")
    check_checksums
    extract_sources

    7z x -y -bd "FileZilla_Server_win64-setup.exe" -o"FileZilla_Server_win64-setup.dir" > /dev/null
}

package() {
    local a="${pkgdir}/apps/${_realname}"
    mkdir -p "${a}"
    cp -R "FileZilla_Server_win64-setup.dir"/* "${a}"
    rm -R "${a}"/'$PLUGINSDIR'
}
