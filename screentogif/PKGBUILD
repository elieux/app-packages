# Maintainer: David Macek <david.macek.0@gmail.com>

_realname=screentogif
pkgbase="app-${_realname}"
pkgname=("app-i686-${_realname}")
epoch=1
pkgver=2.23.1
pkgrel=1
pkgdesc="Screen, webcam and sketchboard recorder with an integrated editor"
url="https://www.screentogif.com/"
arch=('any')
license=('MS-PL')
replaces=("app-i686-${_realname}")
provides=("${pkgbase}")
options=(!strip)
source=("ScreenToGif.Portable.zip::https://github.com/NickeManarin/ScreenToGif/releases/download/${pkgver}/ScreenToGif.${pkgver}.Portable.zip")
sha512sums=('ff3910796a8d79b62c29714a1645f42fcb969926c87540981c6ddf4ef166b852762e473b005d3a038d198cea936c9d331729f145331b5acee1d75a851d14160f')

_lc() {
    local d f n

    for f in "${@}"
    do
        d="$(dirname "${f}")"
        f="$(basename "${f}")"
        n="$(echo "${f}" | tr 'A-Z' 'a-z')"
        if test "${f}" != "${n}"
        then
            mv "${d}/${f}" "${d}/${n}"
        fi
    done
}

package() {
    local a="${pkgdir}/apps/${_realname}"
    mkdir -p "${a}"
    cp "ScreenToGif.exe" "${a}"
    _lc "${a}"/*
}
