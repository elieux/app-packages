# Maintainer: David Macek <david.macek.0@gmail.com>

_realname=tor
_realver=6.0.4/0.2.8.6
_apparch=32
pkgbase="app-${_realname}"
pkgname=("app-i686-${_realname}")
pkgver=${_realver#*/}
pkgrel=1
pkgdesc="Anonymizing overlay network"
url="http://www.torproject.org/"
arch=('any')
license=('BSD')
makedepends=("unzip")
provides=("${pkgbase}")
options=(!strip)
source=("tor-win32.zip::https://dist.torproject.org/torbrowser/${_realver%/*}/tor-win32-${pkgver}.zip")
sha512sums=('cc210cff85807d0b4f3ebf18a23034c381e7bf7ca5b0b5016736c1a13e2a8cb0e364af0df449c8762de2c27644f8a1c9183071ee697aa6ea3dc6c92264ffc849')

_lc() {
    for f in "${@}"
    do
        n="$(echo "${f}" | tr 'A-Z' 'a-z')"
        if test "${f}" != "${n}"
        then
            mv "${f}" "${n}"
        fi
    done
}

prepare() {
    _lc "data"/* *
}

package() {
    a="${pkgdir}/apps${_apparch}/${_realname}"
    mkdir -p "${a}"
    cp -r "data" "tor" "${a}"
}
