# Maintainer: David Macek <david.macek.0@gmail.com>

_realname=musescore
_apparch=32
pkgbase="app-${_realname}"
pkgname=("app-i686-${_realname}")
pkgver=2.1
pkgrel=1
pkgdesc="Create, play and print beautiful sheet music"
url="http://musescore.org/"
arch=('any')
license=('GPL')
provides=("${pkgbase}")
options=(!strip)
source=("MuseScore.msi::ftp://ftp.osuosl.org/pub/musescore/releases/MuseScore-${pkgver}/MuseScore-${pkgver}.msi")
sha512sums=('b602a812c29c5879dab9406302ea25d8f53f1e678fde4c00c33965b250ed197dda67476cf95e30dd10a541d20308a6b0f742b92d4abfe17956054719ded40375')

_lc() {
    local d f n

    for f in "${@}"
    do
        d="$(dirname "${f}")"
        f="$(basename "${f}")"
        n="$(echo "${f}" | tr 'A-Z' 'a-z')"
        if test "${f}" != "${n}"
        then
            mv "${d}/${f}" "${d}/${n}"
        fi
    done
}

build() {
    msiexec -a "MuseScore.msi" "TARGETDIR=$(cygpath -wa "install")" -qn

    s="install/MuseScore 2"
    _lc "${s}/bin"/*
}

package() {
    s="install/MuseScore 2"

    a="${pkgdir}/apps${_apparch}/${_realname}"
    mkdir -p "${a}"
    cp -r "${s}"/* "${a}"

    d="${pkgdir}/docs${_apparch}/${_realname}"
    mkdir -p "${d}"
    mv "${a}"/{demos,manual} "${a}/sound/FluidR3Mono_License.md" "${d}"
}
