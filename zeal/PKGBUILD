# Maintainer: David Macek <david.macek.0@gmail.com>

_realname=zeal
_choconame=zeal
pkgbase="app-${_realname}"
pkgname=("app-x86_64-${_realname}")
epoch=1
pkgver=0.6.1
pkgrel=1
pkgdesc="An offline documentation browser for software developers"
url="https://zealdocs.org/"
arch=('any')
license=('GPL3')
replaces=("app-i686-${_realname}")
makedepends=("app-lessmsi")
provides=("${pkgbase}")
options=(!strip)
source=("zeal-windows-x64.msi::https://dl.bintray.com/zealdocs/windows/zeal-${pkgver}-windows-x64.msi")
sha512sums=('b05f39c2838eb245654919b4f9bfb3e6f9cbe5aedd49b80dbe0fb1e0e3db0526cc3dc0327be17fc84ec57809a48525acb06c91867948ec6915530358faf2aead')

_lc() {
    local d f n

    for f in "${@}"
    do
        d="$(dirname "${f}")"
        f="$(basename "${f}")"
        n="$(echo "${f}" | tr 'A-Z' 'a-z')"
        if test "${f}" != "${n}"
        then
            mv "${d}/${f}" "${d}/${n}"
        fi
    done
}

prepare() {
    rm -fR "zeal-windows-x64"
    /apps/lessmsi/lessmsi x "zeal-windows-x64.msi" > /dev/null
}

package() {
    local a="${pkgdir}/apps/${_realname}"
    mkdir -p "${a}"
    cp -R "zeal-windows-x64/SourceDir/Zeal"/* "${a}"
    _lc "${a}"/*
}
