# Maintainer: David Macek <david.macek.0@gmail.com>

_realname=wireshark
_choconame=wireshark
pkgbase="app-${_realname}"
pkgname=("app-x86_64-${_realname}")
epoch=1
pkgver=3.7.2
pkgrel=1
pkgdesc="A free network protocol analyzer"
url="https://www.wireshark.org/"
arch=('any')
license=('GPL2')
replaces=("app-i686-${_realname}")
makedepends=("p7zip")
provides=("${pkgbase}")
options=(!strip)
source=("Wireshark-win64.exe::https://1.eu.dl.wireshark.org/win64/Wireshark-win64-${pkgver}.exe")
sha512sums=('29a7ffb842ad34e8f0347cda7c918e61c0567283f15739d102cb61cbca6a3847cdabecd7099ea917f9f9bcfa616f1a5978d556b39a35954221459f649c6b6b89')

_lc() {
    local d f n

    for f in "${@}"
    do
        d="$(dirname "${f}")"
        f="$(basename "${f}")"
        n="$(echo "${f}" | tr 'A-Z' 'a-z')"
        if test "${f}" != "${n}"
        then
            mv "${d}/${f}" "${d}/${n}"
        fi
    done
}

prepare() {
    7z x -y -bd "Wireshark-win64.exe" -o"Wireshark-win64.dir" > /dev/null
}

package() {
    local a="${pkgdir}/apps/${_realname}"
    mkdir -p "${a}"
    cp -R "Wireshark-win64.dir"/* "${a}"
    rm -R "${a}"/npcap-*.exe "${a}"/USBPcapSetup-*.exe "${a}/vc_redist.x64.exe" "${a}/uninstall-wireshark.exe" "${a}"/'$PLUGINSDIR'
    _lc "${a}"/*
}
