# Maintainer: David Macek <david.macek.0@gmail.com>

# adding quotes around the cookie value makes the download fail
DLAGENTS=('http::/usr/bin/curl -qb oraclelicense=accept-securebackup-cookie -fLC - -o %o %u')

_realname=jdk
_realver=8u74-b02
pkgbase="app-${_realname}"
pkgname=("app-i686-${_realname}"
         "app-x86_64-${_realname}")
pkgver=${_realver%-*}
pkgrel=1
pkgdesc="Oracle Java Development Kit"
url="http://www.oracle.com/technetwork/java/javase/downloads/index.html"
arch=('any')
license=('custom')
makedepends=("p7zip" "unzip" "curl")
provides=("${pkgbase}")
options=(!strip)
source=("jdk-windows32.exe::http://download.oracle.com/otn-pub/java/jdk/${_realver}/jdk-${pkgver}-windows-i586.exe"
        "jdk-windows64.exe::http://download.oracle.com/otn-pub/java/jdk/${_realver}/jdk-${pkgver}-windows-x64.exe")
sha512sums=('ea5a90b68ff08a79665b3918bf4abf9a6a6767cf48e7210f0dedeace81113226c0daabf6ca7040852cf961986af2a587233fa020d9e6825d9fd01ca6220b161c'
            'fdaac414f3d82f0fa1042c860403be5e5598a96523ca6b949fd97dbde409d80208cefafac6a9a46513105cb0b9eb2a6bd2bfe989a061f3b9c210c2a001dd66d9')

_lc() {
    for f in "${@}"
    do
        n="$(echo "${f}" | tr 'A-Z' 'a-z')"
        if test "${f}" != "${n}"
        then
            mv "${f}" "${n}"
        fi
    done
}

_prepare() {
    7z x -y -bd "jdk-windows${_apparch}.exe"
    mv "tools.zip" "tools${_apparch}.zip"
    unzip -o "tools${_apparch}.zip" -d "tools${_apparch}"
    attrib -r //s
    _lc "tools${_apparch}"/*
    for f in $(find "tools${_apparch}" -iname '*.pack')
    do
        "tools${_apparch}/bin/unpack200" "${f}" "${f%.pack}.jar"
        rm "${f}"
    done
}

prepare() {
    for _apparch in 32 64
    do
        _prepare
    done
}

_package() {
    s="tools${_apparch}"

    a="${pkgdir}/apps${_apparch}/${_realname}"
    mkdir -p "${a}"
    cp -r "${s}"/{bin,db,include,jre,lib} "${a}"
    rm "${a}"/{bin,jre/bin,jre/bin/plugin2}/msvcr100.dll "${a}/jre/bin"/msvc{r,p}120.dll

    d="${pkgdir}/docs${_apparch}/${_realname}"
    mkdir -p "${d}"
    cp "${s}"/*.{html,txt,zip} "${s}"/{license,release} "${d}"
}

package_app-i686-jdk() {
    _apparch=32
    _package
}

package_app-x86_64-jdk() {
    _apparch=64
    _package
}
