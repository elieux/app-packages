# Maintainer: David Macek <david.macek.0@gmail.com>

_realname=gnupg
_realver=20210420
_choconame=gnupg
pkgbase="app-${_realname}"
pkgname=("app-i686-${_realname}")
epoch=1
pkgver=2.3.1
pkgrel=1
pkgdesc="Complete and free implementation of the OpenPGP standard"
url="https://www.gnupg.org/"
arch=('any')
license=('custom')
makedepends=("p7zip")
provides=("${pkgbase}")
options=(!strip)
source=("gnupg-w32.exe::https://www.gnupg.org/ftp/gcrypt/binary/gnupg-w32-${pkgver}_${_realver}.exe")
sha512sums=('65ee5d05b6cb3b670369bb13f30a61355c959cb922c6fde87b52b5a2f2fcf02dcc5d2e0181aa60d369b180d93239822000996043ad04fd96af834029266908e4')

prepare() {
    7z x -y -bd "gnupg-w32.exe" -o"gnupg-w32.dir" > /dev/null
}

package() {
    local a="${pkgdir}/apps/${_realname}"
    mkdir -p "${a}"
    cp -R "gnupg-w32.dir"/* "${a}"
    rm -R "${a}"/'$PLUGINSDIR' "${a}/gnupg-uninstall.exe.nsis"
    find "${a}" -iname '*.tmp' -delete
}
