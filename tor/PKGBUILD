# Maintainer: David Macek <david.macek.0@gmail.com>

_realname=tor
_realver=6.0.6/0.2.8.9
_apparch=32
pkgbase="app-${_realname}"
pkgname=("app-i686-${_realname}")
pkgver=${_realver#*/}
pkgrel=1
pkgdesc="Anonymizing overlay network"
url="http://www.torproject.org/"
arch=('any')
license=('BSD')
makedepends=("unzip")
provides=("${pkgbase}")
options=(!strip)
source=("tor-win32.zip::https://dist.torproject.org/torbrowser/${_realver%/*}/tor-win32-${pkgver}.zip")
sha512sums=('606e4dc86913f1f66b413da0dbe6e2d9e3436c879c353a7fc58b01f781e6c0425063a85d956b5359d984c4c804424e4268410fdae023fe29aba33edcd5ce8a70')

_lc() {
    for f in "${@}"
    do
        n="$(echo "${f}" | tr 'A-Z' 'a-z')"
        if test "${f}" != "${n}"
        then
            mv "${f}" "${n}"
        fi
    done
}

prepare() {
    _lc "data"/* *
}

package() {
    a="${pkgdir}/apps${_apparch}/${_realname}"
    mkdir -p "${a}"
    cp -r "data" "tor" "${a}"
}
