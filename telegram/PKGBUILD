# Maintainer: David Macek <david.macek.0@gmail.com>

_realname=telegram
_choconame=telegram
pkgbase="app-${_realname}"
pkgname=("app-i686-${_realname}")
epoch=1
pkgver=4.2.4
pkgrel=1
pkgdesc="Desktop Telegram client"
url="https://desktop.telegram.org/"
arch=('any')
license=('GPL3')
provides=("${pkgbase}")
replaces=("app-x86_64-${_realname}")
options=(!strip)
source=("tportable.zip::https://github.com/telegramdesktop/tdesktop/releases/download/v${pkgver}/tportable.${pkgver}.zip")
sha512sums=('4fc198dc5cd1ecf05dbf6a05dbe475ba709b44bbbda93d84aacd55edb9c622059e488bb2c24581b59d28af41eb2b97176a7f778436c8ab680016e6256b839ff7')

_lc() {
    local d f n

    for f in "${@}"
    do
        d="$(dirname "${f}")"
        f="$(basename "${f}")"
        n="$(echo "${f}" | tr 'A-Z' 'a-z')"
        if test "${f}" != "${n}"
        then
            mv "${d}/${f}" "${d}/${n}"
        fi
    done
}

package() {
    local a="${pkgdir}/apps/${_realname}"
    mkdir -p "${a}"
    cp -R "Telegram"/* "${a}"
    _lc "${a}"/*
}
