# Maintainer: David Macek <david.macek.0@gmail.com>

DLAGENTS=('https::/usr/bin/curl -qb "" -fLC - -d id=609&server_id=6 -o %o %u')

_realname=gpu-z
_apparch=32
pkgbase="app-${_realname}"
pkgname=("app-i686-${_realname}")
pkgver=1.19.0
pkgrel=1
pkgdesc="Lightweight utility providing GPU and graphics card information"
arch=('any')
url="http://www.techpowerup.com/gpuz/"
license=('custom')
provides=("${pkgbase}")
options=(!strip)
source=("GPU-Z.exe::https://www.techpowerup.com/download/techpowerup-gpu-z/")
sha512sums=('ec050290e052409600a3e06763c03d956ede1846dca5ccfb4f708c867935cb1b328091ee6a6805acb8b193d8211d094b73bd82a168027a50f2ed6d8f616c95b8')

_lc() {
    local d f n

    for f in "${@}"
    do
        d="$(dirname "${f}")"
        f="$(basename "${f}")"
        n="$(echo "${f}" | tr 'A-Z' 'a-z')"
        if test "${f}" != "${n}"
        then
            mv "${d}/${f}" "${d}/${n}"
        fi
    done
}

pkgver() {
    powershell -command '(Get-Item "GPU-Z.exe").VersionInfo.ProductVersion' | sed -r 's/^([0-9]+[.][0-9]+[.][0-9]+)[.][0-9]+$/\1/'
}

prepare() {
    _lc *
}

package() {
    a="${pkgdir}/apps${_apparch}/${_realname}"
    mkdir -p "${a}"
    cp -r *.exe "${a}"
}
