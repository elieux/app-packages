# Maintainer: David Macek <david.macek.0@gmail.com>

# BROKEN:  The app wants to download its modules on startup, but that only kicks
# in if the program is inside a directory called app-${pkgver}.  Still, it
# modifies the program directory, which is not very nice.
# We can download them too.  Problem is, they are encoded or encrypted.
# 1. https://discord.com/api/updates/distributions/app/manifests/latest?channel=stable&platform=win&arch=x86
# 2. parse the output and get links to the modules like:
#    - https://dl.discordapp.net/distro/app/stable/win/x86/1.0.9002/discord_utils/1/full.distro
#    - https://dl.discordapp.net/distro/app/stable/win/x86/1.0.9002/discord_voice/9/full.distro
#    - https://dl.discordapp.net/distro/app/stable/win/x86/1.0.9002/discord_desktop_core/3/full.distro
#    - https://dl.discordapp.net/distro/app/stable/win/x86/1.0.9002/discord_erlpack/1/full.distro
#    - https://dl.discordapp.net/distro/app/stable/win/x86/1.0.9002/discord_spellcheck/1/full.distro
#    - https://dl.discordapp.net/distro/app/stable/win/x86/1.0.9002/discord_krisp/1/full.distro
# 3. ???
# 4. profit
# The main app (same as the contents of the installer probably) seems to be here:
# - https://dl.discordapp.net/distro/app/stable/win/x86/1.0.9002/full.distro

_realname=discord
_choconame=discord
pkgbase="app-${_realname}"
pkgname=("app-i686-${_realname}")
epoch=1
pkgver=1.0.9046
pkgrel=1
pkgdesc="All-in-one voice and text chat for gamers"
url="https://www.discordapp.com/"
arch=('any')
license=('custom')
makedepends=("p7zip" "unzip")
provides=("${pkgbase}")
options=(!strip)
source=("DiscordSetup.exe::https://dl.discordapp.net/distro/app/stable/win/x86/${pkgver}/DiscordSetup.exe")
sha512sums=('7fe885f69c0fa82e1795709a95e49eac2f9e7d45b94f44fcb3d38fa4ad0522ec668328e96bdd2182018585352339cd36dd9dd1ffc3ea52bb8562faf3b81dba9e')

_lc() {
    local d f n

    for f in "${@}"
    do
        d="$(dirname "${f}")"
        f="$(basename "${f}")"
        n="$(echo "${f}" | tr 'A-Z' 'a-z')"
        if test "${f}" != "${n}"
        then
            mv "${d}/${f}" "${d}/${n}"
        fi
    done
}

_rmmsdll() {
    local ms

    ms=("msvbvm[0-9]+"
        "ucrtbase"
        "mfcm?[0-9]+[a-z]+"
        "msvc[rp][0-9]+(_([0-9]|codecvt_ids|atomic_wait))?"
        "vc([ao]mp|corlib|runtime)[0-9]+(_([0-9]|threads))?"
        "concrt[0-9]+"
        "pgort[0-9]+"
        "api-ms-win-[0-9a-z-]+")
    find "${1}" -iname '*.dll' | grep -Eif <(for m in "${ms[@]}"; do echo "/${m}[.]dll$"; done) | xargs -r rm
}

prepare() {
    7z x -y -bd "DiscordSetup.exe" -o"DiscordSetup.dir" > /dev/null
    unzip -q -o "DiscordSetup.dir/Discord"-*-"full.nupkg" -d"Discord-full"
}

package() {
    local a="${pkgdir}/apps/${_realname}"
    mkdir -p "${a}"
    cp -R "Discord-full/lib/net45"/* "${a}"
    _rmmsdll "${a}"
    _lc "${a}"/*
}
