# Maintainer: David Macek <david.macek.0@gmail.com>

_realname=keepass-plugin-rpc
_realver=2015-10-25T16:50:14Z
_dirname=keepass
_apparch=32
pkgbase="app-${_realname}"
pkgname=("app-i686-${_realname}")
pkgver=1.5.4
pkgrel=1
pkgdesc="A KeePass plugin that provides a RPC server for KeeFox"
url="http://keefox.org/"
arch=('any')
license=('unknown')
depends=("app-i686-keepass")
provides=("${pkgbase}")
options=(!strip)
source=("keefox-tb+fx-windows.xpi::https://addons.mozilla.org/firefox/downloads/file/374447/keefox-${pkgver}-tb+fx-windows.xpi")
sha512sums=('2d715b4519f1162294c3d27ade70adfacf116db41dc83642cb4c52439cb86a40393a778f4c984ae83a10e308a1fb6be99b3de04a4cb47d728a2831f614c43fd0')

pkgver() {
    v="$(dd if="deps/KeePassRPC.plgx" bs=1 skip=56 count=20 status=none)"
    if [ "${v}" == "${_realver}" ]
    then
        echo "${pkgver}"
    else
        warning "Timestamp has changed. Fix version number."
    fi
}

package() {
    a="${pkgdir}/apps${_apparch}/${_dirname}"
    mkdir -p "${a}/plugins"
    cp "deps"/*.plgx "${a}/plugins"
}
