# Maintainer: David Macek <david.macek.0@gmail.com>

_realname=aws-cli
_choconame=awscli
pkgbase="app-${_realname}"
pkgname=("app-x86_64-${_realname}")
epoch=1
pkgver=2.15.36
pkgrel=1
pkgdesc="Universal Command Line Interface for Amazon Web Services"
url="https://aws.amazon.com/cli/"
arch=('any')
license=('Apache')
makedepends=("app-lessmsi")
provides=("${pkgbase}")
options=(!strip)
source=("AWSCLIV2.msi::https://awscli.amazonaws.com/AWSCLIV2.msi")
sha512sums=('ac08a3069d422aa74e50930e34c777e1267122dc860b779a2cfee7af81c316ca7b70a7de6bef994c93d4d0b38a59bfb0058088bff1d9e7823058d40966d0cdb8')

_rmmsdll() {
    local ms

    ms=("msvbvm[0-9]+"
        "ucrtbase"
        "mfcm?[0-9]+[a-z]+"
        "msvc[rp][0-9]+(_([0-9]|codecvt_ids|atomic_wait))?"
        "vc([ao]mp|corlib|runtime)[0-9]+(_([0-9]|threads))?"
        "concrt[0-9]+"
        "pgort[0-9]+"
        "api-ms-win-[0-9a-z-]+")
    find "${1}" -iname '*.dll' | grep -Eif <(for m in "${ms[@]}"; do echo "/${m}[.]dll$"; done) | xargs -r rm
}

prepare() {
    rm -fR "AWSCLIV2"
    /apps/lessmsi/lessmsi x "AWSCLIV2.msi" > /dev/null
}

pkgver() {
    /apps/lessmsi/lessmsi v "AWSCLIV2.msi" | sed -r 's/[.]0$//'
}

package() {
    local a="${pkgdir}/apps/${_realname}"
    mkdir -p "${a}"
    cp -R "AWSCLIV2/SourceDir/Amazon/AWSCLIV2"/* "${a}"
    _rmmsdll "${a}"
}
