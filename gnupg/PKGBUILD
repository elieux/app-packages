# Maintainer: David Macek <david.macek.0@gmail.com>

_realname=gnupg
_realver=2.2.21_20200709
pkgbase="app-${_realname}"
pkgname=("app-i686-${_realname}")
epoch=1
pkgver=${_realver%%_*}
pkgrel=1
pkgdesc="Complete and free implementation of the OpenPGP standard"
url="https://www.gnupg.org/"
arch=('any')
license=('custom')
makedepends=("p7zip")
provides=("${pkgbase}")
options=(!strip)
source=("gnupg-w32.exe::https://www.gnupg.org/ftp/gcrypt/binary/gnupg-w32-${_realver}.exe")
sha512sums=('fa6dbf8993e96ad46866eb0cdf56528fb477cd521fb776a436ec46d34b76b542e3abdb765ab63d92562d1ae991d7d372c869b4bc28930d8ef8d057fa75023089')

prepare() {
    7z x -y -bd "gnupg-w32.exe" -o"gnupg-w32.dir" > /dev/null
}

package() {
    local a="${pkgdir}/apps/${_realname}"
    mkdir -p "${a}"
    cp -R "gnupg-w32.dir"/* "${a}"
    rm -R "${a}"/'$PLUGINSDIR'
    find "${a}" -iname '*.tmp' -delete
}
