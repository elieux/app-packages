# Maintainer: David Macek <david.macek.0@gmail.com>

_realname=conemu
pkgbase="app-${_realname}"
pkgname=("app-i686-${_realname}"
         "app-x86_64-${_realname}")
pkgver=17.06.05
pkgrel=1
pkgdesc="A powerful, highly customizable console emulator with tabs"
url="http://conemu.github.io/"
arch=('any')
license=('BSD')
makedepends=("p7zip")
provides=("${pkgbase}")
options=(!strip)
source=("ConEmuPack.7z::https://github.com/Maximus5/ConEmu/releases/download/v${pkgver}/ConEmuPack.${pkgver//./}.7z")
sha512sums=('ea71d07d5642d174ebcf1c5d6eb268ac2cc47b1eae0bb97a4e9a10d95bca3b11eb44800b8c57c7d901c9b3c9a149a26dc55753c84cda74f3e6cdc717c3a3d884')

_lc() {
    local d f n

    for f in "${@}"
    do
        d="$(dirname "${f}")"
        f="$(basename "${f}")"
        n="$(echo "${f}" | tr 'A-Z' 'a-z')"
        if test "${f}" != "${n}"
        then
            mv "${d}/${f}" "${d}/${n}"
        fi
    done
}

prepare() {
    _lc *
}

_package() {
    a="${pkgdir}/apps${_apparch}/${_realname}"
    mkdir -p "${a}"
    cp -r "conemu${1}.exe" "${a}/conemu.exe"
    cp -r "conemu" "plugins" "${a}"

    d="${pkgdir}/docs${_apparch}/${_realname}"
    mkdir -p "${d}"
    mv "${a}/conemu"/*.txt "${d}"
}

package_app-i686-conemu() {
    _apparch=32
    _package ""
}

package_app-x86_64-conemu() {
    _apparch=64
    _package "64"
}
