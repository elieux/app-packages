# Maintainer: David Macek <david.macek.0@gmail.com>

_realname=vivaldi
pkgbase="app-${_realname}"
pkgname=("app-i686-${_realname}"
         "app-x86_64-${_realname}")
pkgver=1.12.955.38
pkgrel=1
pkgdesc="A new browser for our friends"
url="https://vivaldi.com/"
arch=('any')
license=('custom')
makedepends=("p7zip")
provides=("${pkgbase}")
options=(!strip)
source=("Vivaldi.x32.exe::https://downloads.vivaldi.com/stable/Vivaldi.${pkgver}.exe"
        "Vivaldi.x64.exe::https://downloads.vivaldi.com/stable/Vivaldi.${pkgver}.x64.exe")
sha512sums=('408f59724f741a5f3875897c8a81ceeca0d1974882c0f48c8a2c2e9a06549ac9f3654ba69d0394ef4f16c2d01dcd8199587ed644de7519d84025780bc4ecd6dd'
            '5ca2527d2ca04acb8048feb29e9bd366d565ae8c7a75768d271d4e0b10f6bf3bfba84e4113b149f0e75a4370c86963155c8c0a8067839e47a70b79c44aeea3d3')

_lc() {
    local d f n

    for f in "${@}"
    do
        d="$(dirname "${f}")"
        f="$(basename "${f}")"
        n="$(echo "${f}" | tr 'A-Z' 'a-z')"
        if test "${f}" != "${n}"
        then
            mv "${d}/${f}" "${d}/${n}"
        fi
    done
}

_prepare() {
    7z x -y -bd "Vivaldi.x${_apparch}.exe"
    7z x -y -bd "vivaldi.7z"
    rm "vivaldi.7z"
    _lc "Vivaldi-bin"/* "Vivaldi-bin/${pkgver}"/*
    mv "Vivaldi-bin" "vivaldi-bin.x${_apparch}"
}

prepare() {
    for _apparch in 32 64
    do
        _prepare
    done
}

_package() {
    a="${pkgdir}/apps${_apparch}/${_realname}"
    mkdir -p "${a}"
    cp -r "vivaldi-bin.x${_apparch}"/*.exe "vivaldi-bin.x${_apparch}/${pkgver}"/* "${a}"
}

package_app-i686-vivaldi() {
    _apparch=32
    _package
}

package_app-x86_64-vivaldi() {
    _apparch=64
    _package
}

