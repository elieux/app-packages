# Maintainer: David Macek <david.macek.0@gmail.com>

_realname=gitter
pkgbase="app-${_realname}"
pkgname=("app-i686-${_realname}")
epoch=1
pkgver=5.0.0
pkgrel=1
pkgdesc="Client for the Gitter communication platform"
url="https://gitter.im/"
arch=('any')
license=('custom')
makedepends=("app-innounp")
provides=("${pkgbase}")
options=(!strip)
source=("GitterSetup.exe::https://update.gitter.im/win/GitterSetup-${pkgver}.exe")
sha512sums=('4d17a1edf9da0d0393795205c8fb1d510b026ad2c9aed0d1672cc2d2267e560816a18fefd05be004828d2b2806093aa39085dd255d322717042d777fe03bcc1f')

_lc() {
    local d f n

    for f in "${@}"
    do
        d="$(dirname "${f}")"
        f="$(basename "${f}")"
        n="$(echo "${f}" | tr 'A-Z' 'a-z')"
        if test "${f}" != "${n}"
        then
            mv "${d}/${f}" "${d}/${n}"
        fi
    done
}

prepare() {
    /apps/innounp/innounp -x -q -m -a -y "GitterSetup.exe" -d"GitterSetup.dir"
}

package() {
    local a="${pkgdir}/apps/${_realname}"
    mkdir -p "${a}"
    cp -R "GitterSetup.dir/{app}"/* "${a}"
    local f
    for f in "${a}"/Gitter*.exe
    do
        mv -f "${f}" "${a}/Gitter.exe"
    done
    _lc "${a}"/*
}
