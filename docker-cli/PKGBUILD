# Maintainer: David Macek <david.macek.0@gmail.com>

_realname=docker-cli
_realver=61853
_choconame=docker-cli
_dirname=docker
pkgbase="app-${_realname}"
pkgname=("app-x86_64-${_realname}")
epoch=1
pkgver=20.10.5
pkgrel=1
pkgdesc="Pack, ship and run any application as a lightweight container"
url="https://docs.docker.com/engine/reference/commandline/cli/"
arch=('any')
license=('Apache')
makedepends=("p7zip")
provides=("${pkgbase}")
replaces=("app-x86_64-docker")
conflicts=("app-x86_64-docker")
options=(!strip)
source=("DockerDesktopInstaller.exe::https://desktop.docker.com/win/stable/amd64/${_realver}/Docker%20Desktop%20Installer.exe")
sha512sums=('2c205207e2706fee4032cc4cfdfc76ea483806ff368cc7cb542d64d56900627e58438ed0620a296a2ac26f1ccefcb39c1b82ec0291e5b028f5af48f6c6df5916')

prepare() {
    7z x -y -bd "DockerDesktopInstaller.exe" -o"DockerDesktopInstaller.dir" > /dev/null
}

pkgver() {
    powershell -command '(Get-Item "DockerDesktopInstaller.dir/resources/bin/com.docker.cli.exe").VersionInfo.ProductVersion'
}

package() {
    local a="${pkgdir}/apps/${_dirname}"
    mkdir -p "${a}"
    cp "DockerDesktopInstaller.dir/resources/bin/com.docker.cli.exe" "${a}/docker.exe"
}
