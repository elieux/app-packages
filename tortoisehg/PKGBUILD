# Maintainer: David Macek <david.macek.0@gmail.com>

_realname=tortoisehg
_choconame=tortoisehg
pkgbase="app-${_realname}"
pkgname=("app-x86_64-${_realname}")
epoch=1
pkgver=6.1.0
pkgrel=1
pkgdesc="Mercurial client implemented as a Windows shell extension"
url="https://www.mercurial-scm.org/wiki/TortoiseHg"
arch=('any')
license=('GPL2')
makedepends=("app-lessmsi")
provides=("${pkgbase}")
options=(!strip)
source=("tortoisehg-x64.msi::https://www.mercurial-scm.org/release/tortoisehg/windows/tortoisehg-${pkgver}-x64.msi")
sha512sums=('8df58e590eb43c0202318c8e2f55d99b440db91307b147e1ab031878dbaf4ed93991a59aefa7191df439aa65135d67d8931930304c758180f06a91e1c14b7a06')

_lc() {
    local d f n

    for f in "${@}"
    do
        d="$(dirname "${f}")"
        f="$(basename "${f}")"
        n="$(echo "${f}" | tr 'A-Z' 'a-z')"
        if test "${f}" != "${n}"
        then
            mv "${d}/${f}" "${d}/${n}"
        fi
    done
}

prepare() {
    rm -fR "tortoisehg-x64"
    /apps/lessmsi/lessmsi x "tortoisehg-x64.msi" > /dev/null
    _lc "tortoisehg-x64/SourceDir/PFiles/TortoiseHg"/*
    _lc "tortoisehg-x64/SourceDir/Programsx86/TortoiseHg"/*
}

package() {
    depends=("app-tortoise-overlays")

    local a="${pkgdir}/apps/${_realname}"
    mkdir -p "${a}"
    cp -R "tortoisehg-x64/SourceDir/PFiles/TortoiseHg"/* "${a}"
    cp -R "tortoisehg-x64/SourceDir/Programsx86/TortoiseHg"/* "${a}"
}
