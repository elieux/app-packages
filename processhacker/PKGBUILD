# Maintainer: David Macek <david.macek.0@gmail.com>

_realname=processhacker
pkgbase="app-${_realname}"
pkgname=("app-i686-${_realname}"
         "app-x86_64-${_realname}")
pkgver=2.39
pkgrel=1
pkgdesc="Views and manages processes, services and more"
url="http://processhacker.sourceforge.net/"
arch=('any')
license=('GPL2' 'GPL3')
makedepends=("unzip")
provides=("${pkgbase}")
options=(!strip)
source=("processhacker-bin.zip::http://downloads.sourceforge.net/${_realname}/${_realname}2/${_realname}-${pkgver}-bin.zip")
sha512sums=('915b9f7c0b1374ce52fa9653ba1084741d15ff79dbb7c04d2a0f41eea8262b2f556d451bf9eefbd2d32831289908b6a1b39ce2cbcafbbfc4ae6e71d701b1aa81')

_lc() {
    local d f n

    for f in "${@}"
    do
        d="$(dirname "${f}")"
        f="$(basename "${f}")"
        n="$(echo "${f}" | tr 'A-Z' 'a-z')"
        if test "${f}" != "${n}"
        then
            mv "${d}/${f}" "${d}/${n}"
        fi
    done
}

prepare() {
    _lc "x86"/* "x64"/* *
}

_package() {
    a="${pkgdir}/apps${_apparch}/${_realname}"
    mkdir -p "${a}"
    cp -r "${1}"/* "${a}"

    d="${pkgdir}/docs${_apparch}/${_realname}"
    mkdir -p "${d}"
    cp -r *.txt "${d}"
}

package_app-i686-processhacker() {
    _apparch=32
    _package "x86"
}

package_app-x86_64-processhacker() {
    _apparch=64
    _package "x64"
}
