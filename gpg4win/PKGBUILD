# Maintainer: David Macek <david.macek.0@gmail.com>

_realname=gpg4win
_apparch=32
pkgbase="app-${_realname}"
pkgname=("app-i686-${_realname}")
pkgver=3.0.0
pkgrel=1
pkgdesc="A secure solution for file and email encryption"
url="http://www.gpg4win.org/index.html"
arch=('any')
license=('custom')
makedepends=("p7zip")
provides=("${pkgbase}")
options=(!strip)
source=("gpg4win.exe::https://files.gpg4win.org/gpg4win-${pkgver}.exe")
sha512sums=('46f00ea8adbef4d52d7a51c70f5f472bbe4921036102cb6733df8f5c8487c691b301a42b181637c16ae47cd8632fd5101841cabcfd5f71f29209ebb0cdc205cd')

_lc() {
    local d f n

    for f in "${@}"
    do
        d="$(dirname "${f}")"
        f="$(basename "${f}")"
        n="$(echo "${f}" | tr 'A-Z' 'a-z')"
        if test "${f}" != "${n}"
        then
            mv "${d}/${f}" "${d}/${n}"
        fi
    done
}

prepare() {
    7z x -y -bd "gpg4win.exe"
    _lc *
}

package() {
    a="${pkgdir}/apps${_apparch}/${_realname}"
    mkdir -p "${a}"
    cp -r * "${a}"
    find "${a}" -iname '*.tmp' -delete
    rm -r "${a}/gpg4win.exe" "${a}"/'$'*
    rm "${a}/bin/pinentry.exe"
    cp "${a}/bin/pinentry-w32.exe" "${a}/bin/pinentry.exe"

    d="${pkgdir}/docs${_apparch}/${_realname}"
    mkdir -p "${d}" "${d}/gnupg"
    mv "${a}/share/doc"/* "${d}"
    rm -r "${a}/share/doc"
    mv "${a}/share/gnupg"/*.man "${d}/gnupg"
    mv "${a}/share/gpg4win" "${d}/gpg4win"
}
