# Maintainer: David Macek <david.macek.0@gmail.com>

_realname=spidermonkey
_realver=69.0.3
pkgbase="app-${_realname}"
pkgname=("app-x86_64-${_realname}")
epoch=1
pkgver=69.0.3
pkgrel=1
pkgdesc="An interactive JavaScript shell from the SpiderMonkey distribution"
url="https://developer.mozilla.org/en-US/docs/Mozilla/Projects/SpiderMonkey/Introduction_to_the_JavaScript_shell"
arch=('any')
license=('MPL')
makedepends=("unzip")
provides=("${pkgbase}")
options=(!strip)
source=("jsshell-win64.zip::https://ftp.mozilla.org/pub/firefox/releases/${_realver}/jsshell/jsshell-win64.zip")
sha512sums=('77ce83c0e0daddc0ae6bd825e395ed2a880d06b6764d9c3c18a846d30151b4854e74352c0a94248a8dd4fb623762d2f224aaf0ad68a53976b02ed59ddf68dd85')

_rmmsdll() {
    local ms

    ms=("msvbvm[0-9]+"
        "ucrtbase"
        "mfcm?[0-9]+[a-z]+"
        "msvc[rp][0-9]+"
        "vc([ao]mp|corlib|runtime)[0-9]+"
        "concrt[0-9]+"
        "pgort[0-9]+"
        "api-ms-win-[0-9a-z-]+")
    find "${1}" -iname '*.dll' | grep -Eif <(for m in "${ms[@]}"; do echo "/${m}[.]dll$"; done) | xargs -r rm
}

pkgver() {
    strings js.exe | grep 'JavaScript-C' | sed 's/JavaScript-C//'
}

package() {
    a="${pkgdir}/apps/${_realname}"
    mkdir -p "${a}"
    cp -r * "${a}"
    rm "${a}/jsshell-win64.zip"
    _rmmsdll "${a}"
}
