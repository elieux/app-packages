# Maintainer: David Macek <david.macek.0@gmail.com>

_realname=discord
_apparch=32
pkgbase="app-${_realname}"
pkgname=("app-i686-${_realname}")
pkgver=0.0.296
pkgrel=1
pkgdesc="All-in-one voice and text chat for gamers"
url="https://www.discordapp.com/"
arch=('any')
license=('custom')
makedepends=("p7zip" "unzip")
provides=("${pkgbase}")
options=(!strip)
source=("DiscordSetup.exe::https://discordapp.com/api/download?platform=win")
sha512sums=('25c35ca8279070f7a02621999e80ab350a42f284dadb525ca2580a58d6d9b6af276fea8c0cd34e226e12482cb48fed0ffb324b57598ad076942f735cfe2703c9')

_lc() {
    local d f n

    for f in "${@}"
    do
        d="$(dirname "${f}")"
        f="$(basename "${f}")"
        n="$(echo "${f}" | tr 'A-Z' 'a-z')"
        if test "${f}" != "${n}"
        then
            mv "${d}/${f}" "${d}/${n}"
        fi
    done
}

pkgver() {
    b="$(cygpath -wa "DiscordSetup.exe" | sed 's/\\/\\\\/g')"
    powershell -command '(Get-Item "DiscordSetup.exe").VersionInfo.ProductVersion'
}

prepare() {
    rm -f "Discord"-*-"full.nupkg"
    7z x -y -bd "DiscordSetup.exe"
    unzip -o "Discord"-*-"full.nupkg"
    _lc "lib/net45"/*
}

package() {
    a="${pkgdir}/apps${_apparch}/${_realname}"
    mkdir -p "${a}"
    cp -r "lib/net45"/* "${a}"
}
