# Maintainer: David Macek <david.macek.0@gmail.com>

_realname=chromium
_realver=419136
pkgbase="app-${_realname}"
pkgname=("app-i686-${_realname}"
         "app-x86_64-${_realname}")
pkgver=55.0.2863.0
pkgrel=1
pkgdesc="The open-source project behind Google Chrome, an attempt at creating a safer, faster, and more stable browser"
url="http://www.chromium.org/Home"
arch=('any')
license=('BSD')
makedepends=("unzip")
provides=("${pkgbase}")
options=(!strip)
source=("chrome-win32.zip::https://storage.googleapis.com/chromium-browser-snapshots/Win/${_realver}/chrome-win32.zip"
        "chrome-win64.zip::https://storage.googleapis.com/chromium-browser-snapshots/Win_x64/${_realver}/chrome-win32.zip")
sha512sums=('e506beea81d34049bc1f44f5a21ae6fde59278d437ffbdb90ec1d4348eb5d32a7540b7d74d3c4b03524dd6c6c7ded5b7f062717ef3c2e52e2097ab525fbe2627'
            '356d3668b334b7b0fc97fa8e9ba791096e220d0fc755cd0f7782ad3f8d277042adcd58dc3462ff0ee44a6f1afe0da32aa670a7e4199c9fbb68ec234493ae6dbe')
noextract=("chrome-win32.zip"
           "chrome-win64.zip")

pkgver() {
    for _apparch in 32 64
    do
        eval _ver${_apparch}="$(unzip -l "chrome-win${_apparch}.zip" | tail -n +4 | head -n -2 | sed -r 's/[ ]+/\t/g' | cut -f 5 | sed 's~^chrome-win32/~~' | sed 's~.manifest$~~' | grep -P '[0-9]+[.][0-9]+[.][0-9]+[.][0-9]+' | sort | tail -1)"
    done
    if [ "${_ver32}" != "${_ver64}" ]
    then
        echo "32-bit version number is different from 64-bit."
    else
        echo "${_ver32}"
    fi
}

prepare() {
    for _apparch in 32 64
    do
        unzip -o "chrome-win${_apparch}.zip" -d "chromium${_apparch}"
    done
}

_package() {
    a="${pkgdir}/apps${_apparch}/${_realname}"
    mkdir -p "${a}"
    cp -r "chromium${_apparch}/chrome-win32"/* "${a}"
    rm "${a}"/*.manifest "${a}/interactive_ui_tests.exe"
    cp "chromium${_apparch}/chrome-win32/${pkgver}.manifest" "${a}"
}

package_app-i686-chromium() {
    _apparch=32
    _package
}

package_app-x86_64-chromium() {
    _apparch=64
    _package
}
