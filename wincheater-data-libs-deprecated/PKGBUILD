# Maintainer: David Macek <david.macek.0@gmail.com>

_realname=wincheater-data-libs-deprecated
_dirname=wincheater
pkgbase="app-${_realname}"
pkgname=("app-i686-${_realname}")
epoch=1
pkgver=2006.11.02
pkgrel=1
pkgdesc="Libraries for WinCheater (deprecated)"
arch=('any')
url="https://web.archive.org/web/20150610153444/http://www.cheater.cz/winch/win_download_knihovny.htm"
license=('custom')
makedepends=("unrar")
provides=("${pkgbase}")
options=(!strip)
source=("cestiny.exe::https://web.archive.org/web/20070204024110if_/http://www.cheater.cz:80/download/cestiny.exe"
        "internet.exe::https://web.archive.org/web/20070205223922if_/http://www.cheater.cz:80/download/internet.exe")
sha512sums=('b41cb59c549f3bb7fd86bda936efa1dd1fbe03b4d61fa9d43696f8f8ec10faa0dd721f6c7e4c8f1c9fad257b81fee866b9425a1d5320c0b96e8dca96aa04aacb'
            'bd9998c48b6fe0a986361a18468d47be46c02cd0b2cf30b06bd593977b21dbf362615dadf631238fa7df537efa1eb6bcf328533e842ecbfede6a658fe0d12aea')
noextract=("cestiny.exe"
           "internet.exe")

_lc() {
    local d f n

    for f in "${@}"
    do
        d="$(dirname "${f}")"
        f="$(basename "${f}")"
        n="$(echo "${f}" | tr 'A-Z' 'a-z')"
        if test "${f}" != "${n}"
        then
            mv "${d}/${f}" "${d}/${n}"
        fi
    done
}

prepare() {
    local f
    for f in *.exe
    do
        unrar x -idq -tsm -o+ "${f}"
    done
    _lc *
}

pkgver() {
    find . -name '*.sc2' -printf '%TY.%Tm.%Td\n' | sort | tail -1
}

package() {
    depends=("app-i686-wincheater")

    local a="${pkgdir}/apps/${_dirname}"
    mkdir -p "${a}"
    cp *.sc2 "${a}"
}
