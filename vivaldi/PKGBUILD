# Maintainer: David Macek <david.macek.0@gmail.com>

_realname=vivaldi
pkgbase="app-${_realname}"
pkgname=("app-x86_64-${_realname}")
epoch=1
pkgver=3.0.1874.38
pkgrel=1
pkgdesc="A new browser for our friends"
url="https://vivaldi.com/"
arch=('any')
license=('custom')
replaces=("app-i686-${_realname}")
makedepends=("p7zip")
provides=("${pkgbase}")
options=(!strip)
source=("Vivaldi.x64.exe::https://downloads.vivaldi.com/stable/Vivaldi.${pkgver}.x64.exe")
sha512sums=('4cacfc3c1936d6b1211a401df72d4ea68fd64ddfb58885769f1d53620d2251ab2fa1f1dd254a25425d3911f35712dba1a6e80d4d5dbafe737c8f5c7e6c31795d')
noextract=("Vivaldi.x64.exe") # bsdtar detects and extracts a setup.exe

_lc() {
    local d f n

    for f in "${@}"
    do
        d="$(dirname "${f}")"
        f="$(basename "${f}")"
        n="$(echo "${f}" | tr 'A-Z' 'a-z')"
        if test "${f}" != "${n}"
        then
            mv "${d}/${f}" "${d}/${n}"
        fi
    done
}

prepare() {
    7z x -y -bd "Vivaldi.x64.exe" -o"Vivaldi.x64.dir" > /dev/null
    7z x -y -bd "Vivaldi.x64.dir/vivaldi.7z" > /dev/null
}

package() {
    local a="${pkgdir}/apps/${_realname}"
    mkdir -p "${a}"
    cp -R "Vivaldi-bin"/*.exe "Vivaldi-bin/${pkgver}"/* "${a}"
    _lc "${a}"/*
}
