# Maintainer: David Macek <david.macek.0@gmail.com>

_realname=bind
_realver=9.10.4-P4
pkgbase="app-${_realname}"
pkgname=("app-i686-${_realname}"
         "app-x86_64-${_realname}")
pkgver=${_realver//-/.}
pkgrel=1
pkgdesc="The ISC BIND nameserver"
url="https://www.isc.org/downloads/bind/"
arch=('any')
license=('custom:ISC')
makedepends=("unzip")
provides=("${pkgbase}")
options=(!strip)
source=("BIND32.zip::ftp://ftp.isc.org/isc/bind9/${_realver}/BIND${_realver}.x86.zip"
        "BIND64.zip::ftp://ftp.isc.org/isc/bind9/${_realver}/BIND${_realver}.x64.zip")
sha512sums=('fde76e5f952800747933f8fef09be95e93d36672506bb866275502a9769677436519aef6e2748086ebf7d4eed46556ecb7fbb903986fa7c33d272546d00a32e1'
            '287141fdc796af9f305444ffa7656147984e2d147d993e839ce25144433b6f2771e6e249e4f14bf19027658cc4e70791f7d284d8dacafae6bbb8375dbf002d0e')
noextract=("BIND32.zip"
           "BIND64.zip")

_lc() {
    for f in "${@}"
    do
        n="$(echo "${f}" | tr 'A-Z' 'a-z')"
        if test "${f}" != "${n}"
        then
            mv "${f}" "${n}"
        fi
    done
}

prepare() {
    for _apparch in 32 64
    do
        unzip -o "BIND${_apparch}.zip" -d "bind${_apparch}"
        _lc "bind${_apparch}"/*
    done
}

_package() {
    a="${pkgdir}/apps${_apparch}/${_realname}"
    mkdir -p "${a}"
    cp "bind${_apparch}"/*.{exe,dll} "${a}"
    rm "${a}"/vcredist*.exe "${a}/bindinstall.exe"

    d="${pkgdir}/docs${_apparch}/${_realname}"
    mkdir -p "${d}"
    cp "bind${_apparch}"/*.{html,pdf,txt} "bind${_apparch}"/{changes,copyright,faq,history,migration,migration-4to9,openssl-license,readme,sha256} "${d}"
}

package_app-i686-bind() {
    _apparch=32
    _package
}

package_app-x86_64-bind() {
    _apparch=64
    _package
}
