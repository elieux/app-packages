# Maintainer: David Macek <david.macek.0@gmail.com>

_realname=shotcut
_choconame=Shotcut
pkgbase="app-${_realname}"
pkgname=("app-x86_64-${_realname}")
epoch=1
pkgver=22.01.17
pkgrel=1
pkgdesc="Cross-platform, open-source video editor"
url="https://shotcut.org/"
arch=('any')
license=('GPL3')
provides=("${pkgbase}")
options=(!strip)
source=("shotcut-win64.zip::https://github.com/mltframework/shotcut/releases/download/v${pkgver}/shotcut-win64-${pkgver//./}.zip")
sha512sums=('ee53e5b3642b3630d9ffc42215d4eb9c473f807b28f184b17c87ce2caf6fe64956edfa8b351b882c45db6efeb0c5b288a50c412a133df8f42d225fb554703197')

_lc() {
    local d f n

    for f in "${@}"
    do
        d="$(dirname "${f}")"
        f="$(basename "${f}")"
        n="$(echo "${f}" | tr 'A-Z' 'a-z')"
        if test "${f}" != "${n}"
        then
            mv "${d}/${f}" "${d}/${n}"
        fi
    done
}

package() {
    local a="${pkgdir}/apps/${_realname}"
    mkdir -p "${a}"
    cp -R "Shotcut"/* "${a}"
    _lc "${a}"/*
}
