# Maintainer: David Macek <david.macek.0@gmail.com>

_realname=gpg4win
pkgbase="app-${_realname}"
pkgname=("app-i686-${_realname}")
epoch=1
pkgver=3.1.12
pkgrel=1
pkgdesc="A secure solution for file and email encryption"
url="https://www.gpg4win.org/index.html"
arch=('any')
license=('custom')
makedepends=("p7zip")
provides=("${pkgbase}")
options=(!strip)
source=("gpg4win.exe::https://files.gpg4win.org/gpg4win-${pkgver}.exe")
sha512sums=('3ce6abb0a47845d0423cf34214bd6476364fab9fc1890b48e87e64c0fc0038014d8578ce6331cc3b6550e6ce5747fb74bc9f7e3d8d9189096d8560bcf3142999')

_lc() {
    local d f n

    for f in "${@}"
    do
        d="$(dirname "${f}")"
        f="$(basename "${f}")"
        n="$(echo "${f}" | tr 'A-Z' 'a-z')"
        if test "${f}" != "${n}"
        then
            mv "${d}/${f}" "${d}/${n}"
        fi
    done
}

prepare() {
    7z x -y -bd "gpg4win.exe" -o"gpg4win.dir" > /dev/null
}

package() {
    local a="${pkgdir}/apps/${_realname}"
    mkdir -p "${a}"
    cp -R "gpg4win.dir"/* "${a}"
    find "${a}" -iname '*.tmp' -delete
    rm -R "${a}"/'$PLUGINSDIR' "${a}"/'$TEMP'
    rm "${a}/bin/pinentry.exe"
    cp "${a}/bin/pinentry-w32.exe" "${a}/bin/pinentry.exe"
    _lc "${a}"/*
}
