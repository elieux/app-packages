# Maintainer: David Macek <david.macek.0@gmail.com>

_realname=mariadb
pkgbase="app-${_realname}"
pkgname=("app-x86_64-${_realname}")
epoch=1
pkgver=10.4.13
pkgrel=1
pkgdesc="Fast SQL database server, drop-in replacement for MySQL"
url="https://mariadb.org/"
arch=('any')
license=('GPL')
replaces=("app-i686-${_realname}")
provides=("${pkgbase}")
options=(!strip)
source=("mariadb-winx64.zip::https://downloads.mariadb.org/f/mariadb-${pkgver}/winx64-packages/mariadb-${pkgver}-winx64.zip")
sha512sums=('9caad9f397d6ef0bde65b2ccafef0c22d72dcf419a7656dec95dec6fa2ebeeca8046206934f8b5698e25ac4847c17480ec1dfce7f4c94a5d01e8da6148c9c6fc')

_rmmsdll() {
    local ms

    ms=("msvbvm[0-9]+"
        "ucrtbase"
        "mfcm?[0-9]+[a-z]+"
        "msvc[rp][0-9]+(_([0-9]|codecvt_ids))?"
        "vc([ao]mp|corlib|runtime)[0-9]+(_[0-9])?"
        "concrt[0-9]+"
        "pgort[0-9]+"
        "api-ms-win-[0-9a-z-]+")
    find "${1}" -iname '*.dll' | grep -Eif <(for m in "${ms[@]}"; do echo "/${m}[.]dll$"; done) | xargs -r rm
}

package() {
    local a="${pkgdir}/apps/${_realname}"
    mkdir -p "${a}"
    cp -R "mariadb-${pkgver}-winx64"/* "${a}"
    rm -R "${a}/data"
    find "${a}" -name 'debug' -exec rm -r '{}' \; -prune
    find "${a}" -name '*.pdb' -delete
    find "${a}" -name '*.lib' -delete
    _rmmsdll "${a}"
}
