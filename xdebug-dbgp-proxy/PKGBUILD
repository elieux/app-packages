# Maintainer: David Macek <david.macek.0@gmail.com>

_dirname=xdebug-dbgp-tools
_realname=xdebug-dbgp-proxy
pkgbase="app-${_realname}"
pkgname=("app-x86_64-${_realname}")
epoch=1
pkgver=0.6.1 # determined by running the .exe
pkgrel=1
pkgdesc="DBGp proxy from Xdebug"
url="https://xdebug.org/docs/dbgpProxy"
arch=('any')
license=('unknown')
conflicts=("xdebug-dbgp-tools")
provides=("${pkgbase}")
options=(!strip)
source=("dbgpProxy.exe::https://xdebug.org/files/binaries/dbgpProxy.exe")
sha512sums=('2d8331d8f7b4abd7926a0e07d43d7dc286df5ffdc5bcb3af976e2106218086bddec2e05a6e5d65941126afc3443c5e2095113ca155b45fff05797eae20cb102b')

_lc() {
    local d f n

    for f in "${@}"
    do
        d="$(dirname "${f}")"
        f="$(basename "${f}")"
        n="$(echo "${f}" | tr 'A-Z' 'a-z')"
        if test "${f}" != "${n}"
        then
            mv "${d}/${f}" "${d}/${n}"
        fi
    done
}

package() {
    local a="${pkgdir}/apps/${_dirname}"
    mkdir -p "${a}"
    cp "dbgpProxy.exe" "${a}"
    _lc "${a}"/*
}
