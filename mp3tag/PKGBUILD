# Maintainer: David Macek <david.macek.0@gmail.com>

_realname=mp3tag
_apparch=32
pkgbase="app-${_realname}"
pkgname=("app-i686-${_realname}")
pkgver=2.85
pkgrel=1
pkgdesc="Powerful audio metadata editor"
arch=('any')
url="http://mp3tag.de/en/index.html"
license=('custom')
makedepends=("p7zip")
provides=("${pkgbase}")
options=(!strip)
source=("mp3tagsetup.exe::http://download.mp3tag.de/mp3tagv${pkgver//./}setup.exe")
sha512sums=('187c7e51e372ac07ec0f20afb141e1cb3f93a501612bdd2bcbb1fb3144dc2a6d292f74181a86feadc97b2488ca45d8fa4a62d156c621a961df1474a6cdb88a7f')

_lc() {
    local d f n

    for f in "${@}"
    do
        d="$(dirname "${f}")"
        f="$(basename "${f}")"
        n="$(echo "${f}" | tr 'A-Z' 'a-z')"
        if test "${f}" != "${n}"
        then
            mv "${d}/${f}" "${d}/${n}"
        fi
    done
}

prepare() {
    w="*license*.txt"
    7z x -y -bd -x!"${w}" "mp3tagsetup.exe"
    for f in $(7z l -y -bd -i!"${w}" "mp3tagsetup.exe" | grep 'license.*txt' | sed -r 's/[ ]+/\t/g' | cut -f 5 | sed 's/^.*\\//')
    do
        7z e -y -bd -so -i!"*${f}" "mp3tagsetup.exe" > "${f}"
    done
    _lc *
}

package() {
    a="${pkgdir}/apps${_apparch}/${_realname}"
    mkdir -p "${a}" "${a}/help"
    cp -r "data" "export" "lang" *.{exe,dll} "${a}"
    touch "${a}/help/helpmap.idx"
    rm "${a}/mp3tagsetup.exe"

    d="${pkgdir}/docs${_apparch}/${_realname}"
    mkdir -p "${d}"
    cp -r "help" *.txt "${d}"
}
