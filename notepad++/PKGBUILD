# Maintainer: David Macek <david.macek.0@gmail.com>

_realname=notepad++
_apparch=32
pkgbase="app-${_realname}"
pkgname=("app-i686-${_realname}")
pkgver=6.9.2
pkgrel=1
pkgdesc="A free Scintilla-based editor with plugin support"
url="https://notepad-plus-plus.org/"
arch=('any')
license=('GPL')
makedepends=("p7zip" "unzip")
provides=("${pkgbase}")
options=(!strip)
source=("npp.Installer.exe::https://notepad-plus-plus.org/repository/${pkgver%%.*}.x/${pkgver}/npp.${pkgver}.Installer.exe"
        "npp.bin.zip::https://notepad-plus-plus.org/repository/${pkgver%%.*}.x/${pkgver}/npp.${pkgver}.bin.zip")
sha512sums=('4f07fa32551799376e7d66e898beaecd011f55dcf96fe25a25ea6158dbd76b6fb4bd86f3034c125b1241e104dc69205d60dff35719cf0812baccef83b12a564d'
            'a43708774c62073c167143369652ca61b5013188dfefb40318c51759e8c3fd64f217010fa292fba4a8e5c1aa8da3edb58b91320ed9399b4307f45b4e97847492')

_lc() {
    for f in "${@}"
    do
        n="$(echo "${f}" | tr 'A-Z' 'a-z')"
        if test "${f}" != "${n}"
        then
            mv "${f}" "${n}"
        fi
    done
}

prepare() {
    rm -f NppShell_06*.dll
    echo u | 7z x -bd -i!"NppShell_06.dll" "npp.Installer.exe"
    _lc *
    for f in nppshell_06*.dll
    do
        case $(file "${f}") in
        *80386*) mv "${f}" "nppshell_06.32.dll" ;;
        *x86-64*) mv "${f}" "nppshell_06.64.dll" ;;
        esac
    done
}

package() {
    a="${pkgdir}/apps${_apparch}/${_realname}"
    mkdir -p "${a}"
    cp -r * "${a}"
    rm "${a}/dolocalconf.xml" "${a}"/npp.{installer.exe,bin.zip}

    d="${pkgdir}/docs${_apparch}/${_realname}"
    mkdir -p "${d}"
    mv "${a}"/*.{log,txt} "${d}"
}
