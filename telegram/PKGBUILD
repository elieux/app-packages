# Maintainer: David Macek <david.macek.0@gmail.com>

_realname=telegram
_choconame=telegram
pkgbase="app-${_realname}"
pkgname=("app-i686-${_realname}")
epoch=1
pkgver=3.7.1
pkgrel=1
pkgdesc="Desktop Telegram client"
url="https://desktop.telegram.org/"
arch=('any')
license=('GPL3')
provides=("${pkgbase}")
replaces=("app-x86_64-${_realname}")
options=(!strip)
source=("tportable.zip::https://github.com/telegramdesktop/tdesktop/releases/download/v${pkgver}/tportable.${pkgver}.zip")
sha512sums=('56ee1192fb131cc554023e634b6cba440c55685fa39b646314011a83cb44dc707e2effccdc02ba9e7ec067f81fa0c8053c4ab0f2f817a91e31003fb3698456cc')

_lc() {
    local d f n

    for f in "${@}"
    do
        d="$(dirname "${f}")"
        f="$(basename "${f}")"
        n="$(echo "${f}" | tr 'A-Z' 'a-z')"
        if test "${f}" != "${n}"
        then
            mv "${d}/${f}" "${d}/${n}"
        fi
    done
}

package() {
    local a="${pkgdir}/apps/${_realname}"
    mkdir -p "${a}"
    cp -R "Telegram"/* "${a}"
    _lc "${a}"/*
}
