# Maintainer: David Macek <david.macek.0@gmail.com>

_realname=discord
_apparch=32
pkgbase="app-${_realname}"
pkgname=("app-i686-${_realname}")
pkgver=0.0.300
pkgrel=1
pkgdesc="All-in-one voice and text chat for gamers"
url="https://www.discordapp.com/"
arch=('any')
license=('custom')
makedepends=("p7zip" "unzip")
provides=("${pkgbase}")
options=(!strip)
source=("DiscordSetup.exe::https://discordapp.com/api/download?platform=win")
sha512sums=('50a80f11bba0817bf5d75dfbc390ba5bf7c1620ebd173060fcca0878768b025eca77a9e8207cbbc52263fcc07f3086c1d38668e2fb7f9ec805d601aba786b2b2')

_lc() {
    local d f n

    for f in "${@}"
    do
        d="$(dirname "${f}")"
        f="$(basename "${f}")"
        n="$(echo "${f}" | tr 'A-Z' 'a-z')"
        if test "${f}" != "${n}"
        then
            mv "${d}/${f}" "${d}/${n}"
        fi
    done
}

_rmmsdll() {
    local ms

    ms=("msvbvm[0-9]+"
        "ucrtbase"
        "mfcm?[0-9]+[a-z]+"
        "msvc[rp][0-9]+"
        "vc([ao]mp|corlib|runtime)[0-9]+"
        "concrt[0-9]+"
        "pgort[0-9]+"
        "api-ms-win-[0-9a-z-]+")
    find "${1}" -iname '*.dll' | grep -Eif <(for m in "${ms[@]}"; do echo "/${m}[.]dll$"; done) | xargs -r rm
}

pkgver() {
    "${SYSTEMROOT}/System32/WindowsPowerShell/v1.0/powershell" -command '(Get-Item "DiscordSetup.exe").VersionInfo.ProductVersion'
}

prepare() {
    rm -f "Discord"-*-"full.nupkg"
    7z x -y -bd "DiscordSetup.exe"
    unzip -o "Discord"-*-"full.nupkg"
    _lc "lib/net45"/*
}

package() {
    a="${pkgdir}/apps${_apparch}/${_realname}"
    mkdir -p "${a}"
    cp -r "lib/net45"/* "${a}"
    _rmmsdll "${a}"
}
