# Maintainer: David Macek <david.macek.0@gmail.com>

_realname=wincachegrind
_apparch=32
pkgbase="app-${_realname}"
pkgname=("app-i686-${_realname}")
pkgver=1.1
pkgrel=1
pkgdesc="A viewer for cachegrind.out files generated by Xdebug"
url="http://ceefour.github.io/wincachegrind/"
arch=('any')
license=('GPL3')
provides=("${pkgbase}")
options=(!strip)
source=("wincachegrind.zip::https://github.com/ceefour/wincachegrind/releases/download/${pkgver}/wincachegrind-${pkgver}.zip")
sha512sums=('07886b5a6056320e492a52e4ca1f050f21136db438b941744fc80e5978365060f15cf7f4d79964084d68d4261cc5097a27475d8dcb8f12437ddc5a9c55c4e30f')

_lc() {
    local d f n

    for f in "${@}"
    do
        d="$(dirname "${f}")"
        f="$(basename "${f}")"
        n="$(echo "${f}" | tr 'A-Z' 'a-z')"
        if test "${f}" != "${n}"
        then
            mv "${d}/${f}" "${d}/${n}"
        fi
    done
}

prepare() {
    _lc *
}

package() {
    a="${pkgdir}/apps${_apparch}/${_realname}"
    mkdir -p "${a}"
    cp *.exe "${a}"

    d="${pkgdir}/docs${_apparch}/${_realname}"
    mkdir -p "${d}"
    cp -r "test" "license" *.{md,txt} "${d}"
}
