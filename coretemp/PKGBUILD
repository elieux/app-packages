# Maintainer: David Macek <david.macek.0@gmail.com>

_realname=coretemp
pkgbase="app-${_realname}"
pkgname=("app-i686-${_realname}"
         "app-x86_64-${_realname}")
pkgver=1.8.1
pkgrel=1
pkgdesc="A compact program to monitor processor temperature"
arch=('any')
url="http://www.alcpu.com/CoreTemp/"
license=('custom')
makedepends=("unzip")
provides=("${pkgbase}")
options=(!strip)
source=("CoreTemp32.zip::http://www.alcpu.com/CoreTemp/CoreTemp32.zip"
        "CoreTemp64.zip::http://www.alcpu.com/CoreTemp/CoreTemp64.zip"
        "CoreTempTranslator.zip::http://www.alcpu.com/CoreTemp/CoreTempTranslator.zip")
sha512sums=('0bd5e57cfc760731c208805c6fb473f598c89a7370bcbaa82a2ded051192f31e57d2eb35f58d0a70505e748576bcde6d9bb54fb870aa35c09832ac5420554dcb'
            '0c1dfd20c98ae2b0f677be71f650d0b3be9e5bc32531bb8755e2a885b91c41aa74caf178eb5f05393158c2bd346d4c8ff49674549db88d6944bd4acb7c2757b3'
            'b158538ad7b184b9dc8853d2c48a0d0d89780e792b73ab29099374d87aa871d90fcd02035d1c8764f378442c485edafff1c50e73a74c1b34f5b0c8a55b89f291')
noextract=("CoreTemp32.zip"
           "CoreTemp64.zip")

_eqver() {
    local r v

    for v in "${@}"
    do
        if [ -n "${r}" -a "${r}" != "${v}" ]
        then
            echo "Version mismatch between ${r} and ${v}."
        fi
        r="${v}"
    done
    echo "${r}"
}

_lc() {
    local d f n

    for f in "${@}"
    do
        d="$(dirname "${f}")"
        f="$(basename "${f}")"
        n="$(echo "${f}" | tr 'A-Z' 'a-z')"
        if test "${f}" != "${n}"
        then
            mv "${d}/${f}" "${d}/${n}"
        fi
    done
}

pkgver() {
    _vers=( $(
        for _apparch in 32 64
        do
            b="$(cygpath -wa "coretemp${_apparch}/core temp.exe" | sed 's/\\/\\\\/g')"
            wmic datafile where name=\""${b}"\" get version | grep 'Version' -A 1 | tail -1 | tr -d ' ' | sed -r 's/([0-9]+[.][0-9]+[.][0-9]+)[.]0$/\1/'
        done
    ) )
    _eqver "${_vers[@]}"
}

_prepare() {
    unzip -o "CoreTemp${_apparch}.zip" -d "CoreTemp${_apparch}"
    _lc "CoreTemp${_apparch}"/*
}

prepare() {
    for _apparch in 32 64
    do
        _prepare
    done
    _lc "languages"/* *
}

_package() {
    a="${pkgdir}/apps${_apparch}/${_realname}"
    mkdir -p "${a}"
    cp -r *.{dll,exe,lng} "languages" "coretemp${_apparch}"/*.exe "${a}"
    mv "${a}/core temp.exe" "${a}/coretemp.exe"

    d="${pkgdir}/docs${_apparch}/${_realname}"
    mkdir -p "${d}" "${d}/translator"
    cp "coretemp${_apparch}"/*.{url,txt} "${d}"
    cp *.{txt,png} "${d}/translator"
}

package_app-i686-coretemp() {
    _apparch=32
    _package
}

package_app-x86_64-coretemp() {
    _apparch=64
    _package
}
