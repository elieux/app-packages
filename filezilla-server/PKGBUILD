# Maintainer: David Macek <david.macek.0@gmail.com>

_realname=filezilla-server
_apparch=32
pkgbase="app-${_realname}"
pkgname=("app-i686-${_realname}")
pkgver=0.9.60.2
pkgrel=2
pkgdesc="A popular FTP server"
url="http://filezilla-project.org/"
arch=('any')
license=('GPL')
makedepends=("p7zip")
provides=("${pkgbase}")
options=(!strip)
source=("FileZilla_Server.exe::http://downloads.sourceforge.net/filezilla/FileZilla%20Server/${pkgver}/FileZilla_Server-${pkgver//./_}.exe")
sha512sums=('0e0a92f3693d31d09341354ce212f42e1941743cf5f49bffe58b0c05cbc04865470e96c145ae0ffeea060a86d618da2a7de78a38946a9c3a2dcb956d0f2b3cfa')

_lc() {
    local d f n

    for f in "${@}"
    do
        d="$(dirname "${f}")"
        f="$(basename "${f}")"
        n="$(echo "${f}" | tr 'A-Z' 'a-z')"
        if test "${f}" != "${n}"
        then
            mv "${d}/${f}" "${d}/${n}"
        fi
    done
}

prepare() {
    7z x -y -bd "FileZilla_Server.exe"
    _lc *
}

package() {
    a="${pkgdir}/apps${_apparch}/${_realname}"
    mkdir -p "${a}"
    cp -r "filezilla server.exe" "filezilla server interface.exe" *.dll "${a}"
    cp -r "license.txt" *.htm "${a}"
}
