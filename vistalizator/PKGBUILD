# Maintainer: David Macek <david.macek.0@gmail.com>

_realname=vistalizator
_apparch=32
pkgbase="app-${_realname}"
pkgname=("app-i686-${_realname}")
pkgver=2.75
pkgrel=1
pkgdesc="Tool for changing display language in all Windows editions"
url="http://www.froggie.sk/index.html"
arch=('any')
license=('custom')
makedepends=("unzip")
provides=("${pkgbase}")
options=(!strip)
source=("Vistalizator.exe::http://www.froggie.sk/download/Vistalizator.exe"
        "Languages.zip::http://www.froggie.sk/download/Languages.zip")
sha512sums=('0d9c8ad7aa400183ba4be559d2e4eeafd137e068a8fe70f79c7b9f721fe21ceffee6a70abfdc72a627a10f97c35d1a6606702df52b8d77293beb25b46efa2012'
            '01f8db29de056cdd9a50b9e9e82451cd9bcfb65023bda3257537340777d16e5347de1ce14b66b06b11c77a505746f34b37db9b8e022541d52f002d3afcb3f88e')

_lc() {
    local d f n

    for f in "${@}"
    do
        d="$(dirname "${f}")"
        f="$(basename "${f}")"
        n="$(echo "${f}" | tr 'A-Z' 'a-z')"
        if test "${f}" != "${n}"
        then
            mv "${d}/${f}" "${d}/${n}"
        fi
    done
}

pkgver() {
    b="$(cygpath -wa "Vistalizator.exe" | sed 's/\\/\\\\/g')"
    wmic datafile where name=\""${b}"\" get version | grep 'Version' -A 1 | tail -1 | tr -d ' ' | sed -r 's/([0-9]+)[.]([0-9]+)[.]([0-9]+).0$/\1.\2\3/'
}

prepare() {
    _lc *
}

package() {
    a="${pkgdir}/apps${_apparch}/${_realname}"
    mkdir -p "${a}"
    cp -r *.exe "languages" "${a}"
}
