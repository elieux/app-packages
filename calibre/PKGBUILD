# Maintainer: David Macek <david.macek.0@gmail.com>

_realname=calibre
pkgbase="app-${_realname}"
pkgname=("app-i686-${_realname}"
         "app-x86_64-${_realname}")
pkgver=2.68.0
pkgrel=1
pkgdesc="Ebook management application"
url="http://calibre-ebook.com/"
arch=('any')
license=('GPL3')
provides=("${pkgbase}")
options=(!strip)
source=("calibre32.msi::http://download.calibre-ebook.com/${pkgver}/${_realname}-${pkgver}.msi"
        "calibre64.msi::http://download.calibre-ebook.com/${pkgver}/${_realname}-64bit-${pkgver}.msi")
sha512sums=('34161bf0ea652e935d0908b13dc2217cb31cf16bc5f3b1f84754dc58b9bc648b4a6a1bfd92914a9f435fe92c5ee4869f064d09375e5ec9cd5685edff11bca4a2'
            '4f2806dbb5d161f8efde68e942aa7eb40b47a37af9307e6ba58202de6f438122dc6eda668dbe03ac2933c44e8f370c1cb5035dd6e2940a4f069a7b86306b2884')

_lc() {
    for f in "${@}"
    do
        n="$(echo "${f}" | tr 'A-Z' 'a-z')"
        if test "${f}" != "${n}"
        then
            mv "${f}" "${n}"
        fi
    done
}

_build() {
    msiexec -a "calibre${_apparch}.msi" "TARGETDIR=$(cygpath -wa "install${_apparch}")" -qn
    attrib -r //s
    _lc "install${_apparch}/PFiles"/*
    _lc "install${_apparch}/PFiles"/calibre*/*
}

build() {
    for _apparch in 32 64
    do
        _build
    done
}

_package() {
    a="${pkgdir}/apps${_apparch}/${_realname}"
    mkdir -p "${a}"
    cp -r "install${_apparch}/PFiles"/calibre*/* "${a}"
    rm -rf "${a}/microsoft.vc90.crt" # write-protected

    d="${pkgdir}/docs${_apparch}/${_realname}"
    mkdir -p "${d}"
    mv "${a}/license" "${d}"
}

package_app-i686-calibre() {
    _apparch=32
    _package
}

package_app-x86_64-calibre() {
    _apparch=64
    _package
}
