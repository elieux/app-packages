# Maintainer: David Macek <david.macek.0@gmail.com>

_realname=caprine
_choconame=caprine
pkgbase="app-${_realname}"
pkgname=("app-i686-${_realname}")
epoch=1
pkgver=2.53.0
pkgrel=1
pkgdesc="Elegant Facebook Messenger desktop app."
url="https://sindresorhus.com/caprine"
arch=('any')
license=('MIT')
makedepends=("p7zip")
provides=("${pkgbase}")
options=(!strip)
source=("Caprine-Setup.exe::https://github.com/sindresorhus/caprine/releases/download/v${pkgver}/Caprine-Setup-${pkgver}.exe")
sha512sums=('e043fc75f8f5ff2ea714ef3b5cd8cd568e5be936dbe49233e61a6b2a6827fc042c72ad327e3e5a4db04c1bfc21adf3d1ccef1703ce1820c6dba5c37e3e2093af')

_lc() {
    local d f n

    for f in "${@}"
    do
        d="$(dirname "${f}")"
        f="$(basename "${f}")"
        n="$(echo "${f}" | tr 'A-Z' 'a-z')"
        if test "${f}" != "${n}"
        then
            mv "${d}/${f}" "${d}/${n}"
        fi
    done
}

prepare() {
    7z x -bd "Caprine-Setup.exe" -o"Caprine-Setup" > /dev/null
    7z x -bd "Caprine-Setup"/'$PLUGINSDIR'/"app-64.7z" -o"app-64" > /dev/null
}

package() {
    local a="${pkgdir}/apps/${_realname}"
    mkdir -p "${a}"
    cp -R "app-64"/* "${a}"
    _lc "${a}"/*
}
