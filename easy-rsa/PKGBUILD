# Maintainer: David Macek <david.macek.0@gmail.com>

_realname=easy-rsa
pkgbase="app-${_realname}"
pkgname=("app-x86_64-${_realname}")
epoch=1
pkgver=3.0.7
pkgrel=1
pkgdesc="Simple shell based CA utility"
url="https://community.openvpn.net/openvpn#Easy-rsasubproject"
arch=('any')
license=('custom')
provides=("${pkgbase}")
replaces=("app-i686-${_realname}")
options=(!strip)
source=("EasyRSA-win64.zip::https://github.com/OpenVPN/easy-rsa/releases/download/v${pkgver}/EasyRSA-${pkgver}-win64.zip")
sha512sums=('61d0b2c18ee75e108e93fc4f46b22387f31479d50e3547acb8b625dab3676b6913af35ce193570d87bbd3472f3a46012bfcfca9186fe6265c495fab3d04ea089')

_lc() {
    local d f n

    for f in "${@}"
    do
        d="$(dirname "${f}")"
        f="$(basename "${f}")"
        n="$(echo "${f}" | tr 'A-Z' 'a-z')"
        if test "${f}" != "${n}"
        then
            mv "${d}/${f}" "${d}/${n}"
        fi
    done
}

package() {
    local a="${pkgdir}/apps/${_realname}"
    mkdir -p "${a}"
    cp -R "EasyRSA-${pkgver}"/* "${a}"
    _lc "${a}"/*
}
