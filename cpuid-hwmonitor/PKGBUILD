# Maintainer: David Macek <david.macek.0@gmail.com>

_realname=hwmonitor
_dirname=cpuid
pkgbase="app-${_realname}"
pkgname=("app-i686-${_realname}"
         "app-x86_64-${_realname}")
pkgver=1.28
pkgrel=1
pkgdesc="Reads PC systems main health sensors (CPUID)"
url="http://www.cpuid.com/softwares/hwmonitor.html"
arch=('any')
license=('custom')
makedepends=("unzip")
provides=("${pkgbase}")
options=(!strip)
source=("hwmonitor.zip::http://download.cpuid.com/${_realname}/${_realname}_${pkgver}.zip")
sha512sums=('77725d637e109a3e25f865117a04893cabaaf9e5152e9f8a29220b077a00409dc6371ef698c7bbd69ac87fdcf3dfda949772145bb24460f3bfa00fae1de57611')

_lc() {
    for f in "${@}"
    do
        n="$(echo "${f}" | tr 'A-Z' 'a-z')"
        if test "${f}" != "${n}"
        then
            mv "${f}" "${n}"
        fi
    done
}

prepare() {
    _lc *
}

_package() {
    a="${pkgdir}/apps${_apparch}/${_dirname}"
    mkdir -p "${a}"
    cp "hwmonitor_x${_apparch}.exe" "${a}/hwmonitor.exe"
}

package_app-i686-hwmonitor() {
    _apparch=32
    _package
}

package_app-x86_64-hwmonitor() {
    _apparch=64
    _package
}
