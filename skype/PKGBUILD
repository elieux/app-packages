# Maintainer: David Macek <david.macek.0@gmail.com>

_realname=skype
_apparch=32
pkgbase="app-${_realname}"
pkgname=("app-i686-${_realname}")
pkgver=7.40.32.103
pkgrel=1
pkgdesc="P2P software for high-quality voice communication"
url="http://www.skype.com/"
arch=('any')
license=('custom')
provides=("${pkgbase}")
options=(!strip)
source=("SkypeSetup.msi::http://www.skype.com/go/getskype-msi")
sha512sums=('4ae506be881263806d131c22bf641bafc6af1f81c9632fb23aece93df8a5af10cd84e0fc6f5e1308f9180b3d721f7cafd6dc983f47c04dceed41f6dc6acfeb50')

_lc() {
    local d f n

    for f in "${@}"
    do
        d="$(dirname "${f}")"
        f="$(basename "${f}")"
        n="$(echo "${f}" | tr 'A-Z' 'a-z')"
        if test "${f}" != "${n}"
        then
            mv "${d}/${f}" "${d}/${n}"
        fi
    done
}

pkgver() {
    msg2 "Starting build() to determine package version :("
    build > /dev/null
    b="install/Program Files/Skype/Phone/skype.exe"
    if [ -f "${b}" ]
    then
        b="$(cygpath -wa "${b}" | sed 's/\\/\\\\/g')"
        wmic datafile where name=\""${b}"\" get version | grep 'Version' -A 1 | tail -1 | tr -d ' '
    fi
}

build() {
    msiexec -a "SkypeSetup.msi" "TARGETDIR=$(cygpath -wa "install")" -qn
    attrib -r //s
    s="install/Program Files/Skype"
    _lc "${s}/Phone"/* "${s}/Perfmon"/* "${s}/Redist/Skype"/* "${s}/PFiles/Skype/Browser"/*
}

package() {
    s="install/Program Files/Skype"

    a="${pkgdir}/apps${_apparch}/${_realname}"
    mkdir -p "${a}"
    cp "${s}/Phone"/* "${s}/Perfmon/performancecounters.mam" "${s}/PFiles/Skype/Browser/skypebrowserhost.exe" "${s}/Redist/Skype/skype4com.dll" "${a}"
    rm "${a}"/{vccorlib,vcruntime,msvcp,concrt}140.dll "${a}"/api-ms-win-*.dll "${a}/ucrtbase.dll"

    d="${pkgdir}/docs${_apparch}/${_realname}"
    mkdir -p "${d}"
    cp "${s}/third-party_attributions.txt" "${d}"
}
