# Maintainer: David Macek <david.macek.0@gmail.com>

_realname=notepad++
pkgbase="app-${_realname}"
pkgname=("app-i686-${_realname}"
         "app-x86_64-${_realname}")
pkgver=7.3.3
pkgrel=1
pkgdesc="A free Scintilla-based editor with plugin support"
url="https://notepad-plus-plus.org/"
arch=('any')
license=('GPL')
makedepends=("p7zip" "unzip")
provides=("${pkgbase}")
options=(!strip)
source=("npp.bin32.zip::https://notepad-plus-plus.org/repository/${pkgver%%.*}.x/${pkgver}/npp.${pkgver}.bin.zip"
        "npp.Installer32.exe::https://notepad-plus-plus.org/repository/${pkgver%%.*}.x/${pkgver}/npp.${pkgver}.Installer.exe"
        "npp.bin64.zip::https://notepad-plus-plus.org/repository/${pkgver%%.*}.x/${pkgver}/npp.${pkgver}.bin.x64.zip"
        "npp.Installer64.exe::https://notepad-plus-plus.org/repository/${pkgver%%.*}.x/${pkgver}/npp.${pkgver}.Installer.x64.exe")
sha512sums=('230199985d310d27eedf1bf5a9e67d5b2ab0a90ea8f3c82a9887031a247597a844e6864970be1c201c019ec1e1607d5cd044624d3352116dfaca899d0b5b3bb1'
            '120947a461ba91e935bf2056a0563e876970386801811d3acfb2efd4734312f8dbc9050746ab584b093d03c76bf8660a537894cccc5c087989f49457e84fbea3'
            'c51edf594cadcd9f6803d222d990a1462a3002b31e8d45e40e7f23f9b90d0a1a140c33cef3a7875ecd1791d9f5467d18b08a14533ad16fd9fbd1a7c14dddbd6a'
            '9996130f2713e5c63028dc12fb4fc7d2b8875cccb06cf6c5ee122ca171228d7127ac199b77f34727cfa2c25148f14b41b2773356519163330b1ed6474dc347f1')
noextract=("npp.bin32.zip"
           "npp.bin64.zip")

_lc() {
    local d f n

    for f in "${@}"
    do
        d="$(dirname "${f}")"
        f="$(basename "${f}")"
        n="$(echo "${f}" | tr 'A-Z' 'a-z')"
        if test "${f}" != "${n}"
        then
            mv "${d}/${f}" "${d}/${n}"
        fi
    done
}

_prepare() {
    unzip -o "npp.bin${_apparch}.zip" -d "npp${_apparch}"
    rm -f "npp${_apparch}/nppshell_06*.dll"
    echo u | 7z x -bd -i!"NppShell_06.dll" -o"npp${_apparch}" "npp.Installer${_apparch}.exe"
    _lc "npp${_apparch}"/*
    for f in "npp${_apparch}"/nppshell_06*.dll
    do
        case "${_apparch}:$(file "${f}")" in
        32:*80386*|64:*x86-64*) mv "${f}" "npp${_apparch}"/"nppshell_06.dll" ;;
        32:*x86-64*|64:*80386*) rm "${f}" ;;
        esac
    done
}

prepare() {
    for _apparch in 32 64
    do
        _prepare
    done
}

_package() {
    a="${pkgdir}/apps${_apparch}/${_realname}"
    mkdir -p "${a}"
    cp -r "npp${_apparch}"/* "${a}"
    rm "${a}/dolocalconf.xml"

    d="${pkgdir}/docs${_apparch}/${_realname}"
    mkdir -p "${d}"
    mv "${a}"/*.{log,txt} "${d}"
}

# space before () required
package_app-i686-notepad++ () {
    _apparch=32
    _package
}

# space before () required
package_app-x86_64-notepad++ () {
    _apparch=64
    _package
}
