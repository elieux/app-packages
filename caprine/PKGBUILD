# Maintainer: David Macek <david.macek.0@gmail.com>

_realname=caprine
_choconame=caprine
pkgbase="app-${_realname}"
pkgname=("app-i686-${_realname}")
epoch=1
pkgver=2.56.0
pkgrel=1
pkgdesc="Elegant Facebook Messenger desktop app."
url="https://sindresorhus.com/caprine"
arch=('any')
license=('MIT')
makedepends=("p7zip")
provides=("${pkgbase}")
options=(!strip)
source=("Caprine-Setup.exe::https://github.com/sindresorhus/caprine/releases/download/v${pkgver}/Caprine-Setup-${pkgver}.exe")
sha512sums=('4fcc169d2fa52cb29c03ed9b474416657b296b21400959fe88aeaa0ed1f2ecb54e9f6b99b7714e528e85ecdd05130e12cb9d074641b72309d0b6be93845b8971')

_lc() {
    local d f n

    for f in "${@}"
    do
        d="$(dirname "${f}")"
        f="$(basename "${f}")"
        n="$(echo "${f}" | tr 'A-Z' 'a-z')"
        if test "${f}" != "${n}"
        then
            mv "${d}/${f}" "${d}/${n}"
        fi
    done
}

prepare() {
    7z x -bd "Caprine-Setup.exe" -o"Caprine-Setup" > /dev/null
    7z x -bd "Caprine-Setup"/'$PLUGINSDIR'/"app-64.7z" -o"app-64" > /dev/null
}

package() {
    local a="${pkgdir}/apps/${_realname}"
    mkdir -p "${a}"
    cp -R "app-64"/* "${a}"
    _lc "${a}"/*
}
