# Maintainer: David Macek <david.macek.0@gmail.com>

_realname=caprine
_choconame=caprine
pkgbase="app-${_realname}"
pkgname=("app-i686-${_realname}")
epoch=1
pkgver=2.52.0
pkgrel=1
pkgdesc="Elegant Facebook Messenger desktop app."
url="https://sindresorhus.com/caprine"
arch=('any')
license=('MIT')
makedepends=("p7zip")
provides=("${pkgbase}")
options=(!strip)
source=("Caprine-Setup.exe::https://github.com/sindresorhus/caprine/releases/download/v${pkgver}/Caprine-Setup-${pkgver}.exe")
sha512sums=('58a1bcf1f7af889bbfd6222e8818fff5e2e5e006b9f7b36bdf2d05b40fd759be930003b0ec11bb527873849739cd723b6f0a218d66c4209ed5e29a20b407b7d0')

_lc() {
    local d f n

    for f in "${@}"
    do
        d="$(dirname "${f}")"
        f="$(basename "${f}")"
        n="$(echo "${f}" | tr 'A-Z' 'a-z')"
        if test "${f}" != "${n}"
        then
            mv "${d}/${f}" "${d}/${n}"
        fi
    done
}

prepare() {
    7z x -bd "Caprine-Setup.exe" -o"Caprine-Setup" > /dev/null
    7z x -bd "Caprine-Setup"/'$PLUGINSDIR'/"app-64.7z" -o"app-64" > /dev/null
}

package() {
    local a="${pkgdir}/apps/${_realname}"
    mkdir -p "${a}"
    cp -R "app-64"/* "${a}"
    _lc "${a}"/*
}
