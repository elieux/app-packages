# Maintainer: David Macek <david.macek.0@gmail.com>

_realname=coretemp
pkgbase="app-${_realname}"
pkgname=("app-i686-${_realname}"
         "app-x86_64-${_realname}")
pkgver=1.10.2
pkgrel=1
pkgdesc="A compact program to monitor processor temperature"
arch=('any')
url="http://www.alcpu.com/CoreTemp/"
license=('custom')
makedepends=("unzip")
provides=("${pkgbase}")
options=(!strip)
source=("CoreTemp32.zip::http://www.alcpu.com/CoreTemp/CoreTemp32.zip"
        "CoreTemp64.zip::http://www.alcpu.com/CoreTemp/CoreTemp64.zip"
        "CoreTempTranslator.zip::http://www.alcpu.com/CoreTemp/CoreTempTranslator.zip")
sha512sums=('18c185bae96e712b1ea1a3e8d5da82b3dd091daa52a22865fe683324d327ae4d00cfc6137ff6fda86101d85619a260186c821f67398500346766d43b90db4ddb'
            '59b8c52502beb629391bff774df686e6f9e1ad91410ae80cf6d1e5fba22398ec7c043d11059aa4782a378d55bba2aec891c5072ccf32b1006074d0e480c8f274'
            'be6a08b8e797dbf662ae88aff592b29d720c3069ad6fcc255446bc41ae15cec8b4755953e1c2b7e00aea982f04c4de7c4930af55d64ab6601819643abe373d81')
noextract=("CoreTemp32.zip"
           "CoreTemp64.zip")

_eqver() {
    local r v

    for v in "${@}"
    do
        if [ -n "${r}" -a "${r}" != "${v}" ]
        then
            echo "Version mismatch between ${r} and ${v}."
        fi
        r="${v}"
    done
    echo "${r}"
}

_lc() {
    local d f n

    for f in "${@}"
    do
        d="$(dirname "${f}")"
        f="$(basename "${f}")"
        n="$(echo "${f}" | tr 'A-Z' 'a-z')"
        if test "${f}" != "${n}"
        then
            mv "${d}/${f}" "${d}/${n}"
        fi
    done
}

pkgver() {
    _vers=( $(
        for _apparch in 32 64
        do
            b="$(cygpath -wa "coretemp${_apparch}/core temp.exe" | sed 's/\\/\\\\/g')"
            wmic datafile where name=\""${b}"\" get version | grep 'Version' -A 1 | tail -1 | tr -d ' ' | sed -r 's/([0-9]+[.][0-9]+[.][0-9]+)[.]0$/\1/'
        done
    ) )
    _eqver "${_vers[@]}"
}

_prepare() {
    unzip -o "CoreTemp${_apparch}.zip" -d "CoreTemp${_apparch}"
    _lc "CoreTemp${_apparch}"/*
}

prepare() {
    for _apparch in 32 64
    do
        _prepare
    done
    _lc "languages"/* *
}

_package() {
    a="${pkgdir}/apps${_apparch}/${_realname}"
    mkdir -p "${a}"
    cp -r *.{dll,exe,lng} "languages" "coretemp${_apparch}"/*.exe "${a}"
    mv "${a}/core temp.exe" "${a}/coretemp.exe"

    d="${pkgdir}/docs${_apparch}/${_realname}"
    mkdir -p "${d}" "${d}/translator"
    cp "coretemp${_apparch}"/*.{url,txt} "${d}"
    cp *.{txt,png} "${d}/translator"
}

package_app-i686-coretemp() {
    _apparch=32
    _package
}

package_app-x86_64-coretemp() {
    _apparch=64
    _package
}
