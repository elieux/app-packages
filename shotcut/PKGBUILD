# Maintainer: David Macek <david.macek.0@gmail.com>

_realname=shotcut
_choconame=Shotcut
pkgbase="app-${_realname}"
pkgname=("app-x86_64-${_realname}")
epoch=1
pkgver=22.12.21
pkgrel=1
pkgdesc="Cross-platform, open-source video editor"
url="https://shotcut.org/"
arch=('any')
license=('GPL3')
provides=("${pkgbase}")
options=(!strip)
source=("shotcut-win64.zip::https://github.com/mltframework/shotcut/releases/download/v${pkgver}/shotcut-win64-${pkgver//./}.zip")
sha512sums=('54f5efced7ff64ca26027148c35865344e68d3dccbfb0f4e8e3639762b7dd7f002abd77660018644451cad1fc015563822e747d207c8f6ae36ccb936cb680aa3')

_lc() {
    local d f n

    for f in "${@}"
    do
        d="$(dirname "${f}")"
        f="$(basename "${f}")"
        n="$(echo "${f}" | tr 'A-Z' 'a-z')"
        if test "${f}" != "${n}"
        then
            mv "${d}/${f}" "${d}/${n}"
        fi
    done
}

package() {
    local a="${pkgdir}/apps/${_realname}"
    mkdir -p "${a}"
    cp -R "Shotcut"/* "${a}"
    _lc "${a}"/*
}
