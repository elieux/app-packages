# Maintainer: David Macek <david.macek.0@gmail.com>

_realname=vivaldi
pkgbase="app-${_realname}"
pkgname=("app-i686-${_realname}"
         "app-x86_64-${_realname}")
pkgver=1.7.735.46
pkgrel=1
pkgdesc="A new browser for our friends"
url="https://vivaldi.com/"
arch=('any')
license=('custom')
makedepends=("p7zip")
provides=("${pkgbase}")
options=(!strip)
source=("Vivaldi.x32.exe::https://downloads.vivaldi.com/stable/Vivaldi.${pkgver}.exe"
        "Vivaldi.x64.exe::https://downloads.vivaldi.com/stable/Vivaldi.${pkgver}.x64.exe")
sha512sums=('5a8d5c95e217863790ebe408bcc45d773070007f05ae78097050b41873913c9f98605081226795ec99b50978298adb24fcdb5924230a88743c9aae01dc79f7af'
            '7725c6980629b644b619de9d839c8fe4d2d18f58af13a1082767433537afd1308c881933acc4a0893d7a47f554775502ae7c0e6196de03b5f8b46de5fd686a17')

_lc() {
    local d f n

    for f in "${@}"
    do
        d="$(dirname "${f}")"
        f="$(basename "${f}")"
        n="$(echo "${f}" | tr 'A-Z' 'a-z')"
        if test "${f}" != "${n}"
        then
            mv "${d}/${f}" "${d}/${n}"
        fi
    done
}

_prepare() {
    7z x -y -bd "Vivaldi.x${_apparch}.exe"
    7z x -y -bd "vivaldi.7z"
    rm "vivaldi.7z"
    _lc "Vivaldi-bin"/* "Vivaldi-bin/${pkgver}"/*
    mv "Vivaldi-bin" "vivaldi-bin.x${_apparch}"
}

prepare() {
    for _apparch in 32 64
    do
        _prepare
    done
}

_package() {
    a="${pkgdir}/apps${_apparch}/${_realname}"
    mkdir -p "${a}"
    cp -r "vivaldi-bin.x${_apparch}"/*.exe "vivaldi-bin.x${_apparch}/${pkgver}"/* "${a}"
}

package_app-i686-vivaldi() {
    _apparch=32
    _package
}

package_app-x86_64-vivaldi() {
    _apparch=64
    _package
}

