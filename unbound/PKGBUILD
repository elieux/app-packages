# Maintainer: David Macek <david.macek.0@gmail.com>

_realname=unbound
pkgbase="app-${_realname}"
pkgname=("app-x86_64-${_realname}")
epoch=1
pkgver=1.10.0
pkgrel=1
pkgdesc="Validating, recursive, and caching DNS resolver"
arch=('any')
url="https://nlnetlabs.nl/projects/unbound/about/"
license=('custom:BSD')
replaces=("app-i686-${_realname}")
makedepends=("p7zip")
provides=("${pkgbase}")
options=(!strip)
source=("unbound_setup.exe::https://nlnetlabs.nl/downloads/unbound/unbound_setup_${pkgver}.exe")
sha512sums=('10cb592127838ee0c3805e30b5aef38af6d5741189dcbe88e77cff1720c7f070fe18b997d97885d1e709340dec036a91e0996be4da6411fd192acd71f85778a0')

_lc() {
    local d f n

    for f in "${@}"
    do
        d="$(dirname "${f}")"
        f="$(basename "${f}")"
        n="$(echo "${f}" | tr 'A-Z' 'a-z')"
        if test "${f}" != "${n}"
        then
            mv "${d}/${f}" "${d}/${n}"
        fi
    done
}

prepare() {
    7z x -bd "unbound_setup.exe" -o"unbound_setup.dir" > /dev/null
}

package() {
    local a="${pkgdir}/apps/${_realname}"
    mkdir -p "${a}"
    cp -R "unbound_setup.dir"/* "${a}"
    rm -R "${a}"/'$PLUGINSDIR'
    _lc "${a}"/*
}
