# Maintainer: David Macek <david.macek.0@gmail.com>

_realname=teamviewer
_apparch=32
pkgbase="app-${_realname}"
pkgname=("app-i686-${_realname}")
pkgver=11.0.63017
pkgrel=1
pkgdesc="All-in-one software for remote support and online meetings"
url="http://www.teamviewer.com/"
arch=('any')
license=('custom')
makedepends=("unzip")
provides=("${pkgbase}")
options=(!strip)
source=("TeamViewerPortable.zip::http://download.teamviewer.com/download/TeamViewerPortable.zip")
sha512sums=('776809626e81773aa84b84634f3a0c600319d16021c49c9097137242d65c0788d4f253050fdda289ee59748bc73060240009f73dfa34bf4fcddf6fdf8cfcce30')

_lc() {
    for f in "${@}"
    do
        n="$(echo "${f}" | tr 'A-Z' 'a-z')"
        if test "${f}" != "${n}"
        then
            mv "${f}" "${n}"
        fi
    done
}

pkgver() {
    b="$(cygpath -wa "TeamViewer.exe" | sed 's/\\/\\\\/g')"
    wmic datafile where name=\""${b}"\" get version | grep 'Version' -A 1 | tail -1 | tr -d ' ' | sed -r 's/([0-9]+[.][0-9]+[.][0-9]+)[.]0$/\1/'
}

prepare() {
    _lc *
}

package() {
    a="${pkgdir}/apps${_apparch}/${_realname}"
    mkdir -p "${a}"
    cp *.{dll,exe,ini} "${a}"

    d="${pkgdir}/docs${_apparch}/${_realname}"
    mkdir -p "${d}"
    cp *.{txt,inf} "${d}"
}
