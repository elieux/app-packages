# Maintainer: David Macek <david.macek.0@gmail.com>

_dirname=xnview
_realname=nconvert
pkgbase="app-${_realname}"
pkgname=("app-x86_64-${_realname}")
epoch=1
pkgver=7.32
pkgrel=1
pkgdesc="A powerful command line multi-platform batch image processor"
arch=('any')
url="https://www.xnview.com/en/nconvert/"
license=('custom')
replaces=("app-i686-${_realname}")
makedepends=("unzip")
provides=("${pkgbase}")
options=(!strip)
source=("NConvert-win64.zip::http://download.xnview.com/NConvert-win64.zip")
sha512sums=('62f6291339f41bdb84764f818660f7dbc29ff7361e8629fabfd1702e9561fcfa540608a4fb370721d9490b2a13ae8772ea647a7951be292eb03fbaa5b4d34e82')
noextract=("NConvert-win64.zip")

_lc() {
    local d f n

    for f in "${@}"
    do
        d="$(dirname "${f}")"
        f="$(basename "${f}")"
        n="$(echo "${f}" | tr 'A-Z' 'a-z')"
        if test "${f}" != "${n}"
        then
            mv "${d}/${f}" "${d}/${n}"
        fi
    done
}

prepare() {
    unzip -o -j "NConvert-win64.zip" -d "NConvert"
    _lc "NConvert"/*
}

pkgver() {
    unzip -p "NConvert-win64.zip" "XnView/WhatsNew.txt" | head -1 | sed -r 's/^.*NConvert v([0-9.]+).*$/\1/'
}

package() {
    a="${pkgdir}/apps/${_dirname}"
    mkdir -p "${a}"
    cp "NConvert"/*.exe "${a}"
    cp "NConvert"/*.txt "${a}"
}
