# Maintainer: David Macek <david.macek.0@gmail.com>

_realname=sysinternals
pkgbase="app-${_realname}"
pkgname=("app-i686-${_realname}"
         "app-x86_64-${_realname}")
pkgver=2016.08.31
pkgrel=1
pkgdesc="Advanced system utilities for Windows"
url="http://technet.microsoft.com/en-us/sysinternals/bb545021.aspx"
arch=('any')
license=('custom')
makedepends=("unzip")
provides=("${pkgbase}")
options=(!strip)
source=("http://download.sysinternals.com/files/SysinternalsSuite.zip"
        "http://download.sysinternals.com/files/BlueScreen.zip")
sha512sums=('9e9df728feb3a1021c42de2fcbfbc5e7233e2edbc1a61ba7a64e4c1853306963965a666a1e6fbf9c19462f0043c88e245f999edf82f7814c05ed05964f2c919d'
            '2e39c4b36e9928851fc86d9fc541956c3079a1ded09cca5ee677fbe1b3c9272b87651b65e53279cede3c4ed37028fcbae0823c4ee25180d5bc132cef8d155507')

_lc() {
    for f in "${@}"
    do
        n="$(echo "${f}" | tr 'A-Z' 'a-z')"
        if test "${f}" != "${n}"
        then
            mv "${f}" "${n}"
        fi
    done
}

pkgver() {
    #find . -mindepth 1 -maxdepth 1 -name '*.exe' -printf '%TY.%Tm.%Td\n' | sort | tail -1
    find . -mindepth 1 -maxdepth 1 -name '*.zip' -execdir bash -c 'unzip -l "{}" | tail -n +4 | head -n -2' \; | sed -r 's/[ ]+/\t/g' | cut -f 3 | tr '-' '.' | sort | tail -1
}

prepare() {
    _lc *
}

_package() {
    a="${pkgdir}/apps${_apparch}/${_realname}"
    mkdir -p "${a}"
    d="${pkgdir}/docs${_apparch}/${_realname}"
    mkdir -p "${d}"
    for f in "${@}"
    do
        n="${f##*/}"
        n="${n/64/}"
        case "${n}" in
        *.exe|*.sys|*.scr)
            cp "${f}" "${a}/${n}"
            ;;
        *.chm|*.txt|*.hlp)
            cp "${f}" "${d}/${n}"
            ;;
        esac
    done
    if test -z "$(find "${d}" -type f)"
    then
        rm -d "${d}"
    fi
}

package_app-i686-sysinternals() {
    _apparch=32
    _package $(find . -mindepth 1 -maxdepth 1 -not -name '*64*')
}

package_app-x86_64-sysinternals() {
    _apparch=64
    _package $(find . -mindepth 1 -maxdepth 1 -name '*64*') *.txt
}
