# Maintainer: David Macek <david.macek.0@gmail.com>

_realname=youtube-music
_choconame=th-ch-youtube-music
pkgbase="app-${_realname}"
pkgname=("app-x86_64-${_realname}")
epoch=1
pkgver=3.9.0
pkgrel=1
pkgdesc="Unofficial YouTube Music Desktop App with built-in ad blocker and downloader"
url="https://th-ch.github.io/youtube-music/"
arch=('any')
license=('MIT')
makedepends=("p7zip")
provides=("${pkgbase}")
options=(!strip)
source=("YouTube-Music.exe::https://github.com/th-ch/youtube-music/releases/download/v${pkgver}/YouTube-Music-${pkgver}.exe")
sha512sums=('f7a66dccd5ecca4a203334725cad4f97138f6b6d1bcbc47cce1440b62db8159c8e22290c90f8c01cf496bfb3b1f05e663194ca8a49e01355a3a57ad3503d7f0d')
noextract=("YouTube-Music.exe")

_lc() {
    local d f n

    for f in "${@}"
    do
        d="$(dirname "${f}")"
        f="$(basename "${f}")"
        n="$(echo "${f}" | tr 'A-Z' 'a-z')"
        if test "${f}" != "${n}"
        then
            mv "${d}/${f}" "${d}/${n}"
        fi
    done
}

prepare() {
    7z x -bd "YouTube-Music.exe" -o"YouTube-Music" > /dev/null
    7z x -bd "YouTube-Music"/'$PLUGINSDIR'/"app-64.7z" -o"app-64" > /dev/null
}

package() {
    local a="${pkgdir}/apps/${_realname}"
    mkdir -p "${a}"
    cp -R "app-64"/* "${a}"
    mv "${a}/YouTube Music.exe" "${a}/YouTube-Music.exe"
    _lc "${a}"/*
}
