# Maintainer: David Macek <david.macek.0@gmail.com>

_realname=wire
_choconame=wire
pkgbase="app-${_realname}"
pkgname=("app-i686-${_realname}")
epoch=1
pkgver=3.40.5233
pkgrel=1
pkgdesc="Desktop Wire client"
url="https://www.wire.com/"
arch=('any')
license=('custom')
makedepends=("p7zip" "unzip")
provides=("${pkgbase}")
options=(!strip)
source=("Wire-Setup.exe::https://wire-app.wire.com/win/prod/Wire-Setup.exe")
sha512sums=('abf2ee7a36a4925c7e8deb2b417d179d0e48c874605dc1ab752600f1a3245f30b6d724e39a29b3d19600dac11159272f1e0ed76ed40ec61159882aead68f5e72')

_lc() {
    local d f n

    for f in "${@}"
    do
        d="$(dirname "${f}")"
        f="$(basename "${f}")"
        n="$(echo "${f}" | tr 'A-Z' 'a-z')"
        if test "${f}" != "${n}"
        then
            mv "${d}/${f}" "${d}/${n}"
        fi
    done
}

_rmmsdll() {
    local ms

    ms=("msvbvm[0-9]+"
        "ucrtbase"
        "mfcm?[0-9]+[a-z]+"
        "msvc[rp][0-9]+(_([0-9]|codecvt_ids|atomic_wait))?"
        "vc([ao]mp|corlib|runtime)[0-9]+(_([0-9]|threads))?"
        "concrt[0-9]+"
        "pgort[0-9]+"
        "api-ms-win-[0-9a-z-]+")
    find "${1}" -iname '*.dll' | grep -Eif <(for m in "${ms[@]}"; do echo "/${m}[.]dll$"; done) | xargs -r rm
}

prepare() {
    rm -fR "Wire-Setup.dir"
    7z x -y -bd "Wire-Setup.exe" -o"Wire-Setup.dir" > /dev/null
    unzip -q -o "Wire-Setup.dir/wire"-*-"full.nupkg" -d "wire-full"
}

pkgver() {
    powershell -command '(Get-Item "Wire-Setup.exe").VersionInfo.ProductVersion'
}

package() {
    local a="${pkgdir}/apps/${_realname}"
    mkdir -p "${a}"
    cp -R "wire-full/lib/net45"/* "${a}"
    _rmmsdll "${a}"
    _lc "${a}"/*
}
