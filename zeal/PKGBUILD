# Maintainer: David Macek <david.macek.0@gmail.com>

_realname=zeal
_apparch=64
pkgbase="app-${_realname}"
pkgname=("app-x86_64-${_realname}")
pkgver=0.4.0
pkgrel=1
pkgdesc="An offline documentation browser for software developers"
url="https://zealdocs.org/"
arch=('any')
license=('GPL3')
provides=("${pkgbase}")
options=(!strip)
source=("zeal-windows-x64.msi::http://dl.bintray.com/zealdocs/windows/zeal-${pkgver}-windows-x64.msi")
sha512sums=('4ccc5c4f89c7174aa273a6e6100ce61672155b15e7e5d072991bd8fd82b9377bade3f842c069890054735f2e1f8e0b3f3aaeb0693c8b5b90de0bb92fde1c9c58')

_lc() {
    local d f n

    for f in "${@}"
    do
        d="$(dirname "${f}")"
        f="$(basename "${f}")"
        n="$(echo "${f}" | tr 'A-Z' 'a-z')"
        if test "${f}" != "${n}"
        then
            mv "${d}/${f}" "${d}/${n}"
        fi
    done
}

build() {
    msiexec -a "zeal-windows-x64.msi" "TARGETDIR=$(cygpath -wa "install")" -qn
    attrib -r //s
    s="install/Zeal"
    _lc "${s}"/*
}

package() {
    s="install/Zeal"

    a="${pkgdir}/apps${_apparch}/${_realname}"
    mkdir -p "${a}"
    cp -r "${s}"/* "${a}"
}
