# Maintainer: David Macek <david.macek.0@gmail.com>

_realname=nexus-mod-manager
_choconame=nmm
pkgbase="app-${_realname}"
pkgname=("app-i686-${_realname}")
epoch=1
pkgver=0.85.0
pkgrel=1
pkgdesc="Game modifications manager originally from Nexus Mods"
url="https://www.nexusmods.com/site/mods/4"
arch=('any')
license=('GPL2')
makedepends=("app-innounp")
provides=("${pkgbase}")
options=(!strip)
source=("NMM.exe::https://github.com/Nexus-Mods/Nexus-Mod-Manager/releases/download/${pkgver}/NMM-${pkgver}.exe")
sha512sums=('1f88a5b54612304afc14c93637d516944f6b44069e55742d43a137ed32f6bf5383b663aad49fa9c1fe5da52d10fb11381ee912b6194b6ad2ad678896e033a417')

_lc() {
    local d f n

    for f in "${@}"
    do
        d="$(dirname "${f}")"
        f="$(basename "${f}")"
        n="$(echo "${f}" | tr 'A-Z' 'a-z')"
        if test "${f}" != "${n}"
        then
            mv "${d}/${f}" "${d}/${n}"
        fi
    done
}

prepare() {
    /apps/innounp/innounp -x -q -m -a -y "NMM.exe" -d"NMM.dir"
}

package() {
    local a="${pkgdir}/apps/${_realname}"
    mkdir -p "${a}"
    cp -R "NMM.dir/{app}"/* "${a}"
    _lc "${a}"/*
}
