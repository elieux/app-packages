# Maintainer: David Macek <david.macek.0@gmail.com>

_realname=mc
_realver=221
_choconame=mc
pkgbase="app-${_realname}"
pkgname=("app-i686-${_realname}")
epoch=1
pkgver=4.8.24.221
pkgrel=1
pkgdesc="GNU terminal two-pane file manager"
url="https://sourceforge.net/projects/mcwin32/"
arch=('any')
license=('GPL3')
provides=("${pkgbase}")
makedepends=("app-innounp")
options=(!strip)
source=("mcwin32-build-setup.exe::https://downloads.sourceforge.net/mcwin32/mcwin32-build${_realver}-setup.exe")
sha512sums=('973ec69e847ca6e392fc06095e6988cc66dccfbdfb3386117f971d887fcfb709ff8fe81c33bbd46ccba8045eae658ef32772a4bce47b6509796547ab318415e1')

prepare() {
    /apps/innounp/innounp -x -q -m -a -y "mcwin32-build-setup.exe" -d"mcwin32-build-setup.dir"
}

pkgver() {
    powershell -command '(Get-Item "mcwin32-build-setup.exe").VersionInfo.ProductVersion.ToString()' | sed -r 's/^([0-9]+[.][0-9]+[.][0-9]+) [(]build: [0-9]{8}-([0-9]+)[)] *$/\1.\2/'
}

package() {
    local a="${pkgdir}/apps/${_realname}"
    mkdir -p "${a}"
    cp -R "mcwin32-build-setup.dir/{app}"/* "${a}"
    rm "${a}"/*.map
}
