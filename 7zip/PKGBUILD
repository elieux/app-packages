# Maintainer: David Macek <david.macek.0@gmail.com>

_realname=7zip
pkgname=("app-i686-${_realname}"
         "app-x86_64-${_realname}")
pkgver=9.36
pkgrel=1
pkgdesc="A free file archiver for extremely high compression"
url="http://www.7-zip.org/"
arch=('any')
license=('LGPL2' 'custom')
makedepends=("p7zip")
source=("7z32.msi::http://downloads.sourceforge.net/sevenzip/7-Zip/${pkgver}/7z${pkgver//./}.msi"
        "7z64.msi::http://downloads.sourceforge.net/sevenzip/7-Zip/${pkgver}/7z${pkgver//./}-x64.msi"
        "7zextra.7z::http://downloads.sourceforge.net/sevenzip/7-Zip/${pkgver}/7z${pkgver//./}-extra.7z")
md5sums=('8fe4b155dcf748ba229bace1a66643d2'
         '5062d38f5d2fa63eec68024bd0139ed2'
         'eacff1bcbd33a4df84ec3acb564febf7')

prepare() {
    cd "${srcdir}"
    for f in *
    do
        n=$(echo "${f}" | tr 'A-Z' 'a-z')
        if test "${f}" != "${n}"
        then
            mv "${f}" "${n}"
        fi
    done
}

build() {
    for apparch in 32 64
    do
        cd "${srcdir}"
        msiexec -a "7z${apparch}.msi" "TARGETDIR=$(cygpath -w "${srcdir}/msi${apparch}")" -qn
        cd "${srcdir}/msi${apparch}/Files/7-Zip"
        for f in *
        do
            n=$(echo "${f}" | tr 'A-Z' 'a-z')
            if test "${f}" != "${n}"
            then
                mv "${f}" "${n}"
            fi
        done
    done
}

package_app-i686-7zip() {
    apparch=32
    mkdir -p "${pkgdir}/apps${apparch}/${_realname}"
    mkdir -p "${pkgdir}/docs${apparch}/${_realname}"
    mkdir -p "${pkgdir}/docs${apparch}/${_realname}-extra"
    cp "${srcdir}"/*.{dll,exe} "${pkgdir}/apps${apparch}/${_realname}"
    cp "${srcdir}"/*.txt "${pkgdir}/docs${apparch}/${_realname}-extra"
    cp -R "${srcdir}/msi${apparch}/Files/7-Zip"/* "${pkgdir}/apps${apparch}/${_realname}"
    mv "${pkgdir}/apps${apparch}/${_realname}"/*.{chm,txt,ion} "${pkgdir}/docs${apparch}/${_realname}"
}

package_app-x86_64-7zip() {
    apparch=64
    mkdir -p "${pkgdir}/apps${apparch}/${_realname}"
    mkdir -p "${pkgdir}/docs${apparch}/${_realname}"
    mkdir -p "${pkgdir}/docs${apparch}/${_realname}-extra"
    cp "${srcdir}/x64"/*.{dll,exe} "${pkgdir}/apps${apparch}/${_realname}"
    cp "${srcdir}"/*.txt "${pkgdir}/docs${apparch}/${_realname}-extra"
    cp -R "${srcdir}/msi${apparch}/Files/7-Zip"/* "${pkgdir}/apps${apparch}/${_realname}"
    mv "${pkgdir}/apps${apparch}/${_realname}"/*.{chm,txt,ion} "${pkgdir}/docs${apparch}/${_realname}"
}
