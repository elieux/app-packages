# Maintainer: David Macek <david.macek.0@gmail.com>

_realname=teamviewer
_apparch=32
pkgbase="app-${_realname}"
pkgname=("app-i686-${_realname}")
pkgver=12.1.10277
pkgrel=1
pkgdesc="All-in-one software for remote support and online meetings"
url="http://www.teamviewer.com/"
arch=('any')
license=('custom')
makedepends=("unzip")
provides=("${pkgbase}")
options=(!strip)
source=("TeamViewerPortable.zip::http://download.teamviewer.com/download/TeamViewerPortable.zip")
sha512sums=('0f63448e77a859c95b77da5e361572e143ef58a4f8d2ebe371d2c39ad636217215d99c646c05f88736c1c00034794ea22e5e7e3c285012df59af642d7a9e7a02')

_lc() {
    local d f n

    for f in "${@}"
    do
        d="$(dirname "${f}")"
        f="$(basename "${f}")"
        n="$(echo "${f}" | tr 'A-Z' 'a-z')"
        if test "${f}" != "${n}"
        then
            mv "${d}/${f}" "${d}/${n}"
        fi
    done
}

pkgver() {
    powershell -Command '(Get-Item "teamviewer.exe").VersionInfo.FileVersion' | sed -r 's/([0-9]+[.][0-9]+[.][0-9]+)[.]0$/\1/'
}

prepare() {
    _lc *
}

package() {
    a="${pkgdir}/apps${_apparch}/${_realname}"
    mkdir -p "${a}"
    cp *.{dll,exe,ini} "${a}"

    d="${pkgdir}/docs${_apparch}/${_realname}"
    mkdir -p "${d}"
    cp *.{txt,inf} "${d}"
}
