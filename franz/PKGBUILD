# Maintainer: David Macek <david.macek.0@gmail.com>

_realname=franz
pkgbase="app-${_realname}"
pkgname=("app-i686-${_realname}"
         "app-x86_64-${_realname}")
pkgver=5.1.0
pkgrel=1
pkgdesc="Franz is a free messaging app and combines chat & messaging services into one application"
url="http://meetfranz.com/"
arch=('any')
license=('custom')
makedepends=("p7zip")
provides=("${pkgbase}")
options=(!strip)
source=("franz-setup.exe::https://github.com/meetfranz/franz/releases/download/v${pkgver}/franz-setup-${pkgver}.exe")
sha512sums=('bf3597281360fc60c23d2640f74a12ef09b094debdf58b1add4c34d48b197a373df3d7b1cf55c15bdca272180b4fda24253f4759f4e18aee2bbf3ab831f43cb7')

_lc() {
    local d f n

    for f in "${@}"
    do
        d="$(dirname "${f}")"
        f="$(basename "${f}")"
        n="$(echo "${f}" | tr 'A-Z' 'a-z')"
        if test "${f}" != "${n}"
        then
            mv "${d}/${f}" "${d}/${n}"
        fi
    done
}

prepare() {
    7z x -y -bd "franz-setup.exe" > /dev/null
    for _apparch in 32 64
    do
         7z x -y -bd '$PLUGINSDIR'/"app-${_apparch}.7z" -o"franz${_apparch}"
        _lc "franz${_apparch}"/*
    done
}

_package() {
    a="${pkgdir}/apps${_apparch}/${_realname}"
    mkdir -p "${a}"
    cp -r "franz${_apparch}"/* "${a}"

    d="${pkgdir}/docs${_apparch}/${_realname}"
    mkdir -p "${d}"
    mv "${a}/license"* "${d}"
}

package_app-i686-franz() {
    _apparch=32
    _package
}

package_app-x86_64-franz() {
    _apparch=64
    _package
}
