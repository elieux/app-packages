# Maintainer: David Macek <david.macek.0@gmail.com>

_realname=aws-cli
pkgbase="app-${_realname}"
pkgname=("app-x86_64-${_realname}")
epoch=1
pkgver=1.19.109
pkgrel=1
pkgdesc="Universal Command Line Interface for Amazon Web Services"
url="https://aws.amazon.com/cli/"
arch=('any')
license=('Apache')
replaces=("app-i686-${_realname}")
makedepends=("app-lessmsi")
provides=("${pkgbase}")
options=(!strip)
source=("AWSCLI64PY3.msi::https://s3.amazonaws.com/aws-cli/AWSCLI64PY3.msi")
sha512sums=('4a6649cfaa48e6fff18cb4a9af49463db57adaeb34b75039f2fb615d80b129bddf233995c0fe66417a948fb35193b8fb353c45e5be3623de949098ec9230c709')

_rmmsdll() {
    local ms

    ms=("msvbvm[0-9]+"
        "ucrtbase"
        "mfcm?[0-9]+[a-z]+"
        "msvc[rp][0-9]+(_([0-9]|codecvt_ids|atomic_wait))?"
        "vc([ao]mp|corlib|runtime)[0-9]+(_[0-9])?"
        "concrt[0-9]+"
        "pgort[0-9]+"
        "api-ms-win-[0-9a-z-]+")
    find "${1}" -iname '*.dll' | grep -Eif <(for m in "${ms[@]}"; do echo "/${m}[.]dll$"; done) | xargs -r rm
}

prepare() {
    rm -fR "AWSCLI64PY3"
    /apps/lessmsi/lessmsi x "AWSCLI64PY3.msi" > /dev/null
}

pkgver() {
    cat "AWSCLI64PY3/SourceDir/Amazon/AWSCLI/Product.wxs" | grep '<Product' | grep Version= | sed -r 's/^.*Version="([0-9.]+)".*$/\1/'
}

package() {
    local a="${pkgdir}/apps/${_realname}"
    mkdir -p "${a}"
    cp -R "AWSCLI64PY3/SourceDir/Amazon/AWSCLI"/* "${a}"
    rm -R "${a}/resources" "${a}"/*.wxs
    _rmmsdll "${a}"
}
