# Maintainer: David Macek <david.macek.0@gmail.com>

_realname=dropbox
pkgbase="app-${_realname}"
pkgname=("app-i686-${_realname}")
epoch=1
pkgver=108.4.453
pkgrel=1
pkgdesc="The Dropbox synchronization tool"
url="https://www.dropbox.com/"
arch=('any')
license=('custom')
makedepends=("p7zip" "curl" "python3")
provides=("${pkgbase}")
options=(!strip)
install="dropbox.install"
source=("DropboxOfflineInstaller.exe::https://www.dropbox.com/download?full=1&plat=win")
sha512sums=('6befa2a87bbceca49716a07cf90b2fe7bfe21798f764b217a6996af85fb47bb677e534d341ccf9d2f9538181cbe7dc56654919d8faecec8244bdb3db82e5172c')

_lc() {
    local d f n

    for f in "${@}"
    do
        d="$(dirname "${f}")"
        f="$(basename "${f}")"
        n="$(echo "${f}" | tr 'A-Z' 'a-z')"
        if test "${f}" != "${n}"
        then
            mv "${d}/${f}" "${d}/${n}"
        fi
    done
}

_rmmsdll() {
    local ms

    ms=("msvbvm[0-9]+"
        "ucrtbase"
        "mfcm?[0-9]+[a-z]+"
        "msvc[rp][0-9]+(_([0-9]|codecvt_ids))?"
        "vc([ao]mp|corlib|runtime)[0-9]+(_[0-9])?"
        "concrt[0-9]+"
        "pgort[0-9]+"
        "api-ms-win-[0-9a-z-]+")
    find "${1}" -iname '*.dll' | grep -Eif <(for m in "${ms[@]}"; do echo "/${m}[.]dll$"; done) | xargs -r rm
}

prepare() {
    7z x -y -bd "DropboxOfflineInstaller.exe" -o"DropboxOfflineInstaller.dir" > /dev/null
    7z x -y -bd "DropboxOfflineInstaller.dir"/Installer.exe.* -o"Installer" > /dev/null
}

pkgver() {
    powershell -command '(Get-Item "Installer/\`$0/Dropbox.exe").VersionInfo.FileVersion' | sed -r 's/([0-9]+[.][0-9]+[.][0-9]+)[.]0$/\1/'
}

package() {
    local a="${pkgdir}/apps/${_realname}"
    mkdir -p "${a}"
    cp -R "Installer"/'$0'/* "${a}"
    rm -R "${a}/DropboxUninstaller.exe"

    # remove system DLLs
    _rmmsdll "${a}"
    mv "${a}/${pkgver}"/* "${a}"
    rm -R "${a}/${pkgver}"
    rm "${a}/${pkgver}.manifest"
    _pkgver="${pkgver}" _a="${a}" python3 -c '
import os

to_remove = """<dependency><dependentAssembly><assemblyIdentity type="win32" name="{0}" version="901.520.64.112" language="*"></assemblyIdentity></dependentAssembly></dependency>""".format(os.environ["_pkgver"]).encode("ascii")
f = open(os.environ["_a"] + "/Dropbox.exe", "rb+")
data = f.read()
pos = data.find(to_remove)
f.seek(pos)
f.write(b" " * len(to_remove))
f.close()
    '
    # MSYS=winsymlinks:nativestrict ln -s . "${a}/${pkgver}" # Permission denied on msys2-runtime v3.0.7
    # ( cd "${a}" && MSYS2_ARG_CONV_EXCL=* cmd /c mklink /d "${pkgver}" . ) # Works, but doesn't package correctly

    _lc "${a}"/*
}
