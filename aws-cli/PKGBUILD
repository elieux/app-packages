# Maintainer: David Macek <david.macek.0@gmail.com>

_realname=aws-cli
pkgbase="app-${_realname}"
pkgname=("app-x86_64-${_realname}")
epoch=1
pkgver=1.18.54
pkgrel=1
pkgdesc="Universal Command Line Interface for Amazon Web Services"
url="https://aws.amazon.com/cli/"
arch=('any')
license=('Apache')
replaces=("app-i686-${_realname}")
makedepends=("app-lessmsi")
provides=("${pkgbase}")
options=(!strip)
source=("AWSCLI64PY3.msi::https://s3.amazonaws.com/aws-cli/AWSCLI64PY3.msi")
sha512sums=('3619e08dd2e87084e11132a111ae5f724cf67cfd49b28f1c158af647cfe40910a9b783819f4bed81c0fa38c05b80663999f2fa074ef11e52e2f7b8cb0b16f916')

_rmmsdll() {
    local ms

    ms=("msvbvm[0-9]+"
        "ucrtbase"
        "mfcm?[0-9]+[a-z]+"
        "msvc[rp][0-9]+(_([0-9]|codecvt_ids))?"
        "vc([ao]mp|corlib|runtime)[0-9]+(_[0-9])?"
        "concrt[0-9]+"
        "pgort[0-9]+"
        "api-ms-win-[0-9a-z-]+")
    find "${1}" -iname '*.dll' | grep -Eif <(for m in "${ms[@]}"; do echo "/${m}[.]dll$"; done) | xargs -r rm
}

prepare() {
    rm -fR "AWSCLI64PY3"
    /apps/lessmsi/lessmsi x "AWSCLI64PY3.msi" > /dev/null
}

pkgver() {
    cat "AWSCLI64PY3/SourceDir/Amazon/AWSCLI/Product.wxs" | grep '<Product' | grep Version= | sed -r 's/^.*Version="([0-9.]+)".*$/\1/'
}

package() {
    local a="${pkgdir}/apps/${_realname}"
    mkdir -p "${a}"
    cp -R "AWSCLI64PY3/SourceDir/Amazon/AWSCLI"/* "${a}"
    rm -R "${a}/resources" "${a}"/*.wxs
    _rmmsdll "${a}"
}
