# Maintainer: David Macek <david.macek.0@gmail.com>

_realname=hwmonitor
_dirname=cpuid
pkgbase="app-${_realname}"
pkgname=("app-i686-${_realname}"
         "app-x86_64-${_realname}")
pkgver=1.26
pkgrel=1
pkgdesc="Reads PC systems main health sensors (CPUID)"
url="http://www.cpuid.com/softwares/hwmonitor.html"
arch=('any')
license=('custom')
makedepends=("unzip")
provides=("${pkgbase}")
options=(!strip) # digital signatures
source=("hwmonitor.zip::http://www.cpuid.com/medias/files/softwares/${_realname}/${_realname}_${pkgver}.zip")
md5sums=('01fd77c5cd1eacaac539f73fed6b5c69')

prepare() {
    cd "${srcdir}"

    for f in *
    do
        n=$(echo "${f}" | tr 'A-Z' 'a-z')
        if test "${f}" != "${n}"
        then
            mv "${f}" "${n}"
        fi
    done
}

package() {
    mkdir -p "${pkgdir}/apps${_apparch}/${_dirname}"
    cp "${srcdir}/hwmonitor_x${_apparch}.exe" "${pkgdir}/apps${_apparch}/${_dirname}/hwmonitor.exe"
}

package_app-i686-hwmonitor() {
    _apparch=32
    package
}

package_app-x86_64-hwmonitor() {
    _apparch=64
    package
}
