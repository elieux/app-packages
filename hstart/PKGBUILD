# Maintainer: David Macek <david.macek.0@gmail.com>

_realname=hstart
pkgbase="app-${_realname}"
pkgname=("app-x86_64-${_realname}")
epoch=1
pkgver=5.1
pkgrel=1
pkgdesc="Bypass UAC, hide console windows, run apps in the background"
url="https://www.ntwind.com/software/hstart.html"
arch=('any')
license=('custom')
provides=("${pkgbase}")
makedepends=("p7zip")
options=(!strip)
source=("Hstart-setup.exe::https://hstart.s3.amazonaws.com/Hstart_${pkgver}-setup.exe")
sha512sums=('f5f3cd3fc7d5c8924a72715309c50b4f324bc58f25ef6c0424dc4a1692c138722b98ee7596dba32e957cbc58c25a6717fb459d21a392094176eecdfeacd8b2cd')
noextract=("Hstart-setup.exe")

_lc() {
    local d f n

    for f in "${@}"
    do
        d="$(dirname "${f}")"
        f="$(basename "${f}")"
        n="$(echo "${f}" | tr 'A-Z' 'a-z')"
        if test "${f}" != "${n}"
        then
            mv "${d}/${f}" "${d}/${n}"
        fi
    done
}

prepare() {
    7z x -y -bd "Hstart-setup.exe" -o"Hstart-setup.dir" > /dev/null
}

package() {
    local a="${pkgdir}/apps/${_realname}"
    mkdir -p "${a}"
    cp -R "Hstart-setup.dir"/* "${a}"
    rm -R "${a}/uninst.exe" "${a}"/{'$PLUGINSDIR','$TEMP'}
    _lc "${a}"/*
}
