# Maintainer: David Macek <david.macek.0@gmail.com>

_realname=easy-rsa
pkgbase="app-${_realname}"
pkgname=("app-x86_64-${_realname}")
epoch=1
pkgver=3.0.8
pkgrel=1
pkgdesc="Simple shell based CA utility"
url="https://community.openvpn.net/openvpn#Easy-rsasubproject"
arch=('any')
license=('custom')
provides=("${pkgbase}")
replaces=("app-i686-${_realname}")
options=(!strip)
source=("EasyRSA-win64.zip::https://github.com/OpenVPN/easy-rsa/releases/download/v${pkgver}/EasyRSA-${pkgver}-win64.zip")
sha512sums=('34be809027dc757fadd5d429a386cee632453a2c3dc84f51cd5d1aa9f9c25aeb3a3e4dbed1a207e1a2a82a7eeb67202013910fbea6249bf7f2d85db06749d6e3')

_lc() {
    local d f n

    for f in "${@}"
    do
        d="$(dirname "${f}")"
        f="$(basename "${f}")"
        n="$(echo "${f}" | tr 'A-Z' 'a-z')"
        if test "${f}" != "${n}"
        then
            mv "${d}/${f}" "${d}/${n}"
        fi
    done
}

package() {
    local a="${pkgdir}/apps/${_realname}"
    mkdir -p "${a}"
    cp -R "EasyRSA-${pkgver}"/* "${a}"
    _lc "${a}"/*
}
