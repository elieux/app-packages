# Maintainer: David Macek <david.macek.0@gmail.com>

_realname=wireshark
_choconame=wireshark
pkgbase="app-${_realname}"
pkgname=("app-x86_64-${_realname}")
epoch=1
pkgver=4.0.7
pkgrel=1
pkgdesc="A free network protocol analyzer"
url="https://www.wireshark.org/"
arch=('any')
license=('GPL2')
replaces=("app-i686-${_realname}")
makedepends=("p7zip")
provides=("${pkgbase}")
options=(!strip)
source=("Wireshark-win64.exe::https://1.eu.dl.wireshark.org/win64/Wireshark-win64-${pkgver}.exe")
sha512sums=('13a9de08d5b5c7fd047efa6ffd0f6f064e3453bab42ef55395c6900e66970c4c95caa4286ee31583562ff110d03c66b8b0ee80961c93066e33a66c95afd74030')

_lc() {
    local d f n

    for f in "${@}"
    do
        d="$(dirname "${f}")"
        f="$(basename "${f}")"
        n="$(echo "${f}" | tr 'A-Z' 'a-z')"
        if test "${f}" != "${n}"
        then
            mv "${d}/${f}" "${d}/${n}"
        fi
    done
}

prepare() {
    7z x -y -bd "Wireshark-win64.exe" -o"Wireshark-win64.dir" > /dev/null
}

package() {
    local a="${pkgdir}/apps/${_realname}"
    mkdir -p "${a}"
    cp -R "Wireshark-win64.dir"/* "${a}"
    rm -R "${a}"/npcap-*.exe "${a}"/USBPcapSetup-*.exe "${a}/vc_redist.x64.exe" "${a}/uninstall-wireshark.exe" "${a}"/'$PLUGINSDIR'
    _lc "${a}"/*
}
