# Maintainer: David Macek <david.macek.0@gmail.com>

_realname=sysinternals
_apparch=32
pkgbase="app-${_realname}"
pkgname=("app-i686-${_realname}")
pkgver=2015.02.09
pkgrel=2
pkgdesc="Advanced system utilities for Windows"
url="http://technet.microsoft.com/en-us/sysinternals/bb545021.aspx"
arch=('any')
license=('custom')
makedepends=("unzip")
provides=("${pkgbase}")
options=(!strip) # digital signatures
source=("http://download.sysinternals.com/files/SysinternalsSuite.zip"
        "http://download.sysinternals.com/files/BlueScreen.zip")
md5sums=('5b1d576c3df4b0a7d39d5c006370ad95'
         'e985acdd52b7bccdd6feb85ebd74c722')

prepare() {
    cd "${srcdir}"

    for f in *
    do
        n=$(echo "${f}" | tr 'A-Z' 'a-z')
        if test "${f}" != "${n}"
        then
            mv "${f}" "${n}"
        fi
    done
}

package() {
    mkdir -p "${pkgdir}/apps${_apparch}/${_realname}"
    mkdir -p "${pkgdir}/docs${_apparch}/${_realname}"
    cp "${srcdir}"/*.{exe,sys,scr} "${pkgdir}/apps${_apparch}/${_realname}/"
    cp "${srcdir}"/*.{chm,txt,hlp} "${pkgdir}/docs${_apparch}/${_realname}/"
}
