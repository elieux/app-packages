# Maintainer: David Macek <david.macek.0@gmail.com>

_realname=chromium
_realver=768962
pkgbase="app-${_realname}"
pkgname=("app-x86_64-${_realname}")
epoch=1
pkgver=84.0.4147.105
pkgrel=1
pkgdesc="The open-source project behind Google Chrome, an attempt at creating a safer, faster, and more stable browser"
url="https://www.chromium.org/Home"
arch=('any')
license=('BSD')
replaces=("app-i686-${_realname}")
provides=("${pkgbase}")
options=(!strip)
source=("chrome.sync.7z::https://github.com/Hibbiki/chromium-win64/releases/download/v${pkgver}-r${_realver}/chrome.sync.7z")
sha512sums=('2b3c2a611b6aaa59d50a45fc276857378312b8514c10e087097b9f1d6e349ccba5e97d0ed8468237645f65c783a8ec821dcb6338029ced6f8f48f00870256d1c')

_lc() {
    local d f n

    for f in "${@}"
    do
        d="$(dirname "${f}")"
        f="$(basename "${f}")"
        n="$(echo "${f}" | tr 'A-Z' 'a-z')"
        if test "${f}" != "${n}"
        then
            mv "${d}/${f}" "${d}/${n}"
        fi
    done
}

package() {
    local a="${pkgdir}/apps/${_realname}"
    mkdir -p "${a}"
    cp -R "Chrome-bin"/* "${a}"
    mv "${a}/${pkgver}"/* "${a}"
    rm -R "${a}/${pkgver}"
    _lc "${a}"/*
}
