# Maintainer: David Macek <david.macek.0@gmail.com>

_realname=consolez
pkgbase="app-${_realname}"
pkgname=("app-i686-${_realname}"
         "app-x86_64-${_realname}")
pkgver=1.18.1.17087
pkgrel=1
pkgdesc="A console wrapper with tabs, easy copy-pasting and pretty visuals"
url="http://github.com/cbucher/console"
arch=('any')
license=('GPL2')
makedepends=("unzip")
provides=("${pkgbase}")
options=(!strip)
source=("ConsoleZ32.zip::https://github.com/cbucher/console/releases/download/${pkgver%.*}/ConsoleZ.x86.${pkgver}.zip"
        "ConsoleZ64.zip::https://github.com/cbucher/console/releases/download/${pkgver%.*}/ConsoleZ.x64.${pkgver}.zip")
sha512sums=('56a1ed948cd2d569827d8d5eae409268bdf0be8dd650eb8920e2fd81f547cfa9271df32173c1b62bfee26c88210c5e6cf5a0011f6c80ba9e339db0030906b31c'
            '91124121b6621ca0599a8fbecf3f7ef7d426f6207566033ea123b4c8348a4ca6b211d00a96355d1034b0b9d5139c7ab8dc29f1da07536475fdbedb2e27bb6702')
noextract=("ConsoleZ32.zip"
           "ConsoleZ64.zip")

_lc() {
    local d f n

    for f in "${@}"
    do
        d="$(dirname "${f}")"
        f="$(basename "${f}")"
        n="$(echo "${f}" | tr 'A-Z' 'a-z')"
        if test "${f}" != "${n}"
        then
            mv "${d}/${f}" "${d}/${n}"
        fi
    done
}

prepare() {
    for _apparch in 32 64
    do
        unzip -o "ConsoleZ${_apparch}.zip" -d "consolez${_apparch}"
        _lc "consolez${_apparch}"/*
    done
}

_package() {
    a="${pkgdir}/apps${_apparch}/${_realname}"
    mkdir -p "${a}"
    cp "consolez${_apparch}"/*.{exe,dll} "${a}"

    d="${pkgdir}/docs${_apparch}/${_realname}"
    mkdir -p "${d}"
    cp "consolez${_apparch}"/*.chm "${d}"
}

package_app-i686-consolez() {
    _apparch=32
    _package
}

package_app-x86_64-consolez() {
    _apparch=64
    _package
}
