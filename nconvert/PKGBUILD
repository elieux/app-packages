# Maintainer: David Macek <david.macek.0@gmail.com>

_dirname=xnview
_realname=nconvert
pkgbase="app-${_realname}"
pkgname=("app-x86_64-${_realname}")
epoch=1
pkgver=7.12
pkgrel=1
pkgdesc="A powerful command line multi-platform batch image processor"
arch=('any')
url="https://www.xnview.com/en/nconvert/"
license=('custom')
replaces=("app-i686-${_realname}")
makedepends=("unzip")
provides=("${pkgbase}")
options=(!strip)
source=("NConvert-win64.zip::http://download.xnview.com/NConvert-win64.zip")
sha512sums=('535f63f287c123d5861027d242698f801048481ecf7508723113ca4496f7cc588a8317c6845fd2cc55aef189de75be129c4d665eb2371d0260f9c4c0ac5b97dd')
noextract=("NConvert-win64.zip")

_lc() {
    local d f n

    for f in "${@}"
    do
        d="$(dirname "${f}")"
        f="$(basename "${f}")"
        n="$(echo "${f}" | tr 'A-Z' 'a-z')"
        if test "${f}" != "${n}"
        then
            mv "${d}/${f}" "${d}/${n}"
        fi
    done
}

pkgver() {
    unzip -p "NConvert-win64.zip" "XnView/WhatsNew.txt" | head -1 | sed -r 's/^.*NConvert v([0-9.]+).*$/\1/'
}

prepare() {
    unzip -o -j "NConvert-win64.zip" -d "NConvert"
    _lc "NConvert"/*
}

package() {
    a="${pkgdir}/apps/${_dirname}"
    mkdir -p "${a}"
    cp "NConvert"/*.exe "${a}"
    cp "NConvert"/*.txt "${a}"
}
