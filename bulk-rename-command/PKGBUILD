# Maintainer: David Macek <david.macek.0@gmail.com>

_dirname=bulk-rename
_realname=bulk-rename-command
pkgbase="app-${_realname}"
pkgname=("app-x86_64-${_realname}")
epoch=1
pkgver=1.3.3.0
pkgrel=1
pkgdesc="Command-line file renaming software"
arch=('any')
url="http://www.bulkrenameutility.co.uk/Command.php"
license=('custom')
replaces=("app-i686-${_realname}")
makedepends=("unzip")
provides=("${pkgbase}")
options=(!strip)
source=("BRC_Unicode_64.zip::http://www.bulkrenameutility.co.uk/Downloads/BRC_Unicode_64.zip")
sha512sums=('9f06ec09b04dc7f4582557201664d1280a71966c856a3c750289985805d280f58cb9d4406d27029e921db8b750bff971aa531ed15703b00e9f6b4054464c65d5')
noextract=("BRC_Unicode_64.zip")

_lc() {
    local d f n

    for f in "${@}"
    do
        d="$(dirname "${f}")"
        f="$(basename "${f}")"
        n="$(echo "${f}" | tr 'A-Z' 'a-z')"
        if test "${f}" != "${n}"
        then
            mv "${d}/${f}" "${d}/${n}"
        fi
    done
}

pkgver() {
    unzip -q -j -o "BRC_Unicode_64.zip" "BRC64.exe"
    "${SYSTEMROOT}/System32/WindowsPowerShell/v1.0/powershell" -command '(Get-Item "BRC64.exe").VersionInfo.ProductVersion' | dos2unix | tr -d ' ' | tr ',' '.'
}

prepare() {
    unzip -o "BRC_Unicode_64.zip" -d "BRC"
    _lc "BRC"/*
}

package() {
    a="${pkgdir}/apps/${_dirname}"
    mkdir -p "${a}"
    cp "BRC"/*.{dll,exe} "${a}"
    mv "${a}/brc64.exe" "${a}/brc.exe"
    cp "BRC"/*.{pdf,chm} "${a}"
}
