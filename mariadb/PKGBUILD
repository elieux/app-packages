# Maintainer: David Macek <david.macek.0@gmail.com>

_realname=mariadb
pkgbase="app-${_realname}"
pkgname=("app-x86_64-${_realname}")
epoch=1
pkgver=10.4.12
pkgrel=1
pkgdesc="Fast SQL database server, drop-in replacement for MySQL"
url="https://mariadb.org/"
arch=('any')
license=('GPL')
replaces=("app-i686-${_realname}")
provides=("${pkgbase}")
options=(!strip)
source=("mariadb-winx64.zip::https://downloads.mariadb.org/f/mariadb-${pkgver}/winx64-packages/mariadb-${pkgver}-winx64.zip")
sha512sums=('a9c5dabab2515a0bd959107e5446ad2567a7af07cc4b4af79607afdaf8f382b388d27b76a39640c34e9fe2ad9c3c18118c3722c5b9de2d6c6f546de8a402d8ca')

_rmmsdll() {
    local ms

    ms=("msvbvm[0-9]+"
        "ucrtbase"
        "mfcm?[0-9]+[a-z]+"
        "msvc[rp][0-9]+(_([0-9]|codecvt_ids))?"
        "vc([ao]mp|corlib|runtime)[0-9]+(_[0-9])?"
        "concrt[0-9]+"
        "pgort[0-9]+"
        "api-ms-win-[0-9a-z-]+")
    find "${1}" -iname '*.dll' | grep -Eif <(for m in "${ms[@]}"; do echo "/${m}[.]dll$"; done) | xargs -r rm
}

package() {
    local a="${pkgdir}/apps/${_realname}"
    mkdir -p "${a}"
    cp -R "mariadb-${pkgver}-winx64"/* "${a}"
    rm -R "${a}/data"
    find "${a}" -name 'debug' -exec rm -r '{}' \; -prune
    find "${a}" -name '*.pdb' -delete
    find "${a}" -name '*.lib' -delete
    _rmmsdll "${a}"
}
