# Maintainer: David Macek <david.macek.0@gmail.com>

_realname=winmerge
pkgbase="app-${_realname}"
pkgname=("app-x86_64-${_realname}")
epoch=1
pkgver=2.16.4
pkgrel=1
pkgdesc="A differencing and merging tool"
url="https://winmerge.org/"
arch=('any')
license=('GPL')
replaces=("app-i686-${_realname}" "app-i686-winmerge-plugin-7z")
makedepends=("unzip" "app-innounp")
provides=("${pkgbase}" "app-winmerge-plugin-7z" "app-x86_64-winmerge-plugin-7z")
options=(!strip)
source=("WinMerge-x64-Setup.exe::https://downloads.sourceforge.net/${_realname}/WinMerge-${pkgver}-x64-Setup.exe"
        "winmerge-x64-exe.zip::https://downloads.sourceforge.net/${_realname}/winmerge-${pkgver}-x64-exe.zip")
sha512sums=('a0d5dd87b64ce94436c792426c98a280380838451e58f31e42fe9edb737bb35dc369119a11f280d128d702a1846c2b87b0147bafcc271235ca6668e646e2317f'
            '49b96f47712a8bd3dd1cd3e7eb8af17481a72fedc2e312712bb02745b17c7f581bb65f52f02eeac5bff5c033de1ca1c23761a4bf20462c947eff422184789363')

_lc() {
    local d f n

    for f in "${@}"
    do
        d="$(dirname "${f}")"
        f="$(basename "${f}")"
        n="$(echo "${f}" | tr 'A-Z' 'a-z')"
        if test "${f}" != "${n}"
        then
            mv "${d}/${f}" "${d}/${n}"
        fi
    done
}

prepare() {
    /apps/innounp/innounp -x -q -m -a -y "WinMerge-x64-Setup.exe"
    _lc "WinMerge"/* "{app}"/*
}

package() {
    a="${pkgdir}/apps/${_realname}"
    mkdir -p "${a}"
    cp -R "WinMerge"/* "${a}"
    cp -R "{app}/logoimages" "{app}/winmergeu.visualelementsmanifest.xml" "${a}"
    rm "${a}/vcomp140.dll"
}
