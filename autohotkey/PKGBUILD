# Maintainer: David Macek <david.macek.0@gmail.com>

_realname=autohotkey
_choconame=autohotkey
pkgbase="app-${_realname}"
pkgname=("app-x86_64-${_realname}")
epoch=1
pkgver=1.1.33.08
pkgrel=1
pkgdesc="Macro-creation and automation-oriented scripting utility for Windows"
url="https://www.autohotkey.com/"
arch=('any')
license=('GPL2')
provides=("${pkgbase}")
makedepends=("p7zip")
options=(!strip)
source=("AutoHotkey_setup.exe::https://github.com/Lexikos/AutoHotkey_L/releases/download/v${pkgver}/AutoHotkey_${pkgver}_setup.exe")
sha512sums=('2c3e2483db1fb5737500a4b05c17776f49d167d682f32041f0acdfa34e80d3adbd69023e2441496dd2ccc8712b40e45e02a7db47c8f7062e0832b210b5fc4635')

_lc() {
    local d f n

    for f in "${@}"
    do
        d="$(dirname "${f}")"
        f="$(basename "${f}")"
        n="$(echo "${f}" | tr 'A-Z' 'a-z')"
        if test "${f}" != "${n}"
        then
            mv "${d}/${f}" "${d}/${n}"
        fi
    done
}

prepare() {
    7z x -y -bd "AutoHotkey_setup.exe" -o"AutoHotkey_setup.dir" > /dev/null
}

package() {
    local a="${pkgdir}/apps/${_realname}"
    mkdir -p "${a}"
    cp -R "AutoHotkey_setup.dir"/* "${a}"
    mv "${a}/Compiler"/* "${a}"
    rm -R "${a}/Compiler" "${a}/setup.exe" "${a}/Installer.ahk"
    _lc "${a}"/*
}
