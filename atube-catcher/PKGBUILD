# Maintainer: David Macek <david.macek.0@gmail.com>

_realname=atube-catcher
_choconame=atubecatcher
pkgbase="app-${_realname}"
pkgname=("app-i686-${_realname}")
epoch=1
pkgver=10.8.7.0
pkgrel=1
pkgdesc="Downloads videos from the Internet"
url="https://www.atube.me/"
arch=('any')
license=('custom')
makedepends=("app-innounp")
provides=("${pkgbase}")
options=(!strip)
source=("aTube_Catcher.exe::http://files.dsnetwb.com/aTube_Catcher.exe")
sha512sums=('7246c5a7f61d63c4c3f33e86646eb599d118577731c8f92222b52b24919be27825ad13f0ac74ea6d45ff4fb362ffff42c768c541dc8258414b53f68117b8602f')

_lc() {
    local d f n

    for f in "${@}"
    do
        d="$(dirname "${f}")"
        f="$(basename "${f}")"
        n="$(echo "${f}" | tr 'A-Z' 'a-z')"
        if test "${f}" != "${n}"
        then
            mv "${d}/${f}" "${d}/${n}"
        fi
    done
}

prepare() {
    /apps/innounp/innounp -x -q -m -a -y "aTube_Catcher.exe" -d"aTube_Catcher.dir"
}

pkgver() {
    powershell -command '(Get-Item "aTube_Catcher.dir/{app}/aTubeCatcher,1.exe").VersionInfo.ProductVersionRaw.ToString()'
}

package() {
    local a="${pkgdir}/apps/${_realname}"
    mkdir -p "${a}"
    cp -R "aTube_Catcher.dir/{app}"/* "${a}"
    rm "${a}/aTubeCatcher,2.exe"
    mv "${a}/aTubeCatcher,1.exe" "${a}/aTubeCatcher.exe"
    _lc "${a}"/*
}
