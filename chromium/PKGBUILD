# Maintainer: David Macek <david.macek.0@gmail.com>

_realname=chromium
_realver=693954
pkgbase="app-${_realname}"
pkgname=("app-x86_64-${_realname}")
epoch=1
pkgver=78.0.3904.70
pkgrel=2
pkgdesc="The open-source project behind Google Chrome, an attempt at creating a safer, faster, and more stable browser"
url="http://www.chromium.org/Home"
arch=('any')
license=('BSD')
replaces=("app-i686-${_realname}")
makedepends=("p7zip")
provides=("${pkgbase}")
options=(!strip)
source=("Chromium-x64.7z::https://github.com/macchrome/winchrome/releases/download/v${pkgver}-r${_realver}-Win64/Chromium-${pkgver}-x64.7z")
sha512sums=('274a354e564a75f4776420fa3946b1d5bb07f909c5fed63eb6699404e0763cb7245707e15d995ae98f704217fd79658e379e23c6b53f3452561e3f2693ab17a8')

_lc() {
    local d f n

    for f in "${@}"
    do
        d="$(dirname "${f}")"
        f="$(basename "${f}")"
        n="$(echo "${f}" | tr 'A-Z' 'a-z')"
        if test "${f}" != "${n}"
        then
            mv "${d}/${f}" "${d}/${n}"
        fi
    done
}

prepare() {
    _lc "Chromium-${pkgver}-x64"/*
}

package() {
    a="${pkgdir}/apps/${_realname}"
    mkdir -p "${a}"
    cp -r "Chromium-${pkgver}-x64"/* "${a}"
}
