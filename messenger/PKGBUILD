# Maintainer: David Macek <david.macek.0@gmail.com>

_realname=messenger
_apparch=32
pkgbase="app-${_realname}"
pkgname=("app-i686-${_realname}")
pkgver=1.4.3
pkgrel=1
pkgdesc="Beautiful desktop client for Facebook Messenger. Chat without being distracted by your feed or notifications."
url="http://messengerfordesktop.com/"
arch=('any')
license=('MIT')
makedepends=("p7zip")
provides=("${pkgbase}")
options=(!strip)
source=("MessengerSetup.exe::https://github.com/Aluxian/Facebook-Messenger-Desktop/releases/download/v${pkgver}/MessengerSetup.exe")
sha512sums=('5026b834628b22564e12edc44bd40dc5cb6a47aa9c8785ac4df9f40f817b419ba797f1dc88588b5202a713ec2d4000622378ffde23195ce22db521c29185d566')

_lc() {
    for f in "${@}"
    do
        n="$(echo "${f}" | tr 'A-Z' 'a-z')"
        if test "${f}" != "${n}"
        then
            mv "${f}" "${n}"
        fi
    done
}

prepare() {
    7z x -y -bd "MessengerSetup.exe"
    _lc *
}

package() {
    a="${pkgdir}/apps${_apparch}/${_realname}"
    mkdir -p "${a}"
    cp -r * "${a}"
    rm -r "${a}"/*.nsi "${a}/messengersetup.exe" "${a}/uninstall messenger for desktop.exe" "${a}"/'$PLUGINSDIR'
}
