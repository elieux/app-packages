# Maintainer: David Macek <david.macek.0@gmail.com>

_realname=skype
pkgbase="app-${_realname}"
pkgname=("app-i686-${_realname}")
epoch=1
pkgver=8.17.0.2
pkgrel=1
pkgdesc="P2P software for high-quality voice communication"
url="http://www.skype.com/"
arch=('any')
license=('custom')
replaces=("app-i686-${_realname}")
makedepends=("app-innounp")
provides=("${pkgbase}")
options=(!strip)
source=("Skype.exe::https://get.skype.com/go/getskype-skypeforwindows")
sha512sums=('92fbc730bcadb7879cd047b23958fcfb59ad58ea0b3687eb921b3e6e2d2a4de87f1f94cf1e00d8f8a0c0d5276eb2ef50508b11b5d83050267a64134dc106a9a7')

_lc() {
    local d f n

    for f in "${@}"
    do
        d="$(dirname "${f}")"
        f="$(basename "${f}")"
        n="$(echo "${f}" | tr 'A-Z' 'a-z')"
        if test "${f}" != "${n}"
        then
            mv "${d}/${f}" "${d}/${n}"
        fi
    done
}

_rmmsdll() {
    local ms

    ms=("msvbvm[0-9]+"
        "ucrtbase"
        "mfcm?[0-9]+[a-z]+"
        "msvc[rp][0-9]+"
        "vc([ao]mp|corlib|runtime)[0-9]+"
        "concrt[0-9]+"
        "pgort[0-9]+"
        "api-ms-win-[0-9a-z-]+")
    find "${1}" -iname '*.dll' | grep -Eif <(for m in "${ms[@]}"; do echo "/${m}[.]dll$"; done) | xargs -r rm
}

pkgver() {
    "${SYSTEMROOT}/System32/WindowsPowerShell/v1.0/powershell" -command '(Get-Item "Skype.exe").VersionInfo.FileVersion' | dos2unix
}

prepare() {
    /apps/innounp/innounp -x -q -m -a -y "Skype.exe"
    _lc "{app}"/*
}

package() {
    a="${pkgdir}/apps/${_realname}"
    mkdir -p "${a}"
    cp -r "{app}"/* "${a}"
    _rmmsdll "${a}"
}
