# Maintainer: David Macek <david.macek.0@gmail.com>

_realname=keepass-plugin-rpc
_realver=2015-07-08T11:15:57Z
_dirname=keepass
_apparch=32
pkgbase="app-${_realname}"
pkgname=("app-i686-${_realname}")
pkgver=1.5.3
pkgrel=1
pkgdesc="A KeePass plugin that provides a RPC server for KeeFox"
url="http://keefox.org/"
arch=('any')
license=('unknown')
depends=("app-i686-keepass")
provides=("${pkgbase}")
options=(!strip)
source=("keefox-tb+fx-windows.xpi::https://addons.mozilla.org/firefox/downloads/file/350854/keefox-1.5.3-tb+fx-windows.xpi")
md5sums=('8749cb5bf90084a3e333e3610c8e8460')

pkgver() {
    v="$(dd if="deps/KeePassRPC.plgx" bs=1 skip=56 count=20 status=none)"
    if [ "${v}" == "${_realver}" ]
    then
        echo "${pkgver}"
    else
        warning "Timestamp has changed. Fix version number."
    fi
}

package() {
    a="${pkgdir}/apps${_apparch}/${_dirname}"
    mkdir -p "${a}/plugins"
    cp "deps"/*.plgx "${a}/plugins"
}
