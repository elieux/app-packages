# Maintainer: David Macek <david.macek.0@gmail.com>

_realname=tightvnc
_choconame=tightvnc
pkgbase="app-${_realname}"
pkgname=("app-x86_64-${_realname}")
epoch=1
pkgver=2.8.63
pkgrel=1
pkgdesc="Light weight VNC implementation"
url="https://www.tightvnc.com/"
arch=('any')
license=('GPL2')
replaces=("app-i686-${_realname}")
makedepends=("app-lessmsi")
provides=("${pkgbase}")
options=(!strip)
source=("tightvnc-gpl-setup-64bit.msi::https://www.tightvnc.com/download/${pkgver}/tightvnc-${pkgver}-gpl-setup-64bit.msi")
sha512sums=('39a9802ed2f1f081f51ecfea85805dce7289ffdb933fa1d813ab656928f1f96844e384abc45d611635402f34e995379165a0d7a44676939ec5ee6c1b25c7685d')

build() {
    rm -fR "tightvnc-gpl-setup-64bit"
    /apps/lessmsi/lessmsi x "tightvnc-gpl-setup-64bit.msi" > /dev/null
}

package() {
    local a="${pkgdir}/apps/${_realname}"
    mkdir -p "${a}"
    cp -R "tightvnc-gpl-setup-64bit/SourceDir/PFiles/TightVNC"/* "${a}"
}
