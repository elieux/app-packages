# Maintainer: David Macek <david.macek.0@gmail.com>

_realname=wireshark
pkgbase="app-${_realname}"
pkgname=("app-i686-${_realname}"
         "app-x86_64-${_realname}")
pkgver=2.2.6
pkgrel=1
pkgdesc="A free network protocol analyzer"
url="https://www.wireshark.org/"
arch=('any')
license=('GPL2')
makedepends=("p7zip")
provides=("${pkgbase}")
options=(!strip)
source=("Wireshark-win32.exe::https://1.eu.dl.wireshark.org/win32/Wireshark-win32-${pkgver}.exe"
        "Wireshark-win64.exe::https://1.eu.dl.wireshark.org/win64/Wireshark-win64-${pkgver}.exe")
sha512sums=('951311277ab60cd208488464ef9bce83eb0c604a8d841ea0317cc5b22ace0c6df5c2efb623a0586711056493bf17b0e12f09c7b433da7679932c527095ee166d'
            'efa8cf700775db10e0ca4389d96283aa5028b9a4278445e2b4957fbe4c74ff779b6ccd1ebf9d992af77888f774f3f0944231675223eed05ffdf0cf0995d132a0')

_lc() {
    local d f n

    for f in "${@}"
    do
        d="$(dirname "${f}")"
        f="$(basename "${f}")"
        n="$(echo "${f}" | tr 'A-Z' 'a-z')"
        if test "${f}" != "${n}"
        then
            mv "${d}/${f}" "${d}/${n}"
        fi
    done
}

_prepare() {
    7z x -y -bd "Wireshark-win${_apparch}.exe" -o"Wireshark-win${_apparch}"
    _lc "Wireshark-win${_apparch}"/*
}

prepare() {
    for _apparch in 32 64
    do
        _prepare
    done
}

_package() {
    a="${pkgdir}/apps${_apparch}/${_realname}"
    mkdir -p "${a}"
    cp -r "Wireshark-win${_apparch}"/* "${a}"
    rm -r "${a}"/*.nsi "${a}"/winpcap_*.exe "${a}"/usbpcap*.exe "${a}/vcredist_${1}.exe" "${a}/uninstall.exe" "${a}"/'$pluginsdir'

    d="${pkgdir}/docs${_apparch}/${_realname}"
    mkdir -p "${d}"
    mv "${a}/help" "${a}"/*.{txt,chm,html,css} "${d}"
    mv "${d}/[license].txt" "${d}/license.txt"
}

package_app-i686-wireshark() {
    _apparch=32
    _package "x86"
}

package_app-x86_64-wireshark() {
    _apparch=64
    _package "x64"
}
