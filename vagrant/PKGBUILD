# Maintainer: David Macek <david.macek.0@gmail.com>

_realname=vagrant
_choconame=vagrant
pkgbase="app-${_realname}"
pkgname=("app-x86_64-${_realname}")
epoch=1
pkgver=2.3.6
pkgrel=1
pkgdesc="Building and distributing development environments"
url="https://www.vagrantup.com/"
arch=('any')
license=('MIT')
makedepends=("app-lessmsi")
provides=("${pkgbase}")
options=(!strip)
source=("vagrant_windows_amd64.msi::https://releases.hashicorp.com/vagrant/${pkgver}/vagrant_${pkgver}_windows_amd64.msi")
sha512sums=('a45f00efaad2a98f33ca28c4a360ee9cde01d1981d774c2bc5a0056dd5bb59d364ae37d3d7d90f51162536757d8bda47ebbb089c1826166d5f6caf1d67e3a657')

prepare() {
    rm -fR "vagrant_windows_amd64"
    /apps/lessmsi/lessmsi x "vagrant_windows_amd64.msi" > /dev/null
}

package() {
    local a="${pkgdir}/apps/${_realname}"
    mkdir -p "${a}"
    cp -R "vagrant_windows_amd64/SourceDir/HashiCorp/Vagrant"/* "${a}"
    rm -f "${a}/embedded/bin/vcruntime140.dll"

    # remove some of the largest MSYS2 left-overs
    rm -R "${a}/embedded"/{usr/{lib/*.a,include},mingw64/{x86_64-w64-mingw32,lib/gcc,lib/*.a,include}}
}
