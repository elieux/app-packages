# Maintainer: David Macek <david.macek.0@gmail.com>

_realname=ketarin
_apparch=32
pkgbase="app-${_realname}"
pkgname=("app-i686-${_realname}")
pkgver=1.8.7
pkgrel=1
pkgdesc="A small application which automatically updates setup packages"
url="https://ketarin.org"
arch=('any')
license=('custom')
makedepends=("unzip")
provides=("${pkgbase}")
options=(!strip)
source=("Ketarin.zip::https://ketarin.org/downloads/Ketarin/Ketarin-${pkgver}.zip?noredirect")
sha512sums=('06675b13c439b8a2fc2faafec9239162f2d9213c059bcdb179f65e4864da3f8747f4a0b32014cd241094ee01db2a55009a88de30cfdcd6a5e9a911a25a5c2d01')

_lc() {
    for f in "${@}"
    do
        n="$(echo "${f}" | tr 'A-Z' 'a-z')"
        if test "${f}" != "${n}"
        then
            mv "${f}" "${n}"
        fi
    done
}

prepare() {
    _lc *
}

package() {
    a="${pkgdir}/apps${_apparch}/${_realname}"
    mkdir -p "${a}"
    cp *.{exe,dll,config} "${a}"
}
