# Maintainer: David Macek <david.macek.0@gmail.com>

_realname=google-drive
_choconame=googledrive
pkgbase="app-${_realname}"
pkgname=("app-x86_64-${_realname}")
epoch=1
pkgver=78.0.1.0
pkgrel=1
pkgdesc="The Google Drive synchronization tool"
url="https://www.google.com/drive/"
arch=('any')
license=('custom')
replaces=("app-i686-${_realname}")
makedepends=("p7zip")
provides=("${pkgbase}")
options=(!strip)
source=("GoogleDriveSetup.exe::https://dl.google.com/drive-file-stream/GoogleDriveSetup.exe")
sha512sums=('3feac6370f863e1d7eec3078892047582ff0735cfbe6c18aacf14bd0c98e178b9b7615c8ae4becd609714ab73df5570798ad840984b2ec7a1f76f9937f8714d1')

prepare() {
    7z x -y -bd "GoogleDriveSetup.exe" -o"GoogleDriveSetup.dir" > /dev/null
    7z x -y -bd "GoogleDriveSetup.dir/.rsrc/RCDATA/101" -o"101" > /dev/null
}

pkgver() {
    powershell -command '(Get-Item "101/GoogleDriveFS.exe").VersionInfo.FileVersionRaw.ToString()'
}

package() {
    local a="${pkgdir}/apps/${_realname}"
    mkdir -p "${a}"
    cp -R "101"/* "${a}"
}
