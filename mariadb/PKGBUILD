# Maintainer: David Macek <david.macek.0@gmail.com>

_realname=mariadb
pkgbase="app-${_realname}"
pkgname=("app-i686-${_realname}"
         "app-x86_64-${_realname}")
pkgver=10.1.17
pkgrel=1
pkgdesc="Fast SQL database server, drop-in replacement for MySQL"
url="http://mariadb.org/"
arch=('any')
license=('GPL')
makedepends=("unzip")
provides=("${pkgbase}")
options=(!strip)
source=("mariadb-win32.zip::https://downloads.mariadb.org/f/mariadb-${pkgver}/win32-packages/mariadb-${pkgver}-win32.zip"
        "mariadb-winx64.zip::https://downloads.mariadb.org/f/mariadb-${pkgver}/winx64-packages/mariadb-${pkgver}-winx64.zip")
sha512sums=('45060757ad37aa7f867c52ef1edba6f195ce342f14736d715df0b25e08d19738cc367018cf3e03a171e116a40c80d387df8ab7e441aa286a9d5748b417e019f2'
            '5e12b450fbaa1f2c67348379fb70d48ad1ad997d4743cdb83c873d449cfb11504ed51c4f746a8061e6b9d5122541ae5ed7c8a5e34ba9a155b34718c5bed105d4')

_package() {
    a="${pkgdir}/apps${_apparch}/${_realname}"
    mkdir -p "${a}"
    cp -r "mariadb-${pkgver}-win${1}"/* "${a}"
    rm -r "${a}"/{data,mysql-test,sql-bench}
    find "${a}" -name 'debug' -exec rm -r '{}' \; -prune
    find "${a}" -name '*.pdb' -delete
    find "${a}" -name '*.lib' -delete

    d="${pkgdir}/docs${_apparch}/${_realname}"
    mkdir -p "${d}"
    mv "${a}"/COPYING* "${a}"/*.ini "${a}"/{CREDITS,EXCEPTIONS-CLIENT,README} "${d}"
}

package_app-i686-mariadb() {
    _apparch=32
    _package "32"
}

package_app-x86_64-mariadb() {
    _apparch=64
    _package "x64"
}
