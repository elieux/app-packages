# Maintainer: David Macek <david.macek.0@gmail.com>

DLAGENTS=('https::/usr/bin/curl -qb "" -fLC - -A "Chrome" -e "https://filezilla-project.org/" -o %o %u')

_realname=filezilla
_choconame=filezilla
pkgbase="app-${_realname}"
pkgname=("app-x86_64-${_realname}")
epoch=1
pkgver=3.69.3
pkgrel=1
pkgdesc="Fast and reliable FTP, FTPS and SFTP client"
url="https://filezilla-project.org/"
arch=('any')
license=('GPL')
makedepends=("openssl")
replaces=("app-i686-${_realname}")
provides=("${pkgbase}")
options=(!strip)
source=()
sha512sums=()
_sha512sum='b5913049d0501d92830848426cd88ad5c32e43519a8f8fd409f4256b58f58dca890a162246a17c999458d61791ab6b386524c6e6b7e88c458a1b79873b722258'

_dl() {
    (
        cd "${SRCDEST}"
        download_file "${1}"
    )
}

_dlread() {
    local tmp

    tmp=".tmp"
    rm -f "${SRCDEST}/${tmp}"
    _dl "${tmp}::${1}" &> /dev/null
    cat "${SRCDEST}/${tmp}"
    rm "${SRCDEST}/${tmp}"
}

prepare() {
    local localname remotename contentwrapper cipher iv rawkey a plain link remotename
    localname="FileZilla_win64.zip"
    remotename="FileZilla_${pkgver}_win64.zip"
    contentwrapper="$(_dlread "https://filezilla-project.org/download.php?show_all=1" | grep 'id="contentwrapper"')"
    cipher="$(<<< "${contentwrapper}" sed -r 's/^.*>([^<]+)<.*$/\1/')"
    iv="$(<<< "${contentwrapper}" sed -r 's/^.*v1="([^"]+)".*$/\1/' | openssl base64 -d -A | hexdump -e '16/1 "%02x"')"
    rawkey="$(<<< "${contentwrapper}" sed -r 's/^.*v2="([^"]+)".*$/\1/' | openssl base64 -d -A | hexdump -e '16/1 "%02x"')"
    a="$(<<< "${contentwrapper}" sed -r 's/^.*v3="([^"]+)".*$/\1/' | openssl base64 -d -A)"
    test "${a}" = "AES-CBC"
    plain="$(<<< "${cipher}" openssl aes-256-cbc -d -a -A -K "${rawkey}" -iv "${iv}")"
    link="$(<<< "${plain}" grep -F "/${remotename}?" | sed -r 's/^.*href="([^"]+)".*$/\1/')"
    _dl "${localname}::${link}"
    source+=("${localname}")
    sha512sums+=("${_sha512sum}")
    check_checksums
    extract_sources
}

package() {
    local a="${pkgdir}/apps/${_realname}"
    mkdir -p "${a}"
    cp -R "FileZilla-${pkgver}"/* "${a}"
}
