# Maintainer: David Macek <david.macek.0@gmail.com>

_realname=discord
pkgbase="app-${_realname}"
pkgname=("app-i686-${_realname}")
epoch=1
pkgver=0.0.307
pkgrel=1
pkgdesc="All-in-one voice and text chat for gamers"
url="https://www.discordapp.com/"
arch=('any')
license=('custom')
makedepends=("p7zip" "unzip")
provides=("${pkgbase}")
options=(!strip)
source=("DiscordSetup.exe::https://discordapp.com/api/download?platform=win")
sha512sums=('549c4d169b080fd4d10791f2b5ae0f8a7466739faf9e7bc938f514503da66556a60709e84c6d0a7e621de69487943626426918ad3e0f2ef2f3b2f677a46043a2')

_lc() {
    local d f n

    for f in "${@}"
    do
        d="$(dirname "${f}")"
        f="$(basename "${f}")"
        n="$(echo "${f}" | tr 'A-Z' 'a-z')"
        if test "${f}" != "${n}"
        then
            mv "${d}/${f}" "${d}/${n}"
        fi
    done
}

_rmmsdll() {
    local ms

    ms=("msvbvm[0-9]+"
        "ucrtbase"
        "mfcm?[0-9]+[a-z]+"
        "msvc[rp][0-9]+(_([0-9]|codecvt_ids))?"
        "vc([ao]mp|corlib|runtime)[0-9]+(_[0-9])?"
        "concrt[0-9]+"
        "pgort[0-9]+"
        "api-ms-win-[0-9a-z-]+")
    find "${1}" -iname '*.dll' | grep -Eif <(for m in "${ms[@]}"; do echo "/${m}[.]dll$"; done) | xargs -r rm
}

prepare() {
    7z x -y -bd "DiscordSetup.exe" -o"DiscordSetup.dir" > /dev/null
    unzip -q -o "DiscordSetup.dir/Discord"-*-"full.nupkg" -d"Discord-full"
}

pkgver() {
    powershell -command '(Get-Item "DiscordSetup.exe").VersionInfo.ProductVersion'
}

package() {
    local a="${pkgdir}/apps/${_realname}"
    mkdir -p "${a}"
    cp -R "Discord-full/lib/net45"/* "${a}"
    _rmmsdll "${a}"
    _lc "${a}"/*
}
