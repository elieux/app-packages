# Maintainer: David Macek <david.macek.0@gmail.com>

_realname=xdebug-dbgp-tools
pkgbase="app-${_realname}"
pkgname=("app-x86_64-${_realname}")
epoch=1
pkgver=0.2.5 # determined by running the tools, currently 0.2 and 0.3
pkgrel=1
pkgdesc="DBGp tools from Xdebug"
url="https://xdebug.org/docs/dbgpClient"
arch=('any')
license=('unknown')
provides=("${pkgbase}")
options=(!strip)
source=("dbgpClient.exe::https://xdebug.org/files/binaries/dbgpClient.exe"
        "dbgpProxy.exe::https://xdebug.org/files/binaries/dbgpProxy.exe")
sha512sums=('9afcedf27df5b82eafde48a2ac4bdb8cc88f24d869aa9289614a8bc9b0c164e35d397b160aa00258bfa7c60c263622831d3696e45dae2f1a7bceb78851038a92'
            'c07bea4670effd0ba43e6a214aec2ddc8e99a190c068984dbb3eebd3d039f99bf5e29bcabb6269a0bf73fbcea9aa842d766e3bd03022d7263074da2c9d5de2e3')

_lc() {
    local d f n

    for f in "${@}"
    do
        d="$(dirname "${f}")"
        f="$(basename "${f}")"
        n="$(echo "${f}" | tr 'A-Z' 'a-z')"
        if test "${f}" != "${n}"
        then
            mv "${d}/${f}" "${d}/${n}"
        fi
    done
}

package() {
    local a="${pkgdir}/apps/${_realname}"
    mkdir -p "${a}"
    cp "dbgpClient.exe" "dbgpProxy.exe" "${a}"
    _lc "${a}"/*
}
