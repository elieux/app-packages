# Maintainer: David Macek <david.macek.0@gmail.com>

_realname=processhacker
_apparch=32
pkgbase="app-${_realname}"
pkgname=("app-i686-${_realname}"
         "app-x86_64-${_realname}")
pkgver=2.38
pkgrel=1
pkgdesc="Views and manages processes, services and more"
url="http://processhacker.sourceforge.net/"
arch=('any')
license=('GPL2' 'GPL3')
makedepends=("unzip")
provides=("${pkgbase}")
options=(!strip)
source=("processhacker-bin.zip::http://downloads.sourceforge.net/${_realname}/${_realname}2/${_realname}-${pkgver}-bin.zip")
sha512sums=('1562a9bdcf306beee3c13378bdbfa36b16cbce1aa83f961aa59cda65ac59500ed312732d265681c0ce817d8f7785561d1038b41f1b8294061621b7292b55c748')

_lc() {
    for f in "${@}"
    do
        n="$(echo "${f}" | tr 'A-Z' 'a-z')"
        if test "${f}" != "${n}"
        then
            mv "${f}" "${n}"
        fi
    done
}

prepare() {
    _lc "x86"/* "x64"/* *
}

_package() {
    a="${pkgdir}/apps${_apparch}/${_realname}"
    mkdir -p "${a}"
    cp -r "${1}"/* "${a}"

    d="${pkgdir}/docs${_apparch}/${_realname}"
    mkdir -p "${d}"
    cp -r *.txt "${d}"
}

package_app-i686-processhacker() {
    _apparch=32
    _package "x86"
}

package_app-x86_64-processhacker() {
    _apparch=64
    _package "x64"
}
