# Maintainer: David Macek <david.macek.0@gmail.com>

_realname=teamspeak
pkgbase="app-${_realname}"
pkgname=("app-x86_64-${_realname}")
epoch=1
pkgver=3.3.2
pkgrel=1
pkgdesc="Quality voice communication via the Internet"
url="https://www.teamspeak.com/"
arch=('any')
license=('custom')
replaces=("app-i686-${_realname}")
makedepends=("p7zip")
provides=("${pkgbase}")
options=(!strip)
source=("TeamSpeak3-Client-win64.exe::https://files.teamspeak-services.com/releases/client/${pkgver}/TeamSpeak3-Client-win64-${pkgver}.exe")
sha512sums=('b53d9228f05eb28ece2e6a1e6096ca192cd43c97b2f7b0827b2df065213cfdcea0bc0a32f0d56dce39972baf503b0705c8bade17215520b86ca3401d5de2b4aa')

_lc() {
    local d f n

    for f in "${@}"
    do
        d="$(dirname "${f}")"
        f="$(basename "${f}")"
        n="$(echo "${f}" | tr 'A-Z' 'a-z')"
        if test "${f}" != "${n}"
        then
            mv "${d}/${f}" "${d}/${n}"
        fi
    done
}

prepare() {
    w="*license*.rtf"
    7z x -y -bd -x!"${w}" "TeamSpeak3-Client-win64.exe" -o"win"
    for f in $(7z l -y -bd -i!"${w}" "TeamSpeak3-Client-win64.exe" | grep 'license.*rtf' | sed -r 's/[ ]+/\t/g' | cut -f 5 | sed 's/^.*\\//')
    do
        7z e -y -bd -so -i!"*${f}" "TeamSpeak3-Client-win.exe" > "win/${f}"
    done
    _lc "win"/*
}

package() {
    a="${pkgdir}/apps/${_realname}"
    mkdir -p "${a}"
    cp -r "win"/* "${a}"
    rm -r "${a}/update.exe" "${a}/uninstall.exe.nsis" "${a}"/'$pluginsdir'
    for f in "${a}/ts3client_win64".*
    do
        mv "${f}" "${f/_win64/}"
    done
}
