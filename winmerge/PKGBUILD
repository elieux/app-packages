# Maintainer: David Macek <david.macek.0@gmail.com>

_realname=winmerge
_choconame=winmerge
pkgbase="app-${_realname}"
pkgname=("app-x86_64-${_realname}")
epoch=1
pkgver=2.16.52.2
pkgrel=1
pkgdesc="A differencing and merging tool"
url="https://winmerge.org/"
arch=('any')
license=('GPL')
replaces=("app-i686-${_realname}" "app-i686-winmerge-plugin-7z")
makedepends=("app-innounp")
provides=("${pkgbase}" "app-winmerge-plugin-7z" "app-x86_64-winmerge-plugin-7z")
options=(!strip)
source=("WinMerge-Setup.exe::https://github.com/WinMerge/winmerge/releases/download/v${pkgver}/WinMerge-${pkgver}-Setup.exe"
        "winmerge-x64-exe.zip::https://github.com/WinMerge/winmerge/releases/download/v${pkgver}/winmerge-${pkgver}-x64-exe.zip")
sha512sums=('1062acb42bcf8f994c350a9d37dbdb814c48ed36755624ba20ad80addf1c860ece42a3f27b2a63b7a3adbc0b6ca0fd2d6633db49241e7813bde00261e8095d48'
            '15044afcb18ac92c3850c31049d1834d3a511220f33c478654b666a325df053394267e38519cf112671659358c559c3f5493f7505208ecfde006e7b211ddbd16')

_lc() {
    local d f n

    for f in "${@}"
    do
        d="$(dirname "${f}")"
        f="$(basename "${f}")"
        n="$(echo "${f}" | tr 'A-Z' 'a-z')"
        if test "${f}" != "${n}"
        then
            mv "${d}/${f}" "${d}/${n}"
        fi
    done
}

prepare() {
    /apps/innounp/innounp -x -q -m -a -y "WinMerge-Setup.exe" -d"WinMerge-Setup.dir"
}

package() {
    local a="${pkgdir}/apps/${_realname}"
    mkdir -p "${a}"
    cp -R "WinMerge"/* "${a}"
    cp -R "WinMerge-Setup.dir/{app}/LogoImages" "WinMerge-Setup.dir/{app}/WinMergeU.VisualElementsManifest.xml" "${a}"
    rm "${a}/vcomp140.dll"
    _lc "${a}"/*
}
