# Maintainer: David Macek <david.macek.0@gmail.com>

_realver=1655

DLAGENTS=("https::/usr/bin/curl -qb '' -fLC - -d id=${_realver}&server_id=8 -o %o %u")

_realname=gpu-z
_choconame=gpu-z
pkgbase="app-${_realname}"
pkgname=("app-i686-${_realname}")
epoch=1
pkgver=2.35.0
pkgrel=1
pkgdesc="Lightweight utility providing GPU and graphics card information"
arch=('any')
url="https://www.techpowerup.com/gpuz/"
license=('custom')
provides=("${pkgbase}")
options=(!strip)
source=("GPU-Z.exe::https://www.techpowerup.com/download/techpowerup-gpu-z/")
sha512sums=('35d38f651c1973ed8a31ec6bab60c82d2206eae5f9d8b9c795057cfb2d50e9a01f0cf2e5f4c60d048da5746e9292c7d24a3220e00461a1a462d2b347846e0b0e')

pkgver() {
    powershell -command '(Get-Item "GPU-Z.exe").VersionInfo.ProductVersion' | sed -r 's/^([0-9]+[.][0-9]+[.][0-9]+)[.][0-9]+$/\1/'
}

package() {
    local a="${pkgdir}/apps/${_realname}"
    mkdir -p "${a}"
    cp "GPU-Z.exe" "${a}/gpu-z.exe"
}
