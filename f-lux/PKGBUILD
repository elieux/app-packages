# Maintainer: David Macek <david.macek.0@gmail.com>

_realname=f-lux
_apparch=32
pkgbase="app-${_realname}"
pkgname=("app-i686-${_realname}")
pkgver=4.47.0.1
pkgrel=1
pkgdesc="Changes the display color temperature according to the time of the day"
url="http://justgetflux.com/"
arch=('any')
license=('custom')
makedepends=("p7zip")
provides=("${pkgbase}")
options=(!strip)
source=("flux-setup.exe::https://justgetflux.com/flux-setup.exe")
sha512sums=('e497ffe997775a8e511fe8bbe8ef5addeeec66da8805a86090600c36f388adbbe7cc55c576dd4d4b1b760cde926485a2e3e49ba80d2a14e4448366405503ebec')

pkgver() {
    7z x -y -bd "flux-setup.exe" "flux.exe" > /dev/null
    b="$(cygpath -wa "flux.exe" | sed 's/\\/\\\\/g')"
    wmic datafile where name=\""${b}"\" get version | grep 'Version' -A 1 | tail -1 | tr -d ' '
}

prepare() {
    7z x -y -bd "flux-setup.exe"
}

package() {
    a="${pkgdir}/apps${_apparch}/${_realname}"
    mkdir -p "${a}"
    cp -r "flux.exe" "runtime" "${a}"
}
