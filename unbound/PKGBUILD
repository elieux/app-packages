# Maintainer: David Macek <david.macek.0@gmail.com>

_realname=unbound
pkgbase="app-${_realname}"
pkgname=("app-x86_64-${_realname}")
epoch=1
pkgver=1.14.0
pkgrel=1
pkgdesc="Validating, recursive, and caching DNS resolver"
arch=('any')
url="https://nlnetlabs.nl/projects/unbound/about/"
license=('custom:BSD')
replaces=("app-i686-${_realname}")
makedepends=("p7zip")
provides=("${pkgbase}")
options=(!strip)
source=("unbound_setup.exe::https://nlnetlabs.nl/downloads/unbound/unbound_setup_${pkgver}.exe")
sha512sums=('a620a735afff7c02d2b81a7f78232eff55df9816124cef8b6f796e84944ce4cde6a13fbbe8aeaa7678f1b449ad8d60ff85f60acc84ed74d93e0e9afed2c1e56f')

_lc() {
    local d f n

    for f in "${@}"
    do
        d="$(dirname "${f}")"
        f="$(basename "${f}")"
        n="$(echo "${f}" | tr 'A-Z' 'a-z')"
        if test "${f}" != "${n}"
        then
            mv "${d}/${f}" "${d}/${n}"
        fi
    done
}

prepare() {
    7z x -bd "unbound_setup.exe" -o"unbound_setup.dir" > /dev/null
}

package() {
    local a="${pkgdir}/apps/${_realname}"
    mkdir -p "${a}"
    cp -R "unbound_setup.dir"/* "${a}"
    rm -R "${a}"/'$PLUGINSDIR'
    _lc "${a}"/*
}
