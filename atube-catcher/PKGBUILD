# Maintainer: David Macek <david.macek.0@gmail.com>

_realname=atube-catcher
pkgbase="app-${_realname}"
pkgname=("app-i686-${_realname}")
epoch=1
pkgver=3.8.0.9823
pkgrel=1
pkgdesc="Downloads videos from the Internet"
url="https://www.atube.me/"
arch=('any')
license=('custom')
makedepends=("app-innounp")
provides=("${pkgbase}")
options=(!strip)
source=("aTube_Catcher.exe::http://files.dsnetwb.com/aTube_Catcher.exe")
sha512sums=('10564b2a78647ed6080e385395263cd3da830ae85168f98b3cabcad990bacd9fe6c9e1a10dd0b560a029d98269c214cc9d9aab6ee9ae9a3c1889cabcdb997dd3')

_lc() {
    local d f n

    for f in "${@}"
    do
        d="$(dirname "${f}")"
        f="$(basename "${f}")"
        n="$(echo "${f}" | tr 'A-Z' 'a-z')"
        if test "${f}" != "${n}"
        then
            mv "${d}/${f}" "${d}/${n}"
        fi
    done
}

prepare() {
    /apps/innounp/innounp -x -q -m -a -y "aTube_Catcher.exe" -d"aTube_Catcher.dir"
}

pkgver() {
    powershell -command '(Get-Item "aTube_Catcher.dir/{app}/yct.exe").VersionInfo.ProductVersionRaw.ToString()'
}

package() {
    local a="${pkgdir}/apps/${_realname}"
    mkdir -p "${a}"
    cp -R "aTube_Catcher.dir/{app}"/* "${a}"
    _lc "${a}"/*
}
