# Maintainer: David Macek <david.macek.0@gmail.com>

_realname=vivaldi
_choconame=Vivaldi
pkgbase="app-${_realname}"
pkgname=("app-x86_64-${_realname}")
epoch=1
pkgver=5.1.2567.66
pkgrel=1
pkgdesc="A new browser for our friends"
url="https://vivaldi.com/"
arch=('any')
license=('custom')
replaces=("app-i686-${_realname}")
makedepends=("p7zip")
provides=("${pkgbase}")
options=(!strip)
source=("Vivaldi.x64.exe::https://downloads.vivaldi.com/stable/Vivaldi.${pkgver}.x64.exe")
sha512sums=('bf45011ead33ec40e22fa332bf884e0b1a52be923420e2b9e219fab82ecc7a3b9b5170794d001b531168c653d7625d321f5e0876ca65b86260ba2e77c8b649d1')
noextract=("Vivaldi.x64.exe") # bsdtar detects and extracts a setup.exe

_lc() {
    local d f n

    for f in "${@}"
    do
        d="$(dirname "${f}")"
        f="$(basename "${f}")"
        n="$(echo "${f}" | tr 'A-Z' 'a-z')"
        if test "${f}" != "${n}"
        then
            mv "${d}/${f}" "${d}/${n}"
        fi
    done
}

prepare() {
    7z x -y -bd "Vivaldi.x64.exe" -o"Vivaldi.x64.dir" > /dev/null
    7z x -y -bd "Vivaldi.x64.dir/vivaldi.7z" > /dev/null
}

package() {
    local a="${pkgdir}/apps/${_realname}"
    mkdir -p "${a}"
    cp -R "Vivaldi-bin"/*.exe "Vivaldi-bin/${pkgver}"/* "${a}"
    _lc "${a}"/*
}
