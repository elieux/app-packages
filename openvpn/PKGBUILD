# Maintainer: David Macek <david.macek.0@gmail.com>

_realname=openvpn
_choconame=openvpn
pkgbase="app-${_realname}"
pkgname=("app-x86_64-${_realname}")
epoch=1
pkgver=2.5.1.601
pkgrel=1
pkgdesc="A full-featured SSL VPN solution"
url="https://openvpn.net/community/"
arch=('any')
license=('custom')
makedepends=("app-lessmsi")
provides=("${pkgbase}")
options=(!strip)
source=("OpenVPN-amd64.msi::https://swupdate.openvpn.org/community/releases/OpenVPN-${pkgver%.*}-I${pkgver##*.}-amd64.msi")
sha512sums=('84e641ba734f98690034d457b49eae4f5066b428cf65eec45e0b5ff85ebe12bda881b201a24a76194c51e0e5b0f263ed9ad39fc03328501912a35c6578364d0e')

prepare() {
    rm -fR "OpenVPN-amd64"
    /apps/lessmsi/lessmsi x "OpenVPN-amd64.msi" > /dev/null
}

package() {
    local a="${pkgdir}/apps/${_realname}"
    mkdir -p "${a}"
    cp -R "OpenVPN-amd64/SourceDir/OpenVPN"/* "${a}"
    rm -R "${a}"/{easy-rsa,include,config,config-auto,log}
}
