# Maintainer: David Macek <david.macek.0@gmail.com>

_realname=element
_choconame=element-desktop
pkgbase="app-${_realname}"
pkgname=("app-x86_64-${_realname}")
epoch=1
pkgver=1.11.95
pkgrel=1
pkgdesc="A glossy Matrix collaboration client for desktop"
url="https://element.io/download"
arch=('any')
license=('Apache')
makedepends=("p7zip")
provides=("${pkgbase}")
options=(!strip)
source=("Element_Setup.exe::https://packages.element.io/desktop/install/win32/x64/Element%20Setup%20${pkgver}.exe")
sha512sums=('ae35e5e802b4d60eb543cf07ce21b6ec257f51a41801cf1fe8855f442cdc9c8256dc4eb665c2c5109f257191e167c538d07ab5c16b1f56e870402f5baadf8814')
noextract=("Element_Setup.exe")

_lc() {
    local d f n

    for f in "${@}"
    do
        d="$(dirname "${f}")"
        f="$(basename "${f}")"
        n="$(echo "${f}" | tr 'A-Z' 'a-z')"
        if test "${f}" != "${n}"
        then
            mv "${d}/${f}" "${d}/${n}"
        fi
    done
}


prepare() {
    7z x -y -bd "Element_Setup.exe" -o"Element_Setup.dir" > /dev/null
    unzip -q -o "Element_Setup.dir/element-desktop-${pkgver}-full.nupkg" -d "element-desktop-full"
}

package() {
    local a="${pkgdir}/apps/${_realname}"
    mkdir -p "${a}"
    cp -R "element-desktop-full/lib/net45"/* "${a}"
    _lc "${a}"/*
}
