# Maintainer: David Macek <david.macek.0@gmail.com>

_realname=openvpn
_choconame=openvpn
pkgbase="app-${_realname}"
pkgname=("app-x86_64-${_realname}")
epoch=1
pkgver=2.6.3.001
pkgrel=1
pkgdesc="A full-featured SSL VPN solution"
url="https://openvpn.net/community/"
arch=('any')
license=('custom')
makedepends=("app-lessmsi")
provides=("${pkgbase}")
options=(!strip)
source=("OpenVPN-amd64.msi::https://swupdate.openvpn.org/community/releases/OpenVPN-${pkgver%.*}-I${pkgver##*.}-amd64.msi")
sha512sums=('5a529e95dfc8675145633793f02f627826e83485864d6e58c610a712d2fdc129690e838fdd1de3ab3e3c0bb40290178ba0022f8e87ec24e3f61928e07f9d9bb9')

_rmmsdll() {
    local ms

    ms=("msvbvm[0-9]+"
        "ucrtbase"
        "mfcm?[0-9]+[a-z]+"
        "msvc[rp][0-9]+(_([0-9]|codecvt_ids|atomic_wait))?"
        "vc([ao]mp|corlib|runtime)[0-9]+(_[0-9])?"
        "concrt[0-9]+"
        "pgort[0-9]+"
        "api-ms-win-[0-9a-z-]+")
    find "${1}" -iname '*.dll' | grep -Eif <(for m in "${ms[@]}"; do echo "/${m}[.]dll$"; done) | xargs -r rm
}

prepare() {
    rm -fR "OpenVPN-amd64"
    /apps/lessmsi/lessmsi x "OpenVPN-amd64.msi" > /dev/null
}

package() {
    local a="${pkgdir}/apps/${_realname}"
    mkdir -p "${a}"
    cp -R "OpenVPN-amd64/SourceDir/OpenVPN"/* "${a}"
    rm -R "${a}"/{easy-rsa,include,config,config-auto,log}
    _rmmsdll "${a}"
}
