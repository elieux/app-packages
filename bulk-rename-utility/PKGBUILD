# Maintainer: David Macek <david.macek.0@gmail.com>

_dirname=bulk-rename
_realname=bulk-rename-utility
pkgbase="app-${_realname}"
pkgname=("app-x86_64-${_realname}")
epoch=1
pkgver=3.3.1.0
pkgrel=1
pkgdesc="File renaming software"
arch=('any')
url="http://www.bulkrenameutility.co.uk/"
license=('custom')
replaces=("app-i686-${_realname}")
makedepends=("unzip")
provides=("${pkgbase}")
options=(!strip)
source=("BRU_NoInstall.zip::http://www.bulkrenameutility.co.uk/Downloads/BRU_NoInstall.zip")
sha512sums=('88331b606a983d253967323591349d28176d829e3502f8a6a8e8cbf17ac8e44b24ee9e7b4ecb626cedec119d9e551f6d39357e70807a5a0a3bcf962cacd3c8dd')

_lc() {
    local d f n

    for f in "${@}"
    do
        d="$(dirname "${f}")"
        f="$(basename "${f}")"
        n="$(echo "${f}" | tr 'A-Z' 'a-z')"
        if test "${f}" != "${n}"
        then
            mv "${d}/${f}" "${d}/${n}"
        fi
    done
}

prepare() {
    _lc "64-bit"/*
    _lc "All"/*
}

pkgver() {
    "${SYSTEMROOT}/System32/WindowsPowerShell/v1.0/powershell" -command '(Get-Item "'"64-bit/Bulk Rename Utility.exe"'").VersionInfo.ProductVersion' | dos2unix
}

package() {
    a="${pkgdir}/apps/${_dirname}"
    mkdir -p "${a}"
    cp -R "All/js" "64-bit"/*.exe "${a}"
    mv "${a}/bulk rename utility.exe" "${a}/bru.exe"
    cp "All"/*.{chm,pdf,txt} "${a}"
    cp "All/bruhere.dll" "All/bruhere64.dll" "${a}"
}
