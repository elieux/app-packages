# Maintainer: David Macek <david.macek.0@gmail.com>

_realname=sysinternals
_apparch=32
pkgbase="app-${_realname}"
pkgname=("app-i686-${_realname}")
pkgver=2016.04.28
pkgrel=1
pkgdesc="Advanced system utilities for Windows"
url="http://technet.microsoft.com/en-us/sysinternals/bb545021.aspx"
arch=('any')
license=('custom')
makedepends=("unzip")
provides=("${pkgbase}")
options=(!strip)
source=("http://download.sysinternals.com/files/SysinternalsSuite.zip"
        "http://download.sysinternals.com/files/BlueScreen.zip")
sha512sums=('0eabd4ef7356ea5e0573a25a493da78fe7d67e5d150e422f1145d80f382ca96cf2fa8a8d2b2a3427349f3c5fe4a7d3baf0135527381688eab5c941c2f5004540'
            '2e39c4b36e9928851fc86d9fc541956c3079a1ded09cca5ee677fbe1b3c9272b87651b65e53279cede3c4ed37028fcbae0823c4ee25180d5bc132cef8d155507')

_lc() {
    for f in "${@}"
    do
        n="$(echo "${f}" | tr 'A-Z' 'a-z')"
        if test "${f}" != "${n}"
        then
            mv "${f}" "${n}"
        fi
    done
}

pkgver() {
    #find . -mindepth 1 -maxdepth 1 -name '*.exe' -printf '%TY.%Tm.%Td\n' | sort | tail -1
    find . -mindepth 1 -maxdepth 1 -name '*.zip' -execdir bash -c 'unzip -l "{}" | tail -n +4 | head -n -2' \; | sed -r 's/[ ]+/\t/g' | cut -f 3 | tr '-' '.' | sort | tail -1
}

prepare() {
    _lc *
}

package() {
    a="${pkgdir}/apps${_apparch}/${_realname}"
    mkdir -p "${a}"
    cp *.{exe,sys,scr} "${pkgdir}/apps${_apparch}/${_realname}/"

    d="${pkgdir}/docs${_apparch}/${_realname}"
    mkdir -p "${d}"
    cp *.{chm,txt,hlp} "${pkgdir}/docs${_apparch}/${_realname}/"
}
