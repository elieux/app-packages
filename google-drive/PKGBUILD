# Maintainer: David Macek <david.macek.0@gmail.com>

_chocover() {
    # 86.0.9 -> 86.0.9.0
    echo "${1}" | sed -r -e 's/^([0-9]+)[.]([0-9]+)[.]([0-9]+)$/\1.\2.\3.0/'
}

_realname=google-drive
_choconame=googledrive
pkgbase="app-${_realname}"
pkgname=("app-x86_64-${_realname}")
epoch=1
pkgver=115.0.1.0
pkgrel=1
pkgdesc="The Google Drive synchronization tool"
url="https://www.google.com/drive/"
arch=('any')
license=('custom')
replaces=("app-i686-${_realname}")
makedepends=("p7zip")
provides=("${pkgbase}")
options=(!strip)
source=("GoogleDriveSetup.exe::https://dl.google.com/drive-file-stream/GoogleDriveSetup.exe")
sha512sums=('ae10705d41233f7d307f4cd8b376599c14f9158426324d182a9ab3e60bcf9bb2aacdd8afc1918b202c1c890dc619423efc8710005804323f9f692ff3ee2b0e91')
noextract=("GoogleDriveSetup.exe")

prepare() {
    7z x -y -bd "GoogleDriveSetup.exe" -o"GoogleDriveSetup.dir" > /dev/null
}

pkgver() {
    powershell -command '(Get-Item "GoogleDriveSetup.dir/GoogleDriveFS.exe").VersionInfo.FileVersionRaw.ToString()'
}

package() {
    local a="${pkgdir}/apps/${_realname}"
    mkdir -p "${a}"
    cp -R "GoogleDriveSetup.dir"/* "${a}"
}
