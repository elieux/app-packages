# Maintainer: David Macek <david.macek.0@gmail.com>

_realname=chromium
_choconame=chromium
_realver=1250580
pkgbase="app-${_realname}"
pkgname=("app-x86_64-${_realname}")
epoch=1
pkgver=122.0.6261.70
pkgrel=1
pkgdesc="The open-source project behind Google Chrome, an attempt at creating a safer, faster, and more stable browser"
url="https://www.chromium.org/Home"
arch=('any')
license=('BSD')
replaces=("app-i686-${_realname}")
provides=("${pkgbase}")
options=(!strip)
source=("chrome.sync.7z::https://github.com/Hibbiki/chromium-win64/releases/download/v${pkgver}-r${_realver}/chrome.sync.7z")
sha512sums=('bc9222d89eb4afc89695bf65468809c85ac8266c387def253fed516af33112131d1eaafe4e44b2995ba78f192708bb6f1dae3117dbeb02e51364d1facfb4df0a')

_lc() {
    local d f n

    for f in "${@}"
    do
        d="$(dirname "${f}")"
        f="$(basename "${f}")"
        n="$(echo "${f}" | tr 'A-Z' 'a-z')"
        if test "${f}" != "${n}"
        then
            mv "${d}/${f}" "${d}/${n}"
        fi
    done
}

package() {
    local a="${pkgdir}/apps/${_realname}"
    mkdir -p "${a}"
    cp -R "Chrome-bin"/* "${a}"
    mv "${a}/${pkgver}"/* "${a}"
    rm -R "${a}/${pkgver}"
    _lc "${a}"/*
}
