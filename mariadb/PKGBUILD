# Maintainer: David Macek <david.macek.0@gmail.com>

_realname=mariadb
pkgbase="app-${_realname}"
pkgname=("app-i686-${_realname}"
         "app-x86_64-${_realname}")
pkgver=10.1.14
pkgrel=1
pkgdesc="Fast SQL database server, drop-in replacement for MySQL"
url="http://mariadb.org/"
arch=('any')
license=('GPL')
makedepends=("unzip")
provides=("${pkgbase}")
options=(!strip)
source=("mariadb-win32.zip::https://downloads.mariadb.org/f/mariadb-${pkgver}/win32-packages/mariadb-${pkgver}-win32.zip"
        "mariadb-winx64.zip::https://downloads.mariadb.org/f/mariadb-${pkgver}/winx64-packages/mariadb-${pkgver}-winx64.zip")
sha512sums=('bd7ffc13bada8419c4959d28be83126a14b5a5ea16c6aeb4ef5dd4e9b4e49259d7819547e0de6e543671ac7bfb3cfc7dfa66cc719de51b32fa6f1dd2d9bd59f9'
            'ac47919060fd226fae193f9437b901a1c65b8fac1199c748357979fd60de2c9c208f2114f796dec8d8cc1b5a22c23d0c5c275445dba43ce4920e16655c21724a')

_package() {
    a="${pkgdir}/apps${_apparch}/${_realname}"
    mkdir -p "${a}"
    cp -r "mariadb-${pkgver}-win${1}"/* "${a}"
    rm -r "${a}"/{data,mysql-test,sql-bench}
    find "${a}" -name 'debug' -exec rm -r '{}' \; -prune
    find "${a}" -name '*.pdb' -delete
    find "${a}" -name '*.lib' -delete

    d="${pkgdir}/docs${_apparch}/${_realname}"
    mkdir -p "${d}"
    mv "${a}"/COPYING* "${a}"/*.ini "${a}"/{CREDITS,EXCEPTIONS-CLIENT,README} "${d}"
}

package_app-i686-mariadb() {
    _apparch=32
    _package "32"
}

package_app-x86_64-mariadb() {
    _apparch=64
    _package "x64"
}
