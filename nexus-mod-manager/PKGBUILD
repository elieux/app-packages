# Maintainer: David Macek <david.macek.0@gmail.com>

_realname=nexus-mod-manager
_choconame=nmm
pkgbase="app-${_realname}"
pkgname=("app-i686-${_realname}")
epoch=1
pkgver=0.80.10
pkgrel=1
pkgdesc="Game modifications manager originally from Nexus Mods"
url="https://www.nexusmods.com/site/mods/4"
arch=('any')
license=('GPL2')
makedepends=("app-innounp")
provides=("${pkgbase}")
options=(!strip)
source=("NMM.exe::https://github.com/Nexus-Mods/Nexus-Mod-Manager/releases/download/${pkgver}/NMM-${pkgver}.exe")
sha512sums=('ad36bed80dd9a66ebbf516ea77a77276cda5d64c1367ac7af3557d4fb5c467d3ec2c0b3cb96eccee057e87e80be94809b6c2ed187efd8c04aa7143e7d2048bf3')

_lc() {
    local d f n

    for f in "${@}"
    do
        d="$(dirname "${f}")"
        f="$(basename "${f}")"
        n="$(echo "${f}" | tr 'A-Z' 'a-z')"
        if test "${f}" != "${n}"
        then
            mv "${d}/${f}" "${d}/${n}"
        fi
    done
}

prepare() {
    /apps/innounp/innounp -x -q -m -a -y "NMM.exe" -d"NMM.dir"
}

package() {
    local a="${pkgdir}/apps/${_realname}"
    mkdir -p "${a}"
    cp -R "NMM.dir/{app}"/* "${a}"
    _lc "${a}"/*
}
