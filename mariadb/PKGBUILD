# Maintainer: David Macek <david.macek.0@gmail.com>

_realname=mariadb
pkgbase="app-${_realname}"
pkgname=("app-x86_64-${_realname}")
epoch=1
pkgver=10.5.6
pkgrel=1
pkgdesc="Fast SQL database server, drop-in replacement for MySQL"
url="https://mariadb.org/"
arch=('any')
license=('GPL')
replaces=("app-i686-${_realname}")
provides=("${pkgbase}")
options=(!strip)
source=("mariadb-winx64.zip::rsync://rsync.osuosl.org/mariadb/mariadb-${pkgver}/winx64-packages/mariadb-${pkgver}-winx64.zip")
sha512sums=('e5549b827a43b11d4859683cdd153258e802d6b8b4b74a6acac7cfcd4755adec3879f6450d75a5040d7bc3a9c5c0bce790399c2463609b652b8d6f0733301eaa')

_rmmsdll() {
    local ms

    ms=("msvbvm[0-9]+"
        "ucrtbase"
        "mfcm?[0-9]+[a-z]+"
        "msvc[rp][0-9]+(_([0-9]|codecvt_ids))?"
        "vc([ao]mp|corlib|runtime)[0-9]+(_[0-9])?"
        "concrt[0-9]+"
        "pgort[0-9]+"
        "api-ms-win-[0-9a-z-]+")
    find "${1}" -iname '*.dll' | grep -Eif <(for m in "${ms[@]}"; do echo "/${m}[.]dll$"; done) | xargs -r rm
}

package() {
    local a="${pkgdir}/apps/${_realname}"
    mkdir -p "${a}"
    cp -R "mariadb-${pkgver}-winx64"/* "${a}"
    find "${a}" -name '*.pdb' -delete
    _rmmsdll "${a}"
}
