# Maintainer: David Macek <david.macek.0@gmail.com>

_realname=mp3tag
_choconame=mp3tag
pkgbase="app-${_realname}"
pkgname=("app-i686-${_realname}")
epoch=1
pkgver=3.06a
pkgrel=1
pkgdesc="Powerful audio metadata editor"
arch=('any')
url="https://mp3tag.de/en/index.html"
license=('custom')
makedepends=("p7zip")
provides=("${pkgbase}")
options=(!strip)
source=("mp3tagsetup.exe::https://download.mp3tag.de/mp3tagv${pkgver//./}setup.exe")
sha512sums=('2aae6f9f509c06b93cbdc5c48c365767f616ccf5a01ec26ef82e085d228d02c21d3c3a98b116f4081087810d65ada077689376db97212f140738acff775befcb')

_lc() {
    local d f n

    for f in "${@}"
    do
        d="$(dirname "${f}")"
        f="$(basename "${f}")"
        n="$(echo "${f}" | tr 'A-Z' 'a-z')"
        if test "${f}" != "${n}"
        then
            mv "${d}/${f}" "${d}/${n}"
        fi
    done
}

prepare() {
    local w f
    w="*license*.txt"
    7z x -y -bd -x!"${w}" "mp3tagsetup.exe" -o"mp3tagsetup.dir" > /dev/null
    for f in $(7z l -y -bd -i!"${w}" "mp3tagsetup.exe" | grep 'license.*txt' | sed -r 's/[ ]+/\t/g' | cut -f 5 | sed 's/^.*\\//')
    do
        7z e -y -bd -so -i!"*${f}" "mp3tagsetup.exe" > "mp3tagsetup.dir/${f}"
    done
}

package() {
    local a="${pkgdir}/apps/${_realname}"
    mkdir -p "${a}" "${a}/help"
    cp -R "mp3tagsetup.dir"/* "${a}"
    touch "${a}/help/helpmap.idx"
    rm -R "${a}"/'$PLUGINSDIR' "${a}"/'$R0' "${a}/mp3taguninstall.exe.nsis"
    _lc "${a}"/*
}
