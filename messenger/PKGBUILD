# Maintainer: David Macek <david.macek.0@gmail.com>

_realname=messenger
pkgbase="app-${_realname}"
pkgname=("app-i686-${_realname}")
epoch=1
pkgver=2.0.9
pkgrel=1
pkgdesc="Beautiful desktop client for Facebook Messenger. Chat without being distracted by your feed or notifications."
url="http://messengerfordesktop.com/"
arch=('any')
license=('MIT')
makedepends=("unzip")
provides=("${pkgbase}")
options=(!strip)
source=("messengerfordesktop-full.nupkg::https://github.com/Aluxian/Facebook-Messenger-Desktop/releases/download/v${pkgver}/messengerfordesktop-${pkgver}-full.nupkg")
sha512sums=('4288e4a24ff6776890eac8b4810f94f11a90ee67a5b16b92c06834731f7ee9b449e66cb736b6063c0e02448183f105d4922b004ab8e779569d28a6729a4b6d52')

_lc() {
    local d f n

    for f in "${@}"
    do
        d="$(dirname "${f}")"
        f="$(basename "${f}")"
        n="$(echo "${f}" | tr 'A-Z' 'a-z')"
        if test "${f}" != "${n}"
        then
            mv "${d}/${f}" "${d}/${n}"
        fi
    done
}

prepare() {
    _lc "lib/net45"/*
}

package() {
    a="${pkgdir}/apps/${_realname}"
    mkdir -p "${a}"
    cp -r "lib/net45"/* "${a}"
    mv "${a}/messenger%20for%20desktop.exe" "${a}/messenger.exe"
}
