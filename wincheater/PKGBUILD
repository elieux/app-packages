# Maintainer: David Macek <david.macek.0@gmail.com>

_status=("broken" "dependencies")
_realname=wincheater
_apparch=32
pkgbase="app-${_realname}"
pkgname=("app-i686-${_realname}")
pkgver=2.07
pkgrel=3
pkgdesc="Video game cheats and guides database viewer"
url="http://www.cheater.cz/winch/win_uvod.htm"
arch=('any')
license=('custom')
depends=("app-i686-wincheater-data-base")
optdepends=("app-i686-wincheater-data-libs: Databases")
makedepends=("app-i686-innounp")
provides=("${pkgbase}")
options=(!strip)
source=("WinCH2_onlysetup.exe::http://www.cheater.cz/download/WinCH2_onlysetup.exe")
sha512sums=('0f79ab4e2a7767b293b667c5f43b8e00f50795e21b7ec39b1a71971d56e16c359a0619056ef4a1e68a31402d1a2b348b683a67741d8fdab8363ced0382b46e67')

_lc() {
    local d f n

    for f in "${@}"
    do
        d="$(dirname "${f}")"
        f="$(basename "${f}")"
        n="$(echo "${f}" | tr 'A-Z' 'a-z')"
        if test "${f}" != "${n}"
        then
            mv "${d}/${f}" "${d}/${n}"
        fi
    done
}

pkgver() {
    strings -d -e{b,l} "WinCH2_onlysetup.exe" | grep 'FileDescription' -A 1 | tail -1 | sed -r 's/^Scorpions WinCheater ([0-9]+[.][0-9]+) [(]pouze program[)] Setup/\1/' | tr -d ' '
}

prepare() {
    /apps32/innounp/innounp -x -q -m -a -y "WinCH2_onlysetup.exe"
    _lc "{app}"/*
}

package() {
    a="${pkgdir}/apps${_apparch}/${_realname}"
    mkdir -p "${a}"
    cp -r "{app}"/* "${a}"
    rm "${a}/texty.sct"
}
