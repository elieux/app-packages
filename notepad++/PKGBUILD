# Maintainer: David Macek <david.macek.0@gmail.com>

_realname=notepad++
pkgbase="app-${_realname}"
pkgname=("app-x86_64-${_realname}")
epoch=1
pkgver=7.5.5
pkgrel=1
pkgdesc="A free Scintilla-based editor with plugin support"
url="https://notepad-plus-plus.org/"
arch=('any')
license=('GPL')
replaces=("app-i686-${_realname}")
makedepends=("p7zip" "unzip")
provides=("${pkgbase}")
options=(!strip)
source=("npp.bin64.zip::https://notepad-plus-plus.org/repository/${pkgver%%.*}.x/${pkgver}/npp.${pkgver}.bin.x64.zip"
        "npp.Installer64.exe::https://notepad-plus-plus.org/repository/${pkgver%%.*}.x/${pkgver}/npp.${pkgver}.Installer.x64.exe")
sha512sums=('9e800c47076d7a92c9e58e900bbd49ab4616d50039c51ea24a5a8cfb9c423d0f2b8082f214a262c5d7dd1b85f2a41b29285fc8a6d1b926429dea168e14b8b8c3'
            '4ffed4d8db83ca5b85a80b1a7d60acac2122117e3b4f5762702f3e1ce0ee9d4368465ca197da9a8755a4404acea70c781d8fb28c989ece92c7f08840fb2f8f82')
noextract=("npp.bin64.zip")

_lc() {
    local d f n

    for f in "${@}"
    do
        d="$(dirname "${f}")"
        f="$(basename "${f}")"
        n="$(echo "${f}" | tr 'A-Z' 'a-z')"
        if test "${f}" != "${n}"
        then
            mv "${d}/${f}" "${d}/${n}"
        fi
    done
}

prepare() {
    unzip -o "npp.bin64.zip" -d "npp"
    rm -f "npp/nppshell_06*.dll"
    echo u | 7z x -bd -i!"NppShell_06.dll" -o"npp" "npp.Installer64.exe"
    _lc "npp"/*
    for f in "npp"/nppshell_06*.dll
    do
        case "$(file "${f}")" in
        64:*x86-64*) mv "${f}" "npp/nppshell_06.dll" ;;
        64:*80386*) rm "${f}" ;;
        esac
    done
}

package() {
    a="${pkgdir}/apps/${_realname}"
    mkdir -p "${a}"
    cp -r "npp"/* "${a}"
    rm "${a}/dolocalconf.xml"
}
