# Maintainer: David Macek <david.macek.0@gmail.com>

_realname=shotcut
_choconame=Shotcut
pkgbase="app-${_realname}"
pkgname=("app-x86_64-${_realname}")
epoch=1
pkgver=22.04.25
pkgrel=1
pkgdesc="Cross-platform, open-source video editor"
url="https://shotcut.org/"
arch=('any')
license=('GPL3')
provides=("${pkgbase}")
options=(!strip)
source=("shotcut-win64.zip::https://github.com/mltframework/shotcut/releases/download/v${pkgver}/shotcut-win64-${pkgver//./}.zip")
sha512sums=('82adcfdb591ade81503a1d9ca3087872550e5e6c0ef9080a62f47fc268c9381369391ef9f7318b48f319f8810f00ee16a15ff98d0c47e80e026c0e81928ea555')

_lc() {
    local d f n

    for f in "${@}"
    do
        d="$(dirname "${f}")"
        f="$(basename "${f}")"
        n="$(echo "${f}" | tr 'A-Z' 'a-z')"
        if test "${f}" != "${n}"
        then
            mv "${d}/${f}" "${d}/${n}"
        fi
    done
}

package() {
    local a="${pkgdir}/apps/${_realname}"
    mkdir -p "${a}"
    cp -R "Shotcut"/* "${a}"
    _lc "${a}"/*
}
