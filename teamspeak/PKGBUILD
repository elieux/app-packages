# Maintainer: David Macek <david.macek.0@gmail.com>

_realname=teamspeak
pkgbase="app-${_realname}"
pkgname=("app-x86_64-${_realname}")
epoch=1
pkgver=3.1.8
pkgrel=1
pkgdesc="Quality voice communication via the Internet"
url="http://www.teamspeak.com/"
arch=('any')
license=('custom')
replaces=("app-i686-${_realname}")
makedepends=("p7zip")
provides=("${pkgbase}")
options=(!strip)
source=("TeamSpeak3-Client-win64.exe::http://dl.4players.de/ts/releases/${pkgver}/TeamSpeak3-Client-win64-${pkgver}.exe"
        # "pluginsdk.zip::http://dl.4players.de/ts/client/pluginsdk/pluginsdk_${pkgver}.zip"
)
sha512sums=('7bb09c43a822622af7415f68a65f9bbbc40d65192f45c4c034021f40c87402cb249e724b326bdd5e3ba97811e9d1019149f55d07db542c045c4ac70b09f12cbf')

_lc() {
    local d f n

    for f in "${@}"
    do
        d="$(dirname "${f}")"
        f="$(basename "${f}")"
        n="$(echo "${f}" | tr 'A-Z' 'a-z')"
        if test "${f}" != "${n}"
        then
            mv "${d}/${f}" "${d}/${n}"
        fi
    done
}

prepare() {
    w="*license*.rtf"
    7z x -y -bd -x!"${w}" "TeamSpeak3-Client-win64.exe" -o"win"
    for f in $(7z l -y -bd -i!"${w}" "TeamSpeak3-Client-win64.exe" | grep 'license.*rtf' | sed -r 's/[ ]+/\t/g' | cut -f 5 | sed 's/^.*\\//')
    do
        7z e -y -bd -so -i!"*${f}" "TeamSpeak3-Client-win.exe" > "win/${f}"
    done
    _lc "win"/*
}

package() {
    a="${pkgdir}/apps/${_realname}"
    mkdir -p "${a}"
    cp -r "win"/* "${a}"
    rm -r "${a}/update.exe" "${a}/uninstall.exe.nsis" "${a}"/'$pluginsdir'
    mv "${a}/ts3client_win64.exe" "${a}/ts3client.exe"
}
