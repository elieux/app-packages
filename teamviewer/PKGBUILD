# Maintainer: David Macek <david.macek.0@gmail.com>

_realname=teamviewer
_apparch=32
pkgbase="app-${_realname}"
pkgname=("app-i686-${_realname}")
pkgver=11.0.62308
pkgrel=1
pkgdesc="All-in-one software for remote support and online meetings"
url="http://www.teamviewer.com/"
arch=('any')
license=('custom')
makedepends=("unzip")
provides=("${pkgbase}")
options=(!strip)
source=("TeamViewerPortable.zip::http://download.teamviewer.com/download/TeamViewerPortable.zip")
sha512sums=('7b801b0a4af3e7d21883fdda632fc140654eea2520d4524cc963e77da0866a8dd4f988ae7113bc61c4f6ed14ef7526783ba7864c5993bf893a778cda2ffe6798')

_lc() {
    for f in "${@}"
    do
        n="$(echo "${f}" | tr 'A-Z' 'a-z')"
        if test "${f}" != "${n}"
        then
            mv "${f}" "${n}"
        fi
    done
}

pkgver() {
    b="$(cygpath -wa "TeamViewer.exe" | sed 's/\\/\\\\/g')"
    wmic datafile where name=\""${b}"\" get version | grep 'Version' -A 1 | tail -1 | tr -d ' ' | sed -r 's/([0-9]+[.][0-9]+[.][0-9]+)[.]0$/\1/'
}

prepare() {
    _lc *
}

package() {
    a="${pkgdir}/apps${_apparch}/${_realname}"
    mkdir -p "${a}"
    cp *.{dll,exe,ini} "${a}"

    d="${pkgdir}/docs${_apparch}/${_realname}"
    mkdir -p "${d}"
    cp *.{txt,inf} "${d}"
}
