# Maintainer: David Macek <david.macek.0@gmail.com>

_realname=openjdk
_realver=bd75d5f9689641da8e1daabeccb5528b/36
_choconame=openjdk
pkgbase="app-${_realname}"
pkgname=("app-x86_64-${_realname}")
epoch=1
pkgver=25.0.0
pkgrel=1
pkgdesc="Open Java Development Kit"
url="https://openjdk.org/"
arch=('any')
license=('custom')
replaces=("app-i686-jdk" "app-x86_64-jdk")
provides=("${pkgbase}")
options=(!strip)
source=("openjdk_windows-x64_bin.zip::https://download.java.net/java/GA/jdk${pkgver%.0.0}/${_realver}/GPL/openjdk-${pkgver%.0.0}_windows-x64_bin.zip")
sha512sums=('65e6b067d0c7816c41e00a3ba6b9add3982d9cb901738bfdc5f2ca5aa307031df02b6e7dd2e162d7c9dc33fba6e513876213aa78d045480cf51fbc6af0276405')

_rmmsdll() {
    local ms

    ms=("msvbvm[0-9]+"
        "ucrtbase"
        "mfcm?[0-9]+[a-z]+"
        "msvc[rp][0-9]+(_([0-9]|codecvt_ids|atomic_wait))?"
        "vc([ao]mp|corlib|runtime)[0-9]+(_([0-9]|threads))?"
        "concrt[0-9]+"
        "pgort[0-9]+"
        "api-ms-win-[0-9a-z-]+")
    find "${1}" -iname '*.dll' | grep -Eif <(for m in "${ms[@]}"; do echo "/${m}[.]dll$"; done) | xargs -r rm
}

package() {
    local a="${pkgdir}/apps/${_realname}"
    mkdir -p "${a}"
    cp -R "jdk-${pkgver%.0.0}"/* "${a}"
    _rmmsdll "${a}"
}
