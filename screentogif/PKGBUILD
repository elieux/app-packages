# Maintainer: David Macek <david.macek.0@gmail.com>

_realname=screentogif
pkgbase="app-${_realname}"
pkgname=("app-i686-${_realname}")
epoch=1
pkgver=2.27
pkgrel=1
pkgdesc="Screen, webcam and sketchboard recorder with an integrated editor"
url="https://www.screentogif.com/"
arch=('any')
license=('MS-PL')
provides=("${pkgbase}")
options=(!strip)
source=("ScreenToGif.Portable.zip::https://github.com/NickeManarin/ScreenToGif/releases/download/${pkgver}/ScreenToGif.${pkgver}.Portable.zip")
sha512sums=('54dc1122de3c8b16cbfa8fb1c0d0789b2a371aa4705d787b9c72bd11088ed4d1fbb5cb598c718718f26dc73ef728894f57516fca1a1e8270e15264ef225d2e18')

_lc() {
    local d f n

    for f in "${@}"
    do
        d="$(dirname "${f}")"
        f="$(basename "${f}")"
        n="$(echo "${f}" | tr 'A-Z' 'a-z')"
        if test "${f}" != "${n}"
        then
            mv "${d}/${f}" "${d}/${n}"
        fi
    done
}

package() {
    local a="${pkgdir}/apps/${_realname}"
    mkdir -p "${a}"
    cp "ScreenToGif.exe" "${a}"
    _lc "${a}"/*
}
