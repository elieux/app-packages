# Maintainer: David Macek <david.macek.0@gmail.com>

_realname=winmerge
_choconame=winmerge
pkgbase="app-${_realname}"
pkgname=("app-x86_64-${_realname}")
epoch=1
pkgver=2.16.24
pkgrel=1
pkgdesc="A differencing and merging tool"
url="https://winmerge.org/"
arch=('any')
license=('GPL')
replaces=("app-i686-${_realname}" "app-i686-winmerge-plugin-7z")
makedepends=("app-innounp")
provides=("${pkgbase}" "app-winmerge-plugin-7z" "app-x86_64-winmerge-plugin-7z")
options=(!strip)
source=("WinMerge-Setup.exe::https://github.com/WinMerge/winmerge/releases/download/v${pkgver}/WinMerge-${pkgver}-Setup.exe"
        "winmerge-x64-exe.zip::https://github.com/WinMerge/winmerge/releases/download/v${pkgver}/winmerge-${pkgver}-x64-exe.zip")
sha512sums=('ad1629db60a186275c388452c493d7db1cae9a779c93b51cca3c02f6f90d004eafc6ea61036479272f41bd4911f032274fabac25b4746264d7a677cca8595eb8'
            '346c710850e39cc6b0b562caead3e3f769d8cdf4fe912b08dd19be55eadcedbc51844cad1da10c0873d82254e96fef8ac2993b2695dd31250d195b408d71a438')

_lc() {
    local d f n

    for f in "${@}"
    do
        d="$(dirname "${f}")"
        f="$(basename "${f}")"
        n="$(echo "${f}" | tr 'A-Z' 'a-z')"
        if test "${f}" != "${n}"
        then
            mv "${d}/${f}" "${d}/${n}"
        fi
    done
}

prepare() {
    /apps/innounp/innounp -x -q -m -a -y "WinMerge-Setup.exe" -d"WinMerge-Setup.dir"
}

package() {
    local a="${pkgdir}/apps/${_realname}"
    mkdir -p "${a}"
    cp -R "WinMerge"/* "${a}"
    cp -R "WinMerge-Setup.dir/{app}/LogoImages" "WinMerge-Setup.dir/{app}/WinMergeU.VisualElementsManifest.xml" "${a}"
    rm "${a}/vcomp140.dll"
    _lc "${a}"/*
}
