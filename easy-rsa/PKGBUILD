# Maintainer: David Macek <david.macek.0@gmail.com>

_realname=easy-rsa
pkgbase="app-${_realname}"
pkgname=("app-x86_64-${_realname}")
epoch=1
pkgver=3.1.7
pkgrel=1
pkgdesc="Simple shell based CA utility"
url="https://community.openvpn.net/openvpn#Easy-rsasubproject"
arch=('any')
license=('custom')
provides=("${pkgbase}")
replaces=("app-i686-${_realname}")
options=(!strip)
source=("EasyRSA-win64.zip::https://github.com/OpenVPN/easy-rsa/releases/download/v${pkgver}/EasyRSA-${pkgver}-win64.zip")
sha512sums=('f212685274b6cc49a86acd48dae6eaaed1b94576db040c29d510583f26f2827d78a31d566c25f870bff261d601535a96c48333799bc8f4268a1fd2ff621e966b')

_lc() {
    local d f n

    for f in "${@}"
    do
        d="$(dirname "${f}")"
        f="$(basename "${f}")"
        n="$(echo "${f}" | tr 'A-Z' 'a-z')"
        if test "${f}" != "${n}"
        then
            mv "${d}/${f}" "${d}/${n}"
        fi
    done
}

package() {
    local a="${pkgdir}/apps/${_realname}"
    mkdir -p "${a}"
    cp -R "EasyRSA-${pkgver}"/* "${a}"
    _lc "${a}"/*
}
