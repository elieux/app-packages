# Maintainer: David Macek <david.macek.0@gmail.com>

_realname=1password
_choconame=1password
pkgbase="app-${_realname}"
pkgname=("app-x86_64-${_realname}")
epoch=1
pkgver=8.12.2.37
pkgrel=1
pkgdesc="Password manager and secure wallet"
url="https://1password.com/downloads"
arch=('any')
license=('custom')
makedepends=("app-lessmsi")
provides=("${pkgbase}")
options=(!strip)
source=("1PasswordSetup-latest.msi::https://downloads.1password.com/win/1PasswordSetup-latest.msi")
sha512sums=('f775a90ec36a7c3dc7d21cb93b8585a0f709a98002052392c2dffb4a76557d91ebbdba316636e582014857fcb186001b540995a1157d70a94a2a4972c72dca0d')
noextract=("1PasswordSetup-latest.exe")

_lc() {
    local d f n

    for f in "${@}"
    do
        d="$(dirname "${f}")"
        f="$(basename "${f}")"
        n="$(echo "${f}" | tr 'A-Z' 'a-z')"
        if test "${f}" != "${n}"
        then
            mv "${d}/${f}" "${d}/${n}"
        fi
    done
}

prepare() {
    rm -fR "1PasswordSetup-latest"
    /apps/lessmsi/lessmsi x "1PasswordSetup-latest.msi" > /dev/null
}

pkgver() {
    powershell -command '(Get-Item "1PasswordSetup-latest/SourceDir/ProgramFiles64Folder/1Password/app/8/1Password.exe").VersionInfo.FileVersion'
}

package() {
    local a="${pkgdir}/apps/${_realname}"
    mkdir -p "${a}"
    cp -R "1PasswordSetup-latest/SourceDir/ProgramFiles64Folder/1Password/app/8"/* "${a}"
    _lc "${a}"/*
}
