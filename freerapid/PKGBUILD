# Maintainer: David Macek <david.macek.0@gmail.com>

_realname=freerapid
_apparch=32
pkgbase="app-${_realname}"
pkgname=("app-i686-${_realname}")
pkgver=0.9u4
pkgrel=4
pkgdesc="A Java-based download manager for Rapidshare and other file sharing archives"
arch=('any')
url="http://wordrider.net/freerapid/"
license=('GPL')
makedepends=("unzip")
provides=("${pkgbase}")
options=(!strip)
source=("FreeRapid.zip::http://files.kral.hk/frd/FreeRapid-${pkgver}.zip")
sha512sums=('dc770d55d79a72b10bba3b0612e4c6c3cb1dd79071a35c55028664c67340cdef4c067a18fa637f588cc4c37dd02f18638a872b99441762a8733c152c21275da9')

_lc() {
    local d f n

    for f in "${@}"
    do
        d="$(dirname "${f}")"
        f="$(basename "${f}")"
        n="$(echo "${f}" | tr 'A-Z' 'a-z')"
        if test "${f}" != "${n}"
        then
            mv "${d}/${f}" "${d}/${n}"
        fi
    done
}

prepare() {
    _lc "FreeRapid-${pkgver}"/*
}

package() {
    a="${pkgdir}/apps${_apparch}/${_realname}"
    mkdir -p "${a}"
    cp -r "FreeRapid-${pkgver}"/{lib,lookandfeel,search,tools,frd.*,instplgs.dat,objectdb.conf,*.properties} "${a}"
    cp -r "FreeRapid-${pkgver}"/{doc,copyright,license,readme.txt} "${a}"
}
