# Maintainer: David Macek <david.macek.0@gmail.com>

_realname=tortoise-overlays
_realver=2.0.0.0
pkgbase="app-${_realname}"
pkgname=("app-x86_64-${_realname}")
epoch=1
pkgver=1.1.4.26626
pkgrel=1
pkgdesc="Overlays component for Tortoise tools"
url="https://tortoisesvn.net"
arch=('any')
license=('custom')
replaces=("app-i686-${_realname}")
makedepends=("p7zip")
provides=("${pkgbase}")
options=(!strip)
source=("TortoiseGit-64bit.msi::https://download.tortoisegit.org/tgit/${_realver}/TortoiseGit-${_realver}-64bit.msi")
sha512sums=('0073ceb7c08fbc0e3c1506abb6c00ba925b6069807eb9a7d7bc4626cec6c0097ace4ebafb24dd0e5ffb47bb76b87aed03014933faf05777ba02ccb808b3cbdd5')

_lc() {
    local d f n

    for f in "${@}"
    do
        d="$(dirname "${f}")"
        f="$(basename "${f}")"
        n="$(echo "${f}" | tr 'A-Z' 'a-z')"
        if test "${f}" != "${n}"
        then
            mv "${d}/${f}" "${d}/${n}"
        fi
    done
}

pkgver() {
    7z e -y -bd "TortoiseGit-64bit.msi" "F__overlaydll.3C89127D_F335_46EB_8720_CAFE0C0FEB7E" > /dev/null
    b="$(cygpath -wa "F__overlaydll.3C89127D_F335_46EB_8720_CAFE0C0FEB7E" | sed 's/\\/\\\\/g')"
    wmic datafile where name=\""${b}"\" get version | grep 'Version' -A 1 | tail -1 | tr -d ' ' | sed -r 's/^([0-9]+[.][0-9]+[.][0-9]+[.][0-9]+)$/\1/'
}

build() {
    msiexec -a "TortoiseGit-64bit.msi" "TARGETDIR=$(cygpath -wa "install")" -qn
    _lc "install/Program Files/TortoiseGit/Common/TortoiseOverlays"/*
}

package() {
    a="${pkgdir}/apps/${_realname}"
    mkdir -p "${a}"
    cp -r "install/Program Files/TortoiseGit/Common/TortoiseOverlays"/* "${a}"
}
