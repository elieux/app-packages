# Maintainer: David Macek <david.macek.0@gmail.com>

_realname=f.lux
pkgbase="app-${_realname}"
pkgname=("app-i686-${_realname}")
epoch=1
pkgver=4.118.0.0
pkgrel=1
pkgdesc="Changes the display color temperature according to the time of the day"
url="https://justgetflux.com/"
arch=('any')
license=('custom')
makedepends=("p7zip")
provides=("${pkgbase}")
replaces=("app-i686-f-lux")
options=(!strip)
source=("flux-setup.exe::https://justgetflux.com/flux-setup.exe")
sha512sums=('f42068e8a499ca89a8ccfa4b86e8547ceb8e7335909087e1ab17d38bc409b0a53a0e8d65ff186e5acd7d1f19991d03ea021eb09d606a5aa11c08d9e94bf03c84')

prepare() {
    7z x -y -bd "flux-setup.exe" -o"flux-setup.dir" > /dev/null
}

pkgver() {
    powershell -command '(Get-Item "flux-setup.dir/flux.exe").VersionInfo.FileVersionRaw.ToString()'
}

package() {
    local a="${pkgdir}/apps/${_realname}"
    mkdir -p "${a}"
    cp -R "flux-setup.dir"/* "${a}"
    rm -R "${a}"/'$PLUGINSDIR'
}
