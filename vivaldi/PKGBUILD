# Maintainer: David Macek <david.macek.0@gmail.com>

_realname=vivaldi
_apparch=32
pkgbase="app-${_realname}"
pkgname=("app-i686-${_realname}")
pkgver=1.0.344.37
pkgrel=1
pkgdesc="A new browser for our friends"
url="https://vivaldi.com/"
arch=('any')
license=('custom')
makedepends=("p7zip")
provides=("${pkgbase}")
options=(!strip)
source=("Vivaldi.exe::https://vivaldi.com/download/stable/Vivaldi.${pkgver}.exe")
sha512sums=('f44c76dacee9ed3c5d35c8796c6a4b0f0e4744eaeddfbfaa6917667e312db35ce3a0d21978481864b2d0dbe4cb91214b2507b76e62bd156d9aa30d1c2f7608f8')

_lc() {
    for f in "${@}"
    do
        n="$(echo "${f}" | tr 'A-Z' 'a-z')"
        if test "${f}" != "${n}"
        then
            mv "${f}" "${n}"
        fi
    done
}

prepare() {
    7z x -y -bd "Vivaldi.exe"
    7z x -y -bd "vivaldi.7z"
    _lc "Vivaldi-bin"/* "Vivaldi-bin/${pkgver}"/*
}

package() {
    a="${pkgdir}/apps${_apparch}/${_realname}"
    mkdir -p "${a}"
    cp -r "Vivaldi-bin"/*.exe "Vivaldi-bin/${pkgver}"/* "${a}"
}
