# Maintainer: David Macek <david.macek.0@gmail.com>

_realname=consolez
pkgbase="app-${_realname}"
pkgname=("app-i686-${_realname}"
         "app-x86_64-${_realname}")
pkgver=1.17.1.16266
pkgrel=1
pkgdesc="A console wrapper with tabs, easy copy-pasting and pretty visuals"
url="http://github.com/cbucher/console"
arch=('any')
license=('GPL2')
makedepends=("unzip")
provides=("${pkgbase}")
options=(!strip)
source=("ConsoleZ32.zip::https://github.com/cbucher/console/releases/download/${pkgver%.*}/ConsoleZ.x86.${pkgver}.zip"
        "ConsoleZ64.zip::https://github.com/cbucher/console/releases/download/${pkgver%.*}/ConsoleZ.x64.${pkgver}.zip")
sha512sums=('05ac5ecab1bde4614a4bc274e8f8a856e1d5abd1e4485bfc9ef8321f92862cb63caffae9e7232f7b93c17edb0f4f255cc15cbbd5d49e3a99f008bfb53d532f03'
            '2fb9bed227b6f85a7feb5a25d900e95d53fcd7d9a3f1cbae794b0707cb519b5f8d6d89f7c8780a6f6077c7cce9936cce5b686322a812c084e62cfc11682cdc69')
noextract=("ConsoleZ32.zip"
           "ConsoleZ64.zip")

_lc() {
    for f in "${@}"
    do
        n="$(echo "${f}" | tr 'A-Z' 'a-z')"
        if test "${f}" != "${n}"
        then
            mv "${f}" "${n}"
        fi
    done
}

prepare() {
    for _apparch in 32 64
    do
        unzip -o "ConsoleZ${_apparch}.zip" -d "consolez${_apparch}"
        _lc "consolez${_apparch}"/*
    done
}

_package() {
    a="${pkgdir}/apps${_apparch}/${_realname}"
    mkdir -p "${a}"
    cp "consolez${_apparch}"/*.{exe,dll} "${a}"

    d="${pkgdir}/docs${_apparch}/${_realname}"
    mkdir -p "${d}"
    cp "consolez${_apparch}"/*.chm "${d}"
}

package_app-i686-consolez() {
    _apparch=32
    _package
}

package_app-x86_64-consolez() {
    _apparch=64
    _package
}
