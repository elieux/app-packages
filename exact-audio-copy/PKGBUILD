# Maintainer: David Macek <david.macek.0@gmail.com>

_realname=exact-audio-copy
pkgbase="app-${_realname}"
pkgname=("app-i686-${_realname}")
epoch=1
pkgver=1.5
pkgrel=1
pkgdesc="A very accurate CD audio grabber"
url="http://www.exactaudiocopy.de/en/"
arch=('any')
license=('custom')
makedepends=("p7zip")
provides=("${pkgbase}")
options=(!strip)
source=("eac.exe::http://www.exactaudiocopy.de/eac-${pkgver}.exe")
sha512sums=('0aaad97b4436ce2e66825599925631c13fdf1a97a15a91545bd70ba15027667622cc2b3c916406deb0428c6a43ff1729f7da5f11c7a390dfaf4f2d36334eea4a')

_lc() {
    local d f n

    for f in "${@}"
    do
        d="$(dirname "${f}")"
        f="$(basename "${f}")"
        n="$(echo "${f}" | tr 'A-Z' 'a-z')"
        if test "${f}" != "${n}"
        then
            mv "${d}/${f}" "${d}/${n}"
        fi
    done
}

prepare() {
    7z x -y -bd "eac.exe" -o"eac.dir" > /dev/null
}

package() {
    local a="${pkgdir}/apps/${_realname}"
    mkdir -p "${a}"
    cp -R "eac.dir"/* "${a}"
    rm -R "${a}"/'$PLUGINSDIR' "${a}/Microsoft.VC80.CRT"
    _lc "${a}"/*
}
