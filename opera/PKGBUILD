# Maintainer: David Macek <david.macek.0@gmail.com>

_realname=opera
pkgbase="app-${_realname}"
pkgname=("app-i686-${_realname}"
         "app-x86_64-${_realname}")
pkgver=44.0.2510.1449
pkgrel=1
pkgdesc="A fast and free Blink-based web browser"
url="http://www.opera.com/computer"
arch=('any')
license=('custom')
makedepends=("p7zip")
provides=("${pkgbase}")
options=(!strip)
source=("Opera_Setup_x32.exe::http://ftp.opera.com/pub/opera/desktop/${pkgver}/win/Opera_${pkgver}_Setup.exe"
        "Opera_Setup_x64.exe::http://ftp.opera.com/pub/opera/desktop/${pkgver}/win/Opera_${pkgver}_Setup_x64.exe")
sha512sums=('f36ec8dab53a887de289b83e2e8621c90341d3ad0cd4e7332b606ad5e88ee2cb4d504dd28f73b48d78ee6aac18973aa17f0557f8b2b46fcaa5c8d5aae919c928'
            '2b738057c14b3899228993bfff75f3532f7135abb7c948badd389b61418e6a9fdb4cb418c8073c25be06710c32df54cfd33de12dcc1c9a9a4a681223c60a5909')

_lc() {
    local d f n

    for f in "${@}"
    do
        d="$(dirname "${f}")"
        f="$(basename "${f}")"
        n="$(echo "${f}" | tr 'A-Z' 'a-z')"
        if test "${f}" != "${n}"
        then
            mv "${d}/${f}" "${d}/${n}"
        fi
    done
}

_prepare() {
    7z x -y -bd "Opera_Setup_x${_apparch}.exe" -o"Opera${_apparch}"
    _lc "Opera${_apparch}"/*
}

prepare() {
    for _apparch in 32 64
    do
        _prepare
    done
}

_package() {
    a="${pkgdir}/apps${_apparch}/${_realname}"
    mkdir -p "${a}"
    cp -r "Opera${_apparch}"/* "${a}"
    rm -r "${a}"/{assets,root_files_list,files_list,launcher.exe,launcher.visualelementsmanifest.xml,resources.pri} "${a}"/{msvcp100.dll,msvcr100.dll} "${a}"/{installer.exe,opera_autoupdate.exe,opera_autoupdate.licenses,opera_autoupdate.version}
    rm "${a}"/{msvcp,pgort,vccorlib,vcruntime}140.dll "${a}/ucrtbase.dll"
}

package_app-i686-opera() {
    replaces=("app-i686-opera2")
    conflicts=("app-i686-${_realname}<15")
    _apparch=32
    _package
}

package_app-x86_64-opera() {
    replaces=("app-x86_64-opera2")
    conflicts=("app-x86_64-${_realname}<15")
    _apparch=64
    _package
}
