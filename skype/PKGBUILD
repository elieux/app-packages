# Maintainer: David Macek <david.macek.0@gmail.com>

_realname=skype
_choconame=skype
pkgbase="app-${_realname}"
pkgname=("app-i686-${_realname}")
epoch=1
pkgver=8.68.0.96
pkgrel=1
pkgdesc="P2P software for high-quality voice communication"
url="https://www.skype.com/"
arch=('any')
license=('custom')
makedepends=("app-innounp")
provides=("${pkgbase}")
options=(!strip)
source=("Skype.exe::https://get.skype.com/go/getskype-skypeforwindows")
sha512sums=('acaa871f24ddb7b02f7e0b0045d3e43b9ebd16a25f8e0e59de4c82829c7942bafc94bc9accd72287e91b7210f93579a423c8fa9e18386e6f1065c9833bdf25fa')

_lc() {
    local d f n

    for f in "${@}"
    do
        d="$(dirname "${f}")"
        f="$(basename "${f}")"
        n="$(echo "${f}" | tr 'A-Z' 'a-z')"
        if test "${f}" != "${n}"
        then
            mv "${d}/${f}" "${d}/${n}"
        fi
    done
}

_rmmsdll() {
    local ms

    ms=("msvbvm[0-9]+"
        "ucrtbase"
        "mfcm?[0-9]+[a-z]+"
        "msvc[rp][0-9]+(_([0-9]|codecvt_ids))?"
        "vc([ao]mp|corlib|runtime)[0-9]+(_[0-9])?"
        "concrt[0-9]+"
        "pgort[0-9]+"
        "api-ms-win-[0-9a-z-]+")
    find "${1}" -iname '*.dll' | grep -Eif <(for m in "${ms[@]}"; do echo "/${m}[.]dll$"; done) | xargs -r rm
}

prepare() {
    /apps/innounp/innounp -x -q -m -a -y "Skype.exe" -d"Skype.dir"
}

pkgver() {
    powershell -command '(Get-Item "Skype.exe").VersionInfo.FileVersion'
}

package() {
    local a="${pkgdir}/apps/${_realname}"
    mkdir -p "${a}"
    cp -R "Skype.dir/{app}"/* "${a}"
    _rmmsdll "${a}"
    _lc "${a}"/*
}
