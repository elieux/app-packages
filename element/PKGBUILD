# Maintainer: David Macek <david.macek.0@gmail.com>

_realname=element
_choconame=element-desktop
pkgbase="app-${_realname}"
pkgname=("app-x86_64-${_realname}")
epoch=1
pkgver=1.12.9
pkgrel=1
pkgdesc="A glossy Matrix collaboration client for desktop"
url="https://element.io/download"
arch=('any')
license=('Apache')
makedepends=("p7zip")
provides=("${pkgbase}")
options=(!strip)
source=("Element_Setup.exe::https://packages.element.io/desktop/install/win32/x64/Element%20Setup%20${pkgver}.exe")
sha512sums=('88627ba8db38f03b4034fa9e005217fdb1f954e9e590e32a68b1fe6551b960db87571ace780cc76e333946c5af14cc696b7c6f4dba7fed21682672abfaa1bbf6')
noextract=("Element_Setup.exe")

_lc() {
    local d f n

    for f in "${@}"
    do
        d="$(dirname "${f}")"
        f="$(basename "${f}")"
        n="$(echo "${f}" | tr 'A-Z' 'a-z')"
        if test "${f}" != "${n}"
        then
            mv "${d}/${f}" "${d}/${n}"
        fi
    done
}


prepare() {
    7z x -y -bd "Element_Setup.exe" -o"Element_Setup.dir" > /dev/null
    unzip -q -o "Element_Setup.dir/element-desktop-${pkgver}-full.nupkg" -d "element-desktop-full"
}

package() {
    local a="${pkgdir}/apps/${_realname}"
    mkdir -p "${a}"
    cp -R "element-desktop-full/lib/net45"/* "${a}"
    _lc "${a}"/*
}
