# Maintainer: David Macek <david.macek.0@gmail.com>

_realname=google-drive
_choconame=googledrive
pkgbase="app-${_realname}"
pkgname=("app-x86_64-${_realname}")
epoch=1
pkgver=67.0.2.0
pkgrel=1
pkgdesc="The Google Drive synchronization tool"
url="https://www.google.com/drive/"
arch=('any')
license=('custom')
replaces=("app-i686-${_realname}")
makedepends=("p7zip")
provides=("${pkgbase}")
options=(!strip)
source=("GoogleDriveSetup.exe::https://dl.google.com/drive-file-stream/GoogleDriveSetup.exe")
sha512sums=('e1cb33deb09b3c1bbc36c87a7e601236741535b6b1840e350cda6cdacbc822379b0d5f14c9c3bb2884ffd58c52dc58ba6058a9c94f375157aa10ae728e0e5352')

prepare() {
    7z x -y -bd "GoogleDriveSetup.exe" -o"GoogleDriveSetup.dir" > /dev/null
    7z x -y -bd "GoogleDriveSetup.dir/.rsrc/RCDATA/101" -o"101" > /dev/null
}

pkgver() {
    powershell -command '(Get-Item "101/GoogleDriveFS.exe").VersionInfo.FileVersionRaw.ToString()'
}

package() {
    local a="${pkgdir}/apps/${_realname}"
    mkdir -p "${a}"
    cp -R "101"/* "${a}"
}
