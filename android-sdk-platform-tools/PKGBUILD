# Maintainer: David Macek <david.macek.0@gmail.com>

_dirname=android-sdk
_realname=android-sdk-platform-tools
pkgbase="app-${_realname}"
pkgname=("app-i686-${_realname}")
epoch=1
pkgver=33.0.2
pkgrel=1
pkgdesc="Platform-Tools for Google Android SDK (adb and fastboot)"
url="https://developer.android.com/studio/releases/platform-tools.html"
arch=('any')
license=('custom')
provides=("${pkgbase}")
options=(!strip)
source=("platform-tools-latest-windows.zip::https://dl.google.com/android/repository/platform-tools-latest-windows.zip")
sha512sums=('5a766de247c8041e682f2fb5269080b4519ccca5dfafdaaea8e1af35e144ef09461f843875c92a199f79c42feaa9cf48d0293e0ea562d22fae0a8a72d29a39ab')

_lc() {
    local d f n

    for f in "${@}"
    do
        d="$(dirname "${f}")"
        f="$(basename "${f}")"
        n="$(echo "${f}" | tr 'A-Z' 'a-z')"
        if test "${f}" != "${n}"
        then
            mv "${d}/${f}" "${d}/${n}"
        fi
    done
}

pkgver() {
    cat "platform-tools/source.properties" | grep "^Pkg.Revision" | cut -d= -f2
}

package() {
    local a="${pkgdir}/apps/${_dirname}"
    mkdir -p "${a}"
    cp -R "platform-tools" "${a}"
    _lc "${a}/platform-tools"/*
}
