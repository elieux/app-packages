# Maintainer: David Macek <david.macek.0@gmail.com>

_realname=openjdk
_realver=15+36
pkgbase="app-${_realname}"
pkgname=("app-x86_64-${_realname}")
epoch=1
pkgver="$(echo "${_realver%+*}" | sed -r -e 's/^([0-9]+)$/\1.0/' -e 's/^([0-9]+[.][0-9]+)$/\1.0/' | tr '+' '.').${_realver#*+}"
pkgrel=1
pkgdesc="Open Java Development Kit"
url="https://adoptopenjdk.net/"
arch=('any')
license=('custom')
replaces=("app-i686-jdk" "app-x86_64-jdk")
provides=("${pkgbase}")
options=(!strip)
source=("OpenJDK-jdk_x64_windows_hotspot.zip::https://github.com/AdoptOpenJDK/openjdk${pkgver%%.*}-binaries/releases/download/jdk-${_realver/+/%2B}/OpenJDK${pkgver%%.*}U-jdk_x64_windows_hotspot_${_realver/+/_}.zip")
sha512sums=('c21e4ec303df9efbcb8d0b88194a9a2ec6882880bfa50be85dd9c5cbf04f098a31311d551255268cccc09a97f7325d38d6bdb14f221a5070c0c749be3568647e')

_rmmsdll() {
    local ms

    ms=("msvbvm[0-9]+"
        "ucrtbase"
        "mfcm?[0-9]+[a-z]+"
        "msvc[rp][0-9]+(_([0-9]|codecvt_ids))?"
        "vc([ao]mp|corlib|runtime)[0-9]+(_[0-9])?"
        "concrt[0-9]+"
        "pgort[0-9]+"
        "api-ms-win-[0-9a-z-]+")
    find "${1}" -iname '*.dll' | grep -Eif <(for m in "${ms[@]}"; do echo "/${m}[.]dll$"; done) | xargs -r rm
}

package() {
    local a="${pkgdir}/apps/${_realname}"
    mkdir -p "${a}"
    cp -R "jdk-${_realver}"/* "${a}"
    _rmmsdll "${a}"
}
