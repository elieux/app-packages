# Maintainer: David Macek <david.macek.0@gmail.com>

DLAGENTS=('https::/usr/bin/curl -k -qb "" -fLC - -o %o %u')

_realname=dropbox
_apparch=32
pkgbase="app-${_realname}"
pkgname=("app-i686-${_realname}")
pkgver=3.2.6
pkgrel=2
pkgdesc="The Dropbox synchronization tool"
url="http://www.google.com/drive/"
arch=('any')
license=('custom')
makedepends=("p7zip" "curl")
provides=("${pkgbase}")
options=(!strip) # embedded data, digital signatures
source=("Dropbox.exe::https://www.dropbox.com/download?full=1&plat=win")
md5sums=('77dd0e3f9d53f628e71a47432afae446')

prepare() {
    cd "${srcdir}"
    7z x -ry "Dropbox.exe" > /dev/null
}

package() {
    mkdir -p "${pkgdir}/apps${_apparch}/${_realname}"
    find "${srcdir}" -name 'DropboxExt*.dll' -execdir cp "{}" "${pkgdir}/apps${_apparch}/${_realname}" \;
    find "${srcdir}" -type d -name 'Qt*' -execdir cp -R "{}" "${pkgdir}/apps${_apparch}/${_realname}/" \;
    _dir="$(find "${srcdir}" -mindepth 2 -type f -name 'Dropbox.exe' -printf %h)"
    find "${_dir}" -mindepth 1 -maxdepth 1 -not -name 'DropboxUpdateHelper.exe' -not -name 'DropboxUninstaller.exe' -exec cp -R "{}" "${pkgdir}/apps${_apparch}/${_realname}/" \;

    pushd "${pkgdir}/apps${_apparch}/${_realname}"
    for f in *
    do
        n=$(echo "${f}" | tr 'A-Z' 'a-z')
        if test "${f}" != "${n}"
        then
            mv "${f}" "${n}"
        fi
    done
    popd
}
