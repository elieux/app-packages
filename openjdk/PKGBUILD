# Maintainer: David Macek <david.macek.0@gmail.com>

_realname=openjdk
_realver=24a58e0e276943138bf3e963e6291ac2/9
_choconame=openjdk
pkgbase="app-${_realname}"
pkgname=("app-x86_64-${_realname}")
epoch=1
pkgver=24.0.1
pkgrel=1
pkgdesc="Open Java Development Kit"
url="https://openjdk.org/"
arch=('any')
license=('custom')
replaces=("app-i686-jdk" "app-x86_64-jdk")
provides=("${pkgbase}")
options=(!strip)
source=("openjdk_windows-x64_bin.zip::https://download.java.net/java/GA/jdk${pkgver}/${_realver}/GPL/openjdk-${pkgver}_windows-x64_bin.zip")
sha512sums=('e249c846080e3ad0db71ae7b88314996afd6d7acc2e07345599d584f179b2df22380a1468bd01b499fa16d24512990b8c1f022b67134eb1752c33ec2ade4e296')

_rmmsdll() {
    local ms

    ms=("msvbvm[0-9]+"
        "ucrtbase"
        "mfcm?[0-9]+[a-z]+"
        "msvc[rp][0-9]+(_([0-9]|codecvt_ids|atomic_wait))?"
        "vc([ao]mp|corlib|runtime)[0-9]+(_([0-9]|threads))?"
        "concrt[0-9]+"
        "pgort[0-9]+"
        "api-ms-win-[0-9a-z-]+")
    find "${1}" -iname '*.dll' | grep -Eif <(for m in "${ms[@]}"; do echo "/${m}[.]dll$"; done) | xargs -r rm
}

package() {
    local a="${pkgdir}/apps/${_realname}"
    mkdir -p "${a}"
    cp -R "jdk-${pkgver}"/* "${a}"
    _rmmsdll "${a}"
}
