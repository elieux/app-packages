# Maintainer: David Macek <david.macek.0@gmail.com>

_realname=filezilla
pkgbase="app-${_realname}"
pkgname=("app-i686-${_realname}"
         "app-x86_64-${_realname}")
pkgver=3.16.1
pkgrel=1
pkgdesc="Fast and reliable FTP, FTPS and SFTP client"
url="http://filezilla-project.org/"
arch=('any')
license=('GPL')
makedepends=("unzip")
provides=("${pkgbase}")
options=(!strip)
source=("FileZilla_win32.zip::http://downloads.sourceforge.net/${_realname}/FileZilla_Client/${pkgver}/FileZilla_${pkgver}_win32.zip"
        "FileZilla_win64.zip::http://downloads.sourceforge.net/${_realname}/FileZilla_Client/${pkgver}/FileZilla_${pkgver}_win64.zip")
sha512sums=('e1e81fc6fbed6f60b20c2b2e027a167d43ef2cd8e2e5dbc870cd829f607b68392866cc2e96b7912ee54102449990ef899acf653ed22ec44f5adf429be673a99f'
            '4f1c6561103322f7b90194fe00ae554ced740bec5cbf5f84a52583cb49d2ba1519c6eaf8cf4833dc6e2fb8734a423d258c9cfe8bf6e5be4cf0e14cae1b74fcc1')
noextract=("FileZilla_win32.zip"
           "FileZilla_win64.zip")

prepare() {
    for _apparch in 32 64
    do
        unzip -o "FileZilla_win${_apparch}.zip" -d "filezilla${_apparch}"
    done
}

_package() {
    a="${pkgdir}/apps${_apparch}/${_realname}"
    mkdir -p "${a}"
    cp -r "filezilla${_apparch}/FileZilla-${pkgver}"/* "${a}"

    d="${pkgdir}/docs${_apparch}/${_realname}"
    mkdir -p "${d}"
    mv "${a}/docs" "${a}/AUTHORS" "${a}/NEWS" "${a}/GPL.html" "${d}"
}

package_app-i686-filezilla() {
    _apparch=32
    _package
}

package_app-x86_64-filezilla() {
    _apparch=64
    _package
}
