# Maintainer: David Macek <david.macek.0@gmail.com>

_realname=signal
_choconame=signal
pkgbase="app-${_realname}"
pkgname=("app-x86_64-${_realname}")
epoch=1
pkgver=7.84.0
pkgrel=1
pkgdesc="Desktop Signal client"
url="https://www.signal.org/download/windows/"
arch=('any')
license=('GPL3')
makedepends=("p7zip")
provides=("${pkgbase}")
options=(!strip)
source=("signal-desktop-win.exe::https://updates.signal.org/desktop/signal-desktop-win-${pkgver}.exe")
sha512sums=('152519c7b8cf28641039bd638694f268722e0c0a97d3b72fe7de7c3d8391c2691735957c3965f81b54795df68e8c77eed455bf47e272ab08c87aff0496f92391')
noextract=("signal-desktop-win.exe")

_lc() {
    local d f n

    for f in "${@}"
    do
        d="$(dirname "${f}")"
        f="$(basename "${f}")"
        n="$(echo "${f}" | tr 'A-Z' 'a-z')"
        if test "${f}" != "${n}"
        then
            mv "${d}/${f}" "${d}/${n}"
        fi
    done
}

_rmmsdll() {
    local ms

    ms=("msvbvm[0-9]+"
        "ucrtbase"
        "mfcm?[0-9]+[a-z]+"
        "msvc[rp][0-9]+(_([0-9]|codecvt_ids|atomic_wait))?"
        "vc([ao]mp|corlib|runtime)[0-9]+(_([0-9]|threads))?"
        "concrt[0-9]+"
        "pgort[0-9]+"
        "api-ms-win-[0-9a-z-]+")
    find "${1}" -iname '*.dll' | grep -Eif <(for m in "${ms[@]}"; do echo "/${m}[.]dll$"; done) | xargs -r rm
}

prepare() {
    rm -fR "signal-desktop-win.dir"
    7z x -y -bd "signal-desktop-win.exe" -o"signal-desktop-win.dir" > /dev/null
    7z x -y -bd "signal-desktop-win.dir"/'$PLUGINSDIR'/"app-64.7z" -o"app-64" > /dev/null
}

package() {
    local a="${pkgdir}/apps/${_realname}"
    mkdir -p "${a}"
    cp -R "app-64"/* "${a}"
    _rmmsdll "${a}"
    _lc "${a}"/*
}
