# Maintainer: David Macek <david.macek.0@gmail.com>

_realname=bind
_realver=9.10.4-P1
pkgbase="app-${_realname}"
pkgname=("app-i686-${_realname}"
         "app-x86_64-${_realname}")
pkgver=${_realver//-/.}
pkgrel=1
pkgdesc="The ISC BIND nameserver"
url="https://www.isc.org/downloads/bind/"
arch=('any')
license=('custom:ISC')
makedepends=("unzip")
provides=("${pkgbase}")
options=(!strip)
source=("BIND32.zip::ftp://ftp.isc.org/isc/bind9/${_realver}/BIND${_realver}.x86.zip"
        "BIND64.zip::ftp://ftp.isc.org/isc/bind9/${_realver}/BIND${_realver}.x64.zip")
sha512sums=('8b16e99c4096df61bb487564554cdfd5ba9808b51fd9f0183f5e2963ff4a3e0b8e938af6f5ff606880346f75b66485cbfbd3aad92c8a9cf215ef649cbc530716'
            '8d7dd4c7d854dbd48ca86d5554e7a87301ec342a60a139f9bc63e18d44f609a9f338b0db5f41aeb8e2073ea8c089153c3e87a1d82cc1c122ff92df26bdcb6904')
noextract=("BIND32.zip"
           "BIND64.zip")

_lc() {
    for f in "${@}"
    do
        n="$(echo "${f}" | tr 'A-Z' 'a-z')"
        if test "${f}" != "${n}"
        then
            mv "${f}" "${n}"
        fi
    done
}

prepare() {
    for _apparch in 32 64
    do
        unzip -o "BIND${_apparch}.zip" -d "bind${_apparch}"
        _lc "bind${_apparch}"/*
    done
}

_package() {
    a="${pkgdir}/apps${_apparch}/${_realname}"
    mkdir -p "${a}"
    cp "bind${_apparch}"/*.{exe,dll} "${a}"
    rm "${a}"/vcredist*.exe "${a}/bindinstall.exe"

    d="${pkgdir}/docs${_apparch}/${_realname}"
    mkdir -p "${d}"
    cp "bind${_apparch}"/*.{html,pdf,txt} "bind${_apparch}"/{changes,copyright,faq,history,migration,migration-4to9,openssl-license,readme,sha256} "${d}"
}

package_app-i686-bind() {
    _apparch=32
    _package
}

package_app-x86_64-bind() {
    _apparch=64
    _package
}
