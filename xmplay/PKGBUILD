# Maintainer: David Macek <david.macek.0@gmail.com>

_realname=xmplay
pkgbase="app-${_realname}"
pkgname=("app-i686-${_realname}")
epoch=1
pkgver=3.8.3.4
pkgrel=1
pkgdesc="Light weight, pluggable audio player"
url="http://www.un4seen.com/xmplay.html"
arch=('any')
license=('custom')
makedepends=("unzip")
provides=("${pkgbase}")
options=(!strip)
source=("xmplay.zip::http://us.un4seen.com/files/xmplay$(echo "${pkgver}" | sed -r 's/([0-9]+)[.]([0-9]+)[.]([0-9]+)[.]([0-9]+)/\1\2/').zip")
sha512sums=('05ccbf67897b4ac55bc1591839ff559b55bd3c2db6aeaf3a4350a71c97485397a6a68fe5a54cf12cc2d51581b944444554d7adc3fe586fcba1195c6f2d4bacbe')

pkgver() {
    b="$(cygpath -wa "xmplay.exe" | sed 's/\\/\\\\/g')"
    wmic datafile where name=\""${b}"\" get version | grep 'Version' -A 1 | tail -1 | tr -d ' '
}

package() {
    a="${pkgdir}/apps/${_realname}"
    mkdir -p "${a}"
    cp -r *.{dll,exe} "${a}"
    cp -r *.txt "${a}"
}
