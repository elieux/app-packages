# Maintainer: David Macek <david.macek.0@gmail.com>

_realname=vivaldi
_choconame=Vivaldi
pkgbase="app-${_realname}"
pkgname=("app-x86_64-${_realname}")
epoch=1
pkgver=7.1.3570.50
pkgrel=1
pkgdesc="A new browser for our friends"
url="https://vivaldi.com/"
arch=('any')
license=('custom')
replaces=("app-i686-${_realname}")
makedepends=("p7zip")
provides=("${pkgbase}")
options=(!strip)
source=("Vivaldi.x64.exe::https://downloads.vivaldi.com/stable/Vivaldi.${pkgver}.x64.exe")
sha512sums=('110438093a08714e08aa87b12ff007b1a4612308ab79ef4c0cc29a0aa25b9f55db4056c1959d792dddb7423759becad9f2f5d0d98d38c7f975e3a55f7f525c52')
noextract=("Vivaldi.x64.exe")

_lc() {
    local d f n

    for f in "${@}"
    do
        d="$(dirname "${f}")"
        f="$(basename "${f}")"
        n="$(echo "${f}" | tr 'A-Z' 'a-z')"
        if test "${f}" != "${n}"
        then
            mv "${d}/${f}" "${d}/${n}"
        fi
    done
}

prepare() {
    7z x -y -bd "Vivaldi.x64.exe" -o"Vivaldi.x64.dir" > /dev/null
    7z x -y -bd "Vivaldi.x64.dir/vivaldi.7z" > /dev/null
}

package() {
    local a="${pkgdir}/apps/${_realname}"
    mkdir -p "${a}"
    cp -R "Vivaldi-bin"/*.exe "Vivaldi-bin/${pkgver}"/* "${a}"
    _lc "${a}"/*
}
