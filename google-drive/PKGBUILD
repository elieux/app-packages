# Maintainer: David Macek <david.macek.0@gmail.com>

_realname=google-drive
_choconame=google-drive
pkgbase="app-${_realname}"
pkgname=("app-x86_64-${_realname}")
epoch=1
pkgver=61.0.3.0
pkgrel=1
pkgdesc="The Google Drive synchronization tool"
url="https://www.google.com/drive/"
arch=('any')
license=('custom')
replaces=("app-i686-${_realname}")
makedepends=("p7zip")
provides=("${pkgbase}")
options=(!strip)
source=("GoogleDriveSetup.exe::https://dl.google.com/drive-file-stream/GoogleDriveSetup.exe")
sha512sums=('4eca0d6ac138dbf90ea33d830e83b9c8c9341731d39b523253db0be4d67d68e8cd4b0cc37a11e2ae9758218cc6bfc62b77c5883acf9ed2be239364b8a81728ae')

prepare() {
    7z x -y -bd "GoogleDriveSetup.exe" -o"GoogleDriveSetup.dir" > /dev/null
    7z x -y -bd "GoogleDriveSetup.dir/.rsrc/0/RCDATA/101" -o"101" > /dev/null
}

pkgver() {
    powershell -command '(Get-Item "101/GoogleDriveFS.exe").VersionInfo.FileVersionRaw.ToString()'
}

package() {
    local a="${pkgdir}/apps/${_realname}"
    mkdir -p "${a}"
    cp -R "101"/* "${a}"
}
