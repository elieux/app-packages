# Maintainer: David Macek <david.macek.0@gmail.com>

_realname=shotcut
_choconame=Shotcut
pkgbase="app-${_realname}"
pkgname=("app-x86_64-${_realname}")
epoch=1
pkgver=23.09.29
pkgrel=1
pkgdesc="Cross-platform, open-source video editor"
url="https://shotcut.org/"
arch=('any')
license=('GPL3')
provides=("${pkgbase}")
options=(!strip)
source=("shotcut-win64.zip::https://github.com/mltframework/shotcut/releases/download/v${pkgver}/shotcut-win64-${pkgver//./}.zip")
sha512sums=('bc3c446e61be227028e42bea8c56c0782bbf888ac95a75c83672900b0657af42c460dffa7197e03f523971e6f058b128436e5c2ce656c7d76f23aae88c71ed77')

_lc() {
    local d f n

    for f in "${@}"
    do
        d="$(dirname "${f}")"
        f="$(basename "${f}")"
        n="$(echo "${f}" | tr 'A-Z' 'a-z')"
        if test "${f}" != "${n}"
        then
            mv "${d}/${f}" "${d}/${n}"
        fi
    done
}

package() {
    local a="${pkgdir}/apps/${_realname}"
    mkdir -p "${a}"
    cp -R "Shotcut"/* "${a}"
    _lc "${a}"/*
}
