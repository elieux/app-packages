# Maintainer: David Macek <david.macek.0@gmail.com>

_realname=mc
_realver=218
_choconame=mc
pkgbase="app-${_realname}"
pkgname=("app-i686-${_realname}")
epoch=1
pkgver=4.8.24.218
pkgrel=1
pkgdesc="GNU terminal two-pane file manager"
url="https://sourceforge.net/projects/mcwin32/"
arch=('any')
license=('GPL3')
provides=("${pkgbase}")
makedepends=("app-innounp")
options=(!strip)
source=("mcwin32-build-setup.exe::https://downloads.sourceforge.net/mcwin32/mcwin32-build${_realver}-setup.exe")
sha512sums=('423b3c26d0c1188693506323ae936c935a881e1be29d551ce2c1c9031310dd0deffae808b487070f982e02c6a3ebfced724627bfecaeaaa8df0a027b5d5becb6')

prepare() {
    /apps/innounp/innounp -x -q -m -a -y "mcwin32-build-setup.exe" -d"mcwin32-build-setup.dir"
}

pkgver() {
    powershell -command '(Get-Item "mcwin32-build-setup.exe").VersionInfo.ProductVersion.ToString()' | sed -r 's/^([0-9]+[.][0-9]+[.][0-9]+) [(]build: [0-9]{8}-([0-9]+)[)] *$/\1.\2/'
}

package() {
    local a="${pkgdir}/apps/${_realname}"
    mkdir -p "${a}"
    cp -R "mcwin32-build-setup.dir/{app}"/* "${a}"
    rm "${a}"/*.map
}
