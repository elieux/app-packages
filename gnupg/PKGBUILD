# Maintainer: David Macek <david.macek.0@gmail.com>

_realname=gnupg
_realver=20241125
_choconame=gnupg
pkgbase="app-${_realname}"
pkgname=("app-i686-${_realname}")
epoch=1
pkgver=2.4.7
pkgrel=1
pkgdesc="Complete and free implementation of the OpenPGP standard"
url="https://www.gnupg.org/"
arch=('any')
license=('custom')
makedepends=("p7zip")
provides=("${pkgbase}")
options=(!strip)
source=("gnupg-w32.exe::https://www.gnupg.org/ftp/gcrypt/binary/gnupg-w32-${pkgver}_${_realver}.exe")
sha512sums=('601bc04fa88cfe8b5dec2914a08acd87433d20049699fa0daf029933045ee42c22e1e7fe09de58c641593c2d59573a8129f328f48b936d126f2b49de9682e9e0')
noextract=("gnupg-w32.exe")

prepare() {
    7z x -y -bd "gnupg-w32.exe" -o"gnupg-w32.dir" > /dev/null
}

package() {
    local a="${pkgdir}/apps/${_realname}"
    mkdir -p "${a}"
    cp -R "gnupg-w32.dir"/* "${a}"
    rm -R "${a}"/'$PLUGINSDIR'
    find "${a}" -iname '*.tmp' -delete
}
