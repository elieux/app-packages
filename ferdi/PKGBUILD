# Maintainer: David Macek <david.macek.0@gmail.com>

_realname=ferdi
_choconame=ferdi
pkgbase="app-${_realname}"
pkgname=("app-x86_64-${_realname}")
epoch=1
pkgver=5.6.4
pkgrel=1
pkgdesc="Combine your favourite messaging services into one application, community fork of Franz"
url="https://getferdi.com/"
arch=('any')
license=('Apache2')
makedepends=("p7zip")
provides=("${pkgbase}")
options=(!strip)
source=("Ferdi-Setup.exe::https://github.com/getferdi/ferdi/releases/download/v${pkgver}/Ferdi-Setup-${pkgver}.exe")
sha512sums=('9736da07e8be8f5703b5ae27eb78190740d256d35e8aebaf28e9882f2d45a24c5af74ac944dee3cf883741c15f5b1f03513a12726c411c453ec87ec69d2a25a0')

_lc() {
    local d f n

    for f in "${@}"
    do
        d="$(dirname "${f}")"
        f="$(basename "${f}")"
        n="$(echo "${f}" | tr 'A-Z' 'a-z')"
        if test "${f}" != "${n}"
        then
            mv "${d}/${f}" "${d}/${n}"
        fi
    done
}

prepare() {
    7z x -y -bd "Ferdi-Setup.exe" -o"Ferdi-Setup.dir" > /dev/null
    7z x -y -bd "Ferdi-Setup.dir"/'$PLUGINSDIR'/"app-64.7z" -o"app-64" > /dev/null
}

package() {
    local a="${pkgdir}/apps/${_realname}"
    mkdir -p "${a}"
    cp -R "app-64"/* "${a}"
    _lc "${a}"/*
}
