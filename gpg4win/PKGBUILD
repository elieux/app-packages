# Maintainer: David Macek <david.macek.0@gmail.com>

_realname=gpg4win
pkgbase="app-${_realname}"
pkgname=("app-i686-${_realname}")
epoch=1
pkgver=3.1.10
pkgrel=1
pkgdesc="A secure solution for file and email encryption"
url="http://www.gpg4win.org/index.html"
arch=('any')
license=('custom')
makedepends=("p7zip")
provides=("${pkgbase}")
options=(!strip)
source=("gpg4win.exe::https://files.gpg4win.org/gpg4win-${pkgver}.exe")
sha512sums=('270abff8063cf19e54e28d657143981094cdf2499b1e2b610759bc998678a44b8ae38ad917d6bfd214b61557ddb7a134b6bdecca9ff47c250ccdd33a16d14ff7')

_lc() {
    local d f n

    for f in "${@}"
    do
        d="$(dirname "${f}")"
        f="$(basename "${f}")"
        n="$(echo "${f}" | tr 'A-Z' 'a-z')"
        if test "${f}" != "${n}"
        then
            mv "${d}/${f}" "${d}/${n}"
        fi
    done
}

prepare() {
    7z x -y -bd "gpg4win.exe"
    _lc *
}

package() {
    a="${pkgdir}/apps/${_realname}"
    mkdir -p "${a}"
    cp -r * "${a}"
    find "${a}" -iname '*.tmp' -delete
    rm -r "${a}/gpg4win.exe" "${a}"/'$'*
    rm "${a}/bin/pinentry.exe"
    cp "${a}/bin/pinentry-w32.exe" "${a}/bin/pinentry.exe"
}
