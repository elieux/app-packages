# Maintainer: David Macek <david.macek.0@gmail.com>

_realname=7zip
pkgbase="app-${_realname}"
pkgname=("app-i686-${_realname}"
         "app-x86_64-${_realname}")
pkgver=16.02
pkgrel=1
pkgdesc="A free file archiver for extremely high compression"
url="http://www.7-zip.org/"
arch=('any')
license=('LGPL2' 'custom')
makedepends=("p7zip")
provides=("${pkgbase}")
options=(!strip)
source=("7z32.exe::http://7-zip.org/a/7z${pkgver//./}.exe"
        "7z64.exe::http://7-zip.org/a/7z${pkgver//./}-x64.exe"
        "7zextra.7z::http://7-zip.org/a/7z${pkgver//./}-extra.7z")
sha512sums=('bd4388c951f12f472da5ea8e2f0415974fab402c0e7cbe5719d67f10f18bff0726fef4037588006179f89097b8fc0a66970f2f1f70d8f0855f7f685f3be031b6'
            '9b2dd5ea68f17b8d361441f9384b80f43463de2f0c682b83dcb96cec473430ffee7797a62366d6137782b1e752044fbbdc2fc82fe259b589b96202ecc3c80365'
            '1d5ea7357bbc27da99203905647af8c063b01a804900d8e6054decde14df6bc562ae48f5b2751e66e2c896bfd229d4ebcb5ccd2d4e21a0394733470d8a25dfb2')

_lc() {
    for f in "${@}"
    do
        n="$(echo "${f}" | tr 'A-Z' 'a-z')"
        if test "${f}" != "${n}"
        then
            mv "${f}" "${n}"
        fi
    done
}

_prepare() {
    7z x -y -bd "7z${_apparch}.exe" -o"7z${_apparch}"
    _lc "7z${_apparch}"/*
}

prepare() {
    _lc *
    for _apparch in 32 64
    do
        _prepare
    done
}

_package() {
    a="${pkgdir}/apps${_apparch}/${_realname}"
    mkdir -p "${a}"
    cp -r "${@}" "7z${_apparch}/lang" "7z${_apparch}"/*.{exe,dll,sfx} "${a}"
    rm "${a}/uninstall.exe"

    d="${pkgdir}/docs${_apparch}/${_realname}"
    mkdir -p "${d}" "${d}/extra"
    cp "7z${_apparch}"/*.{chm,txt,ion} "${d}"
    cp *.txt "${d}/extra"
}

package_app-i686-7zip() {
    _apparch=32
    _package *.{dll,exe}
    rm "${a}"/7z{32,64}.exe
}

package_app-x86_64-7zip() {
    _apparch=64
    _package "x64"/*.{dll,exe}
}
