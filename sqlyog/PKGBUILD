# Maintainer: David Macek <david.macek.0@gmail.com>

DLAGENTS=('https::/usr/bin/curl -k -qb "" -fLC - -o %o %u')

_realname=sqlyog
pkgbase="app-${_realname}"
pkgname=("app-x86_64-${_realname}")
epoch=1
pkgver=12.5.1
pkgrel=1
pkgdesc="MySQL manager and admin tool"
url="https://github.com/webyog/sqlyog-community"
arch=('any')
license=('GPL2')
replaces=("app-i686-${_realname}")
makedepends=("p7zip" "curl")
provides=("${pkgbase}")
options=(!strip)
source=("SQLyogCommunity64.exe::https://s3.amazonaws.com/SQLyog_Community/SQLyog+${pkgver}/SQLyog-${pkgver}-0.x64Community.exe")
sha512sums=('0bb3236a9e22c6e866d0b7469f3b1a012b5e0d45c99d4c5a53c2037a5fdf2dd208a1f1ed7112852364410f0d9244c9e89796d37ec03daf3b4b4550c4d93ce9ad')

_lc() {
    local d f n

    for f in "${@}"
    do
        d="$(dirname "${f}")"
        f="$(basename "${f}")"
        n="$(echo "${f}" | tr 'A-Z' 'a-z')"
        if test "${f}" != "${n}"
        then
            mv "${d}/${f}" "${d}/${n}"
        fi
    done
}

prepare() {
    7z x -y -bd "SQLyogCommunity64.exe" -o"SQLyogCommunity"
    _lc "SQLyogCommunity"/*
}

package() {
    a="${pkgdir}/apps/${_realname}"
    mkdir -p "${a}"
    cp "SQLyogCommunity"/*.{exe,dll,db} "${a}"
    rm "${a}/uninst.exe"
    cp "SQLyogCommunity"/*.txt "${a}"
}
