# Maintainer: David Macek <david.macek.0@gmail.com>

_realname=nexus-mod-manager
_choconame=nmm
pkgbase="app-${_realname}"
pkgname=("app-i686-${_realname}")
epoch=1
pkgver=0.80.7
pkgrel=1
pkgdesc="Game modifications manager originally from Nexus Mods"
url="https://www.nexusmods.com/site/mods/4"
arch=('any')
license=('GPL2')
makedepends=("app-innounp")
provides=("${pkgbase}")
options=(!strip)
source=("NMM.exe::https://github.com/Nexus-Mods/Nexus-Mod-Manager/releases/download/${pkgver}/NMM-${pkgver}.exe")
sha512sums=('28d6115ce939966c541ca3be4bc2c92f50abce2945829140b94ab8adb0f664fb74e0e542e78aed765a3e3259cd1499adc57861e5a75a33f67aac28ff347beb90')

_lc() {
    local d f n

    for f in "${@}"
    do
        d="$(dirname "${f}")"
        f="$(basename "${f}")"
        n="$(echo "${f}" | tr 'A-Z' 'a-z')"
        if test "${f}" != "${n}"
        then
            mv "${d}/${f}" "${d}/${n}"
        fi
    done
}

prepare() {
    /apps/innounp/innounp -x -q -m -a -y "NMM.exe" -d"NMM.dir"
}

package() {
    local a="${pkgdir}/apps/${_realname}"
    mkdir -p "${a}"
    cp -R "NMM.dir/{app}"/* "${a}"
    _lc "${a}"/*
}
