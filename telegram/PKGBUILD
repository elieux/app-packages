# Maintainer: David Macek <david.macek.0@gmail.com>

_realname=telegram
_choconame=telegram
pkgbase="app-${_realname}"
pkgname=("app-i686-${_realname}")
epoch=1
pkgver=2.7.4
pkgrel=1
pkgdesc="Desktop Telegram client"
url="https://desktop.telegram.org/"
arch=('any')
license=('GPL3')
provides=("${pkgbase}")
replaces=("app-x86_64-${_realname}")
options=(!strip)
source=("tportable.zip::https://github.com/telegramdesktop/tdesktop/releases/download/v${pkgver}/tportable.${pkgver}.zip")
sha512sums=('dbb7a0e67122d56023f0b190ac1bcdccb7678ffb8fbf09cda802538be79481556fdc3b162449f1f1d4a4741d19c04bb8d420cb9ff34fe66da624123ec78572a2')

_lc() {
    local d f n

    for f in "${@}"
    do
        d="$(dirname "${f}")"
        f="$(basename "${f}")"
        n="$(echo "${f}" | tr 'A-Z' 'a-z')"
        if test "${f}" != "${n}"
        then
            mv "${d}/${f}" "${d}/${n}"
        fi
    done
}

package() {
    local a="${pkgdir}/apps/${_realname}"
    mkdir -p "${a}"
    cp "Telegram"/* "${a}"
    _lc "${a}"/*
}
