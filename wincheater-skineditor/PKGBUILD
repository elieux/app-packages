# Maintainer: David Macek <david.macek.0@gmail.com>

_realname=wincheater-skineditor
_dirname=wincheater
_apparch=32
pkgbase="app-${_realname}"
pkgname=("app-i686-${_realname}")
pkgver=2.02
pkgrel=2
pkgdesc="Skin designer for WinCheater"
url="http://www.cheater.cz/wincheater2/download-ostatni/"
arch=('any')
license=('custom')
makedepends=("upx")
provides=("${pkgbase}")
options=(!strip)
source=("SkinEditor2.exe::http://www.cheater.cz/download/SkinEditor2.exe")
sha512sums=('85c5537d596de07847697cd7a1592a473bbd86e5affe9a5272ac90bace26ed558d0cd2e05e57edd6bf8f16367f46e46f770d16972e520d6b0802e29fc5af80f3')

_lc() {
    local d f n

    for f in "${@}"
    do
        d="$(dirname "${f}")"
        f="$(basename "${f}")"
        n="$(echo "${f}" | tr 'A-Z' 'a-z')"
        if test "${f}" != "${n}"
        then
            mv "${d}/${f}" "${d}/${n}"
        fi
    done
}

pkgver() {
    upx -q -f -o "unpx" -d "SkinEditor2.exe" > /dev/null
    strings -d -eS "unpx" | grep -A 1 Caption | grep "pro WinCheater 2" | sed -r 's/SkinEditor ([0-9]+[.][0-9]+) pro WinCheater 2/\1/' | tr -d ' '
}

prepare() {
    _lc *
}

package() {
    a="${pkgdir}/apps${_apparch}/${_dirname}"
    mkdir -p "${a}"
    cp "skineditor2.exe" "${a}"
}
