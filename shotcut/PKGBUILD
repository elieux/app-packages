# Maintainer: David Macek <david.macek.0@gmail.com>

_realname=shotcut
_choconame=Shotcut
pkgbase="app-${_realname}"
pkgname=("app-x86_64-${_realname}")
epoch=1
pkgver=21.12.04
pkgrel=1
pkgdesc="Cross-platform, open-source video editor"
url="https://shotcut.org/"
arch=('any')
license=('GPL3')
provides=("${pkgbase}")
options=(!strip)
source=("shotcut-win64.zip::https://github.com/mltframework/shotcut/releases/download/v${pkgver}/shotcut-win64-${pkgver//./}.zip")
sha512sums=('978f063ed6fd5000cb4c6ca784e687c1b5f6ff0a395054308df18d816a5bd4f74e85aec0c206442e94a1378693a06334f278c0ef162d587ae1bdc2115870ebb1')

_lc() {
    local d f n

    for f in "${@}"
    do
        d="$(dirname "${f}")"
        f="$(basename "${f}")"
        n="$(echo "${f}" | tr 'A-Z' 'a-z')"
        if test "${f}" != "${n}"
        then
            mv "${d}/${f}" "${d}/${n}"
        fi
    done
}

package() {
    local a="${pkgdir}/apps/${_realname}"
    mkdir -p "${a}"
    cp -R "Shotcut"/* "${a}"
    _lc "${a}"/*
}
