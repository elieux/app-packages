# Maintainer: David Macek <david.macek.0@gmail.com>

_realname=atube-catcher
pkgbase="app-${_realname}"
pkgname=("app-i686-${_realname}")
epoch=1
pkgver=3.8.0.9762
pkgrel=1
pkgdesc="Downloads videos from the Internet"
url="http://www.atube.me/"
arch=('any')
license=('custom')
makedepends=("app-innounp")
provides=("${pkgbase}")
options=(!strip)
source=("aTubeCatcher.exe::http://files.dsnetwb.com/aTube_Catcher.exe")
sha512sums=('51bc3ed71a2df572d6f7bc38dce80f86712ddcec42d6f90971f7bf3b70e0a284bf45cba8615cbc8784b4e23fbf74c9480420eb690bba27dfa5bf69b14ebe21ec')

_lc() {
    local d f n

    for f in "${@}"
    do
        d="$(dirname "${f}")"
        f="$(basename "${f}")"
        n="$(echo "${f}" | tr 'A-Z' 'a-z')"
        if test "${f}" != "${n}"
        then
            mv "${d}/${f}" "${d}/${n}"
        fi
    done
}

prepare() {
    /apps/innounp/innounp -x -q -m -a -y "aTubeCatcher.exe"
    _lc "{app}"/*
}

pkgver() {
    /apps/innounp/innounp -e -q -m -a -y "aTubeCatcher.exe" "yct.exe" > /dev/null
    b="$(cygpath -wa "yct.exe" | sed 's/\\/\\\\/g')"
    wmic datafile where name=\""${b}"\" get version | grep 'Version' -A 1 | tail -1 | tr -d ' '
}

package() {
    a="${pkgdir}/apps/${_realname}"
    mkdir -p "${a}"
    cp -r "{app}"/* "${a}"
}
