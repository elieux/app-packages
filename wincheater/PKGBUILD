# Maintainer: David Macek <david.macek.0@gmail.com>

_realname=wincheater
_apparch=32
pkgbase="app-${_realname}"
pkgname=("app-i686-${_realname}")
pkgver=2.07
pkgrel=2
pkgdesc="Video game cheats and guides database viewer"
url="http://www.cheater.cz/winch/win_uvod.htm"
arch=('any')
license=('custom')
depends=("app-i686-wincheater-data")
makedepends=("app-i686-innounp")
provides=("${pkgbase}")
options=(!strip)
source=("WinCH2_onlysetup.exe::http://www.cheater.cz/download/WinCH2_onlysetup.exe")
md5sums=('010edb11b51097259c6827e81ee842dd')

_lc() {
    for f in "${@}"
    do
        n="$(echo "${f}" | tr 'A-Z' 'a-z')"
        if test "${f}" != "${n}"
        then
            mv "${f}" "${n}"
        fi
    done
}

pkgver() {
    strings -d -e{b,l} "WinCH2_onlysetup.exe" | grep 'FileDescription' -A 1 | tail -1 | sed -r 's/^Scorpions WinCheater ([0-9]+[.][0-9]+) [(]pouze program[)] Setup/\1/' | tr -d ' '
}

prepare() {
    /apps32/innounp/innounp -x -q -m -a -y "WinCH2_onlysetup.exe"
    _lc "{app}"/*
}

package() {
    a="${pkgdir}/apps${_apparch}/${_realname}"
    mkdir -p "${a}"
    cp -r "{app}"/* "${a}"
    rm "${a}/texty.sct"
}
