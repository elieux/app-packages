# Maintainer: David Macek <david.macek.0@gmail.com>

_realname=xnretro
pkgbase="app-${_realname}"
pkgname=("app-x86_64-${_realname}")
epoch=1
pkgver=1.30.0
pkgrel=1
pkgdesc="Vintage camera effects on desktop"
url="https://www.xnview.com/en/xnretro/"
arch=('any')
license=('custom')
provides=("${pkgbase}")
options=(!strip)
source=("XnRetro-win-x64.zip::https://download.xnview.com/XnRetro-win-x64.zip")
sha512sums=('d82911c4c7ed88aca1858b9ca40e0a4adb6406a303362968f6ead21187d31a16a954abdde06317f5da93fb2f58831811e9df31545b7178cadf020fd9eeef4ba8')

_lc() {
    local d f n

    for f in "${@}"
    do
        d="$(dirname "${f}")"
        f="$(basename "${f}")"
        n="$(echo "${f}" | tr 'A-Z' 'a-z')"
        if test "${f}" != "${n}"
        then
            mv "${d}/${f}" "${d}/${n}"
        fi
    done
}

_rmmsdll() {
    local ms

    ms=("msvbvm[0-9]+"
        "ucrtbase"
        "mfcm?[0-9]+[a-z]+"
        "msvc[rp][0-9]+(_([0-9]|codecvt_ids|atomic_wait))?"
        "vc([ao]mp|corlib|runtime)[0-9]+(_([0-9]|threads))?"
        "concrt[0-9]+"
        "pgort[0-9]+"
        "api-ms-win-[0-9a-z-]+")
    find "${1}" -iname '*.dll' | grep -Eif <(for m in "${ms[@]}"; do echo "/${m}[.]dll$"; done) | xargs -r rm
}

pkgver() {
    powershell -command '(Get-Item "XnRetro/XnRetro.exe").VersionInfo.FileVersionRaw.ToString()' | sed -r 's/[.]0$//'
}

package() {
    local a="${pkgdir}/apps/${_realname}"
    mkdir -p "${a}"
    cp -R "XnRetro"/* "${a}"
    _rmmsdll "${a}"
    rm "${a}/The latest supported Visual C++ downloads.website"
    _lc "${a}"/*
}
