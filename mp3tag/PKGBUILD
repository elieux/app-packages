# Maintainer: David Macek <david.macek.0@gmail.com>

_realname=mp3tag
pkgbase="app-${_realname}"
pkgname=("app-i686-${_realname}")
epoch=1
pkgver=2.99a
pkgrel=1
pkgdesc="Powerful audio metadata editor"
arch=('any')
url="http://mp3tag.de/en/index.html"
license=('custom')
makedepends=("p7zip")
provides=("${pkgbase}")
options=(!strip)
source=("mp3tagsetup.exe::http://download.mp3tag.de/mp3tagv${pkgver//./}setup.exe")
sha512sums=('f44e88a09f63148c62dd887285fa19fe5112a9c8bf4ce95762e01aba8e07aa10128d93584a0ddff4f219efb5c7363784b29b1a1ddae279e2a453ba4df0d71de5')

_lc() {
    local d f n

    for f in "${@}"
    do
        d="$(dirname "${f}")"
        f="$(basename "${f}")"
        n="$(echo "${f}" | tr 'A-Z' 'a-z')"
        if test "${f}" != "${n}"
        then
            mv "${d}/${f}" "${d}/${n}"
        fi
    done
}

prepare() {
    w="*license*.txt"
    7z x -y -bd -x!"${w}" "mp3tagsetup.exe"
    for f in $(7z l -y -bd -i!"${w}" "mp3tagsetup.exe" | grep 'license.*txt' | sed -r 's/[ ]+/\t/g' | cut -f 5 | sed 's/^.*\\//')
    do
        7z e -y -bd -so -i!"*${f}" "mp3tagsetup.exe" > "${f}"
    done
    _lc *
}

package() {
    a="${pkgdir}/apps/${_realname}"
    mkdir -p "${a}" "${a}/help"
    cp -r "data" "export" "lang" *.{exe,dll} "${a}"
    touch "${a}/help/helpmap.idx"
    rm "${a}/mp3tagsetup.exe"
    cp -r "help" *.txt "${a}"
}
