# Maintainer: David Macek <david.macek.0@gmail.com>

_realname=slack
_apparch=32
pkgbase="app-${_realname}"
pkgname=("app-i686-${_realname}")
pkgver=2.1.2
pkgrel=1
pkgdesc="Client for the Slack team communication platform"
url="http://www.slack.com/"
arch=('any')
license=('custom')
makedepends=("p7zip" "unzip")
provides=("${pkgbase}")
options=(!strip)
source=("SlackSetup.exe::https://slack.com/ssb/download-win"
        "en_US.dic::http://slack-ssb-updates.s3.amazonaws.com/dictionaries/en_US.dic"
        "en_US.aff::http://slack-ssb-updates.s3.amazonaws.com/dictionaries/en_US.aff")
sha512sums=('7a9de4abebfb761fa234d7c3cabe4827b7cb295da97cdc7d79de07503cc2e01579404f1771a78e5ca9b5ee6516ce769f9d1cfd349c607f06f846d57d4f5c8201'
            'ebc0b25da54868590cdbc6e283a4d4d38f6d76efbaac9e5fa31b5ef86fc4d9970fa895f4313cfaa4693fec1f2c3ad7afb5393139ee3b367f1ae888f6fd2d6eca'
            '9c7122a026698ee12b8581cd1a5e520b86ef65899fd834dc116ad13d4b8af5465c99440f86b20440208cb94eac06841654f5c8295b4e8757e9420fe37093a1ba')

_lc() {
    for f in "${@}"
    do
        n="$(echo "${f}" | tr 'A-Z' 'a-z')"
        if test "${f}" != "${n}"
        then
            mv "${f}" "${n}"
        fi
    done
}

pkgver() {
    b="$(cygpath -wa "SlackSetup.exe" | sed 's/\\/\\\\/g')"
    wmic datafile where name=\""${b}"\" get version | grep 'Version' -A 1 | tail -1 | tr -d ' ' | sed -r 's/^([0-9]+[.][0-9]+[.][0-9]+)[.]0+$/\1/'
}

prepare() {
    rm -f "slack"-*-"full.nupkg"
    7z x -y -bd "SlackSetup.exe"
    unzip -o "slack"-*-"full.nupkg"
    _lc "lib/net45"/*
}

package() {
    a="${pkgdir}/apps${_apparch}/${_realname}"
    mkdir -p "${a}" "${a}/app" "${a}/dictionaries"
    cp -r "lib/net45"/* "${a}/app"
    rm "${a}/app"/{vccorlib,msvc{p,r}}120.dll "${a}/app/resources/app.asar.unpacked/node_modules/edge-atom-shell/build/Release"/{vccorlib,msvc{p,r}}120.dll
    cp en_US.* "${a}/dictionaries"

    d="${pkgdir}/docs${_apparch}/${_realname}"
    mkdir -p "${d}"
    mv "${a}/app/license" "${d}"
}
