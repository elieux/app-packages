# Maintainer: David Macek <david.macek.0@gmail.com>

_realname=zeal
_apparch=32
pkgbase="app-${_realname}"
pkgname=("app-i686-${_realname}")
pkgver=0.3.1
pkgrel=1
pkgdesc="An offline documentation browser for software developers"
url="https://zealdocs.org/"
arch=('any')
license=('GPL3')
provides=("${pkgbase}")
options=(!strip)
source=("zeal-windows-x86.msi::https://bintray.com/artifact/download/zealdocs/windows/zeal-${pkgver}-windows-x86.msi")
sha512sums=('cf151a8e1efabff4ea9d1a6ee1db79024ed06d0830630257322ceccdd9902cdc78c03a7d8a70dc368d3cc33740e076cfddd693bee006b5f1d0d2d8655e329b9b')

_lc() {
    local d f n

    for f in "${@}"
    do
        d="$(dirname "${f}")"
        f="$(basename "${f}")"
        n="$(echo "${f}" | tr 'A-Z' 'a-z')"
        if test "${f}" != "${n}"
        then
            mv "${d}/${f}" "${d}/${n}"
        fi
    done
}

build() {
    msiexec -a "zeal-windows-x86.msi" "TARGETDIR=$(cygpath -wa "install")" -qn
    attrib -r //s
    s="install/PFiles/Zeal"
    _lc "${s}"/*
}

package() {
    s="install/PFiles/Zeal"

    a="${pkgdir}/apps${_apparch}/${_realname}"
    mkdir -p "${a}"
    cp -r "${s}"/* "${a}"
}
