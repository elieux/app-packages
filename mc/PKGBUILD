# Maintainer: David Macek <david.macek.0@gmail.com>

_realname=mc
_realver=227
_choconame=mc
pkgbase="app-${_realname}"
pkgname=("app-i686-${_realname}")
epoch=1
pkgver=4.8.28.227
pkgrel=1
pkgdesc="GNU terminal two-pane file manager"
url="https://sourceforge.net/projects/mcwin32/"
arch=('any')
license=('GPL3')
provides=("${pkgbase}")
makedepends=("app-innounp")
options=(!strip)
source=("mcwin32-build-setup.exe::https://downloads.sourceforge.net/mcwin32/mcwin32-build${_realver}-setup.exe")
sha512sums=('34f67d910ca2b24229fedf2451deb670a7a56adb9bf10729ee14f2df9449699953a525dcbb976c6fd0b88e150269edc393e4c505d4cd66605ffdfe9b0c47f7ee')

prepare() {
    /apps/innounp/innounp -x -q -m -a -y "mcwin32-build-setup.exe" -d"mcwin32-build-setup.dir"
}

pkgver() {
    powershell -command '(Get-Item "mcwin32-build-setup.exe").VersionInfo.ProductVersion.ToString()' | sed -r 's/^([0-9]+[.][0-9]+[.][0-9]+) [(]build: [0-9]{8}-([0-9]+)[)] *$/\1.\2/'
}

package() {
    local a="${pkgdir}/apps/${_realname}"
    mkdir -p "${a}"
    cp -R "mcwin32-build-setup.dir/{app}"/* "${a}"
    rm "${a}"/*.map
}
